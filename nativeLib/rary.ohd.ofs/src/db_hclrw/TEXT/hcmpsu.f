C MODULE HCMPSU
C-----------------------------------------------------------------------
C
      SUBROUTINE HCMPSU (FUNCNM,ISTAT)
C
C  THIS ROUTINE SETS UP THE COMPUTE COMMAND. IT FINDS THE FUNCTION
C  RECORD, SETS DEFAULTS FOR TECHNIQUES AND WRITES THIS TO A FILE
C  FOR TECHNIQUES THAT ARE ON, IT GETS THOSE RECORDS AND SETS THE
C  DEFAULTS FOR ANY ARGUMENTS IN THOSE TECHNIQUES
C
C  DEFAULTS ARE SET BY FIRST SETTING ALL TO THE GLOBAL VALUE AND THEN
C  RESETTING THOSE SPECIFIED WITH LOCAL DEFAULTS.
C
C  COMMON HPTRAY CONTAINS THE TECHNIQUE NAME, VALUE, NUMBER OF ARGUMENT
C  WORDS AND THE POINTER TO THE FIRST ARGUMENT IN THE IGARG OR IARG
C  ARRAYS.
C
C  ARGUMENT LIST:
C
C       NAME      TYPE   I/O   DIM   DESCRIPTION
C       ------    ----   ---   ---   -----------
C       FUNCNM    A*8     I     1    FUNCTION NAME
C       ISTAT     I*4     O     1    STATUS:
C                                      0=OK
C                                      1=ERROR
C

      CHARACTER*8 FUNCNM
      CHARACTER*8 RTNNAM,RTNOLD
      PARAMETER (MFBUF=128)
      DIMENSION IFBUF(MFBUF)
      PARAMETER (MDBUF=500)
      DIMENSION IDBUF(MDBUF)
      PARAMETER (MTBUF=500)
      DIMENSION ITBUF(MTBUF)
      PARAMETER (MSVRAY=600)
      DIMENSION ISVRAY(2,MSVRAY)
C
      EQUIVALENCE (ITBUF(1),IFBUF(1))
C
      INCLUDE 'uiox'
      INCLUDE 'ufreei'
      INCLUDE 'udebug'
      INCLUDE 'udatas'
      INCLUDE 'hclcommon/hunits'
      INCLUDE 'hclcommon/hindx'
      INCLUDE 'hclcommon/hgtech'
      INCLUDE 'hclcommon/htechn'
      INCLUDE 'hclcommon/hgargm'
      INCLUDE 'hclcommon/hargmn'
      INCLUDE 'hclcommon/hcurfc'
      INCLUDE 'hclcommon/hptray'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/db_hclrw/RCS/hcmpsu.f,v $
     . $',                                                             '
     .$Id: hcmpsu.f,v 1.2 2001/06/13 13:36:51 dws Exp $
     . $' /
C    ===================================================================
C
C   
      RTNNAM='HCMPSU'
C
      IF (IHCLTR.GT.1) WRITE (IOGDB,*) 'ENTER ',RTNNAM
C
      IOPNUM=-1
      CALL FSTWHR (RTNNAM,IOPNUM,RTNOLD,IOLDOP)
C
      CALL UMEMST (0,IGTECH,MGTECH)
      CALL UMEMST (0,ITECH,MTECH)
      CALL UMEMST (0,IGARG,MGARG)
      CALL UMEMST (0,IARG,MARG)
      N=MPTRAY/2
      CALL UMEMST (0,IPTRAY,N)
C
      NPTRAY=0
C
      IF (FUNCNM.EQ.' ') THEN
C     GET CARD FROM SAVED FILE
         NNC=1
         CALL HCARDR (NNC,ISTAT)
         IF (ISTAT.NE.0) THEN 
            CALL ERROR
            GO TO 280
            ENDIF
C     GET FUNCTION NAME
         IF (NFIELD.LT.2) THEN
            WRITE (LP,290)
            CALL ERROR
            ISTAT=1
            GO TO 280
            ENDIF
         CALL UMEMST (IBLNK,FUNCNM,2)
         IF=2
         J=IFSTRT(IF)
         N=IFSTOP(IF)-J+1
         IF (N.GT.8) N=8
         CALL UPACK1 (IBUF(J),FUNCNM,N)
         ENDIF
C
C  GET THE FUNCTION DEFINITION RECORD
      ITYPEF=2
      CALL HGTRCD (ITYPEF,FUNCNM,MFBUF,IFBUF,ISTAT)
      IF (ISTAT.EQ.0) GO TO 20
         WRITE (LP,300) FUNCNM
         CALL ERROR
         GO TO 280
C
C  FOUND FUNCTION - GET TECHNIQUE DEFAULTS
20    N=MSVRAY*2
      CALL UMEMST (0,ISVRAY,N)
      IAX1=1
      IHCFUN=IFBUF(2)
      IF (IFBUF(9).EQ.0) GO TO 250
C
C  IF LOCAL FUNCTION GET DEFAULT FROM LOCAL DEFINITION FILE
C  IF GLOBAL FUNCTION GET DEFAULT FROM GLOBAL DEFINITION FILE
C  AND THEN CHECK LOCAL
      CALL HGTDFR (ITYPEF,IFBUF(7),IFBUF(8),MDBUF,IDBUF,ISTAT)
      IF (ISTAT.NE.0) THEN
         CALL ERROR
         GO TO 280
         ENDIF
C
C  CHECK EACH DEFAULT
      NPTRAY=1
      IPTRAY(NPTRAY)=IFBUF(9)
30    IF (IABS(IDBUF(6)).EQ.2) GO TO 50
40       WRITE (LP,310) IFBUF(7),IFBUF(8)
         CALL ERROR
         ISTAT=1
         GO TO 280
C
C  COMPARE NAME TO FUNCTION NAME
50    CALL UNAMCP (FUNCNM,IDBUF(4),IMATCH)
      IF (IMATCH.NE.0) GO TO 40
C
C  SET DEFAULTS FOR TECHNIQUES IN ITECH ARRAY
      ND=IDBUF(7)*2+6
      DO 80 IX=8,ND,2
         IT=IDBUF(IX)
         IF (IT.GE.1.AND.IT.LE.MARG) GO TO 60
         IF (IT.GE.-MGARG.AND.IT.LE.-1) GO TO 70
            WRITE (LP,320) IABS(IT)
            CALL ERROR
            ISTAT=1
            GO TO 280
60       ITECH(IT)=IDBUF(IX+1)
         GO TO 80
70       IGTECH(-IT)=IDBUF(IX+1)
80       CONTINUE
C
C  IF FUNCTION IS GLOBAL CHECK FOR LOCAL DEFAULTS TO RESET
      IF (ITYPEF.GE.0) GO TO 90
      CALL HGTRDN (KLDFGD,IFBUF(7),IDBUF,MDBUF,ISTAT)
      IF (ISTAT.NE.0) THEN
         CALL ERROR
         GO TO 280
         ENDIF
C
C  CHECK IF NO LOCAL DEFAULTS
      IF (IDBUF(1).EQ.0) GO TO 90
      ITYPEF=0
      GO TO 30
C
C  SET ARRAY TO WRITE OUT DEFAULTS
90    IAX=1
      ND=IFBUF(9)+9
C
C  PROCESS ALL ALL TECHNIQUES IN FUNCTION RECORD
      DO 110 IX=10,ND
         IF (IAX.GE.MSVRAY) GO TO 260
         IAX=IAX+1
         ISVRAY(1,IAX)=IFBUF(IX)
         IF (IFBUF(IX).LT.0) GO TO 100
            ISVRAY(2,IAX)=ITECH(IFBUF(IX))
            GO TO 110
100      ISVRAY(2,IAX)=IGTECH(-IFBUF(IX))
110      CONTINUE
C
C  SET ARGUMENTS FOR TECHNIQUES (ALL OF THEM ON OR OFF)
      IAX1=IAX+1
C
C  SET NUMBER OF TECHNIQUES
      ISVRAY(1,1)=IAX
C
C  CHECK THROUGH ISVRAY FOR TECHNIQUE NUMBERS
      DO 240 I=2,IAX
C     GET THE TECHNIQUE ARRAY USING THE INTERNAL TECHNIQUE NUMBER
         IX=3
         CALL HGTDIN (ISVRAY(1,I),IX,IRECX,IRECD,ITBUF,MTBUF,ISTAT)
         ITYPET=ITBUF(3)
         IF (NPTRAY+7.LT.MPTRAY) GO TO 120
            WRITE (LP,330)
            CALL ERROR
            ISTAT=1
            GO TO 280
120      NPTRAY=NPTRAY+1
         CALL UMEMOV (ITBUF(4),IPTRAY(NPTRAY),2)
C     INTERNAL TECHNIQUE POINTER
         IPTRAY(NPTRAY+4)=ISVRAY(1,I)
C     CHECK IF NO ARGUMENTS
         IF (ITBUF(10).EQ.0) GO TO 230
C     GET DEFAULT TECHNIQUE ARRAY
         CALL HGTDFR (ITBUF(3),ITBUF(8),ITBUF(9),MDBUF,IDBUF,ISTAT)
         IF (ISTAT.NE.0) THEN
            CALL ERROR
            GO TO 280
            ENDIF
C     GET ARGUMENT NUMBERS FROM TECHNIQUE RECORD
130      NA=IDBUF(7)
         ND=8
         DO 170 K=1,NA
            IAP=IABS(IDBUF(ND))
C        MOVE IN ARGUMENT DEFAULT
            N=IDBUF(ND+1)
C        CHECK FOR DATE WITH STAR AND CONVERT TO TODAY
            IF (N.NE.7) GO TO 140
            CALL HSETDY (IDBUF(ND+2))
140         IF (IHCLDB.GT.0) WRITE (IOGDB,340) IAP,ND,N
            IF (IDBUF(ND).LT.0) GO TO 150
            CALL UMEMOV (IDBUF(ND+2),IARG(IAP),N)
            GO TO 160
150         CALL UMEMOV (IDBUF(ND+2),IGARG(IAP),N)
160         ND=ND+N+2
170         CONTINUE
C     IF TECHNIQUE IS GLOBAL, CHECK FOR LOCAL DEFAULTS
         IF (ITYPET.GE.0) GO TO 180
         CALL HGTRDN (KLDFGD,ITBUF(8),IDBUF,MDBUF,ISTAT)
         IF (ISTAT.NE.0) THEN
            CALL ERROR
            GO TO 280
            ENDIF
C     CHECK IF TECHNIQUE IS SET
         IF (IDBUF(1).EQ.0) GO TO 180
            ITYPET=0
            GO TO 130
180      NARGWD=0
         NA=ITBUF(10)*4+9
C     COMPUTE TOTAL ARGUMENT LENGTH - FIRST TYPE IS IN WORD 13
C     WORD 14 IS POINTER TO IARG OR IGARG ARRAY
C     LENGTH OF TYPE 1,2,4=1, LENGTH OF TYPE 5=7, LENGTH OF STR IS-
         DO 200 IX=13,NA,4
            IF (ITBUF(IX).LT.0) GO TO 190
            NARGWD=NARGWD+1
C        ADD 6 MORE IF DATE
            IF (ITBUF(IX).EQ.5) NARGWD=NARGWD+6
            GO TO 200
C        ALLOW FOR BLANK SUPPRESSING OF STRING
190         NARGWD=NARGWD+(-ITBUF(IX)+8)/4
200         CONTINUE
         IF (IHCLDB.GT.0) WRITE (IOGDB,350) NARGWD,ITBUF(14),IAX1
         IF (NARGWD+IAX1.GT.MSVRAY) GO TO 260
C     SET POINTER TO FIRST ARGUMENT WORD AND NUMBER
         ISVRAY(1,IAX1)=ITBUF(14)
         ISVRAY(2,IAX1)=NARGWD
         IF (ITBUF(3).LT.0) GO TO 210
            CALL UMEMOV (IARG(ITBUF(14)),ISVRAY(1,IAX1+1),NARGWD)
            GO TO 220
210      CALL UMEMOV (IGARG(-ITBUF(14)),ISVRAY(1,IAX1+1),NARGWD)
220      IAX1=IAX1+1+(NARGWD+1)/2
C     SET FIRST ARGUMENT LOCATION
         IPTRAY(NPTRAY+5)=NARGWD
         NPTRAY=NPTRAY+6
         IPTRAY(NPTRAY)=ITBUF(14)
         GO TO 240
C     NO ARGUMENTS
230      NPTRAY=NPTRAY+6
         IPTRAY(NPTRAY-1)=0
         IPTRAY(NPTRAY)=0
240      CONTINUE
C
C  WRITE SAVED DEFAULTS OUT TO FILE
      IAX1=IAX1-1
      ISVRAY(2,1)=IAX1
250   CALL HSAVED (ISVRAY,IAX1,ISTAT)
C
C  SET OPTIONS THAT ARE NOT IDENTIFIER SPECIFIC
      CALL HSETOP
      GO TO 280
C
260   WRITE (LP,360)
      CALL ERROR
      ISTAT=1
C
280   IF (IHCLDB.GT.0) THEN
         WRITE (IOGDB,370) (IPTRAY(I),I=1,NPTRAY)
         WRITE (IOGDB,380) ITECH
         WRITE (IOGDB,390) IGTECH
         ENDIF
C
      CALL FSTWHR (RTNOLD,IOLDOP,RTNOLD,IOLDOP)
C
      IF (IHCLTR.GT.1) WRITE (IOGDB,*) 'EXIT ',RTNNAM,' - ISTAT=',ISTAT
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
290   FORMAT ('0**ERROR** IN HCMPSU - MISSING FUNCTION NAME.')
300   FORMAT ('0**ERROR** IN HCMPSU - FUNCTION ',A,' NOT DEFINED.')
310   FORMAT ('0**ERROR** IN HCMPSU - DEFAULT RECORDS ',2I6,' INVALID.')
320   FORMAT ('0**ERROR** IN HCMPSU - MAXIMUM NUMBER OF TECHNIQUES (',
     *   I4,') EXCEEDED.')
330   FORMAT ('0**ERROR** IN HCMPSU - TECHNIQUE/ARGUMENT POINTER ',
     *   'ARRAY IS FULL.')
340   FORMAT (' IN HCMPSU - IAP=',I4,' ND=',I4,' N=',I4)
350   FORMAT (' NARGWD=',I4,' ITBUF14=',I4,' IAX1=',I4)
360   FORMAT ('0**ERROR** IN HCMPSU - ARRAY ISVRAY IS FULL.')
370   FORMAT (' IN HCMPSU - IPTRAY=',I4 /
     *  (5X,5(4A2,1X,I4,1X,I4,1X,I4,1X)))
380   FORMAT (' IN HCMPSU - ITECH= ',50I2 / (19X,50I2))
390   FORMAT (' IN HCMPSU - IGTECH=',50I2 / (19X,50I2))
C
      END
