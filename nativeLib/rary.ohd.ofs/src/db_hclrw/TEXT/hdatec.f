C MODULE HDATEC
C-----------------------------------------------------------------------
C
      SUBROUTINE HDATEC (ISF,IEF,NHRADD,NHSWCH,IPRINT,JULDA,INTHR,JULHR,
     *   ISTAT)
C
C   ROUTINE TO DECODE A DATE FIELD IN COMMON BLOCK UFREEI
C   AND RETURN THE JULIAN DAY AND INTERNAL HOUR AND JULIAN HOUR.
C
C  ARGUMENT LIST:
C
C     ISF    - START OF THE DATE FIELD IN /UFREEI/
C     IEF    - END OF THE DATE FIELD IN /UFREEI/
C     NHRADD - THE NUMBER OF HOURS TO ADD TO THE INTERNAL
C              HOUR AND JULIAN HOUR IF NO HOUR IS DECODED
C     NHSWCH - SWITCH SPECIFYING IF INTERNAL HOUR RANGE
C               0=00 TO 23
C               1=01 TO 24
C     IPRINT - SWITCH SPECIFYING WHETHER OR NOT TO PRINT
C              A WARNING MESSAGE IF FIELD DECODED IS NOT
C              A VALID DATE
C               0=DO NOT PRINT MESSAGE
C               NE 0, PRINT MESSAGE AND CALL WARN
C     JULDA  - JULIAN DAY RETURNED
C     INTHR  - INTERNAL HOUR RETURNED
C     JULHR  - JULIAN HOUR RETURNED
C     ISTAT  - STATUS FLAG
C               0=OK
C               1=INVALID DATE FIELD
C               2=ERROR IN ARGUMENT INPUT
C                 ONE OF ISF, IEF, NHRADD OR NHSWCH NOT IN VALID RANGE
C
      DIMENSION IDARAY(7)
C
      INCLUDE 'uio'
      INCLUDE 'udebug'
      INCLUDE 'ufreei'
      COMMON /MDFLTD/ INHOUR
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/db_hclrw/RCS/hdatec.f,v $
     . $',                                                             '
     .$Id: hdatec.f,v 1.2 2000/03/14 11:49:07 page Exp $
     . $' /
C    ===================================================================
C
C
      IF (IHCLTR.GT.0) WRITE (IOGDB,*) '*** ENTER HDATEC'
C
      ISTAT=0
C
C  CHECK VALUES FOR ISF AND IEF
      IF (ISF.GT.80.OR.ISF.LT.1.OR.IEF.GT.80.OR.IEF.LT.1) GO TO 10
      IF (ISF.LE.IEF) GO TO 30
C
10    WRITE (LP,20) ISF,IEF
20    FORMAT ('0**WARNING** IN HDATEC - VALUES FOR START AND ',
     *   'END OF DATE FIELD ARE ',I3,' AND ',I3,' AND HAVE BEEN ',
     *   'RESET TO 1.')
      CALL WARN
      ISTAT=2
      IS=1
      IE=1
      GO TO 40
C
30    IS=ISF
      IE=IEF
C
C  DECODE DATE FIELD
40    CALL HCKDAT (IS,IE,IDARAY,IER)
      IF (IER.NE.0) THEN
         ISTAT=1
         IF (IPRINT.EQ.1) THEN
            WRITE (LP,50) (IBUF(I),I=IS,IE)
50    FORMAT ('0**WARNING** INVALID DATE FIELD : ',80A1)
            CALL WARN
            GO TO 150
            ENDIF
         ENDIF
C
      INHOUR=0
      IF (IDARAY(5).EQ.-1) INHOUR=1
C
C  EXPAND DATE AND GET DEFAULT HOUR AND TIME ZONE CODE IF NEEDED
      CALL HSETDY (IDARAY)
C
C  COMPUTE JULIAN DAY AND INTERNAL HOUR FROM JULIAN HOUR
      JULHR=IDARAY(1)
      JULDA=JULHR/24
      INTHR=JULHR-JULDA*24
      JULDA=JULDA+1
C
C  INTHR IS NOW IN 00 TO 23 RANGE - CHECK IF 01 TO 24 WANTED
      IF (NHSWCH.EQ.0.OR.NHSWCH.EQ.1) GO TO 90
         WRITE (LP,80) NHSWCH
80    FORMAT ('0**WARNING** IN HDATEC - ',
     *   'VALUE OF ARGUMENT NHSWCH (',I6,') MUST BE 0 OR 1. ',
     *   'ZERO IS ASSUMED.')
         CALL WARN
         ISTAT=2
         JHSWCH=0
         GO TO 100
C
90    JHSWCH=NHSWCH
C
100   IF (JHSWCH.EQ.0) GO TO 110
C
C  01 TO 24 HOUR RANGE WANTED
      IF (INTHR.NE.0) GO TO 110
      JULDA=JULDA-1
      INTHR=24
C
C  CHECK IF NHRADD NEEDS TO BE ADDED TO INTHR AND JULHR
C  ADD NHRADD IF NO HOUR DECODED IN DATE FIELD
C  IF NHRADD EQUAL TO 0 JUST RETURN
110   IF (NHRADD.EQ.0) GO TO 160
C
C  NHRADD MUST BE IN RANGE -24 TO 24
      IF (NHRADD.GE.-24.AND.NHRADD.LE.24) GO TO 130
C
C  NHRADD OUT OF RANGE
      WRITE (LP,120) NHRADD
120   FORMAT ('0**WARNING** IN HDATEC - ',
     *   'NUMBER OF HOURS TO ADD MUST BE IN RANGE -24 TO 24. ',
     *   'VALUE ENTERED IS ',I6,'. ZERO IS ASSUMED.')
      CALL WARN
      ISTAT=2
      GO TO 160
C
C  CHECK IF HOURS WERE DECODED
C     INHOUR=0 IF HOURS DECODED
C     INHOUR=1 IF NO HOURS DECODED
130   IF (INHOUR.EQ.0) GO TO 160
C
C  NO HOURS ENTERED - ADD NHRADD TO JULHR AND INTHR
      JULHR=JULHR+NHRADD
      INTHR=INTHR+NHRADD
      IF (INTHR.GT.0) GO TO 140
C
C  NEGATIVE OR ZERO INTHR
      IF (INTHR.EQ.0.AND.NHSWCH.EQ.0) GO TO 160
      INTHR=INTHR+24
      JULDA=JULDA-1
C
140   IF (INTHR.LT.24) GO TO 160
      IF (INTHR.EQ.24.AND.NHSWCH.EQ.1) GO TO 160
      INTHR=INTHR-24
      JULDA=JULDA+1
      GO TO 160
C
150   JULDA=0
      INTHR=0
      JULHR=0
C
160   IF (IHCLTR.GT.0) WRITE (IOGDB,*) '*** EXIT HDATEC'
      RETURN
C
      END
