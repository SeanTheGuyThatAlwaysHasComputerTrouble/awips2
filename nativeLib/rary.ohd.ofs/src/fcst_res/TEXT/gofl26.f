C MEMBER GOFL26
C  (from old member FCGOFL26)
C
      SUBROUTINE GOFL26(GATOP2,QIMHYD,SIGELV,STOR,ELEV)
C
C THIS UTILITY SUBROUTINE SETS THE GATE OPENING INDICATOR (NGATE) FROM
C OBSERVED OPENINGS WHEN AVAILABLE AND DETERMINES WHETHER THE PROGRAM
C GOES TO THE FLASHBOARD SUBROUTINE(FLSH26).  THE LOGICAL VARIABLE
C (GOFLSH) IS SET TO TRUE IF THE PROGRAM GOES TO FLSH26.  GOFL26 MUST
C BE ENTERED BEFORE OTHER SUBROUTINES AND AFTER OTHER SUBROUTINES
C IN CASE THE ELEVATION COMPUTED AT THE END OF THE TIME INTERVAL IN
C ANOTHER SUBROUTINE EXCEEDS A CRITICAL LEVEL.  DECISION ON OPENING A
C FLOOD GATE (IF ANY) IS BASED ON THE EXPECTED INFLOW AND MAXIMUM
C NON-SPILLWAY OUTFLOW FOR THE NEXT NFUTUR TIME INTERVALS.  NFUTUR IS
C INPUT BY THE USER AND REPRESENTS THE USER'S ESTIMATE OF THE
C OPERATOR'S KNOWLEDGE OF FUTURE INFLOWS.
C
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX
C     JULY, 1982
C
C SUBROUTINE GOFL26 IS IN
C
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IFCST -- SIMULATED RUN(IFCST=0) OR ADJUSTED RUN(IFCST=1).
C     FCST -- LOGICAL VARIABLE.  IF TRUE, THE TIME INTERVAL IS IN THE
C       FORECAST PERIOD (AFTER RUN TIME).
C     GOFLSH -- LOGICAL VARIABLE.  IF TRUE, PROGRAM SHOULD BE IN FLSH26.
C     BGNGAT -- OBSERVED GATE OPENING AT BEGINNING OF TIME
C       INTERVAL.  WILL BE -999.0 IF MISSING.
C     GATOPN -- OBSERVED GATE OPENING AT END OF TIME INTERVAL. WILL BE
C       -999.0 WHEN MISSING. (CHANGE BY JTO 9-83)
C     GATMAX -- MAXIMUM FLOOD GATE OPENING.  WILL BE -999.0 IF THERE IS
C       NO FLOOD GATE OR CONTROLLED BOARDS.
C     NS2 -- ARRAY POSITION OF INFLOWS, OUTFLOWS, ETC. AT END OF THIS
C       TIME INTERVAL.
C     SIGELV -- SIGNIFICANT ELEVATION ARRAY.  PROVISION IS MADE FOR TWO
C       SETS OF BOARDS AND A FLOOD GATE.  TEN SIGNIFICANT ELEVATIONS
C       CAN BE USED.  FLASHBOARDS ARE ARBITRARILY DESIGNATED AS LARGE
C       BOARDS IF THERE IS ONLY ONE SET.  ELEVATIONS CAN BE THE SAME
C       FOR DIFFERENT POSITIONS BUT STORAGES COMPUTED IN THE
C       SUBROUTINE AND PUT IN THE SIGSTO ARRAY ARE CHANGED SLIGHTLY
C       TO KEEP FROM HAVING TWO STORAGE VALUES THAT ARE EQUAL.
C       SIGNIFICANT ELEVATIONS ARE LISTED IN THE POSITIONS OF THE
C       SIGELV ARRAY AS FOLLOWS:
C       ( 1) -- SPILLWAY CREST ELEVATIONS FOR LARGE BOARDS.
C       ( 2) -- HINGE ELEVATION FOR LARGE BOARDS.
C       ( 3) -- TOP ELEVATION FOR LARGE BOARDS.
C       ( 4) -- ELEVATION WHERE LARGE BOARDS GO DOWN.  IF THE TOP
C               ELEVATION IS THE SAME FOR LARGE AND SMALL BOARDS, THE
C               ELEVATION WHERE BOARDS GO DOWN MUST BE SLIGHTLY HIGHER
C               FOR LARGE BOARDS SINCE THE SMALL BOARDS TEND TO GO
C               DOWN FIRST.
C       ( 5) SPILLWAY CREST ELEVATION FOR SMALL BOARDS.  MUST BE -999.0
C            IF ONLY ONE SET OF BOARDS.
C       ( 6) HINGE ELEVATION FOR SMALL BOARDS.  MUST BE -999.0 IF
C            ONLY ONE SET OF BOARDS.
C       ( 7) TOP ELEVATION FOR SMALL BOARDS.  MUST BE -999.0 IF ONLY
C            ONE SET OF BOARDS.
C       ( 8) ELEVATION WHERE SMALL BOARDS GO DOWN.  MUST BE -999.0 IF
C            ONLY ONE SET OF BOARDS.
C       ( 9) SPILLWAY CREST ELEVATION FOR FLOOD GATE.  MUST BE -999.0
C            IF NO FLOOD GATE.
C       (10) ELEVATION WHERE FLOOD GATE IS NORMALLY OPENED.  ELEVATION
C            MUST BE BELOW THE TOP OF ANY FLASHBOARDS.  GATE IS
C            OPERATED INSOFAR AS POSSIBLE TO CAUSE INFLOW TO BE PASSED.
C     NSIGEL -- HIGHEST POSITION NUMBER OF ELEVATIONS USED IN SIGELV
C       ARRAY.
C     NUMLG -- NO. OF LARGE FLASHBOARDS ON DAM.
C     NUMSM -- NO. OF SMALL FLASHBOARDS.  MUST BE 0 IF ONLY ONE SET
C       OF BOARDS.
C     NLGDN -- NO. OF UNCONTROLLED LARGE FLASHBOARDS DOWN.
C     NSMDN -- NO. OF UNCONTROLLED SMALL FLASHBOARDS DOWN.
C     NGATE -- INDICATOR FOR FLOOD GATE OPENING DURING TIME INTERVAL.
C       VALUES OF 0,1, AND 2 INDICATE CLOSED, PARTLY OPEN, OR FULLY OPEN
C       RESPECTIVELY.  MUST BE -999 IF THERE IS NO FLOOD GATE.
C       CONTROLLED BOARDS WILL BE TREATED AS A FLOOD GATE.
C     NFUTUR -- NO. OF FUTURE TIME INTERVALS USED FOR DECISION ON
C       OPENING FLOOD GATE WHEN POOL REACHES ELEVATION WHERE GATE IS
C       NORMALLY OPENED.  IF TOP OF ANY BOARDS WILL BE REACHED IN
C       NFUTUR INTERVALS, GATE WILL BE OPENED TO PASS INFLOW.  USER
C       MUST SPECIFY NFUTUR ACCORDING TO HIS ESTIMATE OF OPERATOR'S
C       ABILITY TO FORECAST FUTURE INFLOWS.  USE -999.0 IF NO GATE.
C     QIMHYD -- HYDROGRAPH ARRAY OF MEAN INFLOWS.
C     S1 -- STORAGE AT BEGINNING OF TIME INTERVAL IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME INTERVAL.
C     ELEV1 -- POOL ELEVATION AT BEGINNING OF TIME INTERVAL.
C     ELEV2 -- POOL ELEVATION AT END OF TIME INTERVAL.
C     QGENMX -- MAXIMUM GENERATION DISCHARGE FOR A POWER DAM.  WILL
C       BE 0 IF NOT A POWER DAM.  WILL BE USED FOR GENERATION DISCHARGE
C       IF NO OBSERVED OR PROPOSED VALUE IS AVAILABLE AND IF A RELATION
C       OF TOTAL DISCHARGE VS MAXIMUM GENERATION DISCHARGE IS NOT USED.
C       A VALUE MUST BE ENTERED IF A TOTAL DISCHARGE VS MAXIMUM
C       GENERATION DISCHARGE RELATION IS USED.
C     SLUICE -- SPECIFIED CONSTANT SLUICE DISCHARGE.  EITHER 0 OR A
C       POSITIVE VALUE MUST BE SPECIFIED.
C     NUM -- NUMBER OF VALUES IN INFLOW OR OUTFLOW TIME SERIES.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION RELATION.  STORAGE
C       MUST BE IN UNITS OF MEAN DISCHARGE FOR THE TIME INTERVAL.
C     ELEV -- POOL ELEVATIONS FOR STOR VS ELEV RELATION.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NTERP -- ARITHMETIC(NTERP=0) OR LOGARITHMIC INTERPOLATION
C       (NTERP=1) FOR STORAGE VS ELEVATION CURVE.
C     IBUG -- NO TRACE OR DEBUG(IBUG=0), TRACE ONLY(IBUG=1, TRACE AND
C       DEBUG(IBUG=2).
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
      INCLUDE 'common/flas26'
C
      DIMENSION QIMHYD(1),SIGELV(1),STOR(1),ELEV(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/gofl26.f,v $
     . $',                                                             '
     .$Id: gofl26.f,v 1.1 1995/09/17 19:05:45 dws Exp $
     . $' /
C    ===================================================================
C
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** GOFL26 ENTERED)
      WRITE(IODBUG,40)  IFCST,FCST,NS2,GOFLSH,NUMLG,NUMSM,NLGDN,NSMDN,
     $NGATE,NFUTUR,BGNGAT,       GATMAX,QGENMX,SLUICE,       S1,
     $ELEV1,ELEV2,NUM,NTERP,IBUG
   40 FORMAT(1H0,99H     IFCST,FCST,NS2,GOFLSH,NUMLG,NUMSM,NLGDN,NSMDN,
     $NGATE,NFUTUR,BGNGAT,       GATMAX,QGENMX,SLUICE/1X,I6,L5,I6,L5,6I6
     $,4F12.3/1H0,33H S1,   ELEV1,ELEV2,NUM,NTERP,IBUG/1X,3F12.3,3I6)
C
C SET GOFLSH TO FALSE AND CHANGE LATER IF REQUIRED.
C
   50 GOFLSH=.FALSE.
C
C SET THE NGATE PARAMETER IF THERE IS AN OBSERVED FLOOD GATE OPENING AT
C BEGINNING OR END OF TIME INTERVAL.
C
      IF(GATMAX.EQ.-999.0) NGATE=-999
      IF(IFCST.EQ.0) GO TO 140
      IF(FCST) GO TO 140
      IF(NGATE.EQ.-999) GO TO 140
      GATOP1=BGNGAT
      IF(GATOP1.EQ.-999.0.AND.GATOP2.EQ.-999.0) GO TO 140
      IF(GATOP1.EQ.-999.0) GO TO 80
      IF(GATOP2.EQ.-999.0) GO TO 90
C
C ANY GATE OPENING THAT BRINGS THE BOTTOM OF THE GATE ABOVE THE WATER
C ELEVATION WILL CAUSE UNCONTROLLED FLOW OVER THE GATED SPILLWAY.
C COMPUTE THE DIFFERENCE IN POOL ELEVATION AND SPILLWAY ELEVATION AT THE
C BEGINNING AND END OF THE TIME INTERVAL IF THE ENDING POOL ELEVATION
C IS NOT MISSING.
C
      IF(ELEV2.EQ.-999.0) GO TO 71
      DIF1=ELEV1-SIGELV(9)
      DIF2=ELEV2-SIGELV(9)
      IF(GATOP1.GT.DIF1.AND.GATOP2.GT.DIF2) GO TO 120
   71 IF(GATOP1.EQ.0..AND.GATOP2.EQ.0.) GO TO 130
      GO TO 110
   80 GATOP=GATOP2
      GO TO 100
   90 GATOP=GATOP1
  100 IF(GATOP.EQ.GATMAX) GO TO 120
      IF(GATOP.EQ.0.)GO TO 130
  110 NGATE=1
      GO TO 180
  120 NGATE=2
      GO TO 180
  130 NGATE=0
  140 IF(NLGDN.GT.0) GO TO 180
      IF(NUMSM.GT.0.AND.NSMDN.GT.0) GO TO 180
      IF(NGATE.GT.0) GO TO 180
      TOP=SIGELV(3)
      IF(NUMSM.GT.0.AND.TOP.GT.SIGELV(7)) TOP=SIGELV(7)
      IF(NGATE.EQ.-999) GO TO 160
      IF(ELEV1.LT.SIGELV(10)) GO TO 160
C
C CHECK IF FLOOD GATE NEEDS TO BE OPENED (IF THERE IS A FLOOD GATE).
C IF THE GATE IS CLOSED, THE DECISION TO OPEN IT DEPENDS ON THE POOL
C ELEVATION EXCEEDING THE LOWEST TOP OF BOARDS SIGELV(3) OR SIGELV(7)
C IN THE NEXT NFUTUR TIME INTERVALS.
C
      CALL NTER26(TOP,STOTOP,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      DIFSTO=STOTOP-S1
      SUM=0.
      DO 150 I=1,NFUTUR
      II=NS2+I-1
      IF(II.GT.NUM) GO TO 150
      SUM=SUM+QIMHYD(II)-QGENMX-SLUICE
      IF(SUM.GT.DIFSTO) GO TO 170
  150 CONTINUE
C
C NO BOARDS ARE DOWN OR GATE OPEN FOR NEXT STATEMENTS.  CHECK IF
C ELEVATIONS AT BEGINNING OR END OF TIME INTERVAL EXCEED LOWEST TOP
C OF BOARDS (TOP).
C
  160 IF(ELEV1.GT.TOP.OR.ELEV2.GT.TOP) GO TO 180
      GO TO 190
  170 NGATE=1
  180 GOFLSH=.TRUE.
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
  190 IF(IBUG-1)260,240,200
  200 WRITE(IODBUG,40)  IFCST,FCST,NS2,GOFLSH,NUMLG,NUMSM,NLGDN,NSMDN,
     $NGATE,NFUTUR,BGNGAT,       GATMAX,QGENMX,SLUICE,       S1,
     $ELEV1,ELEV2,NUM,NTERP,IBUG
      WRITE(IODBUG,210)(SIGELV(I),I=1,NSIGEL)
  210 FORMAT(1H0,27H SIGNIFICANT ELEVATIONS ARE /(1X,10F12.3))
      WRITE(IODBUG,220)IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
  220 FORMAT(1H0,9H IFLAG IS,I6,43H.  NO OF POINTS ON ELEV. - STORAGE CU
     $RVE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION
     $ARE/(1X,5(F12.3,F9.3,3X)))
      IF(NGATE.EQ.-999) GO TO 240
      JEND=NS2+NFUTUR-1
      WRITE(IODBUG,230)(QIMHYD(I),I=NS2,JEND)
  230 FORMAT(1H0,111HFUTURE MEAN TIME INTERVAL INFLOWS INCLUDING
     $PRESENT TIME INTERVAL FOR DECISION ON OPENING FLOOD GATE ARE/(1X,1
     $0F12.3))
  240 WRITE(IODBUG,250)
  250 FORMAT(1H0,10X,17H** LEAVING GOFL26)
  260 RETURN
      END
