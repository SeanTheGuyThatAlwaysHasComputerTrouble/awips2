C MEMBER QMIN26
C  (from old member FCQMIN26)
C ......................................................................
      SUBROUTINE QMIN26(QIHYD,QIMHYD,SQIM,OBSQO,QOHYD,OBSQOM,QOMHYD,
     $STOHYD,ELVHYD,RULEL,ELEVQ,DAMQ,TOTLQ,SOTOTL,PEAKO,PKPOS,STOR,ELEV,
     $WORK)
C ......................................................................
C SUBROUTINE QMIN26 MINIMIZES THE PEAK OUTFLOW FROM A DAM.  COMPUTATIONS
C MUST BE MADE FOR THE ENTIRE OUTFLOW HYDROGRAPH IN THIS SUBROUTINE
C RATHER THAN USING THE PROCEDURE IN OTHER OPERATIONAL RESERVOIR
C SUBROUTINES OF MAKING COMPUTATIONS FOR ONE TIME PERIOD.  SINCE IT IS
C NOT USUALLY FEASIBLE TO IMMEDIATELY INCREASE OUTFLOW TO THE DESIRED
C MINIMUM PEAK OUTFLOW,  DAM OPERATORS USUALLY USE ONE OF TWO OPTIONS
C  FOR INCREASING OUTFLOW.  OPTION ONE (IOPTR=1) PERMITS THE OUTFLOW TO
C BE LINEARLY INCREASED TO THE DESIRED MAXIMUM OUTFLOW OVER A SPECIFIED
C NUMBER OF TIME PERIODS (NPERDR).  IN OPTION TWO (IOPTR=2) INFLOW IS
C PASSED UNTIL THE DESIRED MAXIMUM OUTFLOW IS REACHED.  IN SOME CASES
C FOR OPTION TWO, OUTFLOW MUST BE INCREASED TO A NEW HIGHER MAXIMUM OUT-
C FLOW WHEN THE BEGINNING INFLOW IS ABOVE THE BEGINNING OUTFLOW WHICH IS
C THE PREVIOUS MAXIMUM OUTFLOW.  IN THIS CASE
C A VALUE IS NEEDED FOR NPERDR.  THEREFORE, NPERDR IS NEEDED FOR BOTH
C OPTIONS.  TWO OPTIONS ARE
C ALSO USED FOR EVACUATION OF STORAGE.  IN OPTION TWO (IOPTF=2), WHICH
C IS OFTEN USED BY POWER DAM OPERATORS, INFLOW IS PASSED (AFTER THE
C INFLOW RECEDES TO THE MAXIMUM OUTFLOW) UNTIL INFLOW RECEDES TO A
C DESIRED LIMITING OUTFLOW (QLIMNF) AND QLIMNF IS PASSED UNTIL THE POOL
C DROPS TO THE DESIRED CONSTANT OR RULE CURVE ELEVATION.  QLIMNF
C CAN BE MAXIMUM GENERATION OR SOME VALUE LESS THAN FLOOD DISCHARGE.  IN
C OPTION ONE (IOPTF=1) MAXIMUM OUTFLOW IS CONTINUED UNTIL THE POOL CAN
C BE BROUGHT DOWN TO THE DESIRED LEVEL BY LINEARLY REDUCING MAXIMUM
C OUTFLOW TO INFLOW OVER A SPECIFIED NUMBER OF TIME PERIODS (NPERDF).
C NPERDF IS ALSO NEEDED FOR IOPTF=2 SINCE OUTFLOW UNDER THIS OPTION MAY
C BE GREATER THAN INFLOW WHEN INFLOW STARTS TO RECEDE AND THE OUTFLOW
C WILL HAVE TO BE BROUGHT DOWN TO THE INFLOW HYDROGRAPH.
C     WHEN IOPTM IS 2, THE OPERATOP PASSES FLOOD DISCHARGE (QFLOOD) AS
C THE MAXIMUM OUTFLOW WHEN COMPUTATIONS SHOW AN OUTFLOW BETWEEN QLIMNF
C AND QFLOOD.  THIS PERMITS THE RESERVATION OF STORAGE FOR FUTURE RAIN-
C FALL.  WHEN IOPTM IS 1, THE OPERATOR PASSES THE COMPUTED MAXIMUM OUT-
C FLOW WHEN BETWEEN QLIMNF AND QFLOOD IN ORDER TO SAVE WATER FOR POWER
C GENERATION OR OTHER PURPOSES.
C      THE POOL ELEVATION WILL NOT BE ALLOWED TO DROP BELOW A LOWER
C LIMITING EEVATION OR RISE ABOVE AN UPPER LIMITING ELEVATION UNLESS THE
C INFLOW CANNOT BE PASSED AT THE UPPER ELEVATION.  IF THE DESIRED
C OUTFLOW FOR A TIME INTERVAL CANNOT BE PASSED DUE TO MAXIMUM DISCHARGE
C LIMITATIONS, MAXIMUM DISCHARGE WILL BE PASSED THROUGH A ROUTING
C PROCESS.  FUTURE OUTFLOWS WILL BE ADJUSTED UPWARD TO THE END OF MAX-
C IMUM OUTFLOW BY THE DIFFERENCE IN DESIRED AND POSSIBLE DISCHARGE
C DIVIDED BY THE NUMBER OF TIME INTERVALS.  WHEN INFLOW CANNOT BE PASSED
C AT THE UPPER LIMITING ELEVATION, THE INFLOWS WILL BE ROUTED THROUGH
C THE DAM.
C      THE NEWTON-RAPHSON METHOD OF ITERATIVE SOLUTION IS USED TO
C COMPUTE THE MINIMIZED MAXIMUM OUTFLOW.
C     A SIMULATED RUN WILL BE MADE FIRST WITH NO OBSERVED OUTFLOW OR
C STORAGE VALUES EXCEPT THE BEGINNING STORAGE AT FIRST OF FIRST TIME
C PERIOD.  AN ADJUSTED RUN WILL THEN BE MADE USING OBSERVED AND ADJUSTED
C OUTFLOW AND STORAGE VALUES.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C ......................................................................
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     FEBRUARY, 1982
C ......................................................................
C SUBROUTINE QMIN26 IS IN
C ......................................................................
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IFCST -- SIMULATED RUN (IFCST=0) AND ADJUSTED RUN (IFCST=1).
C     NS2  -- POSITION IN ARRAYS AT THE END OF THIS TIME INTERVAL
C     NRUN -- POSITION IN INFLOW, OUTFLOW, AND STORAGE ARRAYS AT RUN
C       TIME
C     NUM -- NO. OF VALUES IN EACH OF THE INFLOW OUTFLOW AND STORAGE
C       ARRAYS.
C     IOPTR -- OPTION FOR INCREASING OUTFLOW.  LINEAR INCREASE TO
C       MAXIMUM OVER NPERDR TIME PERIODS (IOPTR=1) OR PASS INFLOW UNTIL
C       MAXIMUM OUTFLOW IS REACHED (IOPTR=2).
C     IOPTM -- WHEN IOPTM IS 2, FLOOD DISCHARGE (QFLOOD) WILL BE PASSED
C       WHEN THE COMPUTED MAXIMUM OUTFLOW IS BETWEEN QLIMNF AND QFLOOD.
C       WHEN IOPTM IS 1, THE COMPUTED OUTFLOW WILL BE PASSED WHEN BE-
C       TWEEN QLIMNF AND QFLOOD.
C     IOPTF -- OPTION FOR EVACUATING STORAGE WHEN INFLOW IS FALLING.
C       WHEN IOPTF=2, PASS INFLOW UNTIL INFLOW DECREASES TO A DESIRED
C       LIMITING OUTFLOW VALUE (QLIMNF)  AND THEN PASS QLIMNF UNTIL
C       DESIRED POOL ELEVATION IS REACHED.  WHEN IOPTF=1, PASS MAXIMUM
C       OUTFLOW UNTIL DESIRED POOL ELEVATION CAN BE REACHED BY LINEARLY
C       DECREASING OUTFLOW TO INFLOW OVER NPERDF PERIODS OF TIME.
C     NPERDR -- NO. OF PERIODS FOR LINEARLY INCREASING OUTFLOW TO MAX-
C       IMUM (NEEDED FOR BOTH OPTIONS 1 AND 2).
C     NPERDF -- NO. OF PERIODS FOR LINEARLY DECREASING MAXIMUM OUTFLOW
C       TO INFLOW.  NEEDED FOR BOTH OPTIONS 1 AND 2.
C     LOBSTO -- POSITION IN THE STORAGE ARRAY OF THE LAST OBSERVED
C       STORAGE OR LAST STORAGE COMPUTED FROM ADJUSTED MEAN INFLOW,
C       OBSERVED MEAN OUTFLOW, AND BEGINNING OBSERVED STORAGE.
C       FURNISHED BY THE SUPERVISORY EXECUTION ROUTINE.
C     BGNQI -- CARRYOVER INSTANTANEOUS INFLOW AT BEGINNING OF FIRST TIME
C       PERIOD.
C     BGNQO -- CARRYOVER INSTANTANEOUS OUTFLOW AT BEGINNING OF FIRST
C       TIME PERIOD.
C     BGNSTO -- POOL STORAGE AT BEGINNING OF FIRST TIME PERIOD.
C     QIHYD -- HYDROGRAPH VALUES FOR INSTANTANEOUS INFLOW.
C     QIMHYD -- HYDROGRAPH VALUES FOR MEAN INFLOW.
C     SQIM -- ARRAY OF CUMULATIVE MEAN INFLOW VALUES COMPUTED IN SUMN26.
C     OBSQO -- ARRAY OF OBSERVED INSTANTANEOUS OUTFLOWS.
C     QOHYD -- HYDROGRAPH OF INSTANTANEOUS OUTFLOWS.  OBSERVED VALUES
C       THROUGH RUN TIME WILL BE PUT IN THE QOHYD ARRAY PRIOR TO ENTRY
C       INTO QMIN26 FOR THE ADJUSTED RUN BUT THE OBSQO ARRAY IS STILL
C       USED IN THE SUBROUTINE.
C     OBSQOM -- ARRAY OF OBSERVED MEAN OUTFLOWS.
C     QOMHYD -- HYDROGRAPH OF MEAN OUTFLOWS.  OBSERVED AND COMPUTED
C       VALUES THROUGH RUN TIME WILL BE FURNISHED BY THE SUPERVISORY
C       ROUTINE FOR THE ADJUSTED RUN.
C     STOHYD -- HYDROGRAPH OF STORAGES.  STORAGE MUST  BE IN UNITS OF
C       MEAN DISCHARGE FOR THE TIME PERIOD.  FOR THE ADJUSTED RUN
C       OBSERVED OR COMPUTED VALUES THROUGH RUN TIME ARE PUT IN THE
C       ARRAY BY THE SUPERVISORY ROUTINE.
C     ELVHYD -- HYDROGRAPH OF POOL ELEVATIONS.  OBSERVED VALUES THROUGH
C       RUN TIME ARE PUT IN THE ARRAY BY THE SUPERVISORY ROUTINE.
C     EUPRMN -- LIMITING UPPER POOL ELEVATION.
C     ELWRMN -- LIMITING LOWER POOL ELEVATION.  THE POOL WILL NOT BE
C       ALLOWED TO DROP BELOW ELWRMN.
C     EMXSTO -- ESTIMATED MAXIMUM STORAGE COMPUTED IN SUMN26 BY ASSUMING
C       A LIMITING OUTFLOW FOR NON-FLOOD SITUATIONS SUCH AS MAXIMUM
C       GENERATION FLOW.
C     PCTERM -- ALLOWABLE TOLERANCE IN ITERATED DISCHARGE VALUES AS A
C       FRACTION OF THE LAST VALUE.
C     QLIMNF -- MAXIMUM DESIRABLE OUTFLOW IN NON-FLOOD SITUATIONS.  MAY
C       BE MAXIMUM GENERATION FOR POWER DAMS OR A VALUE LESS THAN FLOOD
C       DISCHARGE.
C     QFLOOD -- DISCHARGE THAT WILL BARELY REACH FLOOD STAGE BELOW THE
C       DAM.
C     ELVNRM -- ELEVATION TO WHICH THE POOL WILL BE DRAWN DOWN AT END
C       OF STORAGE EVACUATUON.  IF -999.0, RULE CURVE ELEVATION WILL
C       BE USED.
C     RULEL -- ARRAY OF RULE CURVE ELEVATIONS WITH POSITIONS IN ARRAY
C       CORRESPONDING TO POSITIONS IN INFLOW  OUTFLOW AND STORAGE ARRAYS
C     ELEVQ -- ELEVATION VALUES FOR ELEVATION VS MAXIMUM OUTFLOW
C       RELATION.
C     DAMQ -- MAXIMUM OUTFLOW VALUES (INCLUDING TURBINE FLOW) FOR ELEVQ
C       VS DAMQ RELATION.
C     NDAMQ -- NO. OF PAIRS OF ELEVQ AND DAMQ VALUES.
C     TOTLQ -- DISCHARGE FOR TOTLQ VS SOTOTL RELATION.
C     SOTOTL -- STORAGE+DISCHARGE/2 VALUES FOR TOTLQ VS SOTOTL RELATION.
C       STORAGES MUST BE TOTAL STORAGES NOT STORAGES ABOVE SPILLWAY
C       CREST.
C     NTOTLQ -- NO. OF PAIRS OF TOTLQ AND SOTOTL VALUES.
C     TESTPK -- TEST VALUE FOR DETERMINING IF PEAK OUTFLOW BETWEEN QO1
C       AND QO2 WILL REPLACE ONE OF THOSE VALUES.
C     PEAKO -- ARRAY OF PEAK VALUES ABOVE TESTPK THAT OCCUR BETWEEN TIME
C       PERIOD POINTS.  SUPERVISORY ROUTINE WILL REPLACE VALUES IN QOHYD
C       ARRAY WITH PEAKO VALUES.
C     PKPOS -- ARRAY OF POSITION NUMBERS FOR PUTTING PEAKO VALUES IN
C       QOHYD ARRAY.
C     NUMPKO -- NO. OF PAIRS OF PEAKO AND PKPOS VALUES.
C     NTOTPK -- NO. OF POSITIONS AVAILABLE IN TIME SERIES ARRAY FOR
C       PEAKO OR PKPOS VALUES.
C     NTERPQ -- ARITHMETIC (NTERPQ=0) OR LOGARITHMIC INTERPOLATION
C       (NTERPQ=1) OF ELEVQ VS DAMQ RELATION.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION CURVE.  MUST BE IN UNITS
C       OF MEAN DISCHARGE FOR THE TIME PERIOD.
C     ELEV -- ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NTERP -- INDICATES ARITHMETIC (NTERP=0) OR LOGARITHMIC
C       INTERPOLATION (NTERP=1) OF STORAGE VS ELEVATION RELATION.
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1),TRACE AND
C       DEBUG (IBUG=2).
C THIS SUBROUTINE WILL COMPUTE HYDROGRAPHS OF INSTANTANEOUS AND MEAN
C OUTFLOWS, POOL STORAGES AND POOL ELEVATIONS.  NO OBSERVED VALUES
C EXCEPT BEGINNING STORAGE WILL BE USED IN THE SIMULATED RUN.  ALL
C OBSERVED VALUES WILL BE USED IN THE ADJUSTED RUN.
C ......................................................................
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/ionum'
      INCLUDE 'common/errdat'
      INCLUDE 'common/resv26'
      INCLUDE 'common/omin26'
C
      DIMENSION QIHYD(1),QIMHYD(1),QOHYD(1),QOMHYD(1),OBSQO(1),
     $OBSQOM(1),STOHYD(1),ELVHYD(1),ELEVQ(1),DAMQ(1),RULEL(1),
     $STOR(1),ELEV(1),SQIM(1),TOTLQ(1),SOTOTL(1),PEAKO(1),PKPOS(1),
     $WORK(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/qmin26.f,v $
     . $',                                                             '
     .$Id: qmin26.f,v 1.3 1998/10/14 13:39:06 page Exp $
     . $' /
C    ===================================================================
C
C
C BGNSTO, BGNQI, AND BGNQO ARE PASSED TO QMIN26 IN S1, QI1, AND
C QO1, RESPECTIVELY.
C
      BGNSTO=S1
      BGNQI=QI1
      BGNQO=QO1
C ......................................................................
C WRITE TRACE AND DEBUG IF REQUIRED.
C ......................................................................
      IF(IBUG-1) 50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** QMIN26 ENTERED)
      WRITE(IODBUG,40) IFCST,NRUN,NUM,IOPTR,IOPTM,IOPTF,NPERDR,NPERDF,
     $LOBSTO,BGNQI,BGNQO,BGNSTO,EUPRMN,ELWRMN,EMXSTO,PCTERM,QLIMNF,
     $QFLOOD,ELVNRM,NTERPQ,NTERP,IBUG
   40 FORMAT(1H0,111H   IFCST,NRUN,NUM,IOPTR,IOPTM,IOPTF,NPERDR,NPERDF,
     $LOBSTO,BGNQI,BGNQO,BGNSTO,EUPRMN,ELWRMN,EMXSTO,PCTERM,QLIMNF/1X,
     $9I6,5F12.3/1X,3F12.3/1H0,32H QFLOOD,ELVNRM,NTERPQ,NTERP,IBUG/1X,
     $2F12.3,3I6)
C ......................................................................
C COMPUTE STORAGE VALUES FOR LIMITIMG UPPER AND LOWER ELEVATIONS AND FOR
C DESIRED DRAWNDOWN ELEVATION AT END OF EVACUATION.  IF ELVNRM IS
C -999.0, RULE CURVE ELEVATION WILL BE USED AND STORAGE WILL BE COM-
C PUTED LATER.  SET DIFVOL = 0 FOR LATER USE.
C ......................................................................
   50 CALL NTER26(EUPRMN,STOUPR,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      CALL NTER26(ELWRMN,STOLWR,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      DIFVOL=0.
      IF(ELVNRM.EQ.-999.0) GO TO 60
      CALL NTER26(ELVNRM,STOFAL,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
   60 IF(IFCST.EQ.0) THEN 
        LOBSTO=0
        GO TO 110
      ENDIF
C ......................................................................
C LOBSTO WILL BE THE ARRAY POSITION OF THE LAST OBSERVED STORAGE OR
C THE LAST STORAGE COMPUTED FROM OBSERVED MEAN OUTFLOW AND WILL BE
C PROVIDED BY THE SUPERVISORY ROUTINE.
C COMPUTE MISSING ELEVATIONS FROM POSITION 1 THROUGH LOBSTO IN ELEVATION
C (ELVHYD) ARRAY.  ALSO, SET INSTANTANEOUS OUTFLOW VALUES IF MISSING IN
C QOHYD ARRAY TO MEAN OUTFLOW.
C ......................................................................
      DO 100 I=1,LOBSTO
      IF(QOHYD(I).EQ.-999.0) QOHYD(I)=QOMHYD(I)
      ELEV2=ELVHYD(I)
      IF(ELEV2.NE.-999.0) GO TO 100
      S2=STOHYD(I)
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      ELVHYD(I)=ELEV2
  100 CONTINUE
C ......................................................................
C COMPUTE MAXIMUM INSTANTANEOUS INFLOW FOR  LATER USE IN OBTAINING A
C FIRST ESTIMATE OF MINIMIZED MAXIMUM OUTFLOW.
C ......................................................................
  110 JBGN=LOBSTO+1
      IF(LOBSTO.GT.0) GO TO 120
      PEAKQI=BGNQI
      GO TO 130
  120 PEAKQI=QIHYD(LOBSTO)
  130 DO 140 I=JBGN,NUM
      IF(QIHYD(I).LE.PEAKQI) GO TO 140
      PEAKQI=QIHYD(I)
  140 CONTINUE
C ......................................................................
C COMPUTE A FIRST ESTIMATE OF MINIMIZED PEAK OUTFLOW BY AVERAGING
C INSTANTANEOUS OUTFLOW IN POSITION LOBSTO AND PEAK INFLOW (PEAKQI).
C SET VALUES AT POSITION LOBSTO OF INFLOW (QIBGN), OUTFLOW (OUT),
C STORAGE (STORBG), AND SUM OF INFLOW (SQIMBG).
C ......................................................................
      IF(LOBSTO.GT.0) GO TO 150
      QIBGN=BGNQI
      STORBG=BGNSTO
      SQIMBG=0.
      OUT=BGNQO
      GO TO 160
  150 OUT=QOHYD(LOBSTO)
      QIBGN=QIHYD(LOBSTO)
      STORBG=STOHYD(LOBSTO)
      SQIMBG=SQIM(LOBSTO)
  160 ESTQMX=(OUT+PEAKQI)*0.5
C ......................................................................
C IF ESTQMX IS GREATER THAN PEAKQI, OUT WILL BE USED FOR ESTQMX.  OUT IS
C OUTFLOW AT POSITION LOBSTO, PEAKQI IS PEAK INFLOW AND ESTQMX IS THE
C FIRST ESTIMATE OF PEAK OUTFLOW.
C ......................................................................
      IF(ESTQMX.LT.PEAKQI) GO TO 170
      ESTQMX=OUT
      NPOS=LOBSTO
C ......................................................................
C SINCE THE BEGINNING OUTFLOW AT LOBSTO IS EQUAL TO OR GREATER THAN THE
C PEAK INFLOW, THE POOL WILL BE BROUGHT DOWN UNDER OPTIONS 1 OR 2.  GO
C TO STATEMENT 750 AND DO LOOP 1490 FOR STARTING OUTFLOW COMPUTATIONS.
C ......................................................................
      GO TO 750
C ......................................................................
C USE THE NEWTON-RAPHSON METHOD OF ITERATIVE SOLUTION FOR THE MINIMIZED
C MAXIMUM OUTFLOW.  TEN ITERATIONS (IF NEEDED) WILL BE MADE IN DO LOOP
C 400.
C ......................................................................
  170 JEND=NUM-LOBSTO
      DO 400 I=1,10
      II=I
C ......................................................................
C IF THE ESTIMATED ESTQMX FOR ANY LOOP IS LESS THAN OUT, IT WILL BE SET
C TO OUT.
C ......................................................................
      IF(ESTQMX.LT.OUT) ESTQMX=OUT
C ......................................................................
C COMPUTE THE POSITION OF ESTQMX ON THE RECESSION SIDE OF THE HYDROGRAPH
C IN DO LOOP 200.  THE POSITION WILL BE BETWEEN NPOS AND NPOS+1 ARRAY
C POSITIONS OR AT NPOS POSITION.
C ......................................................................
      DO 200 J=1,JEND
      K=NUM-J
      IF(K.EQ.0) GO TO 180
      QTEST=QIHYD(K)
      GO TO 190
  180 QTEST=BGNQI
  190 IF(ESTQMX.GT.QTEST) GO TO 200
      NPOS=K
      GO TO 210
  200 CONTINUE
      NPOS=LOBSTO
      ESTQMX=OUT
      GO TO 750
C ......................................................................
C COMPUTE THE STORAGE DIFFERENCE TO THE UPPER POOL ELEVATION AND THE
C SUM OF INFLOWS FROM LOBSTO TO NPOS POSITION.  IF ESTQMX IS EQUAL TO
C OUT, NO FURTHER ITERATION WILL BE MADE.  THE NPOS POSITION IS THE
C POSITION PRECEDING THE INTERSECTION OF PEAK OUTFLOW WITH THE RECESS-
C ION SIDE OF THE INFLOW HYDROGRAPH.
C ......................................................................
  210 IF(ESTQMX.EQ.OUT) GO TO 410
      IF(NPOS.EQ.0) GO TO 220
      SQIMND=SQIM(NPOS)
      QIHYD1=QIHYD(NPOS)
      GO TO 230
  220 SQIMND=SQIMBG
      QIHYD1=BGNQI
  230 DIFSTO=STOUPR-STORBG
      SUMIN=SQIMND-SQIMBG
C ......................................................................
C SUMIN WILL HAVE TO BE INCREASED BY THE INFLOW VOLUME FROM QIHYD1 TO
C INFLOW ESTQMX.  COMPUTE THE FRACTION (FRAC) OF THE TIME PERIOD FROM
C QIHYD1   TO ESTQMX AND THE ADJUSTMENT (FRACIN) TO SUMIN.  SUMIN IS
C THE SUM OF INFLOWS AND QIHYD1 IS THE INFLOW AT THE NPOS ARRAY
C POSITION.
C ......................................................................
      FRAC=1.-(ESTQMX-QIHYD(NPOS+1))/(QIHYD1-QIHYD(NPOS+1))
      FRACIN=(ESTQMX+QIHYD1)*FRAC*0.5
      SUMIN=SUMIN+FRACIN
C ......................................................................
C COMPUTE SUMMATION OF OUTFLOWS (SUMOUT) INCLUDING THE OUTFLOW FROM
C POSITION NPOS
C TO INTERSECTION OF ESTQMX WITH THE INFLOW HYDROGRAPH.  SUMMATION OF
C OUTFLOWS WILL BE DIFFERENT FOR OPTIONS ONE AND TWO (IOPTR=1 AND 2).
C ......................................................................
      IF(IOPTR.EQ.2) GO TO 250
C ......................................................................
C FOLLOWING STATEMENTS ARE FOR OPTION ONE (IOPTR=1).  OPTION ONE PERMITS
C THE OUTFLOW TO BE LINEARLY INCREASED TO THE DESIRED MAXIMUM OUTFLOW
C OVER A SPECIFIED NO. OF TIME PERIODS (NPERDR).
C ......................................................................
  240 JPERDR=NPERDR
      IF((NPOS-LOBSTO).LE.JPERDR) JPERDR=NPOS-LOBSTO
      PERIDS=NPOS-LOBSTO-JPERDR
      PERDR=JPERDR
      SUMOUT=((OUT+ESTQMX)*0.5*PERDR)+(ESTQMX*PERIDS)+ESTQMX*FRAC
      GO TO 340
C ......................................................................
C COMPUTE SUMMATION OF OUTFLOWS FOR IOPTR=2.  IN THIS OPTION, INFLOW IS
C PASSED UNTIL MINIMIZED MAXIMUM OUTFLOW IS REACHED.  FIRST, CHECK IF
C ESTQMX IS LESS THAN INFLOW AT POSITION LOBSTO (QIBGN).  IF SO, OUTFLOW
C VOLUME WILL BE COMPUTED THE SAME AS FOR IOPTR=1.
C ......................................................................
  250 IF(ESTQMX.LT.QIBGN) GO TO 240
C ......................................................................
C POSITION OF ESTQMX ON RISING SIDE OF INFLOW HYDROGRAPH WILL BE COMPUT-
C ED NEXT.
C ......................................................................
      DO 260 J=JBGN,NUM
      IF(ESTQMX.GT.QIHYD(J)) GO TO 260
      NP=J-1
      GO TO 270
  260 CONTINUE
C ......................................................................
C FOR THE FOLLOWING THREE STATEMENTS THE POOL WILL BE DRAWN DOWN
C FROM LOBSTO POSITION UNDER ONE OF THE DRAWDOWN OPTIONS.  THE PROGRAM
C WILL GO TO STATEMENT 750 AND DO LOOP 1490 TO COMPUTE OUTFLOWS,
C STORAGES AND ELEVATIONS.
C ELEVATIONS.
C ......................................................................
      NP=LOBSTO
      NPOS=LOBSTO
      GO TO 750
C
C COMPUTE FRACTION OF TIME PERIOD BETWEEN QIHYD(J-1) AND QIHYD(J) WHERE
C ESTQMX EQUALS INFLOW ON RISING SIDE OF INFLOW HYDROGRAPH.
C ......................................................................
  270 IF(NP.GT.0) GO TO 280
      QIH=BGNQI
      GO TO 290
  280 QIH=QIHYD(NP)
  290 FRACR=(ESTQMX-QIH)/(QIHYD(NP+1)-QIH)
      IF(NP.EQ.0) GO TO 300
      FLOIN=SQIM(NP)-SQIMBG
      FRACRN=(ESTQMX+QIHYD(NP))*0.5*FRACR
      GO TO 310
  300 FLOUT=0.
      FRACRN=(ESTQMX+BGNQI)*0.5*FRACR
  310 PERIDS=NPOS-NP-1
      IF(NP.EQ.0) GO TO 330
C ......................................................................
C SINCE THE POOL MAY BE IN THE PROCESS OF BEING DRAWN DOWN AT POSITION
C LOBSTO, OUTFLOW WILL NOT BE ALLOWED TO DROP BELOW THE LAST OBSERVED
C OUTFLOW UNTIL ESTQMX IS REACHED  EVEN THOUGH THE INFLOW MIGHT
C TEMPORARILY DROP TO A LOWER LEVEL.  IOPTR OF 2 WOULD ORDINARILY SET
C THE OUTFLOW EQUAL TO INFLOW UNTIL THE PEAK OUTFLOW IS REACHED.
C ......................................................................
      ADD=0.
      DO 320 J=JBGN,NP
  320 IF(QIHYD(J).LT.OUT) ADD=ADD+(OUT-QIHYD(J))*0.5
      FLOUT=FLOIN+ADD
  330 SUMOUT=FLOUT+FRACRN+(ESTQMX*(1.-FRACR))+(ESTQMX*PERIDS)+ESTQMX*FRA
     $C
C ......................................................................
C USE THE NEWTON-RAPHSON ITERATIVE EQUATION: X(N+1)=X(N)-F(X(N))/F'(X(N
C )) TO SOLVE FOR ESTQMX.  THE EQUATION IS ITERATED UNTIL X(N+1)-X(N)
C IS LESS THAN A STATED TOLERANCE.  USE FQOM FOR F(X(N)) AND FPQOM FOR
C F'(X(N)).
C ......................................................................
  340 FQOM=SUMIN-SUMOUT-DIFSTO
C ......................................................................
C USE THE RATIO OF BACKWARD DIFFERENCES IN THE FOLLOWING EQUATION TO
C APPROXIMATE FPQOM:  FPQOM=(F(X(N))-F(X(N-1)))/(X(N)-X(N-1)).  X(N) IS
C ESTQMX AND X(N-1) IS THE PREVIOUS ESTQMX (PREQMX).  FOR THE FIRST
C LOOP THROUGH DO LOOP 30, THERE IS NO PREVIOUS ESTQMX AND FPQOM CANNOT
C BE COMPUTED.  SAVE ESTQMX AS PREQMX AND ASSUNE ESTQMX FOR THE SECOND
C LOOP IS 0.8*ESTQMX.  SAVE FQOM AS PRFQOM.
C ......................................................................
      IF(I.GT.1) GO TO 350
      PREQMX=ESTQMX
      ESTQMX=0.8*PREQMX
      PRFQOM=FQOM
      GO TO 370
  350 IF(ESTQMX.EQ.PREQMX) ESTQMX=PREQMX+0.001
      FPQOM=(FQOM-PRFQOM)/(ESTQMX-PREQMX)
      DIFQMX=-FQOM/FPQOM
C ......................................................................
C  CHECK IF DIFQMX IS LESS THAN PCTERM*ESTQMX.  IF SO, REVISE ESTQMX AND
C GO OUT OF DO LOOP.  IF NOT, SET PREQMX EQUAL TO ESTQMX AND COMPUTE NEW
C ESTQMX FOR NEXT ITERATION.  PCTERM IS A USER-SPECIFIED TOLERANCE
C FRACTION.
C ......................................................................
      IF(ABS(DIFQMX).LT.(PCTERM*ESTQMX)) GO TO 360
      PREQMX=ESTQMX
      ESTQMX=ESTQMX+DIFQMX
      PRFQOM=FQOM
      GO TO 370
  360 ESTQMX=ESTQMX+DIFQMX
      GO TO 410
  370 IF(IBUG.NE.2.OR.II.EQ.1) GO TO 400
      IF(IOPTR.EQ.2) WRITE(IODBUG,380) NP,FRACR,FRACRN
  380 FORMAT(1X,16H NP,FRACR,FRACRN/1X,I6,2F12.3)
      WRITE(IODBUG,390) II,NPOS,LOBSTO,IOPTR,PCTERM,
     & DIFSTO,FRAC,FRACIN,SUMIN,SUMOUT,PREQMX,ESTQMX,FQOM,DIFQMX
  390 FORMAT(1H0,'II,NPOS,LOBSTO,IOPTR= ',4I6,5X,'PCTERM= ',F6.3,
     & /5X,'DIFSTO,FRAC,FRACIN,SUMIN,SUMOUT,PREQMX,ESTQMX,',
     & ' FQOM,DIFQMX'/1X,10F11.0)
  400 CONTINUE
  410 IF(IBUG.NE.2) GO TO 420
      WRITE(IODBUG,390) II,NPOS,LOBSTO,IOPTR,PCTERM,
     & DIFSTO,FRAC,FRACIN,SUMIN,SUMOUT,PREQMX,ESTQMX,FQOM,DIFQMX
C
C SAVE ESTQMX AS THIS VALUE WILL BE NEEDED IF ESTQMX IS LATER CHANGED
C DUE TO INCREASING PEAK OUTFLOW TO FLOOD DISCHARGE.
C
  420 SAVQMX=ESTQMX
      IF(ESTQMX.LT.OUT) ESTQMX=OUT
      IF(IOPTM.EQ.1) GO TO 510
      IF(.NOT.(ESTQMX.GT.QLIMNF.AND.ESTQMX.LT.QFLOOD)) GO TO 510
      ESTQMX=QFLOOD
C ......................................................................
C SINCE ESTQMX HAS BEEN INCREASED TO QFLOOD, COMPUTE NEW VALUE OF NPOS.
C NPOS IS THE ARRAY POSITION IMMEDIATELY PRIOR TO THE INTERSECTION OF
C THE PEAK OUTFLOW WITH THE RECESSION SIDE OF THE INFLOW HYDROGRAPH.
C ......................................................................
      DO 450 J=1,JEND
      K=NUM-J
      IF(K.EQ.0) GO TO 430
      QTEST=QIHYD(K)
      GO TO 440
  430 QTEST=BGNQI
  440 IF(ESTQMX.GT.QTEST) GO TO 450
      NPOS=K
      GO TO 460
  450 CONTINUE
      NPOS=LOBSTO
      GO TO 720
  460 IF(NPOS.EQ.LOBSTO) GO TO 720
      IF(IOPTR.EQ.1) GO TO 510
C ......................................................................
C COMPUTE POSITION (NP) IN DO LOOP 470 OF REVISED ESTQMX (QFLOOD) ON
C RISING SIDE OF INFLOW HYDROGRAPH.  IF ESTQMX IS LESS THAN INFLOW
C (QIBGN) AT POSITION LOBSTO, NP CANNOT BE COMPUTED.  COMPUTE NEW FRACR
C AFTER DO LOOP 470.
C ......................................................................
      IF(ESTQMX.LT.QIBGN) GO TO 510
      DO 470 J=JBGN,NUM
      IF(QFLOOD.GT.QIHYD(J)) GO TO 470
      NP=J-1
      GO TO 480
  470 CONTINUE
  480 IF(NP.GT.0) GO TO 490
      QIH=BGNQI
      GO TO 500
  490 QIH=QIHYD(NP)
  500 FRACR=(ESTQMX-QIH)/(QIHYD(NP+1)-QIH)
C ......................................................................
C IN FOLLOWING DO LOOP 620 INSTANTANEOUS AND MEAN OUTFLOWS ARE PUT IN
C INSTANTANEOUS OUTFLOW (QOHYD) AND MEAN OUTFLOW (QOMHYD) ARRAYS,
C RESPECTIVELY, THROUGH POSITION NPOS.
C QOHYD AND QOMHYD ARRAYS, RESPECTIVELY, THROUGH POSITION NPOS.
C ......................................................................
  510 DO 620 I=JBGN,NPOS
      IF(IOPTR.GT.1) GO TO 550
C ......................................................................
C FOR FOLLOWING STATEMENTS, IOPTR=1.  THIS IS OPTION 1 FOR INCREASING
C OUTFLOW TO PEAK OUTFLOW.
C ......................................................................
  520 IF(I.GT.(LOBSTO+NPERDR)) GO TO 540
      A=I-LOBSTO
      B=NPERDR
      QOHYD(I)=OUT+(ESTQMX-OUT)*A/B
      IF(I.GT.1) GO TO 530
      QOMHYD(I)=(OUT+QOHYD(I))*0.5
      GO TO 620
  530 QOMHYD(I)=(QOHYD(I)+QOHYD(I-1))*0.5
      GO TO 620
  540 QOHYD(I)=ESTQMX
      QOMHYD(I)=ESTQMX
      GO TO 620
C ......................................................................
C FOLLOWING STATEMENTS ARE FOR IOPTR=2.  HOWEVER, IF ESTQMX.LT.QIBGN,
C PROCEDURE FOR IOPTR=1 WILL BE USED.  ESTQMX IS PEAK OUTFLOW AND
C QIBGN IS THE INFLOW AT POSITION LOBSTO (POSITION OF LAST OBSERVED
C STORAGE OR STORAGE COMPUTED FROM OBSERVED MEAN INFLOW, OBSERVED
C BEGINNING STORAGE, AND ADJUSTED MEAN INFLOW.
C ......................................................................
  550 IF(ESTQMX.LT.QIBGN) GO TO 520
      IF(I.GT.NP) GO TO 580
      IF(I.EQ.1) GO TO 560
      QO1=QOHYD(I-1)
      GO TO 570
  560 QO1=BGNQO
  570 QOHYD(I)=QIHYD(I)
C ......................................................................
C SINCE THE POOL MAY BE IN THE PROCESS OF BEING DRAWN DOWN AT THE LAST
C OBSERVED OUTFLOW, OUTFLOW WILL NOT BE ALLOWED TO DROP BELOW OUTFLOW
C AT POSITION LOBSTO.
C ......................................................................
      IF(QOHYD(I).LT.OUT) QOHYD(I)=OUT
      QOMHYD(I)=(QO1+QOHYD(I))*0.5
      GO TO 620
  580 QOHYD(I)=ESTQMX
      QOMHYD(I)=ESTQMX
      IF(I.EQ.(NP+1)) GO TO 590
      GO TO 620
  590 IF(I.EQ.1) GO TO 600
      QI1=QIHYD(I-1)
      GO TO 610
  600 QI1=BGNQI
  610 QOMHYD(I)=(QI1 + ESTQMX)*FRACR*0.5+ESTQMX*(1.-FRACR)
  620 CONTINUE
C ......................................................................
C IF MORE THAN ONE PEAK OCCURS BEFORE COMPUTED POSITION NPOS IS REACHED,
C A HIGHER POOL LEVEL MIGHT OCCUR BEFORE THE POINT WHERE MAXIMUM OUTFLOW
C EQUALS INFLOW ON   RECESSION SIDE OF THE INFLOW HYDROGRAPH AFTER THE
C LAST INFLOW PEAK GREATER THAN ESTQMX.  (THIS POINT IS BETWEEN NPOS AND
C NPOS+1.)  FOLLOWING DO LOOP 660 COMPUTES THE HIGHEST POOL STORAGE AND,
C IF THIS STORAGE IS BEFORE NPOS AND IS ABOVE EUPRMN STORAGE, OUTFLOWS
C ARE ADJUSTED TO BRING THE HIGHEST POOL STORAGE DOWN TO EUPRMN STORAGE.
C EUPRMN IS THE LIMITING UPPER STORAGE.
C ......................................................................
      IF(IBUG.NE.2) GO TO 650
      WRITE(IODBUG,630) JBGN,NPOS,ESTQMX,(QOMHYD(I),I=JBGN,NPOS)
  630 FORMAT(1H0,44H    JBGN,NPOS,ESTQMX,(QOMHYD(I),I=JBGN,NPOS)/1X,2I6,
     $F12.3/(1X,10F12.3))
      WRITE(IODBUG,640) (QOHYD(I),I=JBGN,NPOS)
  640 FORMAT(1H0,   23H (QOHYD(I),I=JBGN,NPOS)/(1X,10F12.3))
  650 S1=STORBG
      PKSTOR=S1
      IPOS=LOBSTO
      DO 660 I=JBGN,NPOS
      S2=S1+QIMHYD(I)-QOMHYD(I)
      IF(S2.LE.PKSTOR) GO TO 660
      PKSTOR=S2
      IPOS=I
  660 S1=S2
      IF(IPOS.EQ.LOBSTO.OR.IPOS.EQ.NPOS) GO TO 720
      IF(PKSTOR.LE.STOUPR) GO TO 720
C ......................................................................
C DO LOOP 670 ADJUSTS OUTFLOWS BETWEEN LOBSTO AND IPOS TO KEEP POOL
C FROM EXCEEDING STORAGE AT UPPER LIMITING ELEVATION.  TO SIMPLIFY
C PROGRAMMING, IT IS ASSUMED THAT THE ADJUSTMENT FOR MEAN OUTFLOWS CAN
C BE USED FOR ALL INSTANTANEOUS OUTFLOWS.  IPOS IS THE ARRAY POSITION
C WHERE THE LIMITING UPPER POOL ELEVATION IS EXCEEDED.
C ......................................................................
      PERIDS=IPOS-LOBSTO
      ADD=(PKSTOR-STOUPR)/PERIDS
      DO 670 I=JBGN,IPOS
      QOMHYD(I)=QOMHYD(I)+ADD
  670 QOHYD(I)=QOHYD(I)+ADD
      IF(QOMHYD(IPOS).LE.ESTQMX) GO TO 720
      ESTQMX=QOMHYD(IPOS)
C ......................................................................
C SINCE ESTQMX HAS BEEN INCREASED, A NEW VALUE OF NPOS MUST BE COMPUTED
C IN DO LOOP 700.  ESTQMX IS PEAK OUTFLOW AND NPOS IS THE ARRAY POSITION
C IMMEDIATELY PRIOR TO INTERSECTION OF PEAK OUTFLOW WITH INFLOW
C RECESSION.
C ......................................................................
      DO 700 J=1,JEND
      K=NUM-J
      IF(K.EQ.0) GO TO 680
      QTEST=QIHYD(K)
      GO TO 690
  680 QTEST=BGNQI
  690 IF(ESTQMX.GT.QTEST) GO TO 700
      NPOS=K
      GO TO 720
  700 CONTINUE
C ......................................................................
C IF NPOS IS GREATER THAN IPOS, VALUES MUST BE PUT IN THE OUTLOW ARRAYS
C FROM IPOS TO NPOS.
C ......................................................................
      IF(NPOS.LE.IPOS) GO TO 720
      ISTART=IPOS+1
      DO 710 I=ISTART,NPOS
      QOHYD(I)=ESTQMX
  710 QOMHYD(I)=ESTQMX
C
C SET DIFVOL FOR LATER USE.  IF THE ORIGINAL ESTQMX HAS BEEN CHANGED AND
C NPOS.GT.0,  COMPUTE A NEW FRAC (FRACTION OF TIME INTERVAL FROM NPOS
C TO INTERSECTION OF PEAK OUTFLOW AND INFLOW RECESSION.
C
  720 IF(SAVQMX.EQ.ESTQMX) GO TO 750
      IF(NPOS.EQ.0) GO TO 750
      FRAC=1.-(ESTQMX-QIHYD(NPOS+1))/(QIHYD(NPOS)-QIHYD(NPOS+1))
C
C IF IOPTM IS 2 AND ORIGINAL ESTQMX WAS INCREASED TO QFLOOD, THE EXCESS
C OF OUTFLOW VOLUME (VOLUM) PLUS STORAGE DIFFERENCE (DIFSTO) OVER INFLOW
C VOLUME (VOLIN) MUST BE COMPUTED.  THIS VALUE (DIFVOL) WILL BE USED
C TO KEEP FROM EXCEEDING QFLOOD IF POSSIBLE  WHEN ADJUSTMENTS ARE
C NEEDED DUE TO DAM NOT BEING ABLE TO PASS DESIRED OUTFLOWS.
C
      IF(IOPTM.EQ.1) GO TO 750
      IF(SAVQMX.GE.QFLOOD) GO TO 750
      VOLUM=0.
      DO 730 J=JBGN,NPOS
  730 VOLUM=VOLUM+QOMHYD(J)
      VOLUM=VOLUM+FRAC*ESTQMX
      VOLIN=SQIM(NPOS)-SQIMBG+FRAC*(ESTQMX+QIHYD(NPOS))*0.5
      DIFVOL=VOLUM+DIFSTO-VOLIN
      IF(IBUG.NE.2) GO TO 750
      WRITE(IODBUG,740) IOPTR,IOPTM,IOPTF,SAVQMX,ESTQMX,FRAC,VOLUM,
     $SUMOUT,DIFVOL
  740 FORMAT(1H0,   62H IOPTR,IOPTM,IOPTF,SAVQMX,ESTQMX,FRAC,VOLUM,
     $SUMOUT,DIFVOL/1X,3I6,6F12.3)
C ......................................................................
C IN FOLLOWING DO LOOP 1490 STORAGES AND ELEVATIONS ARE COMPUTED FROM
C JBGN (LOBSTO+1) TO NPOS.  OUTFLOWS, STORAGES AND ELEVATIONS ARE COMP-
C ED FROM NPOS+1 TO NUM.  CHECKS ARE MADE TO SEE IF THE DESIRED OUTFLOWS
C CAN BE PASSED AND TO KEEP THE POOL ELEVATION FROM DROPPING BELOW
C ELWRMN (LOWER LIMITING ELEVATION).  AN
C AN ADJUSTMENT (ADJUST) IS COMPUTED TO ADD TO FUTURE OUTFLOWS WHEN THE
C DAM CANNOT PASS DESIRED OUTFLOWS.
C ......................................................................
  750 ADJUST=0.
      QMX=ESTQMX
      DO 1490 I=JBGN,NUM
      IF(I.GT.1) GO TO 760
      QO1=BGNQO
      QI1=BGNQI
      S1=BGNSTO
      CALL NTER26(BGNSTO,ELEV1,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      GO TO 770
  760 QO1=QOHYD(I-1)
      QI1=QIHYD(I-1)
      S1=STOHYD(I-1)
      ELEV1=ELVHYD(I-1)
  770 QO2=QOHYD(I)
      QI2=QIHYD(I)
      QIM=QIMHYD(I)
      QOM=QOMHYD(I)
      IF(I.LE.NPOS) GO TO 1080
      IF(ELVNRM.EQ.-999.0) GO TO 780
      FALSTO=STOFAL
      GO TO 790
  780 FALELV=RULEL(I)
      CALL NTER26(FALELV,FALSTO,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
  790 IF(IOPTF.EQ.1) GO TO 820
C ......................................................................
C THE TIME PERIOD IS PAST NPOS AND OPTION TWO (IOPTF=2) IS TO BE USED.
C NPOS IS THE ARAY POSITION IMMEDIATELY PRIOR TO INTERSECTION OF PEAK
C OUTFLOW AND INFLOW RECESSION AND IOPTF IS RECESSION OPTION NO. 2.
C  IN THIS OPTION INFLOW IS PASSED UNTIL INFLOW IS LESS THAN QLIMNF
C AND THEN QLIMNF IS PASSED UNTIL THE POOL RECEDES TO ELVNRM (OR RULE
C CURVE IF ELVNRM=-999.0).   FIRST, COMPUTE MEAN OUTFLOW FOR FIRST
C PERIOD PAST NPOS.
C ......................................................................
      IF(I.GT.(NPOS+1)) GO TO 800
      FRACF=(QI1-QO1)/(QI1-QI2)
      QO2=QI2
      QOM=QO1*FRACF+(QO1+QO2)*0.5*(1.-FRACF)
      GO TO 810
  800 QO2=QI2
      IF(QO2.LT.QLIMNF) QO2=QLIMNF
      QOM=(QO1+QO2)*0.5
  810 S2=S1+QIM-QOM
      IF(S2.GE.FALSTO) GO TO 1100
      S2=FALSTO
      QOM=S1+QIM-S2
      IF(QOM.LT.0.) THEN
        QOM=0.
        QO2=0.0
      ENDIF
      GO TO 1100
C ......................................................................
C FOLLOWING STATEMENTS COMPUTE OUTFLOW AFTER NPOS FOR OPTION ONE (IOPTF=
C 1).  IN THIS OPTION THE SAME OUTFLOW IS CONTINUED PAST NPOS UNTIL THE
C POOL CAN BE DRAWN DOWN TO ELVNRM OVER NPERDF TIME PERIODS.  WHEN ARRAY
C POSITION IS (NPOS+1), COMPUTE IN DO LOOP 950 THE ARRAY POSITION WHEN
C OUTFLOW IS BROUGHT BACK TO INFLOW.  ELVNRM IS NORMAL ELEVATION AND
C NPERDF IS THE USER-SPECIFIED NUMBER OF TIME INTERVALS FOR BRINGING
C THE POOL DOWN TO NORMAL ELEVATION.
C ......................................................................
  820 JB=NPOS+1
      IF(I.NE.JB) GO TO 960
      IF(NPOS.GT.0) GO TO 830
      SQIMBG=0.
      GO TO 840
  830 SQIMBG=SQIM(NPOS)
  840 STODIF=S1-FALSTO
      IF(STODIF.GT.0.)GO TO 850
      S2=FALSTO
      GO TO 1120
  850 JE=NUM+NPERDF+1
      SJ1=S1
C
C SET AN INITIAL VALUE FOR KEND TO MAKE SURE THAT AN UNDEFINED VALUE
C DOES NOT CAUSE A LATER IF TEST AT STATEMENT 930 TO GO TO THE WRONG
C PLACE.
C
      KEND=0
      DO 950 J=JB,JE
C
C THE ARRAY POSITION OF KEND MAY BE GREATER THAN NUM SO THAT VALUES AT
C NUM CAN BE COMPUTED.  INFLOW PAST NUM IS ASSUMED TO THE THE SAME AS AT
C NUM.  THE POOL IS BROUGHT BACK TO NORMAL BETWEEN KEND-1 AND KEND ARRAY
C POSITIONS.  NUM IS THE LAST POSITION IN THE ARRAYS.
C
      IF(J.GT.NUM) GO TO 860
      QIJ=QIHYD(J)
      SQIMJ=SQIM(J)
      GO TO 870
  860 QIJ=QIHYD(NUM)
      SQIMJ=SQIMJ+QIMHYD(NUM)
  870 SUMQIM=SQIMJ-SQIMBG
      IF((J-NPOS).GT.NPERDF) GO TO 880
      PERIDS=J-NPOS
      SUMQOM=(QO1+QIJ)*0.5*PERIDS
      GO TO 890
  880 PERIDS=J-NPOS-NPERDF
      PERDF=NPERDF
      SUMQOM=(QO1*PERIDS)+(QO1+QIJ)*0.5*PERDF
  890 SJ2= S1+SUMQIM-SUMQOM
      STORUL=FALSTO
      IF(ELVNRM.NE.-999.0) GO TO 900
      FALELV=RULEL(J)
      CALL NTER26(FALELV,STORUL,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
  900 IF(SJ2.GE.STORUL) GO TO 910
      KEND=J
C
C FRACKN IS THE FRACTION OF THE TIME INTERVAL FROM KEND-1 TO KEND WHEN
C NORMAL CONSTANT OR RULE CURVE STORAGE (STORUL)
C IS REACHED AND IS COMPUTED BY ASSUMING A STRAIGHT LINE RELATION FROM
C STORAGES COMPUTED AT BEGINNING AND END OF THE TIME INTERVAL.
C
      FRACKN=1.-(STORUL-SJ2)/(SJ1-SJ2)
  910 IF(IBUG.NE.2) GO TO 930
      WRITE(IODBUG,920) J,NPOS,QIJ,SQIMJ,SUMQIM,SUMQOM,SJ1,SJ2,STORUL
  920 FORMAT(1H0,   46H J,NPOS,QIJ,SQIMJ,SUMQIM,SUMQOM,SJ1,SJ2,STORUL/1X
     $,2I6,7F12.3)
  930 IF(KEND.NE.J) GO TO 950
      IF(IBUG.NE.2) GO TO 960
      WRITE(IODBUG,940) FRACKN
  940 FORMAT(1H0,10H FRACKN IS,F6.3)
      GO TO 960
  950 SJ1=SJ2
      KEND=JE
  960 IF(KEND-I)970,980,990
  970 S2=S1
      QOM=QIM
      S2=S1+QIM-QOM
      QO2=QIHYD(I)
      GO TO 1140
  980 S2=FALSTO
      QOM=S1+QIM-FALSTO
      QO2=QIHYD(I)
      IF(QOM.GE.0.) GO TO 1140
      QOM=0.
      QO2=0.
      S2=S1+QIM-QOM
      GO TO 1140
  990 IF((KEND-I).GE.(NPERDF+1)) GO TO 1070
      PERIDS=KEND-I
      PERDF=NPERDF
      IF(NPOS.GT.0) GO TO 1000
      QONPOS=BGNQO
      GO TO 1010
 1000 QONPOS=QOHYD(NPOS)
 1010 IF(KEND.LE.NUM) GO TO 1020
      QIEND=QIHYD(NUM)
      GO TO 1050
 1020 IF(KEND.EQ.1) GO TO 1030
      QND1=QIHYD(KEND-1)
      GO TO 1040
 1030 QND1=BGNQI
 1040 QND2=QIHYD(KEND)
      QIEND=QND1+(QND2-QND1)*FRACKN
 1050 QO2=QIEND+(QONPOS-QIEND)*(PERIDS+FRACKN-1.)/PERDF
      IF((KEND-I).EQ.NPERDF) GO TO 1060
      QOM=(QO1+QO2)*0.5
      GO TO 1100
 1060 QOM=QONPOS*FRACKN+(QONPOS+QO2)*0.5*(1.-FRACKN)
      GO TO 1100
 1070 QO2=QO1
      QOM=QO2
      GO TO 1100
C ......................................................................
C FOR THE NEXT TWO STATEMENTS, THE ARRAY POSITION (I) IS LESS THAN OR
C EQUAL TO NPOS (ARRAY POSITION PRIOR TO INTERSECTION OF PEAK OUTFLOW
C AND INFLOW RECESSION).
C ......................................................................
 1080 QO2=QO2+ADJUST
      QOM=QOM+ADJUST
      QOMADJ=QOM
      IF(IOPTM.EQ.1) GO TO 1100
      IF(DIFVOL.LE.0.) GO TO 1100
      IF(QOM.LE.QFLOOD) GO TO 1100
C
C DIFVOL IS THE EXCESS OUTFLOW WHEN IOPTM IS 2 AND ESTQMX IS INCREASED
C TO QFLOOD.  DECREASE QOM TO QFLOOD IF IT IS GREATER UNTIL DIFVOL IS 0.
C QFLOOD IS FLOOD DISCHARGE AND QOM IS MEAN OUTFLOW FOR TIME INTERVAL.
C ESTQMX IS PEAK OUTFLOW.
C
      DIF=QOM-QFLOOD
      IF(DIF.LT.DIFVOL) GO TO 1090
      DIFVOL=0.
      QO2=QO2-DIFVOL
      QOM=QOM-DIFVOL
      GO TO 1100
 1090 DIFVOL=DIFVOL-DIF
      QOM=QFLOOD
      QO2=QFLOOD
 1100 S2=S1+QIM-QOM
      IF(S2.GE.STOLWR) GO TO 1110
      S2=STOLWR
      GO TO 1120
 1110 IF(S2.LE.STOUPR) GO TO 1140
      S2=STOUPR
 1120 QOM=S1+QIM-S2
      IF(QOM.GE.0.) GO TO 1130
      QOM=0.0
      QO2=0.0
      S2=S1+QIM-QOM
      GO TO 1140 
 1130 QO2=QI2
C
C COMPUTE ELEVATION AND MAXIMUM  POSSIBLE DISCHARGE (QMAX2) AT END OF
C TIME INTERVAL.
C
 1140 CALL NTER26 (S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      CALL NTER26(ELEV2,QMAX2,ELEVQ,DAMQ,NDAMQ,IFLAG,NTERPQ,IBUG)
      IF(QMAX2.GE.QO2) GO TO 1350
C ......................................................................
C DO LOOP 1150 COMPUTES POSITION (LPOS) OF INSTANTANEOUS OUTFLOW (QO2)
C IN THE MAXIMUM DAM DISCHARGE (DAMQ) ARRAY.  IF(CHANGE IN
C STORAGE)/(CHANGE IN DISCHARGE) FOR THIS DISCHARGE INTERVAL IS LESS
C THAN 0.5, MORE THAN ONE ROUTING STEP IS NEEDED.
C ......................................................................
      DO 1150 L=2,NDAMQ
      IF(QO2.GT.DAMQ(L)) GO TO 1150
      LPOS=L
      GO TO 1160
 1150 CONTINUE
C ......................................................................
C IF QO2 IS GREATER THAN DAMQ(NDAMQ), COMPUTE NSTEPS (NO. OF ROUTING
C STEPS IN THE TIME INTERVAL) FOR DISCHARGE INTERVAL FROM
C DAMQ(NDAMQ-1) TO DAMQ(NDAMQ).
C ......................................................................
      LPOS=NDAMQ
C ......................................................................
C FOR FOLLOWING STATEMENTS, QO2 IS BETWEEN DAMQ(LPOS-1) AND DAMQ(LPOS).
C  COOPUTE STORAGES   FOR ELEVQ(LPOS-1) AND ELEVQ(LPOS) AND THEN COMPUTE
C (CHANGE IN STORAGE)/(CHANGE IN DISCHARGE) FOR THE DISCHARGE INTERVAL.
 1160 ELEVQ1=ELEVQ(LPOS-1)
      ELEVQ2=ELEVQ(LPOS)
      CALL NTER26(ELEVQ1,STOR1,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      CALL NTER26(ELEVQ2,STOR2,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      TESTK=(STOR2-STOR1)/(DAMQ(LPOS)-DAMQ(LPOS-1))
      NSTEPS=1
      IF(TESTK.LT.0.5) NSTEPS=0.5/TESTK+1.
      STEPS=NSTEPS
C
C ASSUMING A STRAIGHT LINE FROM BEGINNING (QO1) TO ENDING (QO2)
C TIME INTERVAL INSTANTANEOUS DISCHARGES, ROUTING WILL START APPROX.
C WHERE THE OUTFLOW LINE CROSSES THE QMAX LINE.  COMPUTE QMAX1, FRACTION
C OF TIME INTERVAL (FRAC1) BEFORE ROUTING BEGINS AND FRACTION OF TIME
C INTERVAL (FRAC2) DURING WHICH ROUTING OCCURS.
C
      CALL NTER26(ELEV1,QMAX1,ELEVQ,DAMQ,NDAMQ,IFLAG,NTERPQ,IBUG)
      A=(QMAX1-QO1)/(QO2-QMAX2)
      FRAC1=A/(1.+A)
      IF(FRAC1.LT.0.01) FRAC1=0.
      FRAC2=1.-FRAC1
C
C IF FRAC2 IS LESS THAN 0.1,  ASSUME FRAC1 IS 1.0 AND THERE IS NO ROUT-
C ING DURING THE TIME INTERVAL.
C
      IF(FRAC2.GT.0.1) GO TO 1170
      QO2=QMAX2
      QOM=(QO1+QO2)*0.5
      S2=S1+QIM-QOM
      GO TO 1330
C
C DETERMINE NO. OF ROUTING STEPS FOR FRAC2 WHEN NSTEPS IS GREATER THAN 1
C
 1170 IF(NSTEPS.EQ.1) GO TO 1180
      NSTEPS=FRAC2*STEPS+1.
      STEPS=NSTEPS
C
C CHANGE STORAGE IN SOTOTL ARRAY (STORAGE+DISCHARGE/2 VALUES) TO UNITS
C OF MEAN DISCHARGE FOR A ROUTING STEP.
C COMPUTE NEW STORAGE+DISCHARGE/2. VALUES AND PUT IN THE WORK ARRAY.
C ARRAY.
C
 1180 DO 1190 J=1,NTOTLQ
      STOR2=(SOTOTL(J)-TOTLQ(J)*0.5)*STEPS/FRAC2
 1190 WORK(J)=STOR2+TOTLQ(J)*0.5
      IF(IBUG.NE.2) GO TO 1210
      WRITE(IODBUG,1200) NSTEPS,FRAC2,(TOTLQ(J),WORK(J),J=1,NTOTLQ)
 1200 FORMAT(1H0,I4, 9H STEPS IN,F6.3,49H FRACTION OF THE TIME PERIOD WE
     $RE USED IN ROUTING/60H ALTERNATING VALUES OF DISCHARGE AND STORAGE
     $+DISCHARGE/2 ARE/(1X,10F12.3))
C
C COMPUTE INFLOW(QQI1) AND OUTFLOW (QQO1) AT BEGINNING OF FIRST ROUTING
C STEP.  COMPUTE INCREMENTAL INFLOW FOR ROUTING STEP.
C
 1210 QQI1=QI1+(QI2-QI1)*FRAC1
      DELQI=(QI2-QQI1)/STEPS
C
C COMPUTE BEGINNING DISCHARGE (QQO1) AND BEGINNING STORAGE (SS1) IN
C UNITS OF MEAN DISCHARGE FOR THE ROUTING STEP.
C
      QQO1=QO1+FRAC1*(QO2-QO1)
      QMAXB=QMAX1+(QMAX2-QMAX1)*FRAC1
      AVGQO=(QMAX1+QMAXB)*0.5*FRAC1*STEPS/FRAC2
      AVGQI=(QI1+QQI1)*0.5*FRAC1*STEPS/FRAC2
      SS1=S1*STEPS/FRAC2+AVGQI-AVGQO
C
C STORAGE IN SOTOTL MUST BE TOTAL STORAGE NOT STORAGE ABOVE SPILLWAY
C SINCE TOTAL DISCHARGE (INCLUDING NON-SPILLWAY) IS USED IN THE TOTLQ
C VS SOTOTL RELATION.
C
      SI=SS1-QQO1*0.5
C
C IF NSTEPS.GT.1, SET AN INITIAL VALUE FOR PTEST WHICH WILL BE USED FOR
C COMPUTING THE HIGHEST OUTFLOW VALUE DURING THE TIME INTERVAL.  ALSO,
C SET INITIAL VALUE FOR FRACTION OF TIME FROM BEGINNING OF TIME INTERVAL
C TO HIGHEST OUTFLOW.
C
      IF(NSTEPS.EQ.1) GO TO 1220
      PTEST=QQO1
      FRACPK=FRAC1
C
C FOLLOWING DO LOOP 1280 COMPUTES ROUTED VALUES FOR EACH ROUTING STEP.
C
 1220 DO 1280 J=1,NSTEPS
      QQI2=QQI1+DELQI
      AVGQI=(QQI1+QQI2)*0.5
      SO=AVGQI+SI
      IF(SO.GT.0.) GO TO 1230
      QQO2=0.
      GO TO 1240
 1230 CALL TERP26(SO,QQO2,WORK,TOTLQ,NTOTLQ,IFLAG,IBUG)
C
C COMPUTE AVERAGE OUTFLOW (AVGQO) FOR ROUTING STEP AND STORAGE (SS2)
C AT END OF STEP.
C
 1240 AVGQO=(QQO1+QQO2)*0.5
      SS2=SS1+AVGQI-AVGQO
C
C CHECK FOR HIGHEST OUTFLOW IF NSTEPS.GT.1.
C
      IF(NSTEPS.EQ.1) GO TO 1250
      IF(PTEST.GE.QQO2) GO TO 1250
      PTEST=QQO2
      FRACPK=FRACPK+FRAC2/STEPS
 1250 IF(IBUG.NE.2) GO TO 1270
C
C WRITE DEBUG IF REQUIRED.
C
      WRITE(IODBUG,1260) QQI1,QQI2,QQO1,QQO2,SS1,SS2
 1260 FORMAT(1H0,   28H QQI1,QQI2,QQO1,QQO2,SS1,SS2/1X,6F12.3)
 1270 IF(J.EQ.NSTEPS) GO TO 1280
C
C SET VALUES FOR NEXT LOOP.
C
      QQI1=QQI2
      QQO1=QQO2
      SS1=SS2
 1280 SI=SO-QQO1
C
C SET STORAGE (S2) AT END OF TIME INTERVAL TO STORAGE (SS2) AT END OF
C LAST ROUTING STEP.  S2 IS CONVERTED TO UNITS OF MEAN OUTFLOW FOR THE
C TIME INTERVAL.  SET OUTFLOW AT END OF TIME INTERVAL (QO2) TO OUTFLOW
C AT END OF LAST ROUTING STEP (QQO2).  COMPUTE REVISED ELEVATION (ELEV2)
C AT END OF TIME INTERVAL.
C
      S2=SS2*FRAC2/STEPS
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      QO2=QQO2
      QOM=S1+QIM-S2
C
C IF NSTEPS.GT.1, CHECK PTEST AGAINST BEGINNING (QO1) AND ENDING (QO2)
C TIME INTERVAL DISCHARGE AND, IF HIGHER THAN
C BOTH, PUT IN PEAKO ARRAY AND NEAREST QOHYD (INSTANTANEOUS OUTFLOW)
C ARRAY POSITION INTO PKPOS
C ARRAY.   SUPERVISORY ROUTINE WILL PUT PEAKO VALUES INTO NEAREST
C POSITION IN QOHYD ARRAY.  EVEN OBSERVED VALUES WILL BE REPLACED
C AS PTEST IS A BETTER VALUE FOR ROUTING DOWNSTREAM. REDUCE PTEST BY .01
C *PTEST FOR CHECK TO KEEP FROM MAKING MINOR CHANGES IN QOHYD VALUES.
C
      IF(NSTEPS.GT.1) GO TO 1290
      NUMPKO=0
      GO TO 1330
 1290 IF(PTEST.LE.TESTPK) GO TO 1330
      TEST=PTEST-0.01*PTEST
      IF(TEST.LE.QO1.OR.TEST.LE.QO2) GO TO 1330
      IF(NUMPKO.EQ.-999) NUMPKO=0
      NUMPKO=NUMPKO+1
      IF(NUMPKO.GT.NTOTPK) GO TO 1310
      PEAKO(NUMPKO)=PTEST
      IF(I.EQ.1) GO TO 1300
      IF(FRACPK.GE.0.5) GO TO 1300
      PKPOS(NUMPKO)=I-1 + 0.01
      GO TO 1330
 1300 PKPOS(NUMPKO)=I + 0.01
      GO TO 1330
 1310 WRITE(IPR,1320) NTOTPK,NUMPKO
 1320 FORMAT(1H0,10X,31H**WARN**THE NUMBER OF POSITIONS,I6,86H IN THE PE
     $AKO ARRAY IN QMIN26 IS NOT SUFFICIENT  TO HOLD THE REQUIRED NO. OF
     $ POSITIONS,I6)
      CALL WARN
C
C IF IOPTM IS 2 AND IF QOM IS GREATER THAN QFLOOD, DECREASE QOM TO
C QFLOOD IF DIFVOL IS GREATER THAN THE DIFFERENCE.  QOM IS TIME INTERVAL
C MEAN DISCHARGE, QFLOOD IS FLOOD DISCHARGE, AND DIFVOL IS EXCESS
C AVAILABLE STORAGE.
C
 1330 IF (IOPTM.EQ.1) GO TO 1350
      IF(DIFVOL.LE.0.) GO TO 1350
      IF(QOM.LE.QFLOOD) GO TO 1350
      DIF=QOM-QFLOOD
      IF(DIF.LT.DIFVOL) GO TO 1340
      DIFVOL=0.
      QO2=QO2-DIFVOL
      QOM=QOM-DIFVOL
      GO TO 1350
 1340 DIFVOL=DIFVOL-DIF
      QOM=QFLOOD
      QO2=QFLOOD
 1350 IF(I.EQ.NPOS) GO TO 1360
      IF(I.GT.NPOS) GO TO 1420
      IF(QOMADJ.EQ.QOM) GO TO 1420
      PERIDS=NPOS-I
      PERIDS=PERIDS+FRAC
C
C IF THE DAM CANNOT PASS THE DESIRED OUTFLOW, THE DIFFERENCE BETWEEN
C DESIRED AND ACTUAL DIVIDED BY THE NUMBER OF TIME INTERVALS IS ADDED TO
C FUTURE PERIODS TO THE NPOS ARRAY POSITION AND A FRACTION OF THE PERIOD
C BEYOND NPOS.
C
      ADJ=ADJUST
      ADJUST=ADJUST+(QOMADJ-QOM)/PERIDS
      IF(IOPTM.EQ.1) GO TO 1360
      IF(DIFVOL.EQ.0.) GO TO 1360
      IF(QO2.NE.QFLOOD) GO TO 1360
      ADJUST=ADJUST-DIFVOL/PERIDS
      IF(ADJUST.LT.0.) ADJUST=0.
      QOMADJ=QOM+ADJUST
 1360 QMX=ESTQMX+ADJUST
      IF(IOPTM.EQ.1) GO TO 1370
      IF(QMX.GT.QFLOOD.AND.DIFVOL.GT.0.) QMX=QFLOOD
 1370 IF(QO2.GT.QMX) QO2=QMX
      IF(QO2.LT.QOM) QO2=QOM
      IF(QMX.LE.QIHYD(NPOS).AND.QMX.GT.QIHYD(NPOS+1)) GO TO 1410
C ......................................................................
C COMPUTE POSITION OF REVISED MAX. OUTFLOW BETWEEN NPOS AND NPOS+1) ON
C FALLING SIDE OF INFLOW HYDROGRAPH.
C ......................................................................
      DO 1400 J=1,NUM
      K=NUM-J
      IF(K.EQ.0) GO TO 1380
      QTEST=QIHYD(K)
      GO TO 1390
 1380 QTEST=BGNQI
 1390 IF(QMX.GT.QTEST) GO TO 1400
      NPOS=K
      GO TO 1410
 1400 CONTINUE
      NPOS=LOBSTO
 1410 IF(NPOS.EQ.0.OR.I.EQ.NPOS) GO TO 1420
      IF(ADJUST.EQ.0.) GO TO 1420
      QIHYD1=QIHYD(NPOS)
      FRAC=1.-(QMX-QIHYD(NPOS+1))/(QIHYD1-QIHYD(NPOS+1))
      IF(FRAC.LT.0.) FRAC=0.
C
C REVISE VALUE OF ADJUST AND QMX USING NEW FRAC VALUE (FRACTION OF TIME
C INTERVAL FROM NPOS POSITION TO NPOS+1).
C
      IF(I.EQ.NPOS) GO TO 1420
      PERIDS=NPOS-I
      PERIDS=PERIDS+FRAC
      ADJUST=ADJ+(QOMADJ-QOM)/PERIDS
      QMX=ESTQMX+ADJUST
C
C SINCE OBSERVED INSTANTANEOUS OR MEAN OUTFLOWS MIGHT OCCUR AFTER LOBSTO
C BUT PRIOR TO RUN TIME, THESE OBSERVED VALUES WILL BE PUT IN QOHYD AND
C QOMHYD ARRAYS IF THIS IS AN ADJUSTED RUN.
C
 1420 IF(IFCST.EQ.0) GO TO 1422
      IF(I.GT.NRUN) GO TO 1422
      QO22=OBSQO(I)
      QOMM=OBSQOM(I)
      IF(QO22.EQ.-999.0.AND.QOMM.EQ.-999.0) GO TO 1422
      IF(QOMM.NE.-999.0) GO TO 1421
      QOMM=(QO1+QO22)*0.5
 1421 ADJUST=ADJUST+(QOM-QOMM)/PERIDS
      QOM=QOMM
      IF(QO22.NE.-999.0) QO2=QO22
      S2=S1+QIM-QOM
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
 1422 QOHYD(I)=QO2
      ELVHYD(I)=ELEV2
      STOHYD(I)=S2
      QOMHYD(I)=QOM
      IF(IBUG.NE.2) GO TO 1490
      IF(I.LE.NPOS) GO TO 1440
      WRITE(IODBUG,1430) I,QO1,QO2,QOM,QI1,QI2,QIM,S1,S2,ELEV1,ELEV2
 1430 FORMAT(1H0,   44H I,QO1,QO2,QOM,QI1,QI2,QIM,S1,S2,ELEV1,ELEV2/1X,
     $I6,9F12.3/1X,F12.3)
      GO TO 1490
 1440 IF(IOPTM.EQ.2) WRITE(IODBUG,1450) DIFVOL
 1450 FORMAT(1H0, 8H DIFVOL=,F12.3)
      IF(QMAX2.GE.QO2) GO TO 1470
      WRITE(IODBUG,1460) I,NPOS,NSTEPS,QO1,QQO1,QO2,QQO2,QOM,QI1,QQI1,
     $ QI2,QQI2,QIM,S1,S2,ELEV1,ELEV2,QMAX1,QMAX2,FRAC1,FRAC2,ADJUST,
     $ ESTQMX,QMX,QOMADJ
 1460 FORMAT(1H0, 104H  I,NPOS,NSTEPS,QO1,QQO1,QO2,QQO2,QOM,QI1,QQI1,
     $QI2,QQI2,QIM,S1,S2,ELEV1,ELEV2,QMAX1,QMAX2,FRAC1,FRAC2/25H ADJUST,
     $ESTQMX,QMX,QOMADJ/1X,3I6,7F12.3/1X,10F12.3/1X,10F12.3)
      GO TO 1490
 1470 WRITE(IODBUG,1480)I,NPOS,QO1,QO2,QOM,QI1,QI2,QIM,S1,S2,ELEV1,ELEV2
     $,QMAX2,ADJUST,ESTQMX,QMX,QOMADJ
 1480 FORMAT(1H0,  80H I,NPOS,QO1,QO2,QOM,QI1,QI2,QIM,S1,S2,ELEV1,ELEV2,
     $QMAX2,ADJUST,ESTQMX,QMX,QOMADJ/1X,2I6,9F12.3/1X,6F12.3)
 1490 CONTINUE
      IF(IBUG-1) 1660,1640,1500
 1500 WRITE(IODBUG,1510)
 1510 FORMAT(1H0,51H FOLLOWING ARE VARIABLE VALUES AT THE END OF QMIN26)
      WRITE(IODBUG,40) IFCST,NRUN,NUM,IOPTR,IOPTM,IOPTF,NPERDR,NPERDF,
     $LOBSTO,BGNQI,BGNQO,BGNSTO,EUPRMN,ELWRMN,EMXSTO,PCTERM,QLIMNF,
     $QFLOOD,ELVNRM,NTERPQ,NTERP,IBUG
      WRITE(IODBUG,1520) (OBSQO(I),I=1,NRUN)
 1520 FORMAT(1H0,36H OBSERVED INSTANTANEOUS OUTFLOWS ARE/(1X,10F12.3))
      WRITE(IODBUG,1530) (OBSQOM(I),I=1,NRUN)
 1530 FORMAT(1H0,27H OBSERVED MEAN OUTFLOWS ARE/(1X,10F12.3))
      WRITE(IODBUG,1550) (QIHYD(I),I=1,NUM)
 1550 FORMAT(1H0,41H VALUES IN INSTANTANEOUS INFLOW ARRAY ARE/(1X,10F12.
     $3))
      WRITE(IODBUG,1560) (QIMHYD(I),I=1,NUM)
 1560 FORMAT(1H0,32H VALUES IN MEAN INFLOW ARRAY ARE/(1X,10F12.3))
      WRITE(IODBUG,1570) (QOHYD(I),I=1,NUM)
 1570 FORMAT(1H0,42H VALUES IN INSTANTANEOUS OUTFLOW ARRAY ARE/(1X,10F12
     $.3))
      WRITE(IODBUG,1580) (QOMHYD(I),I=1,NUM)
 1580 FORMAT(1H0,33H VALUES IN MEAN OUTFLOW ARRAY ARE/(1X,10F12.3))
      WRITE(IODBUG,1590) (ELVHYD(I),STOHYD(I),I=1,NUM)
 1590 FORMAT(1H0,54H ALTERNATING ARRAY VALUES OF ELEVATION AND STORAGE A
     $RE/(1X,10F12.3))
       WRITE(IODBUG,1600) NDAMQ,(ELEVQ(I),DAMQ(I),I=1,NDAMQ)
 1600 FORMAT(1H0,57H NO. OF POINTS ON ELEVATION VS MAXIMUM DISCHARGE CUR
     $VE IS,I4,1H./1H0,58H ALTERNATING VALUES OF ELEVATION AND MAXIMUM D
     $ISCHARGE ARE/(1X,10F12.3))
      WRITE(IODBUG,1610)IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
 1610 FORMAT(1H0,9H IFLAG IS,I6,43H.  NO OF POINTS ON ELEV. - STORAGE CU
     $RVE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION
     $ARE/(1X,5(F12.3,F9.3,3X)))
      IF(NUMPKO.EQ.0) GO TO 1640
      WRITE(IODBUG,1620) NTOTPK,(PEAKO(I),I=1,NUMPKO)
 1620 FORMAT(1H0,52H TOTAL NO. OF POSITIONS AVAILABLE FOR PEAK VALUES IS
     $,I6,18H.  PEAK VALUES ARE/(1X,10F12.3))
      WRITE(IODBUG,1630) (PKPOS(I),I=1,NUMPKO)
 1630 FORMAT(1H0,55H POSITIONS IN OUTFLOW ARRAY FOR PLACING PEAK VALUES
     $ARE/(1X,10F12.3))
 1640 WRITE(IODBUG,1650)
 1650 FORMAT(1H0,10X,17H** LEAVING QMIN26)
 1660 RETURN
      END
