C MEMBER RULE26
C  (from old member FCRULE26)
C **********************************************************************
      SUBROUTINE RULE26(RULEL,STOR,ELEV,DATE,RULELV)
C **********************************************************************
C SUBROUTINE RULE26 USES A RULE CURVE TO PRESCRIBE FUTURE POOL ELEVA-
C TIONS FOR RESERVOIRS THAT DO NOT GENERATE HYDROELECTRIC POWER.  THE
C RULE CURVE IS ALSO USED AS ONE FACTOR IN DETERMINING OUTFLOW FROM
C HYDROELECTRIC RESERVOIRS BUT WILL BE INCORPORATED IN THE POWER GENER-
C ATION SUBROUTINE FOR THOSE RESERVOIRS.  A SIMULATED RUN IS MADE FIRST
C FOR ALL TIME PERIODS STARTING WITH THE BEGINNING OBSERVED OR ADJUSTED
C STORAGE.  AFTER THE SIMULATED RUN, MISSING STORAGES PRIOR TO THE LAST
C OBSERVED STORAGE WILL BE COMPUTED WITH THE ADJUST-Q SUBROUTINE.  A
C SECOND RUN WILL THEN BE MADE BY USING THE ADJUSTED STORAGES AND
C OBSERVED OUTFLOWS.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C **********************************************************************
C  THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX--CONSULTING HYDROLOGIST
C     NOVEMBER, 1981
C **********************************************************************
C SUBROUTINE RULE26 IS IN
C **********************************************************************
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     AGBDEV--ADJUSTMENT TO RULE CURVE FOR SIMULATED RUN COMPUTED IN
C       ARUL26.
C     AVGDEV--ADJUSTMENT TO RULE CURVE FOR ADJUSTED RUN COMPUTED IN
C       ARUL26.
C     ELEV--ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     ELEV2--POOL ELEVATION AT END OF TIME PERIOD.
C     FCST -- LOGICAL VARIABLE.  IF TRUE, TIME PERIOD IS IN FORECAST
C             PERIOD.
C     IBUG--NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1), TRACE AND
C           DEBUG (IBUG=2).
C     IFCST--SIMULATED (NON-FORECAST) RUN WHEN IFCST=0
C             ADJUSTED (FORECAST) RUN WHEN IFCST=1.
C     LOBSTO--POSITION OF LAST OBSERVED STORAGE IN STORAGE ARRAY OR LAST
C       COMPUTED STORAGE FROM OBSERVED MEAN OUTFLOW, ADJUSTED MEAN
C       INFLOW AND OBSERVED BEGINNING STORAGE.  WILL BE PROVIDED BY
C       SUPERVISORY EXECUTION ROUTINE.
C     NS2--POSITION IN ARRAYS AT END OF THIS TIME PERIOD.
C     NTERP--INDICATES ARITHMETIC (NTERP=0) OR LOGARITHMIC INTERPOLATION
C            (NTERP=1).
C     NTIMES--NO. OF TIME STEPS BEYOND LOBSTO FOR BRINGING STORAGE BACK
C       TO RULE CURVE STORAGE.  THIS IS NECESSARY TO PREVENT EXTREME
C       OUTFLOWS WHEN THE LAST OBSERVED STORAGE IS CONSIDERABLY
C       DIFFERENT FROM THE RULE CURVE STORAGE.
C     NSE--NO. OF PAIRS OF STOR AND ELEV VALUES.
C     QIM--MEAN INFLOW FOR TIME PERIOD.
C     QLIMRL -- ALLOWABLE MAXIMUM OUTFLOW WHEN BRINGING POOL BACK TO
C       RULE CURVE.  COULD BE MAXIMUM GENERATION FOR POWER DAMS BUT
C       CANNOT EXCEED FLOOD DISCHARGE.
C     QO2--OUTFLOW AT END OF TIME PERIOD.
C     QOM--MEAN OUTFLOW FOR TIME PERIOD.
C     RULEL -- ARRAY OF RULE CURVE ELEVATIONS COMPUTED IN ERUL26.  FIRST
C       POSITION MUST BE AT THE END OF THE FIRST TIME PERIOD WHICH IS
C       (NS2-1) PERIODS PRIOR TO THIS TIME PERIOD.
C     S1--POOL STORAGE AT BEGINNING OF TIME PERIOD IN UNITS OF MEAN
C         DISCHARGE FOR THE TIME PERIOD.
C     S2--POOL STORAGE AT END OF TIME PERIOD IN UNITS OF MEAN DISCHARGE
C         FOR THE TIME PERIOD.
C     STOR--STORAGES FOR STORAGE VS ELEVATIOM CURVE IN UNITS OF MEAN
C           DISCHARGE FOR THE TIME PERIOD.
C **********************************************************************
C  QO2, QOM, S2, AND ELEV2 WILL BE COMPUTED IN THIS SUBROUTINE EXCEPT
C IN THE ADJUSTED RUN WHEN THE VALUES ARE OBSERVED.  ALL OTHER VARIABLES
C IN THE ARGUMENT LIST WILL BE FURNISHED BY THE SUPERVISORY EXECUTION
C ROUTINE.
C **********************************************************************
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fctime'
      INCLUDE 'common/resv26'
      INCLUDE 'common/rulc26'
C
      DIMENSION RULEL(*),STOR(*),ELEV(*),DATE(*),RULELV(*)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/rule26.f,v $
     . $',                                                             '
     .$Id: rule26.f,v 1.4 2004/09/09 17:15:28 hsu Exp $
     . $' /
C    ===================================================================
C
C **********************************************************************
C WRITE TRACE AND DEBUG IF REQUIRED.
C **********************************************************************
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** RULE26 ENTERED)
      WRITE(IODBUG,40)IFCST,FCST,NS2,LOBSTO,NTIMES,AGBDEV,AVGDEV,QLIMRL,
     $QIM,QO2,QOM,S1,S2,ELEV2,NTERP,IBUG
   40 FORMAT(1H0,107H  IFCST,FCST,NS2,LOBSTO,NTIMES,AGBDEV,AVGDEV,QLIMRL
     $,QIM,       QO2,        QOM,S1,      S2,ELEV2,NTERP,IBUG/1X,I6,L5,
     $3I6,7F12.3/2F12.3,2I6)
   50 IF(IFCST.EQ.1)GO TO 60
C **********************************************************************
C SET LOBST  TO 0 FOR THE SIMULATED RUN SINCE NO OBSERVED STORAGES ARE
C USED DURING THE RUN.  IF NO ADJUSTMENT IS DESIRED FOR THE RULE CURVE
C ELEVATIONS, AGBDEV AND AVGDEV WILL BE FURNISHED AS 0 BY THE SUPERVIS-
C ORY EXECUTION ROUTINE.
C **********************************************************************
      LOBST=0
      ADJ=AGBDEV
      GO TO 70
   60 ADJ=AVGDEV
      LOBST=LOBSTO
C **********************************************************************
C AGBDEV AND AVGDEV ARE ADJUSTMENTS TO THE RULE CURVE FOR SIMULATED AND
C ADJUSTED RUNS, RESPECTIVELY.
C **********************************************************************
      IF(FCST) GO TO 70
      OBSQO2=QO2
C
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      IF(IGO.EQ.1) GO TO 100
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; ADJUSTED BETWEEN OBSERVED
C VALUES USING SIMULATED AND OBSERVED VALUES; OR MISSING.
C WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED IN STATEMENT 91
C FROM THE OBSERVED MEAN INFLOW, THE ADJUSTED MEAN INFLOW, AND THE
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
      IF(QOM.EQ.-999.0) GO TO 70
      GO TO 91
C **********************************************************************
C COMPUTE RULE CURVE STORAGE (RULST2) AT END OF THIS TIME PERIOD AND FOR
C PERIOD NTIMES PAST LOBSTO (RULSTF) FROM ADJUSTED RULE CURVE ELEVATION
C IF AN ADJUSTMENT WAS MADE.
C **********************************************************************
   70 RULELA=RULEL(NS2)+ADJ
      NF=LOBST+NTIMES
C$     IF (NF .GT. NUM) NF = NUM
C$     RULELF=RULEL(NF)+ADJ
      CALL NTER26(RULELA,RULST2,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      IF(NS2.GT.LOBST.AND.NS2.LE.NF) GO TO 80
      RULELF=RULELA
      RULSTF=RULST2
      S2=RULST2
      GO TO 90
C **********************************************************************
C IF POSSIBLE, THE STORAGE WILL BE BROUGHT BACK TO THE RULE CURVE
C STORAGE (RULSTF)
C NTIMES PERIODS AFTER THE LAST OBSERVED STORAGE.  CHECKS WILL BE NEEDED
C IN THE SUPERVISORY EXECUTION ROUTINE FOR MINIMUM OUTFLOW AND ALLOWABLE
C MAXIMUM OUTFLOW.
C **********************************************************************
   80 TIMES=LOBST +NTIMES-NS2+1
      IF(NF.LE.NUM) THEN
        RULELF=RULEL(NF)
        GO TO 500
      ENDIF
      MINODX=24/NTIM24
      JHRBGN=KHRBGN+(JULDAT-1)*24
      JHR=JHRBGN+NF*MINODX
      IADD=NF/NTIM24
      IDSET = IDARUN + IADD
      NDAYR = 365 + LEAP26(IDSET,IHR)
      JHRYR=NDAYR*24
      IF(JHR.GT.JHRYR) JHR=MOD(JHR,JHRYR)
      DO 320 I=2,NRUL
      J = I
      JHR2=KHRRUL+(DATE(J)-1)*24
      IF(JHR.LT.JHR2) GO TO 350
  320 CONTINUE
  350 A=RULELV(J)-RULELV(J-1)
      JHR1=KHRRUL+(DATE(J-1)-1)*24
      RULELF=RULELV(J-1)+(JHR-JHR1)*A/(JHR2-JHR1)
  500 CONTINUE
C$     IF(IBUG.LT.2) GO TO 600
C$     WRITE(IODBUG,510)NUM,JULDAT,NDAYR,KHRBGN,KHRRUL,LOBSTO,NTIMES,IBUG
C$  510 FORMAT(1H0,' NUM,JULDAT,NDAYR,KHRBGN,KHRRUL,LOBSTO,NTIMES,IBUG',
C$    & /1X,8I6)
C$     WRITE(IODBUG,520) (DATE(I),RULELV(I),I=1,NRUL)
C$  520 FORMAT(1H0,82H ALTERNATING VALUES OF DATES AND RULE CURVE ELEVATIO
C$    $NS FOR RULE CURVE RELATION ARE/(1X,6(F4.0,F8.2,4X)))
C$     WRITE(IODBUG,530) (RULEL(I),I=1,NUM)
C$  530 FORMAT(1H0,' PERIOD RULECURVE ELEVATIONS FOR ALL PERIODS ARE',
C$    & /(1X,8F8.2))
C$  600 CONTINUE
      RULELX=RULELF+ADJ
      CALL NTER26(RULELX,RULSTF,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      S2=S1-((S1-RULSTF)/TIMES)
   90 QOM=S1-S2+QIM
      IF(QOM.LT.0.)QOM=0.
      IF(QOM.GT.QLIMRL) QOM=QLIMRL
      S2=S1+QIM-QOM
      IF(IFCST.EQ.0.OR.FCST)QO2=QOM
      GO TO 120
   91 S2=S1+QIM-QOM
C **********************************************************************
C SET QO2 EQUAL TO QOM UNLESS QO2 IS AN OBSERVED VALUE.  IF QO2 IS COM-
C PUTED FROM QO1 AND QOM, UNREALISTIC VALUES OF QO2 CAN OCCUR.
C THE TIME PERIOD IS BEFORE RUN TIME FOR STATEMENT 100.  QO1 AND QO2 ARE
C INSTANTANEOUS DISCHARGES AT BEGINNING AND END OF TIME INTERVAL AND QOM
C IS THE MEAN DISCHARGE FOR THE TIME INTERVAL.
C **********************************************************************
  100 QO2=QOM
      IF(OBSQO2.NE.-999.0) QO2=OBSQO2
  110 IF(ELEV2.NE.-999.0) GO TO 130
C **********************************************************************
C COMPUTE ELEV2 WITH ARITHMETIC INTERPOLATION (NTERP=0) OR LOGARITHMIC
C INTERPOLATION (NTERP=1).
C **********************************************************************
  120 CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
  130 IF (IBUG-1)190,170,140
  140 RULEL1=BGNRCE
      IF(NS2.GE.2) RULEL1=RULEL(NS2-1)
      RULEL2=RULEL(NS2)
      WRITE(IODBUG,150) 
     & ELEV2,RULEL1,RULEL2,RULELA,RULELF,QO2,QOM,S2,RULST2,RULSTF,LOBST
  150 FORMAT(1H0,' ELEV2,RULEL1,RULEL2,RULELA,RULELF,',
     & 'QO2,QOM,S2,RULST2,RULSTF,LOBST'/1X,5F7.2,5F10.0,I5)
C      WRITE(IODBUG,160)IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
C  160 FORMAT(1H0,10H IFLAG IS ,I6,41H.  NO OF POINTS ON ELEV.-STORAGE CU
C     $RVE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION
C     $ARE/(1X,5(F12.3,F9.3,3X)))
  170 WRITE(IODBUG,180)
  180 FORMAT(1H0,10X,17H** LEAVING RULE26)
  190 RETURN
      END
