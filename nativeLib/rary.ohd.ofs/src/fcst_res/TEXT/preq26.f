C MEMBER PREQ26
C  (from old member FCPREQ26)
C***********************************************************************
      SUBROUTINE PREQ26(QOC,STOR,ELEV)
C***********************************************************************
C SUBROUTINE PREQ26 USES PRESCRIBED INSTANTANEOUS OR MEAN OUTFLOWS FOR
C THE TIME PERIOD AND THE INFLOW TO COMPUTE STORAGE AND ELEVATION.  THE
C SUPERVISORY EXECUTION ROUTINE WILL BYPASS PREQ26 AFTER THE LAST TIME
C PERIOD WHEN PRESCRIBED DISCHARGES ARE TO BE USED.  IF PRESCRIBED DIS-
C CHARGES ARE MISSING, OPTIONS ARE TO PASS INFLOW OR REPEAT THE LAST
C IREPE VALUES OF DISCHARGE.  IF PRESCRIBED DISCHARGES ARE 24-HR. MEAN
C VALUES, MEANS FOR THE TIME PERIODS WILL HAVE TO BE COMPUTED PRIOR TO
C ENTRY INTO PREQ26.  SINCE THE PRESCRIBED DISCHARGE SUBROUTINE USES
C OBSERVED OR PRESCRIBED DISCHARGES, ONLY THE ADJUSTED RUN WILL BE MADE.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, INTERPOLATED BETWEEN OBSERVED VALUES,
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C***********************************************************************
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     OCTOBER, 1981
C***********************************************************************
C SUBROUTINE PREQ26 IS IN
C***********************************************************************
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     FCST -- LOGICAL VARIABLE.  IF TRUE, TIME PERIOD IS IN THE FORECAST
C       PERIOD.
C     NS2 -- POSITION IN ARRAYS AT END OF TIME PERIOD.
C     IOPT -- OPTION WHEN PRESCRIBED DISCHARGES ARE MISSING.  PASS
C       INFLOW (IOPT=0) OR REPEAT LAST IOPT VALUES OF DISCHARGE (IOPT=
C       NO. OF DISCHARGE VALUES TO REPEAT).
C     IQO -- INDICATES INSTANTANEOUS PRESCRIBED DISCHARGES (IQO=1) OR
C       MEAN VALUES (IQO=0).
C     QOC -- CARRYOVER DISCHARGE ARRAY NEEDED FOR REPEATING IOPT
C       DISCHARGE VALUES.   WILL BE INSTANTANEOUS VALUES IF IQO=1 OR
C       MEAN VALUES IF IQO=0.
C     QI2 -- INFLOW AT END OF TIME PERIOD.
C     QIM -- MEAN INFLOW FOR TIME PERIOD.
C     QO1 -- OUTFLOW AT BEGINNING OF TIME PERIOD.
C     QO2 -- OUTFLOW AT END OF TIME PERIOD.  OBSERVED VALUES WILL BE
C       USED IF AVAILABLE.  IF IQO IS 1 AND OBSERVED IS NOT AVAILABLE,
C       QO2 WILL BE PRESCRIBED DISCHARGE IF AVAILBLE AND -999.0 IF NOT
C       AVAILABLE.
C     QOM -- MEAN OUTFLOW FOR TIME PERIOD.  OBSERVED VALUES WILL BE USED
C       USED IF AVAILABLE.  IF IQO IS 0 AND OBSERVED IS NOT AVAILABLE,
C       QOM WILL BE PRESCRIBED DISCHARGE IF AVAILBLE AND -999.0 IF NOT
C       AVAILABLE.
C     S1 -- STORAGE AT BEGINNING OF TIME PERIOD IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME PERIOD.
C     S2 -- STORAGE AT END OF TIME PERIOD.  OBSERVED VALUES AND ADJUSTED
C       VALUES WILL BE USED ONLY FOR ADJUSTED RUN.
C     ELEV2 -- ELEVATION AT END OF TIME PERIOD.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION CURVE IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME PERIOD.
C     ELEV -- ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NTERP -- INDICATES ARITHMETIC (NTERP=0) OR LOGARITHMIC
C       INTERPOLATION (NTERP=1).
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1),TRACE AND
C       DEBUG (IBUG=2).
C***********************************************************************
C QO2, QOM, S2, AND ELEV2 WILL BE COMPUTED IN PREQ26 EXCEPT WHEN THESE
C VALUES ARE OBSERVED.
C***********************************************************************
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
      INCLUDE 'common/pres26'
C  THE FOLLOWING CHANGES MADE ON 3/5/90
      INCLUDE 'common/exg26'
C  END OF CHANGE OF 3/5/90
C
      DIMENSION QOC(1),STOR(1),ELEV(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/preq26.f,v $
     . $',                                                             '
     .$Id: preq26.f,v 1.1 1995/09/17 19:05:59 dws Exp $
     . $' /
C    ===================================================================
C
C***********************************************************************
C WRITE TRACE AND DEBUG IF REQUIRED.
C***********************************************************************
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** PREQ26 ENTERED)
      WRITE(IODBUG,40)IFCST,FCST,NS2,IOPT,IQO,QI2,QIM,QO1,QO2,QOM,S1,S2,
     $ELEV2,NTERP,IBUG
   40 FORMAT(1H0,67H IFCST,FCST,NS2,IOPT,IQO,QI2,QIM,QO1,QO2,QOM,S1,S2,E
     $LEV2,NTERP,IBUG/1X,I5,L5,3I5,8F12.3/1X,2I5)
C
C  TO BE CONSISTENT WITH STRATEGY FOR ALL OTHER SCHEME EXECUTIONS, ONLY
C  CHECK FOR OBSERVED (USING OBSV26) IF NOT SIMULATED AND WITHIN THE
C  OBSERVED PERIOD.
C   MOD MADE BY - JOE OSTROWSKI AUGUST 1983
C
   50 IF(FCST.OR.IFCST.EQ.0) GO TO 60
C  THE FOLLOWING CHANGES MADE ON 3/5/90
      IF(.NOT.WRAJQI .AND. .NOT.WRAJQM) GO TO 51
C  END OF CHANGE OF 3/5/90
      OBSQO2=QO2
C
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      IF(IGO.EQ.1) GO TO 140
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; OR INTERPOLATED BETWEEN
C OBSERVED VALUES.  WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED IN STATEMENT 130
C  THE FOLLOWING CHNAGES MADE ON 3/5/90
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
      IF(QOM.EQ.-999.0) GO TO 60
      GO TO 130
   51 IF(IQO.EQ.0) GO TO 55
      IF(QO2.NE.-999.0) GO TO 54
      IF(IOPT.GT.0) GO TO 53
   52 QO2=QI
      IF(QO2.LE.0.) QO2=0.0
      QOM=QIM
      IF(QOM.LE.0.0) QOM=0.0
      GO TO 150
   53 QO2=QOC(1)
   54 QOM=(QO1+QO2)*0.5
      GO TO 150
   55 IF(QOM.NE.-999.0) GO TO 58
      IF(IOPT.LE.0) GO TO 52
      QOM=QOC(1)
   58 QO2=QOM
      GO TO 150
C  END OF CHANGE OF 3/5/90
C
C CHECK IF PRESCRIBED DISCHARGES ARE INSTANTANEOUS (IQO=1) OR MEAN
C VALUES (IQO=0). SET QO TO ENDING OUTFLOW(QO2) OR MEAN OUTFLOW (QOM)
C AND QI TO ENDING INFLOW (QI2) OR MEAN INFLOW (QIM) TO SIMPLIFY
C PROGRAMMING.
C***********************************************************************
   60 IF(IQO.EQ.0)GO TO 70
      QO=QO2
      QI=QI2
      GO TO 80
   70 QI=QIM
      QO=QOM
C***********************************************************************
C CHECK FOR OBSERVED OR PRESCRIBED DISCHARGE.
C***********************************************************************
   80 IF(QO.NE.-999.0)GO TO 110
      IF(IOPT.GT.0) GO TO 90
      QO=QI
      IF(QO.LT.0.) QO=0.
      GO TO 110
C***********************************************************************
C GET BACK OUTFLOW VALUE FROM CARRYOVER ARRAY (QOC) TO REPEAT LAST
C IOPT VALUES.
C  (THE REPEAT VALUE IS ALWAYS THE FIRST VALUE IN THE CARRYOVER ARRAY
C  AS THE CARRYOVER ARRAY IS UPDATED FOR EACH TIME PERIOD.  JTO - 8/83)
C***********************************************************************
   90 CONTINUE
      QO=QOC(1)
  110 IF(IQO.EQ.0)GO TO 120
      QO2=QO
      QOM=(QO1+QO2)*0.5
      GO TO 130
  120 QOM=QO
  130 S2=S1+QIM-QOM
      IF(S2.LT.0.)S2=0.
      QO2=QO
      IF(IFCST.EQ.0.OR.FCST) GO TO 160
C  THE FOLLOWING CHANGES MADE ON 3/5/90
      IF(.NOT.WRAJQI .AND. .NOT.WRAJQM) GO TO 160
C  END OF CHANGE OF 3/5/90
C***********************************************************************
C SET ENDING OUTFLOW (QO2) EQUAL TO MEAN OUTFLOW (QOM) IF QO2 IS NOT
C OBSERVED.
C***********************************************************************
  140 QO2=QOM
      IF(OBSQO2.NE.-999.0) QO2=OBSQO2
  150 IF(ELEV2.NE.-999.0) GO TO 170
C***********************************************************************
C COMPUTE ELEV2 WITH ARITHMETIC (NTERP=0) OR LOGARITHMIC INTERPOLATION
C (NTERP=1).
C***********************************************************************
  160 CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
  170 IF(IBUG-1)260,240,180
  180 WRITE(IODBUG,190)
  190 FORMAT(1H0,40H VALUES AT END OF PREQ26 ARE AS FOLLOWS:)
      WRITE(IODBUG,40)IFCST,FCST,NS2,IOPT,IQO,QI2,QIM,QO1,QO2,QOM,S1,S2,
     $ELEV2,NTERP,IBUG
      IF(IOPT.EQ.0)GO TO 240
      WRITE(IODBUG,200)IOPT, (QOC(I),I=1,IOPT)
  200 FORMAT(1H0,42H THE NUMBER OF OUTFLOW VALUES TO REPEAT IS,I4,33H.
     $FOLLOWING ARE CARRYOVER VALUES/(1X,10F12.3))
C 220 WRITE(IODBUG,230)IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
  230 FORMAT(1H0,9H IFLAG IS,I6,43H.  NO OF POINTS ON ELEV. - STORAGE CU
     $RVE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION
     $ARE/(1X,5(F12.3,F9.3,3X)))
  240 WRITE(IODBUG,250)
  250 FORMAT(1H0,10X,17H** LEAVING PREQ26)
  260 RETURN
      END
