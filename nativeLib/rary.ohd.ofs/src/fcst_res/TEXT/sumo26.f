C MEMBER SUMO26
C  (from old member FCSUMO26)
C
      SUBROUTINE SUMO26(JSTART,JEND,JENDWK,JDARUL,SUMOUT,STOR1,FLOUT,
     $QMNDAY,QMNWEK,RULSTO,ADJSTO,SQIM,QIMHYD,BGNSUM,IBUG)
C
C     THIS SUBROUTINE IS USED IN QGEN26 TO COMPUTE INFLOW AND REQUIRED
C OUTFLOW VOLUME FOR A DAILY OR WEEKLY CYCLE OF RETURNING POOL ELEVATION
C TO RULE CURVE (OR ADJUSTED RULE CURVE) ELEVATION.  INFLOW AND OUTFLOW
C VOLUMES ARE ESTIMATED TO THE END OF THE DAILY OR WEEKLY CYCLE IF
C PAST THE MAXIMUM ARRAY POSITION OF THE  ARRAYS.  THE MEAN OUTFLOW
C MUST BE MORE THAN OR EQUAL TO THAT REQUIRED TO KEEP FROM EXCEEDING
C THE UPPER LIMITING STORAGE AND MUST NOT BE LESS THAN REQUIRED
C MIMIMUM OUTFLOW FOR THE DAY OR WEEK.  LIMITS OF MAXIMUM TURBINE DIS-
C CHARGE AND UPPER OR LOWER STORAGES WILL BE IMPOSED LATER IN QGEN26.
C
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     MAY, 1982
C     REVISION NO. 1 BY W. E. FOX -- AUGUST, 1989
C
C SUBROUTINE SUMO26 IS IN
C
C VARIABLES IN THE ARGUMENT LIST ARE DEFINED AS FOLLOWS:
C     ADJSTO -- ADJUSTMENT TO STORAGE COMPUTED FROM ELEVATION DEVIATION
C       COMPUTED IN ARUL26.  WILL BE 0. IF ARUL26 IS NOT USED.
C     BGNSUM -- CUMULATIVE MEAN INFLOW AT FIRST OT FIRST TIME INTERVAL
C       IN PERIOD.
C     FLOUT -- REQUIRED OUTFLOW TO KEEP FROM EXCEEDING UPPER LIMITING
C       STORAGE.
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1), TRACE AND
C       DEBUG (IBUG=2).
C     JSTART -- ARRAY POSITION AT END OF FIRST TIME INTERVAL IN PERIOD.
C     JEND -- - ARRAY POSITION AT END OF LAST TIME INTERVAL IN PERIOD.
C     JENDWK -- COMPUTED ARRAY POSITION AT END OF DAILY OR WEEKLY CYCLE.
C       JENDWK  MAY  BE PAST THE LAST AVAILABLE POSITION (NUM) BUT IS
C       NEEDED FOR COMPUTATIONAL PURPOSES.
C     JDARUL -- DAY OF WEEK WHEN POOL IS BROUGHT BACK TO RULE CURVE.
C       WILL BE -999 IF BROUGHT BACK EACH DAY.
C     QIMHYD -- HYDROGRAPH ARRAY OF MEAN TIME INTERVAL INFLOWS.
C     QMNDAY -- REQUIRED MINIMUM MEAN DAILY  OUTFLOW.  WILL BE -999.0
C       IF NOT USED.
C     QMNWEK -- REQUIRED MINIMUM MEAN WEEKLY OUTFLOW.  WILL BE -999.0
C       IF NOT USED.
C     RULSTO -- ARRAY OF RULE CURVE STORAGES CORRESPONDING TO POSITIONS
C       IN INFLOW, OUTFLOW AND STORAGE ARRAYS.
C     SQIM -- ARRAY OF CUMULATIVE MEAN INFLOW COMPUTED IN SUMN26.
C     STOR1 -- POOL STORAGE AT BEGINNING OF FIRST TIME INTERVAL IN
C       JSTART TO JEND PERIOD.
C     SUMOUT -- SUMMATION OF OUTFLOW REQUIRED TO BRING POOL BACK TO THE
C       RULE CURVE AT THE INDICATED TIME OF DAY (AND DAY OF WEEK IF A
C       WEEKLY CYCLE).  IF END OF CYCLE IS PAST NUM,  ESTIMATED INFLOW
C       AND OUTFLOW WILL BE USED PAST NUM.  NUM WILL BE THE SAME AS JEND
C       WHEN JEND IS LESS THAN JENDWK.
C
      DIMENSION RULSTO(*),SQIM(*),QIMHYD(*)
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/sumo26.f,v $
     . $',                                                             '
     .$Id: sumo26.f,v 1.2 1996/07/12 13:58:53 dws Exp $
     . $' /
C    ===================================================================
C
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** SUMO26 ENTERED)
      WRITE(IODBUG,40) JSTART,JEND,SUMOUT,STOR1,FLOUT,
     $QMNDAY,QMNWEK,ADJSTO,BGNSUM
   40 FORMAT(1H0,' JSTART,JEND,SUMOUT,STOR1,FLOUT,'
     $'QMNDAY,QMNWEK,ADJSTO,BGNSUM'/1X,2I4,7F10.0)
C
C COMPUTE SUM OF INFLOW AND NUMBER OF TIME INTERVALS (DIF).
C
   50 SUMIN=SQIM(JEND)-BGNSUM
      DIF=JEND-JSTART+1
      IF(JENDWK.LE.JEND) GO TO 60
C
C THE COMPUTED END (JEND24) OF THE DAILY OR WEEKLT CYCLE IS PAST THE
C LAST ARRAY POSITION (NUM).  ASSUME INFLOW PAST NUM IS SAME AS MEAN
C INFLOW FOR NUM POSITION AND ASSUME RULE CURVE AT JENDWK IS SAME AS
C RULE CURVE AT NUM.  JEND IS LESS THAN JENDWK ONLY WHEN JEND=NUM.
C
      PRDS=JENDWK-JEND
      SUMIN=SUMIN+QIMHYD(JEND)*PRDS
      DIF=DIF+PRDS
   60 SUMOUT=STOR1+SUMIN-RULSTO(JEND)-ADJSTO
      QMEAN=SUMOUT/DIF
      IF(QMEAN.LT.FLOUT) QMEAN=FLOUT
      IF(JDARUL.EQ.-999) GO TO 70
      IF(QMNWEK.EQ.-999.0) GO TO 70
      IF(QMEAN.LT.QMNWEK)QMEAN=QMNWEK
CR1 *** REVISION NO. 1 *** BEGIN ***
CR1 FOR WEEKLY CYCLE QMEAN AND SUMOUT SHOULD BE CHANGED ONLY FOR
CR1 WEEKLY MINIMUM OR LESS THAN 0
      GO TO 80
CR1 *** REVISION NO. 1 *** END ***
   70 IF(QMNDAY.EQ.-999.0) GO TO 80
      IF(QMEAN.LT.QMNDAY) QMEAN=QMNDAY
   80 IF(QMEAN.LT.0.) QMEAN=0.
      SUMOUT=QMEAN*DIF
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)140,120,90
   90 WRITE(IODBUG,40) JSTART,JEND,SUMOUT,STOR1,FLOUT,
     $QMNDAY,QMNWEK,ADJSTO,BGNSUM
      WRITE(IODBUG,100) (RULSTO(I),I=JSTART,JEND)
  100 FORMAT(1H0,' PERIOD RULECURVE STORAGES:'/(1X,8F10.0))
      WRITE(IODBUG,110) (QIMHYD(I),I=JSTART,JEND)
      WRITE(IODBUG,111) (SQIM(I),I=JSTART,JEND)
  110 FORMAT(1H0,' PERIOD MEAN AND SUMMATION OF MEAN INFLOWS:',
     $ /(1X,8F10.0))
 111  FORMAT(1X,8F10.0)
  120 WRITE(IODBUG,130)
  130 FORMAT(1H0,10X,17H** LEAVING SUMO26)
  140 RETURN
      END
