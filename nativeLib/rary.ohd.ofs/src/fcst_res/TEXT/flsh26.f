C MEMBER FLSH26
C  (from old member FCFLSH26)
C
C @PROCESS LVL(77)
C
      SUBROUTINE FLSH26(GATOP2,SIGELV,SIGSTO,SIGFAL,SIGRIS,QIMHYD,PEAKO,
C                             LAST UPDATE: 10/17/94.07:59:47 BY $WC30KH
C
     $PKPOS,ELVSOH,STOSOH,O,SOH,TMPSOH,ELVLG,QLG,ELVSM,QSM,ELVFL,QFLUD,
     $TOTALQ,QGEN,STOR,ELEV)
C
C     FLSH26 COMPUTES OUTFLOW FROM A DAM WITH FLASHBOARDS.  FLASHBOARDS
C ARE INSTALLED ON A DAM TO PERMIT ADDITIONAL STORAGE OF WATER FOR POWER
C OR WATER SUPPLY PURPOSES.  THE BOARDS ARE USUALLY HINGED TO A
C SUPPORT BAR OR PIPE ONE-THIRD OF THE DISTANCE FROM THE BOTTOM OF
C THE BOARDS.  THEORETICALLY, THE BOARDS SHOULD FALL OVER WHEN WATER
C OVERFLOWS THE TOP OF THE BOARDS BUT, DUE TO FRICTION, WATER USUALLY
C OVERFLOWS ABOUT 0.5 TO 0.6 OF A FOOT OVER THE TOP BEFORE THE BOARDS
C FALL.  WHEN THE WATER RECEDES BELOW THE HINGE ELEVATION, THE BOARDS
C AUTOMATICALLY FLIP BACK UP TO THE VERTICAL POSITION.  BOARDS ARE
C SOMETIMES FLIPPED BACK UP MANUALLY BEFORE THE POOL DROPS TO THE
C HINGE ELEVATION.  IF THIS IS A CONSISTENT PRACTICE, THE "HINGE"
C ELEVATION COULD BE ARBITRARILY RAISED TO THE ELEVATION WHERE THE
C BOARDS ARE MANUALLY RAISED.  AT SOME DAMS WITH FLASHBOARDS THERE IS
C A FLOOD GATE (OR CONTROLLED BOARDS) THAT THE OPERATOR CAN OPEN TO TRY
C TO KEEP THE POOL FROM RISING ABOVE THE TOP OF BOARDS.  MORE THAN ONE
C SIZE OF BOARDS OR TWO SETS OF THE SAME SIZE WITH DIFFERENT SPILLWAY
C CREST ELEVATION MIGHT BE INVOLVED.  PROVISION IS MADE IN THE SUBROU-
C TINE FOR TWO SIZES AND/OR TWO SPILLWAY CREST ELEVATIONS AS THIS SATIS-
C FIED THE REQUIREMENT FOR ALL DAMS THAT WERE STUDIED.
C     MODIFIED PULS ROUTING IS USED IN THE SUBROUTINE.  THE INFLOW IS
C ADJUSTED FOR GENERATION OR OTHER NON-SPILLWAY OUTFLOWS AND FOR GATE
C OUTFLOW IF A FLOOD GATE IS PARTIALLY OPEN.  IF A FLOOD GATE IS
C FULLY OPEN, THE GATE DISCHARGE IS INCLUDED IN THE ROUTING RELATION.
C A NEW RELATION OF OUTFLOW VS STORAGE PLUS OUTFLOW/2 IS COMPUTED
C WHENEVER THERE IS A CHANGE IN FLASHBOARDS DOWN OR THE FLOOD GATE IS
C CHANGED TO CLOSED OR FULLY OPEN.  THE ROUTING MAY START AT THE
C BEGINNING OR DURING THE TIME INTERVAL.  THE NUMBER OF ROUTING STEPS
C (NOSTEP) NEEDED WITHIN THE TIME INTERVAL IS DETERMINED PRIOR TO
C ENTRY INTO THE SUBROUTINE.  HOWEVER, ADDITIONAL ROUTING STEPS MAY
C BE NEEDED SINCE CHANGES IN BOARDS CAN OCCUR WITHIN ONE OF THE NSTEP
C ROUTING STEPS.
C     A SIMULATED RUN IS MADE FIRST USING NO OBSERVED OR PROPOSED
C DATA (EXCEPT THE INITIAL STORAGE AT THE BEGINNING OF THE FIRST TIME
C INTERVAL.  THE SECOND RUN IS AN ADJUSTED RUN THAT USES ALL OBSERVED
C OR PROPOSED DATA AND ADJUSTED STORAGES.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     JULY, 1982
C
C SUBROUTINE FLSH26 IS IN
C
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IFCST -- SIMULATED RUN(IFCST=0) OR ADJUSTED RUN(IFCST=1).
C     FCST -- LOGICAL VARIABLE.  IF TRUE, THE TIME INTERVAL IS IN THE
C       FORECAST PERIOD (AFTER RUN TIME).
C     GOFLSH -- LOGICAL VARIABLE.  IF TRUE, PROGRAM SHOULD BE IN FLSH26.
C     BGNGAT -- OBSERVED GATE OPENING AT BEGINNING OF FIRST TIME
C       INTERVAL.  WILL BE -999.0 IF MISSING.
C     GATOP2 -- OBSERVED GATE OPENING AT END OF TIME INTERVAL.
C       WILL BE -999.0 WHEN MISSING. (JTO - 9/83)
C     GATMAX -- MAXIMUM FLOOD GATE OPENING.  WILL BE -999.0 IF THERE IS
C       NO FLOOD GATE OR CONTROLLED BOARDS.
C     NS2 -- ARRAY POSITION OF INFLOWS, OUTFLOWS, ETC. AT END OF THIS
C       TIME INTERVAL.
C     SIGELV -- SIGNIFICANT ELEVATION ARRAY.  PROVISION IS MADE FOR TWO
C       SETS OF BOARDS AND A FLOOD GATE.  TEN SIGNIFICANT ELEVATIONS
C       CAN BE USED.  FLASHBOARDS ARE ARBITRARILY DESIGNATED AS LARGE
C       BOARDS IF THERE IS ONLY ONE SET.  ELEVATIONS CAN BE THE SAME
C       FOR DIFFERENT POSITIONS BUT STORAGES COMPUTED IN THE
C       SUBROUTINE AND PUT IN THE SIGSTO ARRAY ARE CHANGED SLIGHTLY
C       TO KEEP FROM HAVING TWO STORAGE VALUES THAT ARE EQUAL.
C       SIGNIFICANT ELEVATIONS ARE LISTED IN THE POSITIONS OF THE
C       SIGELV ARRAY AS FOLLOWS:
C       ( 1) -- SPILLWAY CREST ELEVATIONS FOR LARGE BOARDS.
C       ( 2) -- HINGE ELEVATION FOR LARGE BOARDS.
C       ( 3) -- TOP ELEVATION FOR LARGE BOARDS.
C       ( 4) -- ELEVATION WHERE LARGE BOARDS GO DOWN.  IF THE TOP
C               ELEVATION IS THE SAME FOR LARGE AND SMALL BOARDS, THE
C               ELEVATION WHERE BOARDS GO DOWN MUST BE SLIGHTLY HIGHER
C               FOR LARGE BOARDS SINCE THE SMALL BOARDS TEND TO GO
C               DOWN FIRST.
C       ( 5) SPILLWAY CREST ELEVATION FOR SMALL BOARDS.  MUST BE -999.0
C            IF ONLY ONE SET OF BOARDS.
C       ( 6) HINGE ELEVATION FOR SMALL BOARDS.  MUST BE -999.0 IF
C            ONLY ONE SET OF BOARDS.
C       ( 7) TOP ELEVATION FOR SMALL BOARDS.  MUST BE -999.0 IF ONLY
C            ONE SET OF BOARDS.
C       ( 8) ELEVATION WHERE SMALL BOARDS GO DOWN.  MUST BE -999.0 IF
C            ONLY ONE SET OF BOARDS.
C       ( 9) SPILLWAY CREST ELEVATION FOR FLOOD GATE.  MUST BE -999.0
C            IF NO FLOOD GATE.
C       (10) ELEVATION WHERE FLOOD GATE IS NORMALLY OPENED.  ELEVATION
C            MUST BE BELOW THE TOP OF ANY FLASHBOARDS.  GATE IS
C            OPERATED INSOFAR AS POSSIBLE TO CAUSE INFLOW TO BE PASSED.
C     NSIGEL -- HIGHEST POSITION NUMBER OF ELEVATIONS USED IN SIGELV
C       ARRAY.
C     SIGSTO -- WORKING ARRAY (NSIGEL VALUES) IN WHICH STORAGES COMPUTED
C       IN THIS SUBROUTINE FROM SIGELV ELEVATIONS ARE PLACED.  SIGSTO(1)
C       MUST BE -9999.0 PRIOR TO FIRST ENTRY INTO FLSH26.  STORAGES
C       MUST BE IN UNITS OF MEAN DISCHARGE FOR THE ROUTING STEP AS
C       DEFINED BY NOSTEP NUMBER OF ROUTING STEPS IN THE TIME INTERVAL.
C     SIGFAL -- ARRAY OF SIGELV POSITION NUMBERS DENOTING SIGNIFICANT
C       ELEVATIONS FOR FALLING POOL.  POSITION NUMBERS MUST SPECIFY
C       ELEVATIONS IN ASCENDING ORDER OF MAGNITUDE.  WHEN ELEVATIONS
C       FOR DIFFERENT ITEMS ARE EQUAL, ALL OF THEM MUST BE INCLUDED.
C       POSSIBLE POSITION NUMBERS (NOT IN ORDER) ARE 2,6, AND 9.
C     NFAL -- NO. OF SIGFAL VALUES.
C     SIGRIS -- ARRAY OF SIGELV POSITION NUMBERS DENOTING SIGNIFICANT
C       ELEVATIONS FOR RISING POOL.  POSITION NUMBERS MUST SPECIFY
C       ELEVATIONS IN ASCENDING ORDER OF MAGNITUDE.  WHEN ELEVATIONS
C       FOR DIFFERENT ITEMS ARE EQUAL, ALL OF THEM MUST BE INCLUDED.
C       POSSIBLE POSITION NUMBERS (NOT IN ORDER) ARE 4,8,9, AND 10.
C     NRIS -- NO. OF SIGRIS VALUES.
C     NUMLG -- NO. OF LARGE FLASHBOARDS ON DAM.
C     NUMSM -- NO. OF SMALL FLASHBOARDS.  MUST BE 0 IF ONLY ONE SET
C       OF BOARDS.
C     NLGDN -- NO. OF UNCONTROLLED LARGE FLASHBOARDS DOWN.
C     NSMDN -- NO. OF UNCONTROLLED SMALL FLASHBOARDS DOWN.
C     NGATE -- INDICATOR FOR FLOOD GATE OPENING DURING TIME INTERVAL.
C       VALUES OF 0,1, AND 2 INDICATE CLOSED, PARTLY OPEN, OR FULLY OPEN
C       RESPECTIVELY.  MUST BE -999 IF THERE IS NO FLOOD GATE.  WHETHER
C       OR NOT THERE IS A FLOOD GATE CAN BE DETERMINED FROM NSIGEL.
C     NFUTUR -- NO. OF FUTURE TIME INTERVALS USED FOR DECISION ON
C       OPENING FLOOD GATE WHEN POOL REACHES ELEVATION WHERE GATE IS
C       NORMALLY OPENED.  IF TOP OF ANY BOARDS WILL BE REACHED IN
C       NFUTUR INTERVALS, GATE WILL BE OPENED TO PASS INFLOW.  USER
C       MUST SPECIFY NFUTUR ACCORDING TO HIS ESTIMATE OF OPERATOR'S
C       ABILITY TO FORECAST FUTURE INFLOWS.  USE -999.0 IF NO GATE.
C     COEF -- COEFFICIENT IN EQUATION Q=C*L*H**1.5 FOR COMPUTING
C       DISCHARGE OVER TOP OF UPRIGHT BOARDS.
C     DISTLG -- LENGTH OF LARGE-BOARD SPILLWAY.
C     DISTSM -- LENGTH OF SMALL-BOARD SPILLWAY.
C     QI1 -- INFLOW AT BEGINNING OF TIME INTERVAL.
C     QI2 -- INFLOW AT END OF TIME INTERVAL.
C     QIMHYD -- HYDROGRAPH ARRAY OF MEAN INFLOWS.
C     Q01 -- OUTFLOW AT BEGINNING OF TIME INTERVAL.
C     OBSQ02 -- OBSERVED OUTFLOW AT END OF TIME INTERVAL.
C     Q02 -- OUTFLOW AT END OF TIME INTERVAL.
C     OBSQOM -- OBSERVED MEAN OUTFLOW FOR TIME INTERVAL.
C     QOM -- MEAN OUTFLOW FOR TIME INTERVAL.
C     Q0K1 -- NON-SPILLWAY OUTFLOW AT BEGINNING OF TIME INTERVAL.
C     Q0K2 -- NON-SPILLWAY OUTFLOW AT END OF TIME INTERVAL.
C     S1 -- STORAGE AT BEGINNING OF TIME INTERVAL IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME INTERVAL.
C     OBSS2 -- OBSERVED STORAGE AT END OF TIME INTERVAL IN SAME UNITS
C       AS S1.
C     S2 -- STORAGE AT END OF TIME INTERVAL.
C     ELEV1 -- POOL ELEVATION AT BEGINNING OF TIME INTERVAL.
C     ELEV2 -- POOL ELEVATION AT END OF TIME INTERVAL.
C     TESTPK -- TEST VALUE OF OUTFLOW (USUALLY FLOOD DISCHARGE) FOR
C       DETERMINING WHETHER A PEAK OUTFLOW BETWEEN Q01 AND Q02 WILL
C       LATER REPLACE ONE OF THESE VALUES IN THE OUTFLOW ARRAY.
C     PEAKO -- ARRAY OF PEAK OUTFLOWS BETWEEN INSTANTANEOUS OUTFLOWS
C       AT REGULAR TIME INTERVALS.  THE PEAK VALUES WILL REPLACE THE
C       NEAREST TIME INTERVAL OUTFLOWS AS INDICATED BY POSITION
C       NUMBERS IN THE PKPOS ARRAY.  THE SUPERVISORY EXECUTION ROUTINE
C       WILL MAKE THESE REPLACEMENTS.
C     PKPOS -- ARRAY OF POSITION NUMBERS THAT INDICATE WHERE THE
C       CORRESPONDING PEAKO VALUES WILL BE PLACED IN THE INSTANTANEOUS
C       OUTFLOW ARRAY.  PKPOS(1) MUST BE -999.0 IF NOT DEFINED.
C     NUMPKO -- NUMBER OF PAIRS OF DEFINED PEAKO AND PKPOS VALUES.
C       MUST BE -999 IF NO DEFINED VALUES.
C     NTOTPK -- TOTAL NUMBER OF POSITIONS AVAILABLE IN THE PEAKO OR
C       PKPOS ARRAY.
C     NOSTEP -- NO. OF ROUTING STEPS IN THE TIME INTERVAL THAT ARE
C       NEEDED FOR ACCURATE ROUTING.  THIS VALUE IS DETERMINED FROM THE
C       ELEVATION VS DISCHARGE RELATION FOR THE DAM WITH ALL BOARDS
C       DOWN AND THE FLOOD GATE (IF ANY) FULLY OPEN.
C     NLGSOH -- NUMBER OF LARGE BOARDS DOWN USED FOR OUTFLOW (O) VS
C       STORAGE + O/2(SOH) RELATION.  MUST BE -999 ON FIRST ENTRY
C       INTO FLSH26.
C     NSMSOH -- NUMBER OF SMALL BOARDS DOWN USED FOR O VS SOH RELATION.
C     NGASOH -- GATE OPENING CONDITION USED FOR O VS SOH RELATION.
C       CLOSED (NGASOH=0), PARTLY OPEN (NGASOH=1), AND FULLY OPEN
C       (NGASOH=2).
C     NPOSOH -- POSITION IN SIGELV ARRAY OF CREST ELEVATION USED FOR O
C       VS SOH RELATION.  VALUE OF -999 CAUSES A NEW O VS SOH RELATION
C       TO BE COMPUTED.
C     ELVSOH -- ARRAY OF ELEVATIONS  IN ASCENDING ORDER FOR USE
C       IN COMPUTING THE O VS SOH RELATION.  ELVSOH VALUES WILL BE
C       OBTAINED BY TAKING ALL OF THE ELEVATION VALUES IN THE
C       FLASH BOARD AND GATE RATINGS THAT ARE NOT DUPLICATES.
C       THIS WILL BE DONE IN THE PARAMETER INPUT SUBROUTINE.
C       THE NUMBER OF PAIRS OF VALUES (NELSOH) WILL BE COMPUTED
C       AT THE SAME TIME.
C     STOSOH -- STORAGE VALUES CORRESPONDING TO ELVSOH ELEVATIONS.
C       THESE VALUES WILL BE COMPUTED AND PUT IN THE STOSOH WORKING
C       ARRAY ON FIRST ENTRY INTO FLSH26.  STORAGES MUST BE IN UNITS
C       OF MEAN DISCHARGE FOR THE ROUTING STEP AS DEFINED BY NOSTEP
C       NUMBER OF ROUTING STEPS IN THE TIME INTERVAL.
C     NELSOH -- NO. OF VALUES IN ELVSOH OR STOSOH ARRAYS.
C     O -- ARRAY OF SPILLWAY DISCHARGE VALUES FOR O VS SOH RELATION.
C       FIRST VALUE MUST BE 0.  VALUES WILL BE COMPUTED IN UTILITY
C       SUBROUTINE OSOH26.
C     SOH -- CORRESPONDING ARRAY OF STORAGE ABOVE SPILLWAY PLUS O/2
C       VALUES.  O AND SOH VALUES WILL  BE STORED IN PARAMETER FILE
C       BUT WILL BE REVISED
C       WHENEVER BOARD OR GATE CONDITIONS CHANGE.  STORAGE
C       MUST BE IN UNITS OF MEAN DISCHARGE FOR THE ROUTING STEP DEFINED
C       BY THE NUMBER OF STEPS (NOSTEP) IN THE TIME INTERVAL.  FIRST
C       VALUE MUST BE 0.
C     TMPSOH -- TEMPORARY ARRAY OF STORAGE + O/2 VALUES CORRESPONDING
C       TO O VALUES BUT STORAGE MAY BE FOR A DIFFERENT TIME STEP
C       THAN THE STEP USED FOR SOH VALUES.
C     NOSOH -- NUMBER OF PAIRS OF O AND SOH (OR TMPSOH) VALUES.  NOSOH
C       IS COMPUTED IN UTILITY SUBROUTINE OSOH26.
C     ELVLG -- POOL ELEVATIONS FOR ELEVATION VS DISCHARGE RELATION FOR
C       LARGE-BOARD SPILLWAY WITH ALL BOARDS DOWN.
C     QLG -- CORRESPONDING DISCHARGE VALUES.
C     NELVLG -- NO. OF PAIRS OF ELVLG AND QLG VALUES.
C     ELVSM -- POOL ELEVATIONS FOR ELEVATION VS DISCHARGE RELATION FOR
C       SMALL-BOARD FLASHBOARD WITH ALL BOARDS DOWN.
C     QSM -- DISCHARGE VALUES FOR ELVSM VS QSM RELATION.
C     NELVSM -- NO. OF PAIRS OF ELVSM AND QSM VALUES.
C     ELVFL -- POOL ELEVATIONS FOR ELEVATION VS DISCHARGE RELATION FOR
C       FLOOD GATE WITH GATE FULLY OPEN.
C     QFLUD -- DISCHARGES CORRESPONDING TO ELVFL VALUES.
C     NELVFL -- NO. OF PAIRS OF ELVFL AND QFLUD VALUES.
C     NTERPQ -- INTERPOLATION INDICATOR WITH 0 AND 1 INDICATING
C       ARITHMETIC AND LOGARITHMIC INTERPOLATION, RESPECTIVELY.
C     QGENMX -- MAXIMUM GENERATION DISCHARGE FOR A POWER DAM.  WILL
C       BE 0 IF NOT A POWER DAM.  WILL BE USED FOR GENERATION DISCHARGE
C       IF NO OBSERVED OR PROPOSED VALUE IS AVAILABLE AND IF A RELATION
C       OF TOTAL DISCHARGE VS MAXIMUM GENERATION DISCHARGE IS NOT USED.
C       A VALUE MUST BE ENTERED IF A TOTAL DISCHARGE VS MAXIMUM
C       GENERATION DISCHARGE RELATION IS USED.
C     SLUICE -- SPECIFIED CONSTANT SLUICE DISCHARGE.  EITHER 0 OR A
C       POSITIVE VALUE MUST BE SPECIFIED.
C     TOTALQ -- TOTAL DISCHARGE VALUES FOR TOTAL DISCHARGE VS MAXIMUM
C       GENERATION DISCHARGE RELATION.  TOTALQ(1) MUST BE -999.0 IF
C       RELATION IS NOT USED.
C     QGEN -- MAXIMUM GENERATION DISCHARGES CORRESPONDING TO TOTAL
C       DISCHARGES.  IF SLUICE DISCHARGE ALSO VARIES WITH TOTAL
C       DISCHARGE, QGEN CAN BE GENERATION PLUS SLUICE DISCHARGES.
C     NQGEN -- NO. OF PAIRS OF TOTALQ AND QGEN VALUES.  MUST BE 0 IF
C       RELATION IS NOT USED.
C     PCTERF -- SPECIFIED FRACTION OF TOTAL OUTFLOW AT END OF TIME
C       INTERVAL TO CHECK AGAINST ERROR IN FIRST ESTIMATE OF QOK2 WHEN
C       TOTALQ VS QGEN RELATION IS USED.  ALSO, USED IN COMPUTING
C       MAXIMUM FLOOD GATE DISCHARGE.
C     NUM -- NUMBER OF VALUES IN INFLOW OR OUTFLOW TIME SERIES.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION RELATION.  STORAGE
C       MUST BE IN UNITS OF MEAN DISCHARGE FOR THE TIME INTERVAL.
C     ELEV -- POOL ELEVATIONS FOR STOR VS ELEV RELATION.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NTERP -- ARITHMETIC(NTERP=0) OR LOGARITHMIC INTERPOLATION
C       (NTERP=1) FOR STORAGE VS ELEVATION CURVE.
C     IBUG -- NO TRACE OR DEBUG(IBUG=0), TRACE ONLY(IBUG=1, TRACE AND
C       DEBUG(IBUG=2).
C
C QO2,QOM,S2,ELEV2 AND VALUES IN THE PEAKO AND PKPOS ARRAYS WILL BE
C COMPUTED IN THIS SUBROUTINE EXCEPT IN THE ADJUSTED RUN WHEN VALUES
C ARE OBSERVED.
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/errdat'
      INCLUDE 'common/where'
      INCLUDE 'common/ionum'
      INCLUDE 'common/resv26'
      INCLUDE 'common/flas26'
C
      DIMENSION SIGELV(*),SIGSTO(*),SIGFAL(*),SIGRIS(*),PEAKO(*),
     $PKPOS(*),ELVSOH(*),STOSOH(*),O(*),SOH(*),TMPSOH(*),ELVLG(*),
     $QLG(*),ELVSM(*),QSM(*),ELVFL(*),QFLUD(*),TOTALQ(*),QGEN(*),
     $STOR(*),ELEV(*),QIMHYD(*)
C
      INTEGER SIGFAL,SIGRIS
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/flsh26.f,v $
     . $',                                                             '
     .$Id: flsh26.f,v 1.3 2002/10/23 19:17:11 dws Exp $
     . $' /
C    ===================================================================
C
C
C NTRIAL IS USED TO DEFINE THE NUMBER OF RUNS THROUGH FLSH26.
C A SECOND RUN IS MADE IF THE
C DISCHARGE RELATION IS USED AND THE FIRST ESTIMATE OF THE NON-SPILLWAY
C DISCHARGE(QOK2) FOR THE END OF THE TIME INTERVAL IS DIFFERENT FROM
C THE SECOND ESTIMATE MORE THAN A DESIGNATED FRACTION(PCTERF) TIMES
C THE TOTAL OUTFLOW QO2 AT THE END OF THE TIME INTERVAL.  SET MEAN
C INFLOW (QIM) FOR THIS TIME INTERVAL FROM MEAN INFLOW ARRAY (QIMHYD).
C
      ns22=ns2
      qgatmx=0.0
      qbds2=0.0
      NTRIAL=0
      QIM=QIMHYD(NS2)
   10 NTRIAL=NTRIAL+1
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)60,20,30
   20 WRITE(IODBUG,40)
      GO TO 60
   30 WRITE(IODBUG,40)
   40 FORMAT(1H0,10X,17H** FLSH26 ENTERED)
      WRITE(IODBUG,50) IFCST,FCST,GOFLSH,BGNGAT,GATMAX,NS2,NUMLG,NUMSM,
     $NLGDN,NSMDN,NGATE,NFUTUR,COEF,DISTLG,DISTSM,QI1,QI2,QO1,QO2,QOM,
     $QOK1,QOK2,S1,S2,ELEV1,ELEV2,TESTPK,NTOTPK,NOSTEP,NLGSOH,NSMSOH,
     $NGASOH,NPOSOH,NTERPQ,QGENMX,SLUICE,PCTERF,NUM,NTERP,IBUG
   50 FORMAT(1H0,114H   IFCST,FCST,GOFLSH,BGNGAT,GATMAX,NS2,NUMLG,NUMSM,
     $NLGDN,NSMDN,NGATE,NFUTUR,COEF,DISTLG,DISTSM,QI1,QI2,QO1,QO2,QOM/1X
     $,I6,2L5,2F12.3,7I6,3F12.3/1X,5F12.3/1H0,84H QOK1,QOK2,S1,S2,ELEV1,
     $ELEV2,TESTPK,NTOTPK,NOSTEP,NLGSOH,NSMSOH,NGASOH,NPOSOH,NTERPQ/1X,
     $7F12.3,6I6/1X,I6/1H0,36H QGENMX,SLUICE,PCTERF,NUM,NTERP,IBUG/1X,
     $3F12.3,3I6)
C
C CHECK IF PROGRAM SHOULD BE IN FLSH26.  IF GOFLSH IS FALSE, CALL GOFL26
C TO SEE IF GOFLSH IS ACTUALLY TRUE AND THE PROGRAM FAILED TO GO THROUGH
C GOFL26 BEFORE COMING TO FLSH26.  IF GOFLSH IS STILL FALSE, GO TO
C RETURN.
C
      IF(GOFLSH) GO TO 60
      CALL GOFL26(GATOP2,QIMHYD,SIGELV,STOR,ELEV)
      IF(GOFLSH) GO TO 60
      IF(IBUG.NE.2) GO TO 1080
      WRITE(IODBUG,51)
   51 FORMAT(1H0,  48H PROGRAM SHPULD NOT BE IN FLSH26.  GO TO RETURN.)
      GO TO 1080
C
C SET REAL NUMBERS FOR NOSTEP AND FLASHBOARD VALUES.
C
   60 STEPS=NOSTEP
      ANUMLG=NUMLG
      ANUMSM=NUMSM
      IF(IFCST.EQ.0) GO TO 70
      IF(FCST) GO TO 70
      OBSQO2=QO2
C
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      OBSS2=S2
      IF(IGO.EQ.1) GO TO 830
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; ADJUSTED BETWEEN OBSERVED
C VALUES USING SIMULATED AND OBSERVED VALUES; OR INTERPOLATED BETWEEN
C OBSERVED VALUES.  WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED IN STATEMENT 841
C FROM THE OBSERVED MEAN INFLOW, THE ADJUSTED MEAN INFLOW, AND THE
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
      IF(QOM.EQ.-999.0) GO TO 70
      GO TO 841
C NOW, COMPUTE DIFFERENCES IN INFLOWS FOR THE TIME INTERVAL
C AND INCREMENTAL INFLOWS FOR EACH ROUTING STEP(NOSTEP IN TIME
C INTERVAL).
C
   70 DIFQI=QI2-QI1
      DELQI=DIFQI/STEPS
      IF(QOK2.EQ.-999.0) GO TO 80
      GO TO 100
   80 IF(NQGEN.EQ.0) GO TO 90
C
C IF NON-SPILLWAY OUTFLOW (QOK2) AT THE END OF THE TIME INTERVAL IS
C MISSING, SET IT EQUAL TO MAXIMUM GENERATION (QGENMX) PLUS SLUICE
C DISCHARGE (SLUICE) IF THE TOTAL DISCHARGE VS MAXIMUM GENERATION
C DISCHARGE RELATION IS NOT USED.  IF THE RELATION IS USED, DISCHARGE
C AT END OF INTERVAL (QO2) IS ESTIMATED AS THE AVERAGE OF BEGINNING
C DISCHARGE (QO1) AND MEAN INFLOW (QIM) AND A FIRST ESTIMATE OF QOK2
C IS COMPUTED.
C
      ESTQO2=(QO1+QIM)*0.5
      CALL TERP26(ESTQO2,QOK2,TOTALQ,QGEN,NQGEN,IFLAG,IBUG)
C
C IF SLUICE ALSO VARIES WITH TOTAL DISCHARGE, QGEN CAN INCLUDE
C (QGENMX+SLUICE) AND THE SLUICE VARIABLE CAN BE SET TO 0.
C
      QOK2=QOK2+SLUICE
      GO TO 110
   90 QOK2=QGENMX+SLUICE
C
C WHEN Q0K2 IS OBSERVED OR PROPOSED OR IS (QGENMX+SLUICE), ONLY ONE RUN
C IS MADE THROUGH FLSH26 AND NTRIAL IS SET TO 2 TO PREVENT A SECOND
C RUN.  COMPUTE THE DIFFERENCE IN NON-SPILLWAY OUTFLOW(DIFQOK) FROM
C START TO END OF TIME INTERVAL AND COMPUTE THE INCREMENTAL NON-SPILLWAY
C OUTFLOW(DELQOK) FOR A ROUTING TIME STEP.
C
  100 NTRIAL=2
C
C IF OBSERVED STORAGE OR MEAN OUTFLOW (OR COMPUTED VALUES) WERE
C AVAILABLE IN OBSV26 DURING THE ADJUSTED RUN, QOK1 WOULD NOT BE
C DEFINED AND WOULD BE -999.0.  IF QOOK1 IS NOT DEFINED, SET IT EQUAL TO
C QOK2.
C
  110 IF(QOK1.EQ.-999.0) QOK1=QOK2
      DIFQOK=QOK2-QOK1
      DELQOK=DIFQOK/STEPS
      QOKM=(QOK1+QOK2)*0.5
C
C COMPUTE STORAGES IN UNITS OF MEAN DISCHARGE FOR THE ROUTING STEP
C FOR SIGELV AND ELVSOH ELEVATIONS (IF THIS IS THE FIRST ENTRY INTO
C FLSH26) AND PUT VALUES IN SIGSTO AND STOSOH ARRAYS, RESPECTIVELY.
C SIGELV IS THESIGNIFICANT ELEVATION ARRAY AND ELVSOH IS THE ARRAY OF
C ELEVATIONS IN ASCENDING ORDER USED IN COMPUTING THE OUTFLOW VS
C (STORAGE+OUTFLOW/2) RELATION.
C
      IF(SIGSTO(1).NE.-999.0) GO TO 170
      DO 130 I=1,NSIGEL
      SIGEL=SIGELV(I)
      IF(SIGEL.EQ.-999.0) GO TO 120
      CALL NTER26(SIGEL,SIGST,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      GO TO 130
  120 SIGST=-999.0
  130 SIGSTO(I)=SIGST*STEPS
C
C CHECK IF ANY TWO VALUES OF SIGSTO (EXCEPT -999.0) ARE EQUAL.  IF SO,
C INCREASE ONE VALUE SLIGHTLY.
C
      DO 150 I=1,NSIGEL
      IF(SIGSTO(I).EQ.-999.0) GO TO 150
      DO 140 J=1,NSIGEL
      IF(I.EQ.J) GO TO 140
      IF(SIGSTO(I).NE.SIGSTO(J)) GO TO 140
      SIGSTO(J)=SIGSTO(I)+0.2
  140 CONTINUE
  150 CONTINUE
      DO 160 I=1,NELSOH
  160 STOSOH(I)=STOR(I)*STEPS
C
C COMPUTE TOTAL NUMBER OF BOARDS DOWN (NBDSDN) FOR LATER USE.
C
  170 NBDSDN=NLGDN
      IF(NUMSM.GT.0)NBDSDN=NBDSDN+NSMDN
C
C ROUTING IS NEEDED AT THE BEGINNING OF THE TIME INTERVAL IF ANY BOARDS
C ARE DOWN, IF THE FLOOD GATE OR CONTROLLED BOARDS (IF ANY) IS FULLY
C OPEN, OR IF THE BEGINNING POOL ELEVATION IS ABOVE THE TOP OF ANY
C BOARDS.  CHECK THESE CONDITIONS.
C
      IF(NBDSDN.NE.0.OR.NGATE.EQ.2) GO TO 220
      STEST=SIGSTO(3)
      IF(NUMSM.EQ.0) GO TO 180
      IF(SIGSTO(7).GE.STEST) GO TO 180
      STEST=SIGSTO(7)
  180 IF(S1*STEPS.GT.STEST) GO TO 220
C
C FOR FOLLOWING STATEMENTS THERE ARE NO BOARDS DOWN AND ELEVATION
C IS BELOW THE TOP OF ANY BOARDS AT THE BEGINNING OF THE TIME INTERVAL.
C CHECK IF THE FLOOD GATE (IF ANY) IS PARTLY OPEN AND, IF SO, ATTEMPT
C TO KEEP THE SAME POOL ELEVATION BY REGULATING THE GATE OR CONTROLLED
C BOARDS.
C
      QGAT1=0.
      QGAT2=0.
      IF(NGATE.NE.1) GO TO 190
      QGAT1=QI1-QOK1
      QGAT2=QI2-QOK2
      IF(QGAT1.LT.0.)QGAT1=0.
      IF(QGAT2.LT.0.)QGAT2=0.
      IF(QGAT1.EQ.0.AND.QGAT2.EQ.0.) GO TO 190
C
C COMPUTE MAXIMUM GATE DISCHARGE AT ELEV1 AND ITERATED ELEVATION
C (ELEV2) AT THE END OF THE TIME INTERVAL TO CHECK IF THE GATE
C CAN PASS THE DESIRED DISCHARGES.
C DISCHARGE (QO1) IMMEDIATELY AFTER START OF TIME INTERVAL MAY BE
C CHANGED DUE TO CHANGE IN GATE OPENING.
C
      CALL NTER26(ELEV1,QMAX1,ELVFL,QFLUD,NELVFL,IFLAG,NTERPQ,IBUG)
C
C COMPUTE STORAGE AND MAXIMUM GATE DISCHARGE AT THE END OF THE TIME
C INTERVAL USING AN ITERATION TECHNIQUE.  USE MAXIMUM
C DISCHARGE AT BEGINNING OF TIME INTERVAL IN COMPUTING A FIRST APPROXIMA
C TION OF MAXIMUM DICHARGE AT END OF TIME INTERVAL.
C
      STOEND=S1+QIM-QMAX1-QOKM
      PREVQ2=QMAX1
      DO 181 I=1,5
      I181=I
      CALL NTER26(STOEND,ELV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      CALL NTER26(ELV2,QMAX2,ELVFL,QFLUD,NELVFL,IFLAG,NTERPQ,IBUG)
      IF(ABS(QMAX2-PREVQ2).LE.(PCTERF*QMAX2)) GO TO 182
      PREVQ2=QMAX2
      STOEND=S1+QIM-(QMAX1+QMAX2)*0.5-QOKM
  181 CONTINUE
  182 IF(IBUG.NE.2) GO TO 184
      WRITE(IODBUG,183) QMAX1,PREVQ2,QMAX2,S1,STOEND,ELEV1,ELV2,I181
  183 FORMAT(1H0,43H MAX. GATE OUTFLOW AT FIRST OF TIME STEP IS,F12.3,
     $41H LAST TWO VALUES OF ITERATED OUTFLOWS ARE,2F12.3/1H0,44H BEGINN
     $ING AND ENDING TIME STEP STORAGES ARE,2F12.3,38H.  BEGINNING AND E
     $NDING ELEVATIONS ARE/1X,2F12.3,' LAST ITERATION',I2)
  184 IF(QGAT1.GT.QMAX1)QGAT1=QMAX1
      IF(QGAT2.GT.QMAX2)QGAT2=QMAX2
      IF(QGAT1.GE.QMAX1.AND.QGAT2.GE.QMAX2) NGATE=2
      GO TO 200
  190 NGATE=0
  200 QO1=QOK1+QGAT1
      IF(NGATE.EQ.2) GO TO 220
      QO2=QOK2+QGAT2
      STOEND=S1+QIM-(QO1+QO2)*0.5
      IF(STOEND.GT.STEST) GO TO 210
C
C NO BOARDS ARE DOWN AND THE POOL ELEVATION DOES NOT EXCEED THE TOP
C OF ANY BOARDS AT THE END OF THE TIME INTERVAL.  SET ENDING VALUES
C OF STORAGE AND OUTFLOW AND MEAN OUTFLOW AND GO TO 820 TO CHECK ON
C NUMBER OF RUNS (NTRIAL) AND TO COMPUTE ENDING ELEVATION FOR THE TIME
C INTERVAL.
C
      S2=STOEND
      QOM=S1+QIM-S2
      IF(IFCST.EQ.0) GO TO 820
      IF(FCST) GO TO 820
      IF(OBSQO2.NE.-999.0)QO2=OBSQO2
      GO TO 820
C THE TOP OF BOARDS IS EXCEEDED WITH NO BOARDS DOWN AND FLOOD GATE
C (IF ANY) CLOSED OR PARTLY OPEN.  COMPUTE FROM FUNCTION FRAC26 THE
C FRACTION (FRAC) OF THE TIME INTERVAL WHEN THE POOL EXCEEDS TOP OF
C BOARDS.
C
  210 QSFRAC=0.
      DIFQO=QO2-QO1
      SFILL=STEST
      FRAC=FRAC26(DIFQI,DIFQO,QI1,QO1,QOK1,QSFRAC,S1,SFILL,IBUG)
C
C COMPUTE NUMBER OF FIRST ROUTING STEP (NBGN) AND FRACTION (SUMFRA)
C OF FIRST ROUTING STEP WHEN TOP OF BOARDS (STEST) IS REACHED.  SET
C BEGINNING ELEVATION (ELV1), OUTFLOW (QQ01), AND STORAGE(SS1).
C SS1 IS IN UNITS OF MEAN DISCHARGE FOR THE ROUTING TIME STEP DEFINED
C BY NOSTEP TIME STEPS IN THE TIME INTERVAL.
C
      ISTEP=STEPS*FRAC
      NBGN=ISTEP+1
      ASTEP=ISTEP
      SUMFRA=STEPS*FRAC-ASTEP
      IF(SUMFRA.GT.0.99) SUMFRA=0.99
      FRACST=SUMFRA
      SS1=STEST
      QQO1=QO1+DIFQO*FRAC
      GO TO 230
C
C FOR THE FOLLOWING STATEMENTS, BOARDS ARE DOWN, A FLOOD GATE IS OPEN,
C OR WATER IS FLOWING OVER TOP OF BOARDS AT THE BEGINNING OF THE TIME
C INTERVAL.
C
  220 NBGN=1
      FRAC=0.
      FRACST=0.
      SUMFRA=0.
      QQO1=QO1
      QQO2=-999.0
      SS1=S1*STEPS
C
C FOLLOWING DO LOOP 760 ROUTES WATER THROUGH THE DAM USING ROUTING
C TIME STEPS AS DEFINED AS NOSTEP NUMBER OF STEPS IN THE TIME INTERVAL.
C
  230 DO 760 I=NBGN,NOSTEP
      AI=I
      STEP1=I-1
      STEP2=I
      IF(I.GT.NBGN) GO TO 240
C
C COMPUTE BEGINNING VALUES OF INFLOW (QQI1) AND NON-SPILLWAY OUTFLOW
C (QOK1).  BEGINNING VALUES OF TOTAL OUTFLOW (QQO1) AND STORAGE (SS1)
C HAVE BEEN PREVIOUSLY DEFINED.  IF FRACST IS NOT 0, THE BEGINNING
C VALUES WILL NOT BE AT THE BEGINNING OF THE TIME STEP.
C
      QQI1=QI1+DELQI*(STEP1+FRACST)
      QQOK1=QOK1+DELQOK*(STEP1+FRACST)
C
C SET BEGINNING VALUE FOR PTEST, THE HIGHEST OUTFLOW VALUE DURING THE
C TIME INTERVAL AND FOR FRACPK, THE FRACTION OF TIME FROM BEGINNING OF
C TIME INTERVAL TO PEAK OUTFLOW.
C
      PTEST=QQO1
      FRACPK=FRAC
C
C COMPUTE ENDING VALUES OF INFLOW AND NON-SPILLWAY OUTFLOW AND MEAN
C VALUES FOR THE REMAINDER OF THE TIME STEP.
C
  240 QQI2=QI1+DELQI*STEP2
      QQOK2=QQOK1+DELQOK*STEP2
      QQIM=(QQI1+QQI2)*0.5
      QQOKM=(QQOK1+QQOK2)*0.5
C
C SUBTRACT AVERAGE NON-SPILLWAY DISCHARGE (QQOKM) FROM AVERAGE INFLOW
C (QQIM) TO OBTAIN AVERAGE INFLOW (RTQQIM) FOR ROUTING EQUATION:
C RTQQIM+SS1-QQ01/2=SS2+QQ02/2.  FLOOD GATE DISCHARGE IF A FLOOD GATE
C IS PARTLY OPEN WILL BE HANDLED SEPARATELY.
C
      RTQQIM=QQIM-QQOKM
C
C CALL SUBROUTINE OSOH26 TO COMPUTE NEW ROUTING RELATION (IF NEEDED),
C MAKE ROUTING COMPUTATIONS AND COMPUTE TOTAL OUTFLOWS AND STORAGES.
C
  250 CALL OSOH26(ANUMLG,ANUMSM,RTQQIM,SUMFRA,QQI1,QQI2,QQIM,QQO1,QQO2,
     $QQOK1,QQOK2,SS1,SS2,SIGELV,SIGSTO,ELVSOH,STOSOH,TMPSOH,O,SOH,
     $ELVLG,QLG,ELVSM,QSM,ELVFL,QFLUD,STOR,ELEV)
C
C CHECK IF POOL IS RISING, STEADY, OR FALLING.  IF STEADY, GO TO 740
C TO SET NEW HIGHEST OUTFLOW IN TIME INTERVAL AND NEW VALUES FOR NEXT
C LOOP IN DO LOOP 760.
C
  260 IF(SS2-SS1) 270,740,470
C
C FOR FOLLOWING STATEMENTS TO 470, POOL IS FALLING.  JS1 AND JS2 WILL
C DEFINE THE NEXT LOWER SIGNIFICANT ELEVATION FOR FALLING POOL FOR
C SS1 AND SS2.
C
  270 JS1=0
      JS2=0
      DO 280 J=1,NFAL
      K=SIGFAL(J)
      IF(SS1.GT.SIGSTO(K)) JS1=J
  280 IF(SS2.GT.SIGSTO(K)) JS2=J
C
C IF JS1 IS 0, SS1 AND SS2 ARE EQUAL TO OR LESS THAN THE LOWEST
C SIGNIFICANT STORAGE.  CALL WARN.
C
      K=SIGFAL(1)
      IF(JS1.NE.0 .OR. NBDSDN.GT.0) GO TO 310
  290 WRITE(IPR,300) SS1,SS2,SIGSTO(K)
  300 FORMAT(1H0,10X,'**WARN**BEGINNING AND ENDING STORAGES AFTER DO'
     $,' LOOP 280 OF',2F12.3,/' ARE EQUAL TO OR LESS THAN THE LOWEST',
     $' SIGNIFICANT STORAGE OF',F12.3,/' ASSUME ENDING TOTAL DISCHARGE'
     $,'(QQO2) IS THE SAME AS NON-SPILLWAY DISCHARGE (QQOK2).')
      CALL WARN
      QQO2=QQOK2
      SS2=SS1+QQIM-(QQO1+QQO2)*0.5
      GO TO 740
C
C IF JS1=JS2, THE POOL DOES NOT DROP BELOW THE NEXT CRITICAL LEVEL
C AND PROGRAM WILL GO TO 740 TO SET VALUES FOR NEXT LOOP OF DO LOOP 760.
C
  310 IF(JS1.EQ.JS2) GO TO 740
C
C FOR FOLLOWING STATEMENTS, POOL DROPS TO OR BELOW NEXT CRITICAL
C ELEVATION BEFORE END OF ROUTING STEP.  COMPUTE FRACTION (FRACST)
C OF TIME STEP FOR POOL TO DROP TO THIS CRITICAL LEVEL ASSUMING
C SS1 TO SS2 IS A STRAIGHT LINE.  SS1 MAY OR MAY NOT BE AT THE START
C OF THE TIME STEP.  TO PREVENT THE POSSIBILITY OF EXTREMELY SMALL
C NUMBERS BEING DIVIDED BY EXTREMELY SMALL NUMBERS, SS2 WILL BE MADE
C 0.1 LESS THAN SS1, IF THE DIFFERENCE IS LESS THAN 0.1.
C
      IF((SS1-SS2).LT.0.1) SS2=SS1-0.1
      K=SIGFAL(JS1)
      FRACST=1.-SUMFRA-( SIGSTO(K)-SS2-SIGSTO(K)*SUMFRA+SS2*SUMFRA)/(SS1
     $-SS2)
      SUMFRA=SUMFRA+FRACST
C
C IN THE ROUTING PROCEDURE (1.-SUMFRA) IS USED AS A DIVISOR.  TO PREVENT
C EXTREMELY LARGE NUMBERS FROM OCCURRING, SUMFRA WILL BE SET TO 1.0 IF
C 0.99 OR GREATER.  SS2 WILL BE MADE 0.1 GREATER THAN
C SIGSTO(K) AND THE PROGRAM WILL GO TO STATEMENT 740 TO SET NEW INITIAL
C VALUES FOR NEXT LOOP OF DO LOOP 760.
C
      IF(SUMFRA.LE.0.99) GO TO 320
      SUMFRA=1.0
      SS2=SIGSTO(K)+0.1
      GO TO 740
C
C SINCE THERE WILL BE A CHANGE IN BOARD OR FLOOD GATE CONDITIONS AT
C SUMFRA FRACTION OF THE ROUTING STEP, ROUTING OR OTHER COMPUTATIONS
C WILL BE REQUIRED FROM SUMFRA TO THE END OF THE REGULAR ROUTING TIME
C STEP.  NEW INITIAL VALUES WILL BE NEEDED BUT END VALUES REMAIN
C THE SAME.
C
  320 QQI1=QQI1+DELQI*FRACST
      QQOK1=QQOK1+DELQOK*FRACST
      QQIM=(QQI1+QQI2)*0.5
      QQOKM=(QQOK1+QQOK2)*0.5
      RTQQIM=QQIM-QQOKM
      SS1=SIGSTO(K)-0.10
C
C THE VARIABLE K DENOTES THE POSITION OF THE SIGNIFICANT ELEVATION IN
C THE SIGELV ARRAY AND WILL BE USED IN A GO TO STATEMENT TO DETERMINE
C BOARD AND GATE CHANGES.
C
      GO TO (330,360,330,330,330,420,330,330,460,330),K
C
C THE K VALUES THAT GO TO 330 ARE FOR SIGNIFICANT ELEVATIONS THAT
C DO NOT REQUIRE A CHANGE IN ROUTING PARAMETERS.  THEREFORE, THE
C OUTFLOW AND STORAGE VALUES ALREADY COMPUTED AT THE END OF THE
C TIME STEP ARE VALID.
C
  330 IF(IBUG.NE.2) GO TO 740
  340 WRITE(IODBUG,350) K,SS1,SS2
  350 FORMAT(1H0, 33H THE ELEVATION IN POSITION NUMBER,I6, 68H OF THE SI
     $GELV ARRAY DOES NOT REQUIRE A CHANGE IN ROUTING PARAMETERS/   38H
     $FOR THIS CHANGE IN POOL STORAGES FROM,F12.3, 3H TO,F12.3/ 79H THE
     $PROGRAM WILL GO TO 740 TO SET NEW VALUES FOR THE NEXT LOOP IN DO L
     $OOP 760.)
      GO TO 740
C
C FOLLOWING STATEMENTS APPLY WHEN THE NEXT LOWER SIGNIFICANT
C ELEVATION IS THE HINGE ELEVATION FOR LARGE BOARDS WHERE THE LARGE
C BOARDS AUTOMATICALLY GO BACK UP.
C
  360 NLGDN=0
      NBDSDN=0
      IF(NUMSM.NE.0) GO TO 370
C
C STATEMENT 770 WILL BE OUTSIDE DO LOOP 760 AND WILL COMPUTE INFLOW,
C OUTFLOW AND STORAGE VALUES TO THE END OF THE TIME INTERVAL WHEN NO
C BOARDS ARE DOWN AND A FLOOD GATE IS NOT OPEN.  IF A FLOOD GATE IS
C OPEN, IT WILL BE CLOSED (NGATE=0) TO KEEP FROM LOSING MORE WATER.
C
      IF(NGATE.NE.-999) NGATE=0
      GO TO 770
C
C NEXT STATEMENTS ARE FOR TWO SETS OF BOARDS WITH ALL LARGE BOARDS
C UP.  CHECK IF HINGE ELEVATION (SIGELV(6)) OF SMALL BOARDS IS LESS
C THAN HINGE ELEVATION (SIGELV(2)) FOR LARGE BOARDS.
C
  370 NBDSDN=NSMDN
      IF(NSMDN.EQ.0) GO TO 380
      IF(SIGELV(6).LT.SIGELV(2)) GO TO 390
      NSMDN=0
      NBDSDN=0
  380 IF(NGATE.NE.-999) NGATE=0
      GO TO 770
C
C ROUTING IS REQUIRED.  CALL OSOH26 FOR ROUTING AND STORAGE
C COMPUTATIONS.
C
  390 CALL OSOH26(ANUMLG,ANUMSM,RTQQIM,SUMFRA,QQI1,QQI2,QQIM,QQO1,QQO2,
     $QQOK1,QQOK2,SS1,SS2,SIGELV,SIGSTO,ELVSOH,STOSOH,TMPSOH,O,SOH,
     $ELVLG,QLG,ELVSM,QSM,ELVFL,QFLUD,STOR,ELEV)
C
C BOARDS GOING BACK UP COULD HAVE CHANGED FALLING TO RISING POOL.
C GO BACK TO STATEMENT 260 TO CHECK POOL TENDENCY AND IF POOL WILL GO
C TO ANOTHER CRITICAL LEVEL DURING THE TIME STEP.  IF SO, PROGRAM
C WILL CHANGE BOARD OR GATE CONDITIONS AND AGAIN COMPUTE OR ROUTE
C THROUGH END OF TIME STEP.
C
      GO TO 260
C
C NEXT LOWER CRITICAL ELEVATION FOR FOLLOWING STATEMENTS IS THE
C ELEVATION OF THE TOP OF LARGE BOARDS (AND OF SMALL BOARDS IF AT
C SAME ELEVATION).
C
C400  IF(NBDSDN.EQ.0.AND.NGATE.LE.0) GO TO 770
C
C IF ABOVE IF STATEMENT WAS TRUE, NO BOARDS WERE DOWN AND NO GATE WAS
C OPEN (OR THERE IS NO GATE) SO THE PROGRAM WENT TO 770 TO COMPUTE
C VALUES TO END OF TIME INTERVAL WITH ONLY NON-SPILLWAY OUTFLOW.
C IF BOARDS ARE DOWN, CALL ROUTING ROUTINE TO COMPUTE VALUES FROM
C TIME AT TOP OF BOARDS TO END OF ROUTING STEP.  GO TO 27 TO CALL
C ROUTING ROUTINE.
C
C     IF(NBDSDN.EQ.0) GO TO 410
C     GO TO 440
C
C NO BOARDS ARE DOWN BUT FLOOD GATE IS OPEN.  ADJUST FLOOD GATE
C DISCHARGE TO TRY TO KEEP POOL AT TOP OF BOARDS.  SET NGATE=1 FOR
C PARTLY OPEN GATE AND COMPUTE SS2 AND QQ02.
C
C 410 NGATE=1
C     QGAT1=QQI1-QQOK1
C     QGAT2=QQI2-QQOK2
C     IF(QGAT1.LT.0.) QGAT1=0.
C     IF(QGAT2.LT.0.) QGAT2=0.
C     QQO1=QGAT1+QQOK1
C     QQO2=QGAT2+QQOK2
C     QQOM=(QQO1+QQO2)*0.5
C     SS2=SS1+(QQIM-QQOM)*(1.-SUMFRA)
C     GO TO 260
C
C FOR FOLLOWING STATEMENTS, POOL HAS DROPPED TO HINGE ELEVATION FOR
C SMALL BOARDS AND SMALL BOARDS GO BACK UP.
C
  420 NSMDN=0
      NBDSDN=NLGDN
      IF(NLGDN.EQ.0) GO TO 430
      IF(SIGELV(2).LT.SIGELV(6)) GO TO 440
      NLGDN=0
      NBDSDN=0
  430 IF(NGATE.NE.-999) NGATE=0
      GO TO 770
  440 CALL OSOH26(ANUMLG,ANUMSM,RTQQIM,SUMFRA,QQI1,QQI2,QQIM,QQO1,QQO2,
     $QQOK1,QQOK2,SS1,SS2,SIGELV,SIGSTO,ELVSOH,STOSOH,TMPSOH,O,SOH,
     $ELVLG,QLG,ELVSM,QSM,ELVFL,QFLUD,STOR,ELEV)
      GO TO 260
C
C POOL HAS DROPPED TO TOP ELEVATION FOR SMALL BOARDS.  IF NO BOARDS
C ARE DOWN AND NO FLOOD GATE DISCHARGE, GO TO 770.  IF BOARDS ARE
C DOWN, GO TO 260 TO COMPUTE NEW VALUES OF VARIABLES TO END OF ROUTING
C TIME STEP.
C
C450  IF(NBDSDN.EQ.0.AND.NGATE.LE.0) GO TO 770
C     IF(NBDSDN.GT.0) GO TO 260
C
C NO BOARDS ARE DOWN BUT FLOOD GATE IS OPEN.  GO BACK TO 410 FOR
C COMPUTATIONS.
C
C     GO TO 410
C
C POOL HAS DROPPED TO SPILLWAY CREST ELEVATION FOR FLOOD GATE.  CLOSE
C FLOOD GATE.
C
  460 NGATE=0
      IF(NBDSDN.GT.0) GO TO 260
      GO TO 770
C
C POOL IS RISING FOR FOLLOWING STATEMENTS.
C
  470 JS1=0
      JS2=0
      DO 480 J=1,NRIS
      K=SIGRIS(J)
      IF(SS1.GT.SIGSTO(K)) JS1=J
  480 IF(SS2.GT.SIGSTO(K)) JS2=J
C
C IF JS2 IS 0, SS1 AND SS2 ARE EQUAL TO OR LESS THAN THE LOWEST
C SIGNIFICANT STORAGE.  GO TO 290 TO CALL WARN.
C
      K=SIGRIS(1)
      IF(JS2.NE.0 .OR. NBDSDN.GT.0) GO TO 490
      GO TO 290
C
C IF JS1=JS2 OR JS1=NRIS THE POOL DOES NOT RISE TO THE NEXT CRITICAL
C LEVEL.  GO TO 25 TO SET VALUES FOR NEXT LOOP OF DO LOOP 50.
C
C 490 IF(JS1.EQ.JS2.OR.JS1.EQ.NRIS) GO TO 740
  490 IF(JS1.GE.NRIS .AND. NBDSDN.LE.0) THEN
        NSMDN=NUMSM
        NLGDN=NUMLG
        NBDSDN=NUMSM+NUMLG
        GO TO 740
      END IF
      IF(JS1.EQ.JS2) GO TO 740
C
C POOL RISES TO NEXT CRITICAL LEVEL FOR NEXT STATEMENTS.  COMPUTE
C FRACTION (FRACST) OF TIME STEP FOR POOL TO RISE TO CRITICAL LEVEL
C ASSUMING STRAIGHT LINE FROM SS1 TO SS2.
C
      IF((SS2-SS1).EQ.0.) GO TO 491
      K=SIGRIS(JS1+1)
C     K=SIGRIS(JS2)
      FRACST=(SIGSTO(K)-SS1-SIGSTO(K)*SUMFRA+SS1*SUMFRA)/(SS2-SS1)
      GO TO 492
  491 FRACST=1.-SUMFRA
  492 SUMFRA=SUMFRA+FRACST
      IF(SUMFRA.LT.0.99) GO TO 500
C
C SET SUMFRA TO 1.0 IF 0.99 OR GREATER TO PREVENT EXCESSIVELY LARGE
C NUMBERS IN ROUTING PROCEDURE AND SET SS2 TO SIGSTO(K)-0.1 SO THAT
C CRITICAL LEVEL WILL BE REACHED IN NEXT LOOP.  GO TO 740 TO SET VALUES
C FOR NEXT LOOP.
C
      SUMFRA=1.0
      SS2=SIGSTO(K)-0.1
      GO TO 740
C
C SET NEW INITIAL VALUES AT SUMFRA FRACTION OF THE ROUTING TIME STEP.
C QQO1 AND QQO2 WILL BE COMPUTED IN OSOH26.
C
  500 QQI1=QQI1+DELQI*FRACST
      QQOK1=QQOK1+DELQOK*FRACST
      QQIM=(QQI1+QQI2)*0.5
      QQOKM=(QQOK1+QQOK2)*0.5
      RTQQIM=QQIM-QQOKM
      SS1=SIGSTO(K)+0.10
C
C THE VARIABLE K SENDS THE PROGRAM TO THE PROPER LOCATION FOR BOARD
C AND GATE CHANGES ACCORDING TO SIGNIFICANT ELEVATIONS.
C
      GO TO (330,330,620,530,330,330,590,530,680,700),K
C
C POOL RISES TO ELEVATION WHERE LARGE OR SMALL BOARDS GO DOWN.
C BOARDS WILL GO DOWN UNTIL INFLOW IS PASSED OR ALL BOARDS ARE DOWN.
C IF ALL BOARDS ARE DOWN AT STATEMENT 550, THE PROGRAM WILL GO TO THE
C CALL ROUTING SUBROUTINE AT STATEMENT 650.  IF ALL BOARDS ARE NOT
C DOWN, OUTFLOWS AND REQUIRED NUMBER OF BOARDS WILL BE COMPUTED.
C
  530 IF(K.EQ.8) GO TO 540
      K54=K
      QTOPLG=0.
      NDOWN=NLGDN
      ELV=SIGELV(4)
      NUMBDS=NUMLG
      CALL NTER26(ELV,QMXLG,ELVLG,QLG,NELVLG,IFLAG,NTERPQ,IBUG)
      QMXBDS=QMXLG
      IF(NUMSM.EQ.0) GO TO 550
      CALL NTER26(ELV,QMXSM,ELVSM,QSM,NELVSM,IFLAG,NTERPQ,IBUG)
      GO TO 550
  540 K58=K
      QTOPSM=0.
      NDOWN=NSMDN
      NUMBDS=NUMSM
      ELV=SIGELV(8)
      CALL NTER26(ELV,QMXSM,ELVSM,QSM,NELVSM,IFLAG,NTERPQ,IBUG)
      QMXBDS=QMXSM
  550 IF(NDOWN.EQ.NUMBDS) GO TO 650
      IF(NGATE.EQ.-999) GO TO 560
      NGATE=2
      CALL NTER26(ELV,QGATMX,ELVFL,QFLUD,NELVFL,IFLAG,NTERPQ,IBUG)
      GO TO 570
  560 QGATMX=0.
C
C COMPUTE DISCHARGE (QBDS2) THROUGH AND OVER SMALL (OR SECOND SET OF
C LARGE) BOARDS IF THERE ARE SMALL OR A SECOND SET OF LARGE BOARDS.
C
  570 QBDS2=0.
      IF(NUMSM.EQ.0.) GO TO 600
      IF(NSMDN.EQ.0) GO TO 590
      ANSMDN=NSMDN
      QBDS2=QMXSM*ANSMDN/ANUMSM
C
C COMPUTE DISCHARGE OVER TOP OF SMALL UPRIGHT BOARDS
C
  590 IF(ELV.LE.SIGELV(7)) GO TO 600
      ANSMDN=NSMDN
      QTOPSM=COEF*DISTSM*(ELV-SIGELV(7))**1.5*(ANUMSM-ANSMDN)/ANUMSM
      QBDS2=QBDS2+QTOPSM
  600 IF(NLGDN.EQ.0) GO TO 620
      ANLGDN=NLGDN
      QBDS2=QMXLG*ANLGDN/ANUMLG+QBDS2
C
C COMPUTE DISCHARGE OVER TOP OF LARGE UPRIGHT BOARDS IF APPLICABLE.
C
  620 IF(ELV.LE.SIGELV(3)) GO TO 630
      ANLGDN=NLGDN
      QTOPLG=COEF*DISTLG*(ELV-SIGELV(3))**1.5*(ANUMLG-ANLGDN)/ANUMLG
      QBDS2=QBDS2+QTOPLG
C
C COMPUTE EXCESS (EXCES1) OF INFLOW OVER OUTFLOW AT BEGINNING OF PARTIAL
C TIME STEP.  IF ALL BOARDS DOWN CANNOT TAKE CARE OF INFLOW AT TIME OF
C QQI1, PUT ALL BOARDS DOWN AND THEN GO TO 650 TO CALL ROUTING ROUTINE.
C
  630 EXCES1=QQI1-QGATMX-QBDS2-QQOK1
      IF(K.EQ.K54) QQ=QTOPLG
      IF(K.EQ.K58) QQ=QTOPSM
      IF(EXCES1.LT.(QMXBDS-QQ)) GO TO 631
      IF(K.EQ.K54) NLGDN=NUMLG
      IF(K.EQ.K58) NSMDN=NUMSM
      GO TO 650
C
C COMPUTE EXCESS (EXCES2) OF INFLOW OVER OUTFLOW AT END OF TIME INTERVAL
C AND THEN GO TO 660 TO COMPUTE NUMBER OF BOARDS DOWN IF ALL BOARDS DO
C NOT NEED TO BE DOWN TO TAKE CARE OF INFLOW.
C
  631 EXCES2=QQI2-QGATMX-QBDS2-QQOK2
      IF(EXCES2.LE.(QMXBDS-QQ)) GO TO 660
C
C ALL BOARDS DOWN CANNOT TAKE CARE OF THE INFLOW AT THE END OF THE
C TIME STEP.  COMPUTE FRACTION (FRACST) OF TIME STEP FROM THIS POINT
C TO TIME EXCESS EQUALS QMXBDS AND TOTAL FRACTION (SUMFRA) OF TIME
C FROM START OF TIME STEP TO TIME EXCESS EQUALS QMXBDS.
C
      IF((EXCES2-EXCES1).EQ.0.) GO TO 632
      FRACST=(1.-SUMFRA)*(QMXBDS-EXCES1)/(EXCES2-EXCES1)
      GO TO 633
  632 FRACST=1.-SUMFRA
  633 SUMFRA=SUMFRA+FRACST
      IF(SUMFRA.LT.0.99) GO TO 640
C
C ASSUME SUMFRA IS 1.0 IF 0.99 OR GREATER, SET SS2 SLIGHTLY ABOVE
C SIGSTO(K) SO THAT THIS CRITICAL LEVEL WILL NOT BE USED AGAIN IN NEXT
C LOOP.  SET BOARDS DOWN TO MAXIMUM NUMBER.  GO TO 740 TO SET NEW
C VALUES FOR NEXT LOOP.
C
      SS2=SIGSTO(K)+0.1
      NDOWN=NUMBDS
      IF(K.EQ.K54)NLGDN=NDOWN
      IF(K.EQ.K58)NSMDN=NDOWN
      QQO2=QGATMX+QBDS2+QQOK2
      GO TO 740
C
C NEW INITIAL VALUES ARE NEEDED AT THE NEW SUMFRA FRACTION OF THE
C TIME STEP AND ROUTING IS NEEDED TO END OF TIME STEP.
C
  640 QQI1=QQI1+DELQI*FRACST
      QQOK1=QQOK1+DELQOK*FRACST
      QQIM=(QQI1+QQI2)*0.5
      QQOKM=(QQOK1+QQOK2)*0.5
      RTQQIM=QQIM-QQOKM
      SS1=SIGSTO(K)+0.10
      NDOWN=NUMBDS
      IF(K.EQ.K54)NLGDN=NUMLG
      IF(K.EQ.K58)NSMDN=NUMSM
C
C GO TO 440 TO CALL OSOH26 AND THEN GO TO 260.
C
  650 GO TO 440
C
C BOARDS WERE STILL GOING DOWN AT THE END OF THE TIME STEP OR THE
C LAST BOARD HAS JUST GONE DOWN.  COMPUTE NUMBER OF BOARDS DOWN, SET
C SS2 SLIGHTLY BELOW SIGSTO(K) SO THAT THIS CRITICAL LEVEL WILL BE
C USED IN NEXT LOOP, AND GO TO 740 TO SET INITIAL VALUES FOR NEXT LOOP.
C END CONTRACTION EFFECTS ON DECREASING DISCHARGE AND FLUCTUATIONS
C ABOVE THE FORECAST INFLOW WILL CAUSE MORE BOARDS TO GO DOWN THAN A
C STRICT PROCEDURE WOULD COMPUTE.  HOWEVER, FLOW OVER TOP OF THE
C BOARDS THAT AREN'T DOWN WOULD CAUSE FEWER BOARDS TO GO DOWN.  SINCE
C THESE EFFECTS TEND TO CANCEL EACH OTHER, FLOW OVER TOP OF THE
C UPRIGHT BOARDS WILL BE IGNORED.  ADD COMPUTED FLOW (QQ) OVER TOP
C OF PREVIOUS NUMBER OF UPRIGHT BOARDS TO EXCES2 IN ORDER TO IGNORE
C FLOW OVER THE NEW COMPUTED NUMBER OF UPRIGHT BOARDS.
C
  660 ANUMBD=NUMBDS
      EXCES2=EXCES2+QQ
      NDOWN=EXCES2*ANUMBD/QMXBDS+1.0
      IF(NDOWN.GT.NUMBDS)NDOWN=NUMBDS
      IF(K.EQ.K54)NLGDN=NDOWN
      IF(K.EQ.K58)NSMDN=NDOWN
      SS2=SIGSTO(K)-0.1
      QQO2=QQI2
      GO TO 740
C
C SPILLWAY CREST ELEVATION FOR FLOOD GATE WAS SURPASSED FOR FOLLOWING
C STATEMENTS.  IF GATE IS OPEN OR BOARDS ARE DOWN, CALL ROUTING
C ROUTINE.  IF GATE IS SHUT, ASSUME IT WILL REMAIN SHUT UNTIL NEXT
C CRITICAL LEVEL IS REACHED.
C
  680 IF(NGATE.LE.0.AND.NBDSDN.EQ.0) GO TO 690
      GO TO 440
C
C SET SS1 SLIGHTLY ABOVE SIGSTO(K) TO KEEP FROM STOPPING AT THIS SAME
C CRITICAL LEVEL AGAIN.
C
  690 SS1=SIGSTO(K)+0.1
      SS2=SS1+QQIM-QQOKM
      GO TO 260
C
C FOR FOLLOWING STATEMENTS THE POOL HAS RISEN TO THE ELEVATION WHERE
C THE FLOOD GATE IS NORMALLY OPENED.  A CHECK WAS MADE IN THE
C DECISION SUBROUTINE FOR ENTERING FLSH26 FOR NEED TO OPEN GATE EVEN
C IF THE ELEVATION WAS ABOVE THE NORMAL ELEVATION FOR OPENING THE GATE
C SINCE ADDITIONAL RAIN MIGHT REQUIRE GATE OPENING.  IF THE GATE IS
C ALREADY FULLY OPEN, OR BOARDS ARE DOWN, THE ROUTING ROUTINE WILL
C BE CALLED IN STATEMENT 650.  IF THE GATE IS ALREADY PARTLY OPEN,
C INFLOW MINUS NON-SPILLWAY OUTFLOW WILL BE PASSED IF POSSIBLE.  IF
C THE GATE IS CLOSED, THE DECISION TO OPEN IT WILL DEPEND ON THE
C OPERATOR'S KNOWLEDGE OF FUTURE INFLOWS.  THE USER WILL SPECIFY THE
C NUMBER OF FUTURE TIME INTERVALS (NFUTUR) THAT HE THINKS THE OPERATOR
C HAS A ROUGH IDEA OF INFLOWS.  IF LEAVING THE GATE CLOSED FOR THESE
C TIME INTERVALS WOULD CAUSE THE POOL TO EXCEED ANY TOP OF BOARDS, THE
C GATE WILL BE OPENED TO TRY TO PASS INFLOW.
C
  700 IF(NBDSDN.NE.0.OR.NGATE.EQ.2) GO TO 650
      IF(NGATE.EQ.1) GO TO 720
      TOP=SIGSTO(3)
      IF(TOP.GT.SIGSTO(7)) TOP=SIGSTO(7)
      DIFSTO=TOP-SIGSTO(K)
      SUM=0.
      DO 710 J=1,NFUTUR
      JJ=NS2+J-1
      IF(JJ.GT.NUM) GO TO 710
      SUM=SUM+QIMHYD(JJ)-QOKM
      IF(SUM.GT.DIFSTO) GO TO 720
  710 CONTINUE
C
C GATE IS NOT OPENED AND NO BOARDS ARE DOWN.  SET SS1 SLIGHTLY ABOVE
C SIGSTO(K), COMPUTE SS2 AND GO TO 260.
C
      SS1=SIGSTO(K)+0.1
      SS2=SS1+QQIM-QQOKM
      GO TO 260
C
C GATE IS PARTLY OPEN OR WILL BE PARTLY OPEN BUT NO BOARDS ARE DOWN.
C COMPUTE MAXIMUM GATE DISCHARGE AT SIGELV(K) AND SEE IF DESIRED GATE
C DISCHARGE TO PASS INFLOW IS POSSIBLE THROUGH END OF TIME STEP.
C
  720 ELV=SIGELV(K)
      CALL NTER26(ELV,QGATMX,ELVFL,QFLUD,NELVFL,IFLAG,NTERPQ,IBUG)
      QTST1=QQI1-QQOK1
      QTST2=QQI2-QQOK2
      IF(QTST1.LT.0.) QTST1=0.
      IF(QTST2.LT.0.) QTST2=0.
C
C OPEN GATE FULLY IN STATEMENT 730 IF EITHER QTST1 OR QTST2 IS GREATER
C THAN MAXIMUM GATE DISCHARGE AND CALL ROUTING ROUTINE IN 650.  IF
C GATE NOT OPENED FULLY, SET SS1 AND COMPUTE SS2.
C
      IF(QTST1.GT.QGATMX.OR.QTST2.GT.QGATMX) GO TO 730
      SS1=SIGSTO(K)+0.1
      SS2=SS1+QQIM-QQOKM-(QTST1+QTST2)*0.5
      GO TO 260
  730 NGATE=2
      GO TO 650
C
C FOLLOWING STATEMENTS SET VALUES FOR THE NEXT LOOP IN DO LOOP 760
C AND SET PEAK VALUES BETWEEN REGULAR TIME INTERVAL VALUES.
C
  740 QQI1=QQI2
      QQOK1=QQOK2
      QQO1=QQO2
      SS1=SS2
      SUMFRA=0.
      IF(NOSTEP.EQ.1) GO TO 760
      IF(PTEST.GE.QQO1) GO TO 760
      PTEST=QQO1
      FRACPK=AI/STEPS
      IF(IBUG.NE.2) GO TO 760
      WRITE(IODBUG,750)QQI2,QQOK2,QQO2,SS2,SUMFRA,PTEST,FRACPK
  750 FORMAT(1HO,42H   QQI2,QQOK2,QQO2,SS2,SUMFRA,PTEST,FRACPK/1X,7F12.3
     $)
  760 CONTINUE
      QO2=QQO2
      S2=SS2/STEPS
      GO TO 780
  770 QO2=QQOK2
      SS2=SS1+(QQIM-QQOKM)*(1.-SUMFRA)
      S2=SS2/STEPS
C
C COMPUTE AVERAGE OUTFLOW(QOM) FOR TIME INTERVAL AND PUT PEAK VALUES
C BETWEEN TIME INTERVAL OUTFLOWS IN PEAKO ARRAY IF REQUIRED.  REDUCE
C PTEST BY ONE PERCENT IF HIGHER THAN TESTPK TO KEEP FROM MAKING MINOR
C CHANGES IN REGULAR TIME INTERVAL OUTFLOWS.
C
  780 QOM=S1+QIM-S2
      IF(NOSTEP.EQ.1) GO TO 820
      IF(PTEST.LE.TESTPK) GO TO 820
      TEST=PTEST-0.01*PTEST
      IF(TEST.LE.QO1.OR.TEST.LE.QO2) GO TO 820
      IF(NUMPKO.EQ.-999) NUMPKO=0
      NUMPKO=NUMPKO+1
      IF(NUMPKO.GT.NTOTPK) GO TO 800
      PEAKO(NUMPKO)=PTEST
      IF(NS2.EQ.1) GO TO 790
      IF(FRACPK.GE.0.5) GO TO 790
      PKPOS(NUMPKO)=NS2-1+0.01
      GO TO 820
  790 PKPOS(NUMPKO)=NS2+0.01
      GO TO 820
  800 WRITE(IPR,810) NTOTPK,NUMPKO
  810 FORMAT(1HO,10X,31H**WARN**THE NUMBER OF POSITIONS, I6,87H IN THE
     $PEAKO ARRAY IN FLSH26 IS INSUFFICIENT TO HOLD THE REQUIRED NO.
     $OF POSITIONS, I6)
      CALL WARN
C
C IF A TOTAL DISCHARGE (TOTALQ) VS MAXIMUM GENERATION DISCHARGE (QGEN)
C RELATION IS USED, A REVISED VALUE OF MAXIMUM GENERATION DISCHARGE
C WILL BE COMPUTED IF THIS IS THE FIRST RUN (NTRIAL) THROUGH FLSH26.
C IF THE REVISED VALUE IS DIFFERENT FROM THE FIRST VALUE MORE THAN
C PCTERF*QO2, A SECOND RUN WILL BE MADE.  IF THE TOTALQ VS QGEN
C RELATION IS NOT USED, NTRIAL HAS ALREADY BEEN SET TO 2 AND THE
C PROGRAM GOES TO THE COMPUTE ELEVATION STATEMENT 77.
C
  820 IF(NTRIAL.EQ.2) GO TO 840
      ESTQOK=QOK2
      CALL TERP26(QO2,QOGEN,TOTALQ,QGEN,NQGEN,IFLAG,IBUG)
      QOK2=QOGEN+SLUICE
      IF(ABS(QOK2-ESTQOK).GT.(PCTERF*QO2)) GO TO 10
C
C FOLLOWING TWO STATEMENTS APPLY TO TIME INTERVALS PRIOR TO FORECAST
C TIME.  SET QO2 TO OBSERVED VALUE (OBSQO2) IF OBSERVED OR TO QOM IF
C NOT OBSERVED.
C
  830 QO2=QOM
      IF(OBSQO2.NE.-999.0.AND.IFCST.EQ.0.AND..NOT.FCST) QO2=OBSQO2
C
C CHECK IF ELEVATION WAS OBSERVED IN ADJUSTED RUN.  IF NOT, COMPUTE
C ELEVATION.
C
  840 IF(IFCST.EQ.0) GO TO 850
      IF(FCST) GO TO 850
  841 IF(OBSS2.NE.-999.0) GO TO 842
      S2=S1+QIM-QOM
  842 IF(ELEV2.NE.-999.0) GO TO 860
  850 CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C
C SET GOFLSH FOR NEXT TIME INTERVAL.
C
  860 IF(FCST) GO TO 861
      IF(NGATE.EQ.-999) GO TO 862
      IF(GATOP2.LE.0.) NGATE=0
C
C IF GATE OPENING BRINGS THE BOTTOM OF THE GATE ABOVE THE WATER SURFACE,
C THE GATE WILL BE CONSIDERED FULLY OPEN (NGATE=2).
C
      DIF=ELEV1-SIGELV(9)
      IF(GATOP2.GT.0..AND.GATOP2.LT.DIF) NGATE=1
      IF(GATOP2.GE.DIF) NGATE=2
  861 IF(NGATE.GT.0) GO TO 863
  862 IF(NLGDN.GT.0) GO TO 863
      IF(NUMSM.GT.0.AND.NSMDN.GT.0) GO TO 863
      GOFLSH=.FALSE.
      GO TO 864
  863 GOFLSH=.TRUE.
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
  864 IF(IBUG-1) 1080,1060,870
  870 WRITE(IODBUG,880)
  880 FORMAT(1H0,60H VALUES OF VARIABLES AT THE END OF FLSH26 ARE AS
     $FOLLOWS:)
      WRITE(IODBUG,50) IFCST,FCST,GOFLSH,BGNGAT,GATMAX,NS2,NUMLG,NUMSM,
     $NLGDN,NSMDN,NGATE,NFUTUR,COEF,DISTLG,DISTSM,QI1,QI2,QO1,QO2,QOM,
     $QOK1,QOK2,S1,S2,ELEV1,ELEV2,TESTPK,NTOTPK,NOSTEP,NLGSOH,NSMSOH,
     $NGASOH,NPOSOH,NTERPQ,QGENMX,SLUICE,PCTERF,NUM,NTERP,IBUG
      WRITE(IODBUG,890)(SIGELV(I),SIGSTO(I),I=1,NSIGEL)
  890 FORMAT (1H0,82H ALTERNATING VALUES OF SIGNIFICANT ELEVATIONS
     $AND CORRESPONDING STORAGES ARE /(1X,10F12.3))
      WRITE(IODBUG,900)(SIGFAL(I),I=1,NFAL)
  900 FORMAT(1H0,83H POSITION NUMBERS IN SIGELV ARRAY OF SIGNIFICANT
     $ELEVATIONS FOR FALLING POOL ARE/(1X,20I5))
      WRITE (IODBUG,910)(SIGRIS(I),I=1,NRIS)
  910 FORMAT(1H0,82H POSITION NUMBERS IN SIGELV ARRAY OF SIGNIFICANT
     $ELEVATIONS FOR RISING POOL ARE/(1X,20I5))
      IF(NGATE.EQ.-999) GO TO 930
      JEND=NS2+NFUTUR-1
      WRITE(IODBUG,920)(QIMHYD(I),I=NS2,JEND)
  920 FORMAT(1H0,103HFUTURE MEAN TIME INTERVAL INFLOWS INCLUDING PRESENT
     $TIME INTERVAL FOR DECISION ON OPENING FLOOD GATE ARE/(1X,10F12.3))
  930 IF(NUMPKO.EQ.-999) GO TO 950
      WRITE(IODBUG,940)(PEAKO(I),PKPOS(I),I=1,NUMPKO)
  940 FORMAT(1H0,111HALTERNATING VALUES OF PEAK DISCHARGES BETWEEN
     $REGULAR TIME INTERVAL VALUES AND CORRESPONDING POSITION NOS./  69H
     $IN TIME INTERVAL OUTFLOW ARRAY WHERE PEAK VALUES WILL BE PLACED
     $ARE/(1X,7(F12.3,F5.1)))
  950 WRITE(IODBUG,960)(ELVSOH(I),STOSOH(I),I=1,NELSOH)
  960 FORMAT(1H0,107HALTERNATING VALUES OF ELEVATIONS FOR USE IN DEVELOP
     $ING ROUTING RELATION AND CORRESPONDING STORAGES IN UNITS/1X, 38HOF
     $ MEAN DISCHARGE FOR ROUTING STEP ARE/(1X,10F12.3))
      IF(FCST) GO TO 962
      IF(NGATE.EQ.-999) GO TO 962
      WRITE(IODBUG,961) GATOP2
  961 FORMAT(1H0,25H OBSERVED GATE OPENING IS,1X,F12.3)
  962 WRITE(IODBUG,970)(O(I),SOH(I),I=1,NOSOH)
  970 FORMAT(1H0,116HALTERNATING VALUES OF OUTFLOW AND STORAGE PLUS
     $OUTFLOW/2 WITH STORAGE IN UNITS OF DISCHARGE FOR ROUTING STEP ARE/
     $(1X,10F12.3))
      WRITE(IODBUG,980)(ELVLG(I),QLG(I),I=1,NELVLG)
  980 FORMAT(1H0,112H ALTERNATING VALUES OF ELEVATION AND CORRESPONDING
     $ DISCHARGE FOR LARGE- BOARD SPILLWAY WITH ALL BOARDS DOWN ARE/(1X,
     $10F12.3))
      IF(NUMSM.EQ.0) GO TO 1000
      WRITE(IODBUG,990)(ELVSM(I),QSM(I),I=1,NELVSM)
  990 FORMAT(1H0,112H ALTERNATING VALUES OF ELEVATION AND CORRESPONDING
     $ DISCHARGE FOR SMALL- BOARD SPILLWAY WITH ALL BOARDS DOWN ARE/(1X,
     $10F12.3))
 1000 IF(NGATE.EQ.-999) GO TO 1020
      WRITE(IODBUG,1010)(ELVFL(I),QFLUD(I),I=1,NELVFL)
 1010 FORMAT(1H0,92H ALTERNATING VALUES OF POOL ELEVATION AND
     $DISCHARGE FOR FLOOD GATE FULLY OPEN ARE /(1X,10F12.3))
 1020 IF(NQGEN.EQ.0) GO TO 1040
      WRITE(IODBUG,1030)(TOTALQ(I),QGEN(I),I=1,NQGEN)
 1030 FORMAT(1H0,100HALTERNATING VALUES OF TOTAL DISCHARGE AND
     $MAXIMUM GENERATION (AND/OR SLUICE) DISCHARGE ARE /(1X,10F12.3))
 1040 WRITE(IODBUG,1050)(STOR(I),ELEV(I),I=1,NSE)
 1050 FORMAT(1H0,94H ALTERNATING VALUES OF STORAGE IN UNITS OF
     $TIME-INTERVAL MEAN DISCHARGE AND ELEVATION/43H FOR STORAGE VS
     $ELEVATION RELATION ARE /(1X,10F12.3))
 1060 WRITE(IODBUG,1070)
 1070 FORMAT(1H0,10X,18H**  LEAVING FLSH26)
 1080 RETURN
      END
