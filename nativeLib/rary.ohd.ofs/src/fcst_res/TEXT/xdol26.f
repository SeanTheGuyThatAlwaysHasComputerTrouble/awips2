C MEMBER XDOL26
C  (from old member FCXDOL26)
C
C DESC ASSIGN WORK SPACE LOCATIONS AND AMOUNTS FOR EX26.
C----------------------------------------------------------------
      SUBROUTINE XDOL26(PO,CO,D,W,IDPT,LOCWS,LOCOWS)
C-----------------------------------------------
C  SUBROUTINE TO ASSIGN WORK SPACE FOR VARIOUS TASKS WITHIN THE
C  EXECUTION ROUTINE OF OPERATION 26 (RES-SNGL).
C
C  THE FIRST THREE ALLOCATIONS ARE REQUIRED FOR ANY COMBINATION OF
C  SCHEMES AND UTILITIES DEFINED AND EXECUTED. THE LAST SIX ARE
C  DEPENDENT ON THE SCHEMES OR UTILITIES DEFINED.
C
C  ALL THE WORK SPACE HAS BEEN ALLOCATED DURING SET-UP (I.E. WHEN THE
C  PIN ROUTINE WAS CALLED), IT IS XDOL26'S JOB TO SET ASIDE AND MARK THE
C  LOCATION OF THAT SPACE.
C
C--------------------------------------------------------------------
C  JTOSTROWSKI - JUNE 1983 - HRL
C--------------------------------------------------------------------
C
      INCLUDE 'common/fprog'
      INCLUDE 'common/resv26'
      INCLUDE 'common/exg26'
      INCLUDE 'common/xqin26'
      INCLUDE 'common/fdbug'
C
      DIMENSION PO(1),CO(1),W(1),LOCWS(1),LOCOWS(1),D(1),IDPT(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/xdol26.f,v $
     . $',                                                             '
     .$Id: xdol26.f,v 1.2 1999/04/23 16:53:24 page Exp $
     . $' /
C    ===================================================================
C
C
      IF (IBUG.GE.1) WRITE(IODBUG,900)
  900 FORMAT(10X,'*** ENTER XDOL26 ***')
C
C----------------------------
C  SET INITIAL VALUES AND COUNTERS
C
      LWSUSE = 0
      DO 2 I=1,13
      LOCOWS(I) = 0
    2 CONTINUE
C
C-------------------------------
C  GIVE OUT THE REQUIRED WORK SPACE FIRST
C
C  GET A COUNT OF ALL DEFINED S/U'S FOR THE OPERATION DEFINITION
C  THE FIRST PART OF THE WORK SPACE HOLDS INFO FOR ALL THE S/U'S USED
C  IN THIS OPERATION DEFINITION. THE CONTENTS OF THIS SECTION ARE:
C    1) THE S/U NUMBER,
C    2) THE ELEMENT IN THE IDPT ARRAY HOLDING THE LOCATION OF THE START
C       OF TIME-SERIES DATA FOR THE FIRST TIME-SERIES USED FOR THIS S/U
C       (IF ANY),
C    3) AN EXECUTION FLAG TO INDICATE IF THE S/U WAS EXECUTED DURING A
C       TIME PERIOD (USED FOR CARRYOVER UPDATING CONTROL).
C
      IF (NSUDEF .EQ. 0) GO TO 11
      DO 10 I=1,NSUDEF
      LOC = (I-1)*3 + 1
      W(LOC) = PO(LOCPTR + (I-1)*4 + 1)
      W(LOC+2) = 0.01
   10 CONTINUE
C
C  SET THE TIME-SERIES DATA POINTER LOCATIONS FOR EACH S/U
C
      CALL XTSS26(PO,W)
C
      LOCWS(1) = 1
      LWSUSE = LWSUSE + NSUDEF*3
C
C---------------------------
C  THE NEXT PORTION OF WORK SPACE IS USED FOR HOLDING SIMULATED TRACES
C  FOR INSTANTANEOUS AND MEAN DISCHARGE, POOL ELEVATION AND STORAGE
C  CONTENTS.
C  ENOUGH SPACE IS NEEDED TO HOLD A VALUE FOR EACH QUANTITY FOR EACH
C  PERIOD IN THE RUN.
C  EACH QUANTITY WILL BE FILLED CONTIGUOUSLY FOR TIME-SERIES PURPOSES.
C
   11 CONTINUE
      LOCWS(2) = LWSUSE + 1
      LWSUSE = LWSUSE + 4*NDD*NTIM24
C
C  INITIALIZE SPACE TO 'MISSING' CONDITION.
C
      NDO = 4*NDD*NTIM24
      DO 15 I=1,NDO
      W(LOCWS(2)+I-1) = -999.0
   15 CONTINUE
C
C-----------------------------------
C  THE NEXT PORTION WILL HOLD INFO FOR THE SCHEMES ONLY. WE NEED SIX
C  WORDS FOR EACH SCHEME PLUS ONE TO HOLD THE NUMBER OF SCHEMES.
C   (AN EXCEPTION IS FOR THE SETMIN AND SETMAX UTILITIES. SINCE THEY SET
C    VALUES FOR COMPUTATIONAL OUTPUT, THEY WILL BE CONSIDERED SCHEMES FO
C    THE PURPOSE OF TEMPORARILY STORING THESE VALUES.)
C  THE VALUES HELD IN THIS SECTION ARE THE NO.(1-24) OF THE SCHEME
C  EXECUTED, THE DISCHARGES (BOTH MEAN AND INSTANTANEOUS),
C  THE POOL ELEVATION (BOTH BEG. AND END OF PERIOD), AND STORAGE FOR
C  EACH SCHEME EXECUTED WITHIN A TIME STEP. THIS ALLOWS FOR
C  CHECKING FOR EXECUTION OF AT LEAST ONE SCHEME.
C
C  WE'VE ADDED 2 SCHEMES IN THE CASE OF MULTIPLE EXECS OF THE SAME
C  SCHEME.
C
      LOCWS(3) = LWSUSE + 1
      NSCH = 0
      IF (NSUDEF .EQ. 0) GO TO 25
      DO 20 I=1,NSUDEF
      LOC = (I-1)*3 + 1
      IBASE = IFIX(W(LOC))/10
      IF (IBASE.LT.150.OR.IBASE.EQ.159.OR.IBASE.EQ.160) NSCH = NSCH + 1
   20 CONTINUE
C
   25 CONTINUE
      NSCH = NSCH + 3
      W(LOCWS(3)) = NSCH + 0.01
      LWSUSE = LWSUSE + NSCH*6 + 1
C
C---------------------------------------------------------------
C  NEXT PORTION OF THE WORK SPACE IS RESERVED FOR CARRYOVER STORAGE.
C  RES-SNGL NEEDS TO KEEP A RECORD OF CARRYOVER FOR INTERNAL USE FOR
C  EACH TIME INTERVAL.
C  WE NEED AN AMOUNT EQUAL TO THE NUMBER OF CARRYOVER VALUES USED BY
C  THIS DEFINITION OF THE OPERATION. (HELD IN 15TH POSITION OF THE PO
C  ARRAY)
C
      LOCWS(4) = LWSUSE + 1
C
      NUMCO = PO(15)
      LWSUSE = LWSUSE + NUMCO
C
C  TRANSFER CARRYOVER TO WORK ARRAY.
C
      CALL UMEMOV(CO,W(LOCWS(4)),NUMCO)
C
C---------------------------------------------------------------------
C  NEXT PORTION IS TO HOLD FLAGS AND OTHER INFO FOR DEFAULT CARRYOVER
C  ASSIGNMENT. THIS INFO WILL BE STORED IN SUBROUTINE XDCO26 AND WILL
C  BE USED IN SUBROUTINE XCOU26 (AFTER EACH TIME PERIOD IN RUN).
C
C  IF A SPILLWAY OPERATION IS DEFINED (SU/10 = 106) WE NEED TO SAVE
C  ASIDE THREE EXTRA SPACES FOR ITS INFO.
C  IF 'STPOOLQ' IS DEFINED (SU/10 = 108), WE NEED TO SET ASIDE SEVEN
C  EXTRA SPACES FOR CO DEFAULT INFO.
C  IF 'FLASHBDS' IS DEFINED (SU/10 = 112), WE NEED TO SET ASIDE FOUR
C  EXTRA SPACES FOR CO DEFALUT INFO.
C
      IF (NSUDEF .EQ. 0) GO TO 500
      LOCWS(5) = LWSUSE + 1
      NXTRA = 0
C
      DO 50 I=1,NSUDEF
      LOC = (I-1)*3 + 1
      IBASE = IFIX(W(LOC))/10
      IF (IBASE.EQ.106) NXTRA = NXTRA + 3
      IF (IBASE.EQ.108) NXTRA = NXTRA + 7
      IF (IBASE.EQ.112) NXTRA = NXTRA + 4
   50 CONTINUE
      LWSUSE = LWSUSE + NSUDEF + NXTRA
C
C-----------------------------------------------------
C  NEXT PORTION OF WORK SPACE IS GIVEN TO THE OPTIONAL NEEDS. THEY ARE:
C   1) 'BACKFLOW' COMPUTATIONS. NEED TO STORE ADJUSTED INST. AND MEAN
C       VALUES.
C   2) 'RULECURVE' STORAGE OR ADD'L WORK SPACE FOR 'ADJUST'.
C   3) 'SUMINF' INFLOW VOLUME STORAGE
C   4) INTERIM PEAK DISCHARGE VALUE STORAGE OR ADD'L WORK SPACE FOR
C      'BACKFLOW' OR 'ADJUST'.
C   5) 'RAINEVAP'. NEED TO STORE AREA VS. ELEVATION CURVE, ADD'L FLOW
C       CONTRIBUTED BY RAINEVAP, EVAP TIME SERIES, AND TEMPORARY HOLDING
C       SPACE FOR CARRYOVER.
C   6) 'ADJUST RULECURVE' MISSING OBS. AND DEVIATION STORAGE.
C   7) 'ELEV VS. MAXQ' STORAGE, STORAGE VALUES FOR 'POOLQ', OR
C       STORAGE VALUES FOR COMBINED FLASHBOARD RATING.
C   8) 'TOTALQ VS. MAX GENQ' STORAGE, ENTRY INTO SURCHARGE CURVE, OR
C       ADD'L WORK SPACE FOR 'MINQ'.
C   9) TOTAL SPILLWAY RATING FOR FLASHBOARDS OR RULE CURVE STORAGE
C      FOR 'POWERGEN'.
C  10) MODIFIED PULS ROUTING CURVE (COMPUTED FROM RATING CURVES) FOR
C      'FILLSPILL', 'SPILLWAY', 'MINQ' AND 'FLASHBDS'.
C  11) RULE CURVE STORAGES, AND RUNNING TIME SERIES FOR MIN AND MAX
C      ALLOWABLE DISCHARGES FOR 'POWERGEN'.
C  12) OUTFLOW AND INFLOW TIME SERIES HOLDING AREA AND ADD'L WORK SPACE
C      FOR LAG/K ROUTING FOR 'STPOOLQ'.
C  13) INFLOW HOLDING SPACE IF 'SUMINF', 'MINQ', 'POWERGEN', 'FLASH-
C      BOARDS', OR 'RULEADJ' ARE USED.
C
C------------------------------------------------------
C  FIRST LOOP THRU ALL SU'S LOOKIN FOR NEED FOR OPTIONAL WORK SPACE.
C  THE NEEDS ARE SIGNALLED BY S/U NUMBERS:
C  OPTION 1)  SU/10 = 155
C         2)  SU/10 = 104, 107, 108, 109, 111, 113, OR 151 (154 FOR
C                     'ADJUST')
C         3)  SU/10 = 152
C         4)  SU/10 = 105, 106, 109, OR 112 ( 155 FOR 'BACKFLOW',
C                     154 FOR 'ADJUST'.)
C         5)  SU/10 = 153
C         6)  SU/10 = 151
C         7)  SU/10 = 105, 106, 109, 110, 112, OR 157 (107 FOR
C                     'POOLQ')
C         8)  SU/10 = 105, 106, OR 112 (111 FOR SURCHARGE ENTRY CURVE,
C                     109 FOR MINQ WORK SPACE)
C         9)  SU/10 = 112 OR 113
C        10)  SU/10 = 105, 106, 109, OR 112
C        11)  SU/10 = 113
C        12)  SU/10 = 108
C        13)  SU/10 = 109, 112, 113, 151 OR 152
C
      DO 110 I=1,NSUDEF
      LOC = (I-1)*3 + 1
      NSC = W(LOC)/10
      IF (NSC.EQ.155) LOCOWS(1) = 1
      IF (NSC.EQ.104.OR.NSC.EQ.107.OR.NSC.EQ.108.OR.NSC.EQ.109.OR.
     . NSC.EQ.111.OR.NSC.EQ.113.OR.NSC.EQ.151.OR.NSC.EQ.154)
     .     LOCOWS(2) = 1
      IF (NSC.EQ.152) LOCOWS(3) = 1
      IF (NSC.EQ.105.OR.NSC.EQ.106.OR.NSC.EQ.109.OR.NSC.EQ.112.OR.
     .  NSC.EQ.155.OR.NSC.EQ.154) LOCOWS(4) = 1
      IF (NSC.EQ.153) LOCOWS(5) = 1
      IF (NSC.EQ.151) LOCOWS(6) = 1
      IF (NSC.EQ.105.OR.NSC.EQ.106.OR.NSC.EQ.107.OR.NSC.EQ.109.OR.NSC
     . .EQ.110.OR.NSC.EQ.112.OR.NSC.EQ.157) LOCOWS(7) = 1
      IF (NSC.EQ.105.OR.NSC.EQ.106.OR.NSC.EQ.109.OR.NSC.EQ.111.
     . OR.NSC.EQ.112) LOCOWS(8) = 1
      IF (NSC.EQ.112.OR.NSC.EQ.113) LOCOWS(9) = 1
      IF (NSC.EQ.105.OR.NSC.EQ.106.OR.NSC.EQ.109.OR.NSC.EQ.112)
     .  LOCOWS(10) = 1
      IF (NSC.EQ.113) LOCOWS(11) = 1
      IF (NSC.EQ.108) LOCOWS(12) = 1
      IF (NSC.EQ.109.OR.NSC.EQ.112.OR.NSC.EQ.113.OR.NSC.EQ.151.OR.
     .    NSC.EQ.152) LOCOWS(13) = 1
  110 CONTINUE
C
C--------------------------------------------
C  IF A 'BACKFLOW' IS FOUND, WE MUST SET ASIDE TWO WORDS FOR EVERY RUN
C  PERIOD. ONE FOR MEAN FLOWS AND THE OTHER FOR INSTANTANEOUS.
C
      IF (LOCOWS(1).EQ.0) GO TO 120
      LOCOWS(1) = LWSUSE + 1
      LWSUSE = LWSUSE + 2*NDD*NTIM24
C
C---------------------------------------------
C  IF 'RULECURVE' VALUES MUST BE STORED, WE NEED ONE VALUE PER TIME
C  PERIOD.(ALSO USED AS WORK SPACE FOR ADJUST)
  120 CONTINUE
      IF (LOCOWS(2).EQ.0) GO TO 130
C
      LOCOWS(2) = LWSUSE + 1
      LWSUSE = LWSUSE + NDD*NTIM24
C
C--------------------------------------------
C  IF AN INFLOW SUMMATION UTILITY IS FOUND, WE NEED A VALUE PER TIME
C  PERIOD.
C
  130 CONTINUE
      IF (LOCOWS(3).EQ.0) GO TO 140
C
      LOCOWS(3) = LWSUSE + 1
      LWSUSE = LWSUSE + NDD*NTIM24
C
C-------------------------------------------
C  IF WE HAVE NEED TO STORE INTERIM PEAK VALUES, WE NEED TO STORE TWO
C  VALUES PER TIME PERIOD.
C  ALSO, SET THE NO. OF SPACES AVAILABLE FOR HOLDING THESE INTERIM
C  VALUES. KEPT IN /RESV26/ AS NTOTPK.
C  THIS SPACE ALSO USED FOR ADD'L WORK SPACE IN 'BACKFLOW' OR 'ADJUST'.
C
  140 CONTINUE
      IF (LOCOWS(4).EQ.0) GO TO 150
C
      LOCOWS(4) = LWSUSE + 1
      LWSUSE = LWSUSE + 2*NDD*NTIM24
      NTOTPK = NDD*NTIM24
C
C-----------------------------------------------
C  IF A RAIN/EVAP UTILITY WAS SPECIFIED, WE NEED TO STORE ENOUGH VALUES
C  THAT DEFINED THE STORAGE CURVE, TO HOLD THE ADD'L FLOW TS, A DAILY
C  EVAP TIME-SERIES, AND TO TEMPORARILY STORE CARRYOVER.
C
  150 CONTINUE
      IF (LOCOWS(5).EQ.0) GO TO 160
C
      LOCOWS(5) = LWSUSE + 1
      LWSUSE = LWSUSE + NDD*NTIM24 + 2*NSE + NDD + NUMCO
C
C-------------------------------------------------------------------
C  IF 'ADJUST RULECURVE' UTILITY IS USED, WE NEED TO STORE MISSING VALUE
C  AND DEVIATION ARRAYS, WE NEED ENOUGH SPACE FOR TWO VALUES PER TIME
C  PERIOD
C
  160 CONTINUE
      IF (LOCOWS(6).EQ.0) GO TO 170
C
      LOCOWS(6) = LWSUSE + 1
C
C  INITIALIZE SPACE TO -999.0
C
      NUM6 = 2*NDD*NTIM24
      DO 165 I=1,NUM6
      W(LWSUSE+I) = -999.0
  165 CONTINUE
C
      LWSUSE = LWSUSE + NUM6
C
C---------------------------------------------------------------------
C  IF WE NEED TO STORE AN ELEV VS. MAXQ CURVE (OR 'POOLQ' STORAGES),
C  RESERVE SPACE EQUAL TO THE NUMBER OF PAIRS OF VALUES IN THE ELEV
C  VS. STORAGE CURVE.
C
  170 CONTINUE
      IF (LOCOWS(7).EQ.0) GO TO 180
C
      LOCOWS(7) = LWSUSE + 1
      LWSUSE = LWSUSE + NSE
      GO TO 180
C
C---------------------------------------------------------------------
C  IF WE NEED TO STORE A TOTAL DISCHARGE VS. MAXIMUM GENERATION Q
C  CURVE (OR SURCHARGE ENTRY CURVE), RESERVE TWO TIMES THE NUMBER OF
C  PAIRS OF VALUES IN THE ELEV VS. STORAGE CURVE.
C
  180 CONTINUE
      IF (LOCOWS(8).EQ.0) GO TO 190
C
      LOCOWS(8) = LWSUSE + 1
      LWSUSE = LWSUSE + 2*NSE
C
C----------------------------------------------------
C  FLASHBOARDS WERE FOUND. NEED TO SET ASIDE SPACE FOR THE COMBINED
C  SPILLWAY RATING. NEED AS MUCH SPACE AS NEEDED TO STORE HALF
C  THE STORAGE - ELEVATION CURVE.
C
  190 CONTINUE
      IF (LOCOWS(9).EQ.0) GO TO 200
C
      LOCOWS(9) = LWSUSE + 1
      LWSUSE = LWSUSE + NSE
C
C---------------------------------------------------------------------
C  WE NEED TO STORE MODIFIED PULS CURVES. WE NEED THREE TIMES THE NO. OF
C  VALUES USED TO STORE LARGEST SPILLWAY RATING CURVE. MUST FIND MAX
C  NO. OF VALUES NEEDED.
C
  200 CONTINUE
      IF (LOCOWS(10).EQ.0) GO TO 210
C
      CALL XMAX26(PO,MAX)
      MAXVAL = MAX0(MAX,NSE)
C
      LOCOWS(10) = LWSUSE + 1
      LWSUSE = LWSUSE + 3*MAXVAL
C
C---------------------------------------------------------------
C  NEED TO STORE RULE CURVE STORAGES AND RUNNING TIME-SERIES OF MIN AND
C  MAX DISCHARGES FOR 'POWERGEN'.
C
  210 CONTINUE
      IF (LOCOWS(11).EQ.0) GO TO 220
C
      LOCOWS(11) = LWSUSE + 1
      LWSUSE = LWSUSE + 3*NDD*NTIM24
C
C---------------------------------------------------------------
C  NEED TO SET ASIDE SPACE FOR INFLOW AND OUTFLOW TIME SERIES AND
C  ADD'L WORK SPACE FOR LAG/K ROUTING.
C
  220 CONTINUE
      IF (LOCOWS(12) .EQ. 0) GO TO 230
C
      LOCOWS(12) = LWSUSE + 1
      LWSUSE = LWSUSE + 8*NDD*NTIM24
C
C--------------------------------------------------------------------
C  'MINQ', 'POWERGEN', 'SUMINF', 'FLASHBOARDS', OR 'RULEADJ' ARE USED.
C  NEED HOLDING SPACE FOR
C  BOTH INST. AND MEAN INFLOWS AS THESE S/U'S USE THE ENTIRE ARRAYS
C  AND THE INFLOWS ARE MODIFIED THRU THE COURSE OF THE RUN AND WE NEED
C  A CONSTANT LOCATION FOR THESE S/U'S TO PULL VALUES FROM.
C
  230 CONTINUE
      IF (LOCOWS(13) .EQ. 0) GO TO 500
C
      LOCOWS(13) = LWSUSE + 1
      LWSUSE = LWSUSE + 2*NDD*NTIM24
C
C  TRANSFER VALUES FROM TIME SERIES (CONVERT MEANS FROM CMSD TO TIMD).
C
      LPTQI = LOCOWS(13)
      LPTQIM = LPTQI + NDD*NTIM24
      IOFF = IDOFST * NTIM24
C
      CALL UMEMOV(D(IDPT(1)+IOFF),W(LPTQI),NUM)
C
      DO 235 I=1,NUM
      W(LPTQIM+I-1) = D(IDPT(2)+IOFF+I-1) * NTIM24
  235 CONTINUE
C
C--------------------------------------------------------
C  ALL DONE GIVING OUT WORK SPACE
C
  500 CONTINUE
C
C  PRINT OUT ALLOCATION LOCATIONS FOR REQUIRED AND OPTIONAL WORK SPACE
C
      LWK = PO(16)
      IF (IBUG.GE.2) WRITE(IODBUG,1690) LWSUSE,LWK,(LOCWS(K),K=1,5),
     .               (LOCOWS(K),K=1,13)
 1690 FORMAT(1H0,' NO. OF WORDS NEEDED FOR WORK SPACE = ',I4,' PO(16) ='
     .,I5/,5X,'LOCATIONS FOR REQUIRED WORK SPACE = ',5I5,/
     .5X,'              OPTIONAL WORK SPACE = ',13I5)
C
      IF (ITRACE.GE.1) WRITE(IODBUG,901)
  901 FORMAT(11X,'*** EXIT XDOL26 ***')
C
      RETURN
      END
