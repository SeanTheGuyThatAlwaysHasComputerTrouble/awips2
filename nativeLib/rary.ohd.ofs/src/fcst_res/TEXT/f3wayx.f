C MEMBER F3WAYX
C  (from old member FCF3WAYX)
C
      SUBROUTINE F3WAYX(X,Y,Z,XX,YY,ZZ,NY,NZ,IBUG)
C
C SUBROUTINE F3WAYX INTERPOLATES THE X VALUE IN A THREE-WAY RELATION
C WHERE YY (ORDINATE VALUES) AND ZZ (PARAMETER VALUES) ARE SINGLE ARRAYS
C AND XX (ABSCISSA VALUES) IS A DOUBLE ARRAY.  EACH PARAMETER (ZZ) VALUE
C HAS AN ASSOCIATED CURVE DEFINED BY A SET OF YY VS XX VALUES.  YY
C VALUES MUST BE THE SAME FOR ALL ZZ CURVES.  Y AND Z ARE KNOWN ORDINATE
C AND PARAMETER VALUES.
C
C THIS SUBROUTINE WAS ADAPTED FROM INTRP3 PROGRAMMED BY JOE OSTROWSKI --
C OFFICE OF HYDROLOGY.  ADAPTED VERSION BY:
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     JUNE, 1982
C
C  MODIFIED TO REARRANGE THE XX ARRAY FROM (NZ,NY) TO (NY,NZ) FOR MORE
C  STRAIGHTFORWARD INPUT AND DISPLAY PURPOSES - (JTO - 1/84)
C
C SUBROUTINE F3WAYX IS IN
C
C VARIABLES IN THE ARGUMENT LIST ARE DEFINED AS FOLLOWS:
C     X -- UNKNOWN ABSCISSA VALUE.
C     Y -- KNOWN ORDINATE VALUE.
C     Z -- KNOWN PARAMETER VALUE.
C     XX -- DOUBLE ARRAY OF ABSCISSA VALUES WITH DIMENSIONS OF (NY,NZ).
C     YY -- SINGLE ARRAY OF ORDINATE VALUES.  YY VALUES ARE THE SAME FOR
C       ALL PARAMETER (ZZ) CURVES.
C     ZZ -- SINGLE ARRAY OF PARAMETER VALUES.  EACH ZZ CURVE IS DEFINED
C       BY THE YY VALUES AND DIFFERENT SETS OF XX VALUES.
C     NY -- NO. OF VALUES IN YY ARRAY.
C     NZ -- NO. OF VALUES IN ZZ ARRAY.
C
      DIMENSION ZZ(1),YY(1),XX(NY,1)
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/f3wayx.f,v $
     . $',                                                             '
     .$Id: f3wayx.f,v 1.1 1995/09/17 19:05:36 dws Exp $
     . $' /
C    ===================================================================
C
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF (IBUG.EQ.1) GO TO 10
      IF (IBUG.GE.5) GO TO 20
      GO TO 50
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** F3WAYX ENTERED)
      WRITE(IODBUG,40) Y,Z
   40 FORMAT(1H0,12H Y AND Z ARE,2F12.3,22H.  X WILL BE COMPUTED.)
C
C BRACKET THE KNOWN Y VALUE.
C
   50 DO 60 I=2,NY
      IF(YY(I).GT.Y) GO TO 70
   60 CONTINUE
      I=NY
   70 Y2=YY(I)
      Y1=YY(I-1)
C
C BRACKET THE KNOWN Z VALUE.
C
      DO 80 J=2,NZ
      IF(ZZ(J).GT.Z) GO TO 90
   80 CONTINUE
      J=NZ
   90 Z2=ZZ(J)
      Z1=ZZ(J-1)
C
C SET UP INTERPOLATION CORNER POINTS
C
      X1=XX(I-1,J-1)
      X2=XX(I,J-1)
      X3=XX(I-1,J)
      X4=XX(I,J)
C
C Z1 SHOULD NOT BE THE SAME AS Z2 BUT IN CASE THEY ARE EQUAL A WILL BE
C SET EQUAL TO X1 AND B EQUAL TO X2.
C
      IF(Z2.NE.Z1) GO TO 91
      A=X1
      B=X2
      GO TO 95
C
C PROPORTION Z VALUE BETWEEN Z LINES.
C
   91 C=(Z-Z1)/(Z2-Z1)
C
C IF X3 IS EQUAL TO X1, A WILL BE X1.
C
      IF(X3.NE.X1) GO TO 92
      A=X1
      GO TO 93
   92 A=X1+C*(X3-X1)
C
C IF X4 IS EQUAL TO X2, B WILL BE X2.
C
   93 IF(X4.NE.X2) GO TO 94
      B=X2
      GO TO 95
   94 B=X2+C*(X4-X2)
C
C CHECK FOR DIVISION BY ZER0.  IF ZERO, USE SMALLER OF A OR B FOR X.
C OTHERWISE, INTERPOLATE (OR EXTRAPOLATE) TO COMPUTE X.
C
   95 IF(Y2.EQ.Y1) GO TO 100
      X=(B-A)*(Y-Y1)/(Y2-Y1)+A
      GO TO 110
  100 X=A
      IF(X.GT.B) X=B
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
  110 CONTINUE
      IF (IBUG.EQ.1) GO TO 190
      IF (IBUG.GE.5) GO TO 120
      GO TO 210
  120 WRITE(IODBUG,130) X,Y,Z
  130 FORMAT(1H0,43H X,Y, AND Z VALUES AT THE END OF F3WAYY ARE,3F12.3)
      WRITE(IODBUG,140) A,B,Y1,Y2
  140 FORMAT(1H0,14H A,B,Y1,Y2 ARE,4F12.3)
      WRITE(IODBUG,150) (YY(I),I=1,NY)
  150 FORMAT(1H0,36H YY VALUES IN THE 3-WAY RELATION ARE/(1X,10F12.3))
      WRITE(IODBUG,160) (ZZ(I),I=1,NZ)
  160 FORMAT(1H0,14H ZZ VALUES ARE/(1X,10F12.3))
      DO 170 I=1,NZ
  170 WRITE(IODBUG,180) ZZ(I),(YY(J),XX(J,I),J=1,NY)
  180 FORMAT(1H0,48H ALTERNATING VALUES OF YY AND XX FOR ZZ VALUE OF,F12
     $.3, 4H ARE/(1X,4(2F12.3,4X)))
  190 WRITE(IODBUG,200)
  200 FORMAT(1H0,10X,17H** LEAVING F3WAYX)
  210 RETURN
      END
