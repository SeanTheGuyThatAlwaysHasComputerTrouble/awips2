C MEMBER XS1126
C  (from old member FCXS1126)
C
C @PROCESS LVL(77)
      SUBROUTINE XS1126(SUNUM,PO,W,LOCWS,LOCOWS)
C---------------------------------------------------------------------
C                             LAST UPDATE: 03/07/94.08:36:06 BY $WC30KH
C
C  SUBROUTINE TO GET PARAMETER AND CARRYOVER INFORMATION FOR THE
C  INDUCED SURCHARGE SCHEME FROM THE PO AND W ARRAYS BEFORE CALLING THE
C  COMPUTATIONAL ROUTINE FOR OUTPUT CALCULATIONS.
C---------------------------------------------------------------------
C  WRITTEN BY - JOE OSTROWSKI - HRL - AUGUST 1983
C---------------------------------------------------------------------
C
C     ELBAC1 -- POOL ELEVATION ONE TIME PERIOD BEFORE ELEV1.
C     ELEV -- ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     ELEV1 -- ELEVATION AT BEGINNING OF TIME PERIOD.
C     ELVFAL -- ARRAY OF POOL ELEVATIONS OR SETS OF ELEVATIONS IN
C       ASCENDING ORDER.  MAY BE USED ALONE OR COMBINED WITH PEAK
C       OUTFLOW OR PEAK POOL ELEVATION IN A DECISION  TABLE TO DEFINE
C       THE EVACUATION OPTION.  RULE CURVE OR RULE CURVE ELEVATIONS
C       PLUS AN ADJUSTMENT ARE DEFINED BY ADDING THE ADJUSTMENT TO
C       -999.0.  FOR EXAMPLE, RULE CURVE ELEVATION WITH AN ADJUSTMENT
C       OF +1.0 WOULD BE DEFINED AS -998.0.
C     ELVMAX -- MAXIMUM ELEVATION DURING A SURCHARGE OPERATION.  WILL BE
C       RESET TO -999.0 WHEN ELEVATION FALLS BELOW HTARG1.
C     ELVSUR -- ELEVATION VALUES FOR ELEVATION VS REQUIRED INFLOW
C       RELATION.
C     EUPRND -- SPECIFIED UPPER ELEVATION ABOVE WHICH THE OPERATION IS
C       ALWAYS IN INDUCED SURCHARGE OR IN EVACUATION OF SURCHARGE
C       STORAGE.  USE RULE CURVE ELEVATION IF ELWRND IS -999.0.
C       RULE CURVE PLUS AN ADDITION IS DEFINED BY ADDING THE ADDITION
C       TO -999.0.
C     ELWRND -- SPECIFIED LOWER ELEVATION FOR RISING POOL.  BETWEEN
C       ELWRND AND EUPRND, INDUCED SURCHARGE WILL BE REQUIRED ONLY IF
C       THE INFLOW EXCEEDS THE VALUE COMPUTED FROM THE ELVSUR VS QISUR
C       RELATION.  RULE CURVE ELEVATION IS DEFINED AS -999.0 AND RULE
C       CURVE PLUS AN ADDITION IS DEFINED BY ADDING THE ADDITION TO
C       -999.0.
C     EVACEL -- ARRAY OF POOL ELEVATIONS FOR THREE-WAY RELATION OF
C       ELEVATION, GATE OPENINGS AND DISCHARGES.
C     EVACQ -- DOUBLE ARRAY (NGAT,NELEV) OF DISCHARGE VALUES
C       CORRESPONDING TO EVACEL AND GATE VALUES.  DISCHARGE WILL INCLUDE
C       MAXIMUM GENERATION AND/OR SLUICE DISCHARGES IF USED.
C     HTARG1, FALELB, DIFQI1, DIFQI3, DIFQI2, QTARG2, QTARG1, AND REDUCE
C       ARE DEFINED IN SUBROUTINES EVAA26, EVAB26, AND EVAC26.
C     FRACT -- FRACTION OF TIME PERIOD OVER WHICH MEAN INFLOW IS
C       COMPUTED FOR INDUCED SURCHARGE RELATION.
C     GATE -- GATE OPENINGS FOR THREE-WAY RELATION.  ALL GATES ARE
C       ASSUMED TO BE OPEN THE SAME AMOUNT.
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1),TRACE AND
C       DEBUG (IBUG=2).
C     IOPTND -- OPTION FOR FOLLOWING SURCHARGE CURVES (IOPTND=0) IN
C       INDUCED SURCHARGE OPERATION UNTIL POOL CRESTS OR FOR NOT
C       DECREASING GATE SETTINGS AS LONG AS POOL IS RISING (IOPTND=1)
C       EVEN THOUGH THE SURCHARGE RELATION INDICATES A DECREASE.
C     ISURG -- = 1 TO INDICATE FIRST TIME INTO INDSRCHGE SCHEME
C     ISTEP -- INDICATOR FOR STEP IN EVACUATION OPTION WHEN MORE THAN
C       ONE SET OF INSTRUCTIONS IS USED FOR THE OPTION.  THE INDICATOR
C       IS 1 FOR THE FIRST SET OF INSTRUCTIONS, 2 FOR THE SECOND SET,
C       ETC.  WHEN THE INDICATOR HAS NOT BEEN PREVIOUSLY DEFINED, IT
C       WILL BE -999.0 BUT WILL BE SET TO 1 WHEN FIRST ENTERING ONE OF
C       THE EVACUATION SUBROUTINES.  IT WILL BE SET TO -999.0 WHEN
C       EVACUATION CEASES.
C     NCON -- DEFINES THE VARIABLE OR COMBINATION OF VARIABLES ON WHICH
C       THE EVACUATION OPTION IS DEPENDENT.  VALUES OF NCON DEFINE THE
C       FOLLOWING CONDITIONS:  (1) POOL ELEVATION  , (2) PEAK OUTFLOW,
C       (3) PEAK POOL ELEVATION, (4) POOL ELEVATION AND PEAK OUTFLOW,
C       AND (5) PEAK OUTFLOW AMD PEAK POOL ELEVATION.
C     NELEV -- NO. OF VALUES IN EVACEL ARRAY.
C     NEQSUR -- NO. OF PAIRS OF ELVSUR AND QISUR VALUES.
C     NSCQI -- NO. OF INFLOW (SCQI) VALUES.
C     NGAT -- NO. OF GATE OPENING VALUES.
C     NSCEL -- NO. OF POOL ELEVATION VALUES.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NSTEPS -- NO. OF COMPUTATIONAL TIME STEPS IN A TIME PERIOD.
C     NUMVAL -- NO. OF VALUES IN OPTEV AND THE OTHER ARRAYS USED IN
C       THE DECISION TABLE.
C     OPTEV -- ARRAY OF EVACUATION OPTIONS DEFINED BY THE DECISION TABLE
C       INVOLVING ONE OR MORE OF THE ELVFAL,PEAKQ, OR PEAKEL ARRAYS.
C       OPTION VALUES RANGE FROM 1 TO 10 AND ARE DEFINED IN UTILITY
C       SUBROUTINES EVAA26, EVAB26, AND EVAC26.
C     OUTMAX -- MAXIMUM OUTFLOW DURING A SURCHARGE OPERATION.  WILL BE
C       RESET TO -999.0 UNDER SAME CONDIRIONS AS ELVMAX.
C     PCTERN -- SPECIFIED TOLERANCE IN SUCCESSIVE ITERATED DISCHARGES AS
C       A FRACTION OF THE DISCHARGE.
C     PEAKEL -- ARRAY OF PEAK ELEVATIONS OR SETS OF PEAK ELEVATIONS IN
C       ASCENDING ORDER  USED ALONE OR COMBINED WITH PEAK OUTFLOW IN A
C       DECISION TABLE TO DEFINE THE EVACUATION OPTION.  WILL BE -999.0
C       IF NOT USED.
C     PEAKQ -- ARRAY OF PEAK OUTFLOWS OR SETS OF PEAK OUTFLOWS IN
C       ASCENDING ORDER USED ALONE OR CONBINED WITH ELEVATION OR PEAK
C       ELEVATION IN A DECISION TABLE TO DEFINE THE EVACUATION OPTION.
C       WILL BE -999.0 IF NOT USED.
C     PEAKQ1 -- IF OUTMAX => PEAKQ1, USE HTARGET1,
C               OTHERWISE, USE HTARGET2
C     QELVMX -- OUTFLOW AT ELVMAX DURING A SURCHARGE OPERATION.  WILL BE
C       RESET TO -999.0 UNDER SAME CONDIRIONS AS ELVMAX.
C     QGENMX -- MAXIMUM ALLOWABLE POWER GENERATION DISCHARGE.
C     QIBAC1 -- INSTANTANEOUS INFLOW ONE TIME PERIOD BEFORE QI1.
C     QI1 -- INFLOW AT BEGINNING OF TIME PERIOD.
C     QISUR -- REQUIRED INFLOWS FOR ENTERING THE SURCHARGE RELATION WHEN
C       ELEVATION IS LESS THAN EUPRND.
C     QMAX -- SEE OUTMAX
C     RULEL1 -- RULE CURVE ELEVATION AT BEGINNING OF TIME PERIOD.
C     RULEL2 -- RULE CURVE ELEVATION AT END OF TIME PERIOD.
C     SCEL1 -- LOWEST POOL ELEVATION IN THE INDUCED SURCHARGE RELATION.
C     SCQI1 -- LOWEST INFLOW VALUE IN THE INDUCED SURCHARGE RELATION.
C     SCQO1 -- LOWEST OUTFLOW VALUE IN THE INDUCED SURCHARGE RELATION.
C     SCQIMN -- LOWEST INFLOW IN THE INDUCED SURCHARGE RELATION
C               SURCHARGE CURVE BELOW SCQOMN WILL BE IGNORED
C     SCQOMN -- LOWEST OUTFLOW IN THE INDUCED SURCHARGE RELATION
C               SURCHARGE CURVE BELOW SCQOMN WILL BE IGNORED
C       MAY BE MAXIMUM GENERATION DISCHARGE FOR A POWER DAM BUT COULD
C       BE A GREATER OR LESSER VALUE.
C     SURCHG -- LOGICAL VARIABLE.  WHEN TRUE, THE PROGRAM SHOULD BE IN
C       NDUC26.
C     TENDCY -- CHANGE IN POOL ELEVATION FROM PREVIOUS ELEVATION TO 
C       ELEV1. TENDCY WILL NEED TO BE SAVED FROM ONE TIME PERIOD TO THE
C       NEXT PERIOD SINCE IT CAN CAHNGE DURING THE TIME PERIOD.  HOWEVER
C       IF THE PROGRAM WAS NOT IN NDUC26 IN THE PREVIOUS TIME PERIOD,
C       TENDCY SHOULD BE SET TO -999.0 AND IT WILL BE COMPUTED IN
C       NDUC26 FROM ELBAC1 AND ELEV1.
C
      INCLUDE 'common/resv26'
      INCLUDE 'common/exg26'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/srcg26'
      INCLUDE 'common/evak26'
C
      DIMENSION PO(1),W(1),LOCWS(*),LOCOWS(*),NEEDVA(7,10),NEEDIT(7),
     .  GATEIT(10),ELVFAL(1),PEAKQ(10),PEAKEL(10),OPTEV(10)
      LOGICAL NEEDIT,NEEDVA,GATEIT,GETGAT,SCHORG
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/xs1126.f,v $
     . $',                                                             '
     .$Id: xs1126.f,v 1.3 2000/03/13 21:06:16 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA NEEDVA/.TRUE.,6*.FALSE.,.TRUE.,6*.FALSE.,
     3            .FALSE.,.TRUE.,.TRUE.,.TRUE.,.TRUE.,.FALSE.,.TRUE.,
     4            .FALSE.,.TRUE.,.TRUE.,.FALSE.,.TRUE.,.FALSE.,.TRUE.,
     5            .TRUE.,.FALSE.,.TRUE.,.FALSE.,.TRUE.,.FALSE.,.TRUE.,
     6            .TRUE.,.FALSE.,.TRUE.,.FALSE.,.TRUE.,.FALSE.,.TRUE.,
     7            .FALSE.,.FALSE.,.TRUE.,.FALSE.,.TRUE.,.FALSE.,.TRUE.,
     8            .FALSE.,6*.TRUE.,7*.TRUE.,7*.TRUE./
C
      DATA GATEIT/.TRUE.,.FALSE.,.TRUE.,.FALSE.,.TRUE.,.FALSE.,.FALSE.,
     .            .FALSE.,.TRUE.,.FALSE./
C
      IF (IBUG.GE.1) WRITE(IODBUG,1600)
 1600 FORMAT('   *** ENTER XS1126 ***')
C
      ISURG=ISURG+1
C
C  GET POINTER INFORMATION FOR THE SCHEME
C
      CALL XPTR26(SUNUM,PO,IORD,IBASE,LEVEL,LOCPM,LOCTS,LOCCO)
C
C  SET EXECUTION FLAG LOCATION
C
      LOCEX = IORD*3
C
C  GET CARRYOVER VALUES FIRST.
C
      IOFFCO = LOCWS(4) + LOCCO - 2
C
C  QIBAC1 = BACKQ1
      QIBAC1 = W(IOFFCO+1)
C
C  ELBAC1 = BACKPOOL
      ELBAC1 = W(IOFFCO+2)
C
C  TENDCY = TENDENCY
      TENDCY = W(IOFFCO+3)
C
C  ELVMAX = MAXEL
      ELVMAX = W(IOFFCO+4)
C
C  QELVMX = QMAXEL
      QELVMX = W(IOFFCO+5)
C
C  OUTMAX = QMAX
      OUTMAX = W(IOFFCO+6)
C
      MPLIER = W(IOFFCO+7) + 1
      MPLIER = ISIGN(1,MPLIER)
      ISTEP = MPLIER*(ABS(W(IOFFCO+7))+0.01)
C
C  SET 'SURCHARGE CONDITION' FOR FIRST PERIOD FROM CARRYOVER VALUES
C
      IF (NS2 .EQ. 1 .AND. ELVMAX .GE. 0.00) SURCHG = .TRUE.
C
C  NOW FILL NECESSARY PARAMETER VALUES
C   (ALL VARIABLES ARE DESCRIBED IN NDUC26)
C
      DT = MINODT
      IHRSCH = MINODT
      NELEV = 0
      NGAT = 0
C
C      EUPRND = PO(LOCPM)
      HUPPER = PO(LOCPM)
C
C      ELWRND = PO(LOCPM+1)
      HCHECK = PO(LOCPM+1)
C
C  FALELB = HLOWER, ELEVATION TO TERMINATE SURCHARGE COMPUTATION
C      FALELB = PO(LOCPM+2)
      HLOWER = PO(LOCPM+2)
      FALELB = HLOWER
      NH = PO(LOCPM+3)
      CT = NH
      FRACT  = CT/DT
C
C  COMPUTE THE SURCHARGE ENTRY CURVE
C
      LOCENT = LOCPM + 4
      LOCEL = LOCOWS(8)
      LOCQI = LOCEL + NSE
C      CALL XENT26(PO,LOCENT,W,LOCOWS)
C
C  SET COUNTERS AND LOCATIONS FOR THE SURCHARGE RELATION.
C
      NSCQI = PO(LOCPM+4)
      NSCEL  = PO(LOCPM+5)
      LOCFLO = LOCPM + 6
      SCQI1 = PO(LOCFLO)
      LOCPOO = LOCFLO + NSCQI
      SCQIN = PO(LOCPOO-1)
      SCEL1  = PO(LOCPOO)
      LOCSCC = LOCPOO + NSCEL
      SCQO1  = PO(LOCSCC)
C
C  SET VALUE FOR CONVERGENCE CRITERION AND GET THE PROPER VALUES FOR THE
C  DECISION TABLE.
C
      LOCNXT = LOCSCC + NSCEL*NSCQI
      PCTERN = PO(LOCNXT)
C
C  THE DECISION TABLE TYPE IS A VALUE FROM ONE TO FIVE.
C
      NCON = PO(LOCNXT+1)
C
C  NOW SET THE DECISION TABLE
C
      NLINES = PO(LOCNXT+2)
      NPLINE = 2
      IF (NCON.GT.3) NPLINE = 3
      LOCNXT = LOCNXT + 3
C
C  NOW PROCESS THE DECISION TABLE ONE LINE AT A TIME.
C
      DO 200 I=1,NLINES
C
C  THE LAST ITEM ON EACH LINE IS THE EVACUATION OPTION.
C
      LOCLST = LOCNXT + NPLINE - 1
      OPTEV(I) = PO(LOCLST)
      GO TO (110,120,130,140,150), NCON
C---------------------------
C  OPTION 1 - NEED TO STORE THE POOL ELEVATION SPEC
C
  110 CONTINUE
      ELVFAL(I) = PO(LOCNXT)
      GO TO 190
C----------------------
C  OPTION 2 - NEED TO STORE THE PEAK DISCHARGE SPEC.
C
  120 CONTINUE
      PEAKQ(I) = PO(LOCNXT)
      GO TO 190
C-------------------------
C  OPTION 3 - NEED TO STORE THE PEAK ELEVATION
C
  130 CONTINUE
      PEAKEL(I) = PO(LOCNXT)
      GO TO 190
C-----------------------------
C  OPTION 4 - NEED TO STORE THE ELEVATION AND PEAK DISCHARGE SPEC.
C
  140 CONTINUE
      ELVFAL(I) = PO(LOCNXT)
      PEAKQ(I)  = PO(LOCNXT+1)
      GO TO 190
C-------------------------
C  OPTION 5 - STORE PEAK ELEVATION AND PEAK DISCHARGE SPEC.
C
  150 CONTINUE
      PEAKEL(I) = PO(LOCNXT)
      PEAKQ(I)  = PO(LOCNXT+1)
C
  190 CONTINUE
      LOCNXT = LOCLST + 1
  200 CONTINUE
      NUMVAL = NLINES
C
C---------------------------------
C  NOW COMPUTE THE NEEDS FOR THE EVACUATION PARAMETERS
C
C
C  DO SOME INITIALIZATION FIRST
C
      DO 100 I=1,7
      NEEDIT(I) = .FALSE.
  100 CONTINUE
C
      GETGAT = .FALSE.
      DO 250 I=1,NLINES
      IOPT = OPTEV(I)
      GETGAT = GETGAT .OR. GATEIT(IOPT)
C
      DO 240 N=1,7
      NEEDIT(N) = NEEDIT(N) .OR. NEEDVA(N,IOPT)
  240 CONTINUE
  250 CONTINUE
C
C  NOW GET THE EVAC PARMS (DESCRIPTIONS IN EVAA26. EVAB26, AND EVAC26.)
C
      IF (.NOT.NEEDIT(1)) GO TO 320
C
C  HTARG1 = HTARGET1
      HTARG1 = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
  320 CONTINUE
      IF (.NOT.NEEDIT(2)) GO TO 330
C
C  DIFQI1 = DIFFQI1
      DIFQI1 = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
  330 CONTINUE
      IF (.NOT.NEEDIT(3)) GO TO 340
C
C  = REDUCE
      REDUCE = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
  340 CONTINUE
      IF (.NOT.NEEDIT(4)) GO TO 350
C
C  QTARG1 = QTARGET1
      QTARG1 = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
  350 CONTINUE
      IF (.NOT.NEEDIT(5)) GO TO 360
C
C  QTARG2 = QTARGET2
      QTARG2 = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
  360 CONTINUE
      IF (.NOT.NEEDIT(6)) GO TO 370
C
C  DIFQI2 = DIFFQI2
      DIFQI2 = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
  370 CONTINUE
      IF (.NOT.NEEDIT(7)) GO TO 380
C
C  DIFQI3 = DIFFQI3
      DIFQI3 = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
C-----------------------------------
C  GET GATE INFO IF GATE IS USED
C
  380 CONTINUE
      IF (.NOT.GETGAT) GO TO 500
C
      NELEV = PO(LOCNXT)
      NGAT  = PO(LOCNXT+1)
      LOCGEL = LOCNXT + 2
      LOCGOP = LOCGEL + NELEV
      LOCGQ  = LOCGOP + NGAT
      LOCNXT = LOCGQ + NGAT*NELEV
C
C      NGSET = PO(LOCNXT)
C      NSTEPS = MINODT/NGSET
      IHRSCH= PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
C--------------------------------
C  GET THE FALLING POOL OPTION AND THE RULE CURVE (IF NECESSARY)
C
  500 CONTINUE
      IOPTND = PO(LOCNXT)
      IF(NGAT.LE.0 .OR. NELEV.LE.0) IOPTND=0
      LOCNXT = LOCNXT + 1
      NRULE = PO(LOCNXT)
      IF (NRULE.EQ.0) GO TO 1000
C
C  GET RULE CURVE 'TIME-SERIES'
C
      CALL XFRU26(PO,LOCNXT,W,LOCOWS)
      LOCNXT = LOCNXT + 2*NRULE + 1
C
C-----------------------------------------
C  NOW COMPUTE THE OUTPUT FOR THE INDUCED SURCHARGE SCHEME
C
 1000 CONTINUE
      LOCNXT = LOCNXT + 1
      I9302 = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
      IF(I9302.NE.199302) GO TO 1010
      IHRSCH = PO(LOCNXT)
      IF(IHRSCH.LT.0) IHRSCH=MINODT
      NSTEPS = MINODT/IHRSCH
      TR = MINODT/NSTEPS
      LOCNXT = LOCNXT + 1
C
      QCHECK = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
      IX = PO(LOCNXT)
      QDIFF = PO(LOCNXT)*TR
      IF(IX.EQ.-999) QDIFF=0.0
      LOCNXT = LOCNXT + 1
C
      IX = PO(LOCNXT)
      HTARG2 = PO(LOCNXT)
      IF(IX.EQ.-9998) HTARG2=HTARG1
      LOCNXT = LOCNXT + 1
C
      IX = PO(LOCNXT)
      HTARG3 = PO(LOCNXT)
      IF(IX.EQ.-9998) HTARG3=HLOWER
      LOCNXT = LOCNXT + 1
C
      IX = PO(LOCNXT)
      REDUC1 = PO(LOCNXT)*TR
      IF(IX.EQ.-999) REDUC1=0.0
      LOCNXT = LOCNXT + 1
C
      IX = PO(LOCNXT)
      REDUC2 = PO(LOCNXT)*TR
      IF(IX.EQ.-999) REDUC2=0.0
      LOCNXT = LOCNXT + 1
C
      IX = PO(LOCNXT)
      QTARG3 = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
      SCQIMN = PO(LOCNXT)
      IF(IFMSNG(SCQIMN).EQ.1) THEN
        IF(SCQIN.GE.100.0) THEN
          SCQIMN=SCQI1
        ELSE
          SCQIMN=SCQO1
        ENDIF
      ENDIF
      LOCNXT = LOCNXT + 1
C
      HPREVQ = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
      SCQOMN = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
      QGENMX = PO(LOCNXT)
      LOCNXT = LOCNXT + 1
C
      PEAKQ1 = PO(LOCNXT)
      IF(IFMSNG(PEAKQ1).EQ.1) PEAKQ1=0.0
      LOCNXT = LOCNXT + 1
C
C  FALELB=HEVACEND
      IX = PO(LOCNXT)
      FALELB = PO(LOCNXT)
      IF(IX.EQ.-9998) FALELB=HLOWER
      LOCNXT = LOCNXT + 1
C
C SET DEFAULT VALUES
      IF(IFMSNG(SCQOMN).EQ.1) SCQOMN=SCQO1
      IF(IFMSNG(QTARG3).EQ.1) QTARG3=SCQOMN
      IF(IFMSNG(QGENMX).EQ.1) QGENMX=SCQOMN
      GO TO 1015
C
 1010 CONTINUE
      QCHECK=-999.0
      QDIFF=0.0
      HTARG2=HTARG1
      HTARG3=HLOWER
      REDUC1=0.0
      REDUC2=0.0
      QTARG3=SCQO1
      SCQIMN=SCQI1
      IF(SCQIN.LT.100.0) SCQIMN=SCQO1
      HPREVQ=-9998.0
      SCQOMN=SCQO1
      QGENMX=SCQO1
      PEAKQ1=0.0
 1015 CONTINUE
C
C  CHANGE HOURLY EVACUATION DISCHARGE REDUCTION RATE TO PER TIME STEP
      NSTEPS = MINODT/IHRSCH
      TR = MINODT/NSTEPS
      REDUCE = TR*REDUCE
      SCHORG = SURCHG
      CALL NDUC26(W(LOCEL),W(LOCQI),ELVFAL,PEAKQ,PEAKEL,OPTEV,PO(LOCGEL)
     .           ,PO(LOCGOP),PO(LOCGQ),PO(LOCPOO),PO(LOCFLO),PO(LOCSCC),
     .            PO(LESSTO),PO(LESELV))
C
C-----------------------------------------
C  SAVE CARRYOVER AND SET EXECUTION FLAG IF SURCHARGE SCHEME WAS
C  ACTUALLY USED AND WE DIDN'T SLIP OUT OF THE SURCHARGE CONDITION.
C
      IF (.NOT.(SURCHG.OR.SCHORG)) GO TO 9000
C
      W(LOCEX) = 1.01
      W(IOFFCO+1) = QI1
      W(IOFFCO+2) = ELEV1
      W(IOFFCO+3) = TENDCY
      W(IOFFCO+4) = ELVMAX
      W(IOFFCO+5) = QELVMX
      W(IOFFCO+6) = OUTMAX
      W(IOFFCO+7) = ISTEP
C
 9000 CONTINUE
      IF (IBUG.GE.1) WRITE(IODBUG,1699)
 1699 FORMAT('    *** EXIT XS1126 ***')
      RETURN
      END
