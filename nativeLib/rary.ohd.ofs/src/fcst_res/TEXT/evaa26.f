C MEMBER EVAA26
C  (from old member FCEVAA26)
C
      SUBROUTINE EVAA26(EVACEL,GATE,EVACQ,STOR,ELEV)
C
C SUBROUTINE EVAA26 IS A UTILITY SUBROUTINE USED FOR EVACUATION OPTIONS
C 1,2,3,4,5,6,9, AND 10.  DEFINITIONS OF THESE OPTIONS ARE AS FOLLOWS:
C     (1)  THE MAXIMUM GATE SETTING IS MAINTAINED UNTIL THE POOL DROPS
C TO A SPECIFIED, OR RULE CURVE (OR RULE CURVE PLUS A SPECIFIED
C ADDITION) ELEVATION.
C     (2)  THE MAXIMUM OUTFLOW IS MAINTAINED UNTIL THE POOL DROPS TO A
C SPECIFIED, OR RULE CURVE (OR RULE CURVE PLUS A SPECIFIED ADDITION)
C ELEVATION.
C     (3A).  THE MAXIMUM GATE SETTING IS MAINTAINED UNTIL THE OUTFLOW IS
C A SPECIFIED AMOUNT GREATER THAN THE INFLOW.
C     (3B).  THE OUTFLOW IS THEN REDUCED AT A SPECIFIED RATE PER TIME
C STEP UNTIL OUTFLOW REACHES A SPECIFIED VALUE OR THE POOL DROPS TO A
C SPECIFIED, OR RULE CURVE, (OR RULE CURVE PLUS A SPECIFIED ADDITION)
C ELEVATION.  OUTFLOW CANNOT BE REDUCED TO LESS THAN INFLOW OR INFLOW
C PLUS A SPECIFIED ADDITION.  IF OUTFLOW REACHES THE SPECIFIED DISCHARGE
C VALUE BEFORE THE SPECIFIED (OR RULE CURVE OR RULE CURVE PLUS A SPECI-
C FIED ADDITION) ELEVATION IS REACHED, THE SPECIFIED DISCHARGE IS USED
C UNTIL THE SPECIFIED ELEVATION IS REACHED.
C     (4A).  THE MAXIMUM OUTFLOW IS MAINTAINED UNTIL THE OUTFLOW IS A
C SPECIFIED AMOUNT GREATER THAN THE INFLOW.
C     (4B).  SAME AS 3B.
C     (5)  COMBINATION OF OPTIONS 1 AND 3B.
C     (6)  COMBINATION OF OPTIONS 2 AND 3B.
C     (7)  SAME AS OPTION 3B.
C     (8)  OUTFLOW IS REDUCED TO A SPECIFIED DISCHARGE OR TO INFLOW PLUS
C A SPECIFIED ADDITION (WHICHEVER IS GREATER).  AFTER OUTFLOW DROPS TO
C THE SPECIFIED DISCHARGE, OPTION 3B IS USED.
C     (9)  OPTION 9 IS A COMBINATION OF OPTIONS 1 AND 8.
C     (10) OPTION 10 IS A COMBINATION OF OPTIONS 2 AND 8.
C
C     SUBROUTINE EVAA26 COMPUTES THE OPTIONS 1 AND 2 AND CALLS EVAB26
C AND EVAC26 TO COMPUTE OTHER PORTIONS OF OPTIONS 3,4,7, AND 8.
C
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     FEBRUARY, 1982
C
C SUBROUTINE EVAA26 IS IN
C
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IOUT -- WHEN IOUT IS DEFINED AS 1, THE POOL HAS DROPPED TO THE
C       LOWEST EVACUATION ELEVATION AND THE SURCHARGE EVACUATION IS NO
C       LONGER NEEDED.  A VALUE OF 0 INDICATES THAT EVACUATION IS STILL
C       NEEDED.
C     IOPTEV -- EVACUATION OPTIONS AS PREVIOUSLY DEFINED.
C     ISTEP -- INDICATOR FOR SET OF INSTRUCTIONS IN USE WHEN MORE THAN
C       ONE SET IS USED FOR THE OPTION.  ISTEP WILL BE 1 FOR THE FIRST
C       SET, 2 FOR THE SECOND SET, ETC.  WILL BE -999 IF NOT PREVIOUSLY
C       DEFINED.  IF -999, ISTEP WILL BE SET TO 1 IN THE SUBROUTINE.
C       ISTEP WILL BE RESET TO -999 IN NDUC26 WHEN IOUT IS 1.
C     HTARG1 -- ELEVATION TO WHICH POOL WILL BE RECEDED BY MAINTAINING
C       MAXIMUM GATE SETTING (OPTION 1) OR MAXIMUM OUTFLOW (OPTION 2).
C       IF OUTMAX => PEAKQ1
C       RULE CURVE OR RULE CURVE PLUS AN  ADDITION IS DEFINED BY -999.0
C       OR BY ADDING THE ADDITION TO -999.0.  FOR EXAMPLE, RULE CURVE
C       PLUS AN ADDITION OF +1.0 IS DEFINED AS -998.0.
C     HTARG2 -- ELEVATION TO WHICH POOL WILL BE RECEDED BY MAINTAINING
C       MAXIMUM GATE SETTING (OPTION 1) OR MAXIMUM OUTFLOW (OPTION 2)
C       IF OUTMAX < PEAKQ1
C       RULE CURVE OR RULE CURVE PLUS AN  ADDITION IS DEFINED BY -999.0
C       OR BY ADDING THE ADDITION TO -999.0.  FOR EXAMPLE, RULE CURVE
C       PLUS AN ADDITION OF +1.0 IS DEFINED AS -998.0.
C     DIFQI1 -- SPECIFIED ADDITION TO INFLOW USED IN FIRST STEP OF
C       OPTIONS 3 AND 4.
C     QTARG1 ARE DEFINED IN EVAC26.  ALL
C       OTHER VARIABLES ARE DEFINED IN NDUC26.
C
C THIS SUBROUTINE WILL COMPUTE OUTFLOW (QQO2), STORAGE (SS2), AND ELEVA-
C       TION (ELRES2) AT THE END OF THE TIME STEP.  ALSO, THE INDICATOR
C       (IOUT) FOR CONTINUING OR NOT CONTINUING EVACUATION WILL BE SET.
C
C  MODIFIED TO REARRANGE THE EVACQ ARRAY FROM (NGAT,NELEV) TO
C  (NELEV,NGAT) FOR STRAIGHTFORWARD INPUT AND DISPLAY PURPOSES -
C  (JTO -1/84)
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
      INCLUDE 'common/srcg26'
      INCLUDE 'common/evak26'
C
      DIMENSION EVACEL(1),GATE(1),EVACQ(NELEV,1),STOR(1),ELEV(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/evaa26.f,v $
     . $',                                                             '
     .$Id: evaa26.f,v 1.3 2002/10/10 11:36:42 xfan Exp $
     . $' /
C    ===================================================================
C
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      OPEN=0.0
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** EVAA26 ENTERED)
      WRITE(IODBUG,40) IOUT,IOPTEV,ISTEP,HTARG1,HTARG2,DIFQI1,
     $QTARG1,FRPD,SFRPD,QIMSUR,QQIM,QQO1,QQO2,SS1,
     $SS2,ELRES1,ELRES2,RULEL1,RULEL2,ELVMAX,OUTMAX,PCTERN,IBUG,OPEN
   40 FORMAT(1H0,
     $' IOUT,IOPTEV,ISTEP,HTARG1,HTARG2,DIFQI1,QTARG1,FRPD,SFRPD'/
     $' QIMSUR,QQIM,QQO1,QQO2,SS1,SS2,ELRES1,ELRES2'/
     $' RULEL1,RULEL2,ELVMAX,OUTMAX,PCTERN,IBUG,OPEN'/
     $1X,3I6,6F12.3/1X,8F12.3/1X,5F12.3,I6,F12.3)
   50 IOUT=0
      IF(ISTEP.EQ.-999) ISTEP=1
C
C IOUT IS SET TO 0 BUT WILL BE CHANGED TO 1 IF THE PROGRAM STOPS SUR-
C CHARGE EVACUATION.  SET FALEL EQUAL TO HTARG1 OR COMPUTE FALEL IF
C HTARG1 IS RULE CURVE OR RULE CURVE PLUS A SPECIFIED ADDITION.
C HTARG1=HTARGET1, HTARG2=HTARGET2
C
      FALEL=HTARG2
      IF(OUTMAX.GT.PEAKQ1) FALEL=HTARG1
      IF(FALEL.LT.-900.0) FALEL=RULEL1+(RULEL2-RULEL1)*SFRPD+FALEL+999.0
C
C THE FOLLOWING STATEMENTS DIRECT THE PROGRAM TO THE PROPER COMPUTA-
C TIONAL ROUTINES.
C
      GO TO (100,200,300,400,500,600,700,800,900,1000),IOPTEV
  300 GO TO (100,700),ISTEP
  400 GO TO (410,700),ISTEP
  500 GO TO (100,700),ISTEP
  600 GO TO (200,700),ISTEP
  800 GO TO(220,700),ISTEP
  900 GO TO(100,220),ISTEP
 1000 GO TO(200,220),ISTEP
C
C MAXIMUM GATE SETTING IS MAINTAINED IN THE FOLLOWING STATEMENTS.  AN
C ITERATION METHOD IS USED TO COMPUTE OUTFLOW AT THE END
C OF THE TIME STEP.  THE MAXIMUM GATE SETTING (OPEN) IS FIRST COMPUTED
C FROM THE ELEVATION, GATE OPENING, AND DISCHARGE RELATION.  THIS GATE
C OPENING (OPEN) AND THE RELATION IS THEN USED IN DO LOOP 140 TO OBTAIN
C ITERATED VALUES OF DISCHARGE.  DISCHARGE AT THE END OF THE TIME STEP
C IS FIRST ASSUMED TO BE THE SAME AS THE DISCHARGE AT THE BEGINNING OF
C THE TIME STEP.
C
  100 CALL F3WAYZ(QELVMX,ELVMAX,OPEN,EVACQ,EVACEL,GATE,NELEV,NGAT,IBUG)
      SS2=SS1+QQIM-QQO1*FRPD
      PREVQ2=QQO1
C
C USE FIVE ITERATIONS IN DO LOOP 140.
C
      DO 140 J=1,5
      J140=J
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      CALL F3WAYX(QQO2,ELRES2,OPEN,EVACQ,EVACEL,GATE,NELEV,NGAT,IBUG)
      IF(ABS(QQO2-PREVQ2).LE.(PCTERN*QQO2)) GO TO 150
      PREVQ2=QQO2
      SS2=SS1+QQIM-(QQO1+PREVQ2)*0.5*FRPD
  140 CONTINUE
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C
C WRITE DEBUG IF REQUIRED.
C
  150 IF(IBUG.NE.2) GO TO 170
      WRITE(IODBUG,160) QQO1,PREVQ2,QQO2,SS1,SS2,ELRES1,ELRES2,J140
  160 FORMAT(1H0,37H OUTFLOW AT BEGINNING OF TIME STEP IS,F12.3,43H.  LA
     $ST TWO VALUES OF ITERATED OUTFLOWS ARE,2F12.3/1H0,44H BEGINNING AN
     $D ENDING TIME STEP STORAGES ARE,2F12.3/39H.  BEGINNING AND ENDING
     $ ELEVATIONS ARE,2F12.3,' ITER NO',I2)
  170 IF(IOPTEV.NE.3) GO TO 180
      IF(QIMSUR.GT.(QQO2-DIFQI1)) GO TO 3000
      IF(QQO2.GT.QTARG1) THEN
C
C  PASS INFLOW PLUS DIFQI1 UNTIL QQO2 = QTARG1
        QQO2=QIMSUR+DIFQI1
        SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
        CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
        GO TO 3000
      ENDIF
      GO TO 190
C
C CHECK IF ELRES2 IS GREATER THAN FALEL
C = HTARGET1 FOR OPTION 2A AND 10, = HTARGET2 FOR OPTION 2B, 6 AND 11.
C
  180 IF(ELRES2.GT.FALEL) GO TO 3000
      IF(IOPTEV.EQ.1) THEN
C
C SET IOUT =1 TO INDICATE SURCHARGE EVACUATION IS NO LONGER NEEDED.
      IOUT=1
      GO TO 3000
      ENDIF
  190 ISTEP=2
      GO TO 3000
  700 CALL EVAB26(STOR,ELEV)
      GO TO 3000
  220 TWOQMS=2.*QIMSUR
      IF(OUTMAX.LE.TWOQMS) CALL EVAC26(STOR,ELEV)
      IF(OUTMAX.GT.TWOQMS) CALL EV3C26(STOR,ELEV)
      GO TO 3000
C
C COMPUTE STORAGE (SS2) AND ELEVATION (ELRES2) AT END OF TIME STEP FOR
C OPTION 2 AND THE FIRST PART OF OPTION 4.  OPTION 2 IS ALSO THE FIRST
C PART OF OPTIONS 6, 10 AND 11.
C
C  200 QQO2=QQO1
C      SS2=SS1+QQIM-QQO1*FRPD
  200 QQO2=OUTMAX
      SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      IF(OPTEV.EQ.4) GO TO 410
C
C CHECK IF ELRES2 IS GREATER THAN FALEL
C FALEL = HTARGET1 IF OUTMAX > PEAKQO1,
C OTHERWISE, FALEL = HTARGET2
      IF(ELRES2.GT.FALEL) GO TO 3000
      IF(IOPTEV.EQ.2) THEN
        IOUT=1
        GO TO 3000
      ENDIF
      IF(IOPTEV.EQ.6 .AND. ELRES2.GE.ELRES1) GO TO 3000
      ISTEP=2
      GO TO 3000
C
C  DIFQI1 = DIFFQI1
 410  IF((QQO1-DIFQI1) .GT. QIMSUR) GO TO 420
      QQO2=OUTMAX
      SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      GO TO 3000
 420  ISTEP=2
      GO TO 3000
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
 3000 IF(IBUG-1)3350,3330,3270
 3270 WRITE(IODBUG,3280)
 3280 FORMAT(1H0,42H FOLLOWING ARE VALUES AT THE END OF EVAA26)
      WRITE(IODBUG,40) IOUT,IOPTEV,ISTEP,HTARG1,HTARG2,DIFQI1,
     $QTARG1,FRPD,SFRPD,QIMSUR,QQIM,QQO1,QQO2,SS1,
     $SS2,ELRES1,ELRES2,RULEL1,RULEL2,ELVMAX,OUTMAX,PCTERN,IBUG,OPEN
      M=0
      IF(IOPTEV.EQ.1.OR.IOPTEV.EQ.3) M=1
      IF(IOPTEV.EQ.5.OR.IOPTEV.EQ.9) M=1
      IF(M.EQ.0) GO TO 3310
      IF(ISURG.NE.1) GO TO 3330
      DO 3290 J=1,NGAT
 3290 WRITE(IODBUG,3300) GATE(J),(EVACEL(K),EVACQ(K,J),K=1,NELEV)
 3300 FORMAT(1H0,82H ALTERNATING POOL ELEVATION AND CORRESPONDING OUTFLO
     $W VALUES FOR A GATE OPENING OF,F12.3, 4H ARE/(1X,5(F10.3,F12.3,2X)
     $))
 3310 WRITE(IODBUG,3320)(STOR(I),ELEV(I),I=1,NSE)
 3320 FORMAT(1H0,78H ALTERNATING VALUES OF STORAGE AND ELEVATION FOR THE
     $ STORAGE - ELEV. CURVE ARE/(1X,5(F12.3,F10.3,2X)))
 3330 WRITE(IODBUG,3340)
 3340 FORMAT(1H0,10X,17H** LEAVING EVAA26)
 3350 RETURN
      END






