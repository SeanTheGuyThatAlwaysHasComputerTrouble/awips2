C MEMBER X17I26
C  (from old member FCX17I26)
C
      SUBROUTINE X17I26(PO,W,LOCWS,D,IDPT,IDTQ,LOCPM,LOCCO,LPTTSD,LAOBS)
C
C--------------------------------------------------------------------
C  SUBROUTINE TO ADJUST THE SIMULATED INSTANTANEOUS DISCHARGES USING
C  OBSERVED INSTANTANEOUS DISCHARGES.
C
C--------------------------------------------------------------------
C  ORIGINALLY PROGRAMMED BY - JOE OSTROWSKI - HRL - SEPT 1984
C--------------------------------------------------------------------
C
      INCLUDE 'common/resv26'
      INCLUDE 'common/exg26'
      INCLUDE 'common/fprog'
      INCLUDE 'common/xco26'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fcary'
C
      DIMENSION PO(1),W(1),LOCWS(1),D(1),IDPT(1)
C
      LOGICAL DIFFS,SELECT(2)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/x17i26.f,v $
     . $',                                                             '
     .$Id: x17i26.f,v 1.3 2004/05/18 20:03:59 hsu Exp $
     . $' /
C    ===================================================================
C
      DATA SELECT/.FALSE.,.TRUE./
      DATA FACT/5.0/
      DATA RDIFF/2.0/
C
C  PRINT TRACE HERE IF REQUESTED
C
      IF (IBUG .GE. 1) WRITE(IODBUG,600)
  600 FORMAT(' *** ENTER X17I26 ***')
C
C  GET PARAMETER INFO (NO. OF BLEND PERIODS AND WHETHER TO USE RATIOS OR
C  DIFFERENCES.)
C
      NBLEND = PO(LOCPM)
      DIFFS = SELECT(IFIX(PO(LOCPM+1)) + 1)
C
C  GET CARRYOVER INFO (DIFF AT LAST OBSERVATION AND NO. OF PREVIOUS
C  PERIODS BLENDED.)
C
      QDIFF = W(LOCWS(4) + LOCCO - 1)
      NSTEPS = W(LOCWS(4) + LOCCO)
C
      IF (IBUG .GE. 2) WRITE(IODBUG,601) NBLEND,DIFFS,QDIFF,NSTEPS,IDTQ
  601 FORMAT('  NO. OF BLEND PERIODS = ',I3,'  USE DIFFERENCES? =>',
     .        L1/'  DIFF. AT LAST OBS. = ', F10.3,'  NO. OF PREVIOUS',
     .        ' BLEND PERIODS = ',I3,
     .        /'  DELTA-T OF TIME SERIES = ',I3)
C
C  SET INTERVAL FREQUENCY FOR LOOKING FOR INST. OBSERVATIONS.
C
      NTIMOB = 24/IDTQ
      NINT = NTIM24/NTIMOB
C
C  SET INITIAL COUNTERS
C
       LOCQO = LOCWS(2)
      NMISS = NSTEPS
      LAOBS = 0
      LOBSQO = IDPT(LPTTSD) + IDOFST*NTIMOB
      NPVAL = NRUN/NINT
C
C  SET VALUES FOR IGNORETS MOD CHECK
C
      IQTYPE = 1
      ISUTYP = 2
      IF (IBUG .GE. 2) WRITE(IODBUG,602)
  602 FORMAT(/10X,'** OBSERVED INST. DISCHARGES **')
      IF (IBUG .GE. 2) WRITE(IODBUG,650) (D(LOBSQO+I-1),I=1,NPVAL)
  650 FORMAT(1X,8F10.0)
C
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C  FIRST THING WE DO IS TO LOOK FOR FIRST OBSERVATION OF RUN.
C  WHEN WE FIND IT WE DO ONE OF A NUMBER OF THINGS:
C   1) SEE IF NO. OF MISSING VALUES ALLOW US TO DISTRIBUTE DIFFERENCES
C   2) SEE IF NO. OF MISSING IS GREATER THAN THE BLEND LENGTH (IN
C      WHICH CASE WE BLEND)
C
      NOBSX = 0
      DO 1100 I=1,NRUN
      if (D(LOBSQO+I-1).GE.0) NOBSX=I
 1100 CONTINUE      
      IF (NOBSX.LE.0) GO TO 900
C
      DO 100 I=1,NRUN
C
C  CAN ONLY LOOK FOR OBSERVATION IF WE'RE ON AN OBSERVED INTERVAL.
C
       IF (MOD(I,NINT) .NE. 0) GO TO 10
C
C  SEE IF TIME-SERIES VALUE IS MISSING
C
      VAL = D(LOBSQO+I-1)
      JULI = I*MINODT + (IDA-1)*24
      IF (IFMSNG(VAL) .EQ. 0 .AND. IFGNOR(IQTYPE,JULI,ISUTYP) .EQ. 0)
     .  GO TO 30
C
C  INCREMENT THE NO. OF MISSING
C
   10 CONTINUE
      NMISS = NMISS + 1
      GO TO 90
C
C  WE'VE FOUND A DATA VALUE. SEE IF THE NO. OF MISSING IS SHORT
C  ENOUGH TO DISTRIBUTE DIFFERENCES OR SO LONG THAT WE MUST BLEND.
C
   30 CONTINUE
      IF (NMISS .GT. NBLEND) GO TO 60
C
C  WE CAN DIST. DIFFERENCES ONLY IF WE HAVE SOME MISSING
C
      IF (NMISS .GT. 0) GO TO 40
C
C  NO MISSING FOUND BEFORE THIS OBSERVATION. JUST RESET COUNTERS
C
       LAOBS = I
       NMISS = 0
       QDIFF = VAL - W(LOCQO+I-1)
       W(LOCQO+I-1) = VAL
       GO TO 90
C
C---------------------------
C  DISTRIBUTE DIFFERENCES AT BEGINNING OF RUN
C
   40 CONTINUE
      DIFEND = VAL - W(LOCQO+I-1)
      W(LOCQO+I-1) = VAL
      ITYPE = 1
C
      CALL X17N26(ITYPE,W,PO,LOCQO,LX,QDIFF,DIFEND,I,NMISS,NSTEPS,0)
C
C  RE-INITIALIZE COUNTERS FOR LATER USE.
C
      LAOBS = I
      QDIFF = DIFEND
      NMISS = 0
      GO TO 90
C
C---------------------------
C  HERE WE NEED TO BLEND
C
   60 CONTINUE
      LAOBS = I
C
C  BLEND FORWARD (ONLY IF WE HAD A PREVIOUS DIFFERENCE IN Q'S)
C
      NBEFOR = NSTEPS
      IF (ABS(QDIFF) .GT. 0.001) CALL X17B26(W(LOCQO),QDIFF,NBEFOR,
     .         NBLEND,1,NRUN,0)
C
C  BLEND BACKWARDS FROM CURRENT OBSERVATION (THIS WILL INCORPORATE
C  ANY FORWARD BLEND EFFECTS).
C
      QDIFF = VAL - W(LOCQO+I-1)
      W(LOCQO+I-1) = VAL
      NBEFOR = 0
      IBSTRT = I - NBLEND + 1
C
      CALL X17B26(W(LOCQO),QDIFF,NBEFOR,NBLEND,IBSTRT,NRUN,1)
C
      NMISS = 0
      GO TO 95
C
C  WE NEED TO CHECK IF CARRYOVER NEEDS TO BE STORED IN /XCO26/ FOR
C  EACH TIME-PERIOD.
C
   90 CONTINUE
      IF (ISAVCO.EQ.0 ) GO TO 95
C
C  LOOP THROUGH LIST OF CO SAVE PERIODS AND SEE IF WE MATCH THE
C  CURRENT PERIOD WITH ONE OF THE SAVE PERIODS.
C
      DO 93 J=1,NCSTOR
      IF (ICOPOS(J) .EQ. I) GO TO 94
   93 CONTINUE
      GO TO 95
C
C  STORE NEEDED CO VALUES IN /XCO26/
C
   94 CONTINUE
      NSAVE = NMISS
      IF (NMISS .GT. NBLEND) NSAVE = 0
      ADJCO(1,J) = QDIFF
      ADJCO(2,J) = NSAVE + 0.01
C
C  IF WE'VE FOUND AN OBSERVED VALUE, THEN WE'RE DONE WITH THE FIRST
C  SECTION OF THE INSTANTANEOUS ADJUSTMENT.
C
   95 CONTINUE
      IF (LAOBS .GT. 0) GO TO 150
C
  100 CONTINUE
C
C  IF WE'VE MADE IT TO HERE, THEN WE HAVE NO OBSERVATIONS AND WE'RE
C  NOT ABLE TO DO ANY ADJUSTING.
C
      GO TO 900
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C  HERE WE GO FROM THE FIRST OBSERVATION OUT TO THE LAST PERIOD OF
C  THE OBSERVED PERIOD OF THE RUN LOOKING FOR MORE OBSERVATIONS OF
C  INSTANTANEOUS DISCHARGES. ONCE AGAIN WHEN WE FIND AN OBSERVATION
C  WE DO ONE OF TWO THINGS (IF THERE WERE MISSING PERIODS BETWEEN
C  OBSERVATIONS) :
C
C    1) DISTRIBUTE DIFFERENCES OR RATIOS IF THE NO. OF MISSING IS
C       LESS THAN THE NO. OF BLEND PERIODS SPECIFIED,
C    2) BLEND IF NO. OF MISSING IS GREATER THAN THE BLEND PERIOD
C       SPECIFICATION.
C
  150 CONTINUE
C
C  IF FIRST OBSERVATION WAS AT END OF OBSERVED PERIOD, THEN WE CAN'T
C  LOOK FOR ANY MORE OBSERVATIONS.
C
      IF (LAOBS .EQ. NRUN) GO TO 900
C
C  INITIALIZE RATIOS FOR LATER POSSIBLE USE
C
      BSIM = W(LOCQO+LAOBS-1) - QDIFF
      IF (BSIM .LT. 0.01) BSIM = 0.01
      BRATIO = W(LOCQO+LAOBS-1)/BSIM
C
      IST = LAOBS + 1
      NBEFOR = 0
C
      DO 500 I=IST,NRUN
C
C  CAN ONLY LOOK FOR OBSERVATION IF WE'RE ON AN OBSERVED INTERVAL.
C
       IF (MOD(I,NINT) .NE. 0) GO TO 410
C
C  SEE IF TIME-SERIES VALUE IS MISSING
C
      VAL = D(LOBSQO+I-1)
      JULI = I*MINODT + (IDA-1)*24
      IF (IFMSNG(VAL) .EQ. 0 .AND. IFGNOR(IQTYPE,JULI,ISUTYP) .EQ. 0)
     .  GO TO 430
C
C  INCREMENT THE NO. OF MISSING
C
  410 CONTINUE
      NMISS = NMISS + 1
      GO TO 490
C
C  WE'VE FOUND A DATA VALUE. SEE IF THE NO. OF MISSING IS SHORT
C  ENOUGH TO DISTRIBUTE DIFFERENCES OR SO LONG THAT WE MUST BLEND.
C
  430 CONTINUE
      IF (NMISS .GT. NBLEND) GO TO 460
C
C  WE CAN DIST. DIFFERENCES ONLY IF WE HAVE SOME MISSING
C
      IF (NMISS .GT. 0) GO TO 440
C
C  NO MISSING FOUND BEFORE THIS OBSERVATION. JUST RESET COUNTERS
C
       LAOBS = I
       NMISS = 0
       QDIFF = VAL - W(LOCQO+I-1)
C
      IF (DIFFS) GO TO 435
      IF (W(LOCQO+I-1) .LT. 0.01) W(LOCQO+I-1) = 0.01
      BRATIO = VAL/W(LOCQO+I-1)
  435 CONTINUE
C
       W(LOCQO+I-1) = VAL
       GO TO 490
C
C  SEE IF WE'RE TO USE DIFFERENCES OR RATIOS
C
  440 CONTINUE
      DIFEND = VAL - W(LOCQO+I-1)
      DIFRA1 = QDIFF
      DIFRA2 = DIFEND
      ITYPE = 1
C
      IF (DIFFS) GO TO 450
C
C  WE WANT TO USE RATIOS. SEE IF WE CAN (I.E. - THE RATIO IS NOT
C   GREATER THAN 5.0 AND ONE RATIO IS NOT GREATER THAN TWICE THE
C   OTHER).
C
      IF (W(LOCQO+I-1) .LT. 0.01) W(LOCQO+I-1) = 0.01
      ERATIO = VAL/W(LOCQO+I-1)
C
      IF (BRATIO.GT.FACT .OR. ERATIO.GT.FACT .OR. ABS(ERATIO-BRATIO)
     .    .GT. RDIFF) GO TO 450
C
C  WE'VE PASSED ALL THE TESTS, THEREFORE WE CAN USE RATIOS
C
      ITYPE = 0
      DIFRA1 = BRATIO
      DIFRA2 = ERATIO
C
  450 CONTINUE
      W(LOCQO+I-1) = VAL
C
      CALL X17N26(ITYPE,W,PO,LOCQO,LX,DIFRA1,DIFRA2,I,NMISS,0,0)
C
C  RE-INITIALIZE COUNTERS FOR LATER USE.
C
      LAOBS = I
      QDIFF = DIFEND
      NMISS = 0
      IF (.NOT.DIFFS) BRATIO = ERATIO
      GO TO 490
C
C---------------------------
C  HERE WE NEED TO BLEND
C
  460 CONTINUE
C
C  BLEND FORWARD (ONLY IF WE HAD A PREVIOUS DIFFERENCE IN Q'S)
C
      IBSTRT = LAOBS + 1
      IF (ABS(QDIFF) .GT. 0.001) CALL X17B26(W(LOCQO),QDIFF,NBEFOR,
     .         NBLEND,IBSTRT,NRUN,0)
C
C  BLEND BACKWARDS FROM CURRENT OBSERVATION (THIS WILL INCORPORATE
C  ANY FORWARD BLEND EFFECTS).
C
      QDIFF = VAL - W(LOCQO+I-1)
      IF (DIFFS) GO TO 465
      IF (W(LOCQO+I-1) .LT. 0.01) W(LOCQO+I-1) = 0.01
      BRATIO = VAL/W(LOCQO+I-1)
  465 CONTINUE
      W(LOCQO+I-1) = VAL
      NBEFOR = 0
      IBSTRT = I - NBLEND + 1
C
C  DO BLENDING ONLY IF THE BEGINNING PERIOD DOES NOT EQUAL THE ENDING
C  PERIOD.
C
      IF (IBSTRT .NE. NRUN)
     .   CALL X17B26(W(LOCQO),QDIFF,NBEFOR,NBLEND,IBSTRT,NRUN,1)
C
      NMISS = 0
      LAOBS = I
      GO TO 500
C
C  WE NEED TO CHECK IF CARRYOVER NEEDS TO BE STORED IN /XCO26/ FOR
C  EACH TIME-PERIOD.
C
  490 CONTINUE
      IF (ISAVCO.EQ.0 ) GO TO 500
C
C  LOOP THROUGH LIST OF CO SAVE PERIODS AND SEE IF WE MATCH THE
C  CURRENT PERIOD WITH ONE OF THE SAVE PERIODS.
C
      DO 493 J=1,NCSTOR
      IF (ICOPOS(J) .EQ. I) GO TO 494
  493 CONTINUE
      GO TO 500
C
C  STORE NEEDED CO VALUES IN /XCO26/
C
  494 CONTINUE
      NSAVE = NMISS
      IF (NMISS .GT. NBLEND) NSAVE = 0
      ADJCO(1,J) = QDIFF
      ADJCO(2,J) = NSAVE + 0.01
C
  500 CONTINUE
C
C  BLEND OUT FROM LAST OBSERVATION (IF NOT AT END OF OBSERVED PERIOD)
C
      IF (LAOBS .EQ. NRUN) GO TO 900
      IBSTRT = LAOBS + 1
      IF (ABS(QDIFF).GT.0.01) CALL X17B26(W(LOCQO),QDIFF,NBEFOR,NBLEND,
     .            IBSTRT,NRUN,0)
C
  900 CONTINUE
      IF (IBUG .GE. 2) WRITE(IODBUG,603)
  603 FORMAT(/10X,'** ADJUSTED INST. DISCHARGES **')
      IF (IBUG .GE. 2) WRITE(IODBUG,650) (W(LOCQO+I-1),I=1,NRUN)
C
      IF (IBUG.GE.1) WRITE(IODBUG,699)
  699 FORMAT('   ***EXIT X17I26 ***')
C
      RETURN
      END
