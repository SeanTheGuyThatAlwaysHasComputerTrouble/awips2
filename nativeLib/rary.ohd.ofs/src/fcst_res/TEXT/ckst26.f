C MEMBER CKST26
C  (from old member FCCKST26)
C
      SUBROUTINE CKST26(NRUN,KSTART,KEND,VOLUME,STOR1,QGENMX,QOMIN,
     $STOUPR,STOLWR,STOMAX,STOMIN,QIMHYD,QOMOBS,QOMHYD,IBUG,FCST)
C
C THIS SUBROUTINE IS USED IN QGEN26 TO COMPUTE STORAGES FOR THE 24-HR
C PERIOD (OR PARTIAL PERIOD) FROM ARRAY POSITION KSTART THROUGH KEND AND
C TO CHECK THESE STORAGES AGAINST UPPER AMD LOWER LIMITING STORAGES.
C OUTFLOWS ARE CHANGED (UNLESS OBSERVED) TO KEEP FROM EXCEEDING LIMITING
C STORAGES EXCEPT THAT OUTFLOW WILL NOT BE ALLOWED TO DROP BELOW REQUIR-
C ED MINIMUM OUTFLOW EVEN IF THE POOL DROPS BELOW THE LOWER LIMIT.  A
C NEW VALUE OF OUTFLOW VOLUME FROM KSTART TO KEND IS COMPUTED BY THE
C SUBROUTINE.
C
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     MAY,1982
C     REVISION NO. 1 BY W. E. FOX --  AUGUST, 1989
C
C SUBROUTINE CKST26 IS IN
C
C VARIABLES IN THE ARGUMENT LIST ARE DEFINED AS FOLLOWS:
C     NRUN -- POSITION IN ARRAYS AT RUN TIME.
C     KSTART -- ARRAY POSITIONS AT END OF FIRST TIME INTERVAL IN 24-HR
C       OR PARTIAL 24-HR PERIOD.
C     KEND -- ARRAY POSITION AT END OF 24-HR OR PARTIAL 24-HR PERIOD.
C     VOLUME -- REVISED OUTFLOW VOLUME AFTER ADJUSTMENT OF OUTFLOWS.
C     STOR1 -- STORAGE AT BEGINNING OF FIRST TIME INTERVAL WHEN ENTERING
C       SUBROUTINE AND AT END OF PERIOD WHEN LEAVING SUBROUTINE.
C     QOMIN -- REQUIRED MINIMUM OUTFLOW.
C
C DEFINITIONS OF REMAINDING VARIABLES CAN BE OBTAINED FROM SUBROUTINE
C QGEN26.
C
      LOGICAL FCST
      DIMENSION STOMAX(1),STOMIN(1),QIMHYD(1),QOMOBS(1),QOMHYD(1)
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/ckst26.f,v $
     . $',                                                             '
     .$Id: ckst26.f,v 1.2 1996/07/12 13:51:38 dws Exp $
     . $' /
C    ===================================================================
C
C
C WRITE TRACE AND DEBUG IF REQUIRED,
C
      IF(IBUG-1)60,10,20
   10 WRITE(IODBUG,30)
      GO TO 60
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** CKST26 ENTERED)
      WRITE(IODBUG,40) VOLUME,STOR1,QGENMX,STOUPR,STOLWR,
     & QOMIN,NRUN,KSTART,KEND,(QIMHYD(I),I=KSTART,KEND)
   40 FORMAT(1H0,' VOLUME,STOR1,QGENMX,STORMX,STORMN,',
     $'QOMIN,NRUN,KSTART,KEND'/1X,6F10.0,3I6/1H0,
     $37H MEAN INFLOWS FROM KSTART TO KEND ARE/(1X,8F10.0))
      WRITE(IODBUG,50)(QOMHYD(K),K=KSTART,KEND)
   50 FORMAT(1H0,18H MEAN OUTFLOWS ARE/(1X,8F10.0))
C
C SET ADD AND VOLUME TO 0.  ADD WILL BE THE DIFFERENCE IN FIRST OUTFLOW
C AND REVISED OUTFLOW DUE TO LIMITING STORAGE.  ADD WILL BE ADDED TO THE
C NEXT OUTFLOW TO TRY TO KEEP THE VOLUME THE SAME FOR THE PERIOD.  A NEW
C VOLUME WILL BE COMPUTED FROM THE REVISED OUTFLOWS.  COMPUTE STORAGES
C FROM KSTART TO KEND IN DO LOOP 10 AND REVISE OUTFLOW IF STORAGE GOES
C OUTSIDE LIMITS EXCEPT THAT OUTFLOW MUST NOT BE LESS THAN REQUIRED
C MINIMUM.
C
   60 ADD=0.
      VOLUME=0.
      DO 110 K=KSTART,KEND
      IF(FCST.OR.K.GT.NRUN) GO TO 70
C$     IF(QOMOBS(K).EQ.-999.0) GO TO 70
C$     QOMHYD(K)=QOMOBS(K)
C$     STOR2=STOR1+QIMHYD(K)-QOMHYD(K)
CR1 *** REVISION NO. 1 *** BEGIN ***
C$     VOLUME=VOLUME+QOMHYD(K)
CR1 *** REVISION NO. 1 *** END ***
C$     GO TO 110
   70 QIMK=QIMHYD(K)
      QOMK=QOMHYD(K)+ADD
      IF(QOMK.LE.QOMIN) QOMK=QOMIN
      IF(QOMK.GE.QGENMX) QOMK=QGENMX
      ADD=0.
      STOR2=STOR1+QIMK-QOMK
      STORMX=STOUPR
      IF(STORMX.EQ.-999.0) STORMX=STOMAX(K)
      STORMN=STOLWR
      IF(STORMN.EQ.-999.0) STORMN=STOMIN(K)
      IF(STOR2.LE.STORMX) GO TO 80
      STOR2=STORMX
      QOMK=STOR1+QIMK-STOR2
      IF(QOMK.LE.QGENMX) GO TO 100
      ADD=QOMK-QGENMX
      QOMK=QGENMX
      GO TO 90
   80 IF(STOR2.GE.STORMN) GO TO 100
      STOR2=STORMN
      QOMK=STOR1+QIMK-STOR2
      IF(QOMK.GE.QOMIN) GO TO 100
      ADD=QOMK-QOMIN
C
C IF LESS THAN REQUIRED MINIMUM, OUTFLOW WILL BE SET TO MINIMUM DIS-
C CHARGE EVEN THOUGH THE POOL DROPS BELOW LOWER LIMITING STORAGE.
C
      QOMK=QOMIN
   90 STOR2=STOR1+QIMK-QOMK
  100 VOLUME=VOLUME+QOMK
      QOMHYD(K)=QOMK
  110 STOR1=STOR2
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)200,180,120
  120 WRITE(IODBUG,130)
  130 FORMAT(1H0,52H VALUES OF VARIABLES AT END OF CKST26 ARE AS FOLLOWS
     $)
      WRITE(IODBUG,40) VOLUME,STOR1,QGENMX,STORMX,STORMN,
     & QOMIN,NRUN,KSTART,KEND,(QIMHYD(I),I=KSTART,KEND)
      WRITE(IODBUG,140) (QOMHYD(K),K=KSTART,KEND)
  140 FORMAT(1H0,82H MEAN OUTFLOWS FOR THE TIME INTERVALS DURING THE 24-
     $HR OR PARTIAL 24-HR PERIOD ARE/(1X,8F10.0))
      IF(STOUPR.EQ.-999.0)WRITE(IODBUG,160)(STOMAX(K),K=KSTART,KEND)
  160 FORMAT(1H0,57H LIMITING UPPER STORAGES AT END OF EACH TIME INTERVA
     $L ARE/(1X,8F10.0))
      IF(STOLWR.EQ.-999.0)WRITE(IODBUG,170)(STOMIN(K),K=KSTART,KEND)
  170 FORMAT(1H0,57H LIMITING LOWER STORAGES AT END OF EACH TIME INTERVA
     $L ARE/(1X,8F10.0))
  180 WRITE(IODBUG,190)
  190 FORMAT(1H0,10X,17H** LEAVING CKST26)
  200 RETURN
      END
