C MEMBER PASN26
C  (from old member FCPASN26)
C **********************************************************************
      SUBROUTINE PASN26(STOR,ELEV)
C **********************************************************************
C SUBROUTINE PASN26 SETS THE DISCHARGE EQUAL TO THE RESERVOIR INFLOW AT
C THE END OF TIME PERIOD.  A SIMULATED RUN IS MADE FIRST FOR ALL TIME
C PERIODS STARTING WITH THE BEGINNING OBSERVED OR ADJUSTED STORAGE.
C AFTER THE SIMULATED RUN, MISSING STORAGES ARE DETERMINED WITH THE
C ADJUST-Q SUBROUTINE, USING SIMULATED AND OBSERVED STORAGES.  A SECOND
C ADJUSTED RUN IS THEN MADE USING ADJUSTED AND OBSERVED STORAGES AND
C OBSERVED OUTFLOWS.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C **********************************************************************
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     OCTOBER, 1981
C **********************************************************************
C SUBROUTINE PASN26 IS IN
C **********************************************************************
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IFCST -- SIMULATED RUN (IFCST=0) AND ADJUSTED RUN (IFCST=1).
C     FCST -- LOGICAL VARIABLE.  IF TRUE, TIME PERIOD IS IN FORECAST
C       PERIOD.
C     QI2 -- INFLOW AT END OF TIME PERIOD.
C     QIM -- MEAN INFLOW FOR TIME PERIOD.
C     QO1 -- OUTFLOW AT BEGINNING OF TIME PERIOD.
C     QO2 -- OUTFLOW AT END OF TIME PERIOD.
C     QOM -- MEAN OUTFLOW FOR TIME PERIOD.
C     S1 -- STORAGE AT BEGINNING OF TIME PERIOD IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME PERIOD.
C     S2 -- STORAGE AT END OF TIME PERIOD IN UNITS OF MEAN DISCHARGE FOR
C       THE TIME PERIOD.
C     ELEV2 -- ELEVATION AT END OF TIME PERIOD.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION CURVE IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME PERIOD.
C     ELEV -- ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NTERP -- INDICATES ARITHMETIC (NTERP=0) OR LOGARITHMIC
C       INTERPOLATION (NTERP=1).
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1),TRACE AND
C       DEBUG (IBUG=2).
C **********************************************************************
C ALL VALUES EXCEPT QO2, QOM, S2, AND ELEV2 ARE FURNISHED BY THE SUPER-
C VISORY EXECUTION ROUTINE.  QO2, QOM, S2, AND ELEV2 ARE COMPUTED IN
C PASN26 EXCEPT DURING THE ADJUSTED RUN WHEN OBSERVED OR ADJUSTED.
C **********************************************************************
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
C
      DIMENSION STOR(1),ELEV(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/pasn26.f,v $
     . $',                                                             '
     .$Id: pasn26.f,v 1.1 1995/09/17 19:05:57 dws Exp $
     . $' /
C    ===================================================================
C
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** PASN26 ENTERED)
      WRITE(IODBUG,40) IFCST,FCST,QI2,QIM,QO1,QO2,QOM,S1,S2,ELEV2,NTERP,
     $IBUG
   40 FORMAT(1H0,56H IFCST,FCST,QI2,QIM,QO1,QO2,QOM,S1,S2,ELEV2,NTERP,
     $IBUG/1X,I6,L5,8F12.3,2I6)
   50 IF(IFCST.EQ.0)GO TO 60
      IF(FCST) GO TO 60
      OBSQO2=QO2
C
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      IF(IGO.EQ.1) GO TO 80
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; OR ADJUSTED BETWEEN OBSERVED
C VALUES USING SIMULATED AND OBSERVED VALUES.
C WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED IN STATEMENT 71
C FROM THE OBSERVED MEAN INFLOW, THE ADJUSTED MEAN INFLOW, AND THE
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
      IF(QOM.EQ.-999.0) GO TO 51
      GO TO 71
   51 IF(QO2.NE.-999.0)GO TO 70
   60 QO2=QI2
      IF(QO2.LT.0.) QO2=0.
   70 QOM=(QO1+QO2)*0.5
C **********************************************************************
C USUALLY MEAN INFLOW (QIM) AND MEAN OUTFLOW (QOM) WILL BE EQUAL BUT IN
C SOME CASES BEGINNING OUTFLOW (QO1) AND INFLOW (QI1) MAY
C NOT BE EQUAL AND, THEREFORE, QIM AND QOM WOULD NOT BE EQUAL.  FOR
C EXAMPLE, QO1 MIGHT BE  REQUIRED MINIMUM FLOW WHEN QI1 IS LESS THAN QO1
C OR MAXIMUM POSSIBLE FLOW WHEN QI1 IS HIGHER THAN QO1.
C **********************************************************************
   71 S2=S1+QIM-QOM
      IF(S2.GE.0.) GO TO 90
      S2=0.
      QOM=S1+QIM
      GO TO 90
C **********************************************************************
C SET ENDING OUTFLOW EQUAL TO MEAN OUTFLOW IF ENDING OUTFLOW (OBSQO2) IS
C NOT OBSERVED.  THIS TIME PERIOD IS PRIOR TO RUN TIME.
C **********************************************************************
   80 QO2=QOM
      IF(OBSQO2.NE.-999.0) QO2=OBSQO2
   90 IF(IFCST.EQ.0) GO TO 100
      IF(FCST) GO TO 100
      IF(ELEV2.NE.-999.0)GO TO 110
C **********************************************************************
C COMPUTE ELEV2 WITH ARITHMETIC (NTERP=0) OR LOGARITHMIC INTERPOLATION
C (NTERP=1).
C **********************************************************************
  100 CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
  110 IF(IBUG-1)170,150,120
  120 WRITE(IODBUG,130)
  130 FORMAT(1H0,40H VALUES AT END OF PASN26 ARE AS FOLLOWS:)
      WRITE(IODBUG,40) IFCST,FCST,QI2,QIM,QO1,QO2,QOM,S1,S2,ELEV2,NTERP,
     $IBUG
      WRITE(IODBUG,140)IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
  140 FORMAT(1H0,9H IFLAG IS,I6,43H.  NO OF POINTS ON ELEV. - STORAGE CU
     $RVE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION
     $ARE/(1X,5(F12.3,F9.3,3X)))
  150 WRITE(IODBUG,160)
  160 FORMAT(1H0,10X,17H** LEAVING PASN26)
  170 RETURN
      END
