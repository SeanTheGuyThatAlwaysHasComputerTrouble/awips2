C MEMBER OVER26
C  (from old member FCOVER26)
C***********************************************************************
      SUBROUTINE OVER26(STOR,ELEV,PEAKO,PKPOS,O,SOH,OH,HS,TOTALQ,QG,
     $WORK)
C***********************************************************************
C THIS SUBROUTINE COMPUTES DAM DISCHARGES OVER UNCONTROLLED SPILLWAYS,
C GATED SPILLWAYS WITH ALL GATES FULLY OPEN, OR THROUGH FULLY-OPEN
C SLUICES (OR DIVERSION TUNNELS).  MODIFIED PULS ROUTING IS USED BY
C CALLING SUBROUTINE RUTE26.  PROVISION IS MADE FOR ADDITIONAL TIME
C STEPS WITHIN THE REGULAR TIME PERIOD IF REQUIRED BY THE SLOPE OF THE
C STORAGE VS DISCHARGE CURVE OR BY ROUTING THAT BEGINS OR ENDS WITHIN
C THE REGULAR TIME PERIOD.  A SIMULATED RUN FOR ALL TIME PERIODS IS MADE
C FIRST, STARTING WITH THE BEGINNING OBSERVED OR ADJUSTED STORAGE AND
C USING ADJUSTED INFLOW.  (THE SIMULATED RUN MIGHT START IN ANOTHER SUB-
C ROUTINE.)  AFTER THE SIMULATED RUN, MISSING STORAGES PRIOR TO THE LAST
C OBSERVED STORAGE ARE COMPUTED WITH THE ADJUST-Q SUBROUTINE.  AN AD-
C JUSTED RUN IS THEN MADE BY USING OBSERVED AND ADJUSTED STORAGES,
C OBSERVED OUTFLOWS AND ADJUSTED INFLOWS.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C***********************************************************************
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     DECEMBER, 1981
C***********************************************************************
C SUBROUTINE OVER26 IS IN
C***********************************************************************
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IFCST -- SIMULATED RUN (IFCST=0) AND ADJUSTED RUN (IFCST=1).
C     IUNC -- UNCONTROLLED SPILLWAY (OR SLUICE) WHEN IUNC=1.  GATED
C       SPILLWAY OR GATED PLUS UNCONTROLLED WHEN IUNC=0.
C     FCST -- LOGICAL VARIABLE. IF TRUE, THE TIME PERIOD IS IN THE
C       FORECAST PERIOD ( AFTER RUN TIME).
C     NS2 -- POSITION IN ARRAYS AT END OF TIME PERIOD.
C     CREST -- SPILLWAY CREST ELEVATION OR INVERT ELEVATION OF SLUICE.
C     STOCR -- RESERVOIR STORAGE AT SPILLWAY CREST.  MUST BE IN UNITS OF
C       MEAN DISCHARGE FOR THE TIME PERIOD.
C     SFILL -- RESERVOIR STORAGE FOR GATED SPILLWAY WHERE INFLOW IS
C       PASSED.  STORAGE MUST BE IN UNITS OF MEAN DISCHARGE FOR THE TIME
C       PERIOD.
C     SPILMX -- MAXIMUM DISCHARGE OVER SPILLWAY AT SFILL STORAGE LEVEL.
C     QI1 -- INFLOW AT BEGINNING OF TIME PERIOD.
C     QI2 -- INFLOW AT END OF TIME PERIOD.
C     QIM -- MEAN INFLOW FOR TIME PERIOD.
C     QO1 -- OUTFLOW AT BEGINNING OF TIME PERIOD.
C     QO2 -- OUTFLOW AT END OF TIME PERIOD.
C     QOM -- MEAN OUTFLOW FOR TIME PERIOD.
C     QOK1 -- NON-SPILLWAY DISCHARGE AT BEGINNING OF TIME PERIOD.
C     QOK2 -- NON-SPILLWAY DISCHARGE AT END OF TIME PERIOD.
C     S1 -- STORAGE AT BEGINNING OF TIME PERIOD IN UNITS OF MEAN DIS-
C       CHARGE FOR THE TIME PERIOD.
C     S2 -- STORAGE AT END OF TIME PERIOD IN UNITS OF MEAN DISCHARGE FOR
C       THE TIME PERIOD.
C     ELEV1 -- ELEVATION AT BEGINNING OF TIME PERIOD.
C     ELEV2 -- ELEVATION AT END OF TIME PERIOD.
C     TESTPK -- TEST VALUE OF OUTFLOW FOR DETERMINING WHETHER OR NOT A
C       PEAK OUTFLOW BETWEEN QO1 AND QO2 WILL LATER REPLACE ONE OF THESE
C       VALUES IN THE OUTFLOW ARRAY.  TESTPK CAN BE FLOOD DISCHARGE OR
C       A LOWER VALUE IF DESIRED.
C     PEAKO -- ARRAY OF PEAK OUTFLOWS ABOVE TESTPK THAT OCCUR BETWEEN
C       INSTANTANEOUS OUTFLOWS AT REGULAR TIME INTERVALS.  THESE PEAK
C       VALUES WILL BE SUBSTITUTED BY THE EXECUTION SUPERVISORY ROUTINE
C       IN THE INSTANTANEOUS OUTFLOW ARRAY AS INDICATED BY POSITION
C       NUMBERS IN THE PKPOS ARRAY.
C     PKPOS -- ARRAY OF POSITION NUMBERS THAT INDICATE WHERE THE
C       CORRESPONDING PEAKO VALUES WILL BE PLACED IN THE INSTANTANEOUS
C       OUTFLOW ARRAY.  UNDEFINED PKPOS VALUE WILL BE -999.0.  PKPOS
C       VALUES ARE REAL NUMBERS IN THE ARRAY BUT WILL BE CONVERTED TO
C       INTEGERS WHEN PUTTING PEAKO VALUES IN THE OUTFLOW ARRAY.
C     NUMPKO -- NUMBER OF PAIRS OF DEFINED PEAKO AND PKPOS VALUES.
C       IF THERE ARE NO DEFINED VALUES, NUMPKO WILL BE -999.
C     NTOTPK -- NO. OF POSITIONS AVAILABLE IN EACH OF THE PEAKO AND
C       PKPOS ARRAYS.
C     O -- ARRAY OF DISCHARGE VALUES FOR O VS SOH RELATION.  FIRST VALUE
C       MUST BE 0.
C     SOH -- CORRESPONDING ARRAY OF (STORAGE + O/2) VALUES.  STORAGE
C       MUST BE IN UNITS OF MEAN DISCHARGE FOR THE TIME INTERVAL.  FIRST
C       VALUE MUST BE 0.
C     NOSOH -- NUMBER OF PAIRS OF O AND SOH VALUES.
C     OH -- DISCHARGE VALUES FOR SPILLWAY RATING.
C     HS -- ELEVATIONS FOR SPILLWAY RATING. (CHANGE - JTO 8/83)
C     NOHS -- NO. OF PAIRS OF OH AND HS VALUES.
C     NTERPQ -- INDICATES ARITHMETIC (NTERPQ=0) OR LOGARITHMIC INTERPO-
C       LATION (NTERPQ=1) FOR OH VS HS RELATION.
C     QGENMX -- MAXIMUM GENRATION DISCHARGE FOR A POWER DAM.  (WILL BE
C       ZERO IF A NON-POWER DAM).  WILL BE USED FOR GENERATION DISCHARGE
C       IF NO OBSERVED VALUE OR PROPOSED VALUE IS AVAILABLE AND IF A
C       RELATION OF TOTAL DISCHARGE VS MAXIMUM GENERATION FLOW IS NOT
C       USED.
C     SLUICE -- SPECIFIED CONSTANT SLUICE DISCHARGE.  EITHER ZERO OR A
C       POSITIVE VALUE MUST BE SPECIFIED.
C     TOTALQ -- ARRAY OF TOTAL DISCHARGES VS MAXIMUM GEMERATION DIS
C       CHARGE RELATION.  FOR THIS RELATION, UNCONTROLLED SPILLWAY AND
C       UNCONTROLLED PENSTOCKS ARE ASSUMED. A NON-POWER DAM WOULD NOT
C       NEED THIS TYPE OF RELATION SINCE THE DISCHARGE THROUGH UNCON-
C       TROLLED SLUICES WOULD BE INCORPORATED IN THE ROUTING SCHEME.
C     QG -- ARRAY OF MAXIMUM GENERATION DISCHARGES FOR TOTALQ VS QG
C       RELATION.
C     NQG -- NUMBER OF PAIRS OF TOTALQ AND QG VALUES.  VALUE OF 0
C       INDICATES TOTALQ VS QG RELATION IS NOT USED.
C     PCTERG -- SPECIFIED FRACTION OF OUTFLOW AT END OF TIME PERIOD TO
C       CHECK AGAINST ERROR IN FIRST ESTIMATE OF QOK2.  IF ERROR IN QOK2
C       EXCEEDS PCTERG*QO2, THE PROGRAM WILL GO THROUGH OVER26 AGAIN
C       USING THE SECOND ESTIMATE OF QOK2 AS COMPUTED AT THE END OF THE
C       SUBROUTINE.  THE PROGRAM WILL LEAVE OVER26 AFTER THE SECOND RUN
C       REGARDLESS OF ERROR.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION CURVE.  MUST BE IN UNITS
C       OF MEAN DISCHARGE FOR THE TIME PERIOD.
C     ELEV -- ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     WORK -- WORKING ARRAY FOR COMPUTATIONAL PURPOSES.  WORK ARRAY
C       REQUIRES NOSOH POSITIONS.
C     NTERP -- ARITHMETIC (NTERP=0) OR LOGARITHMUC INTERPOLATION
C       (NTERP=1) FOR STORAGE VS ELEVATION CURVE.
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1),TRACE AND
C       DEBUG (IBUG=2).
C***********************************************************************
C QO2, QOM, S2,, ELEV2, AND VALUES IN THE PEAKO AND PKPOS ARRAYS WILL
C BE COMPUTED IN THIS SUBROUTINE EXCEPT IN THE ADJUSTED RUN WHEN VALUES
C ARE OBSERVED.
C***********************************************************************
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
      INCLUDE 'common/fila26'
      INCLUDE 'common/root26'
C
      DIMENSION O(1),SOH(1),OH(1),HS(1),STOR(1),ELEV(1),WORK(1),PEAKO(1)
     $,PKPOS(1),TOTALQ(1),QG(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/over26.f,v $
     . $',                                                             '
     .$Id: over26.f,v 1.1 1995/09/17 19:05:56 dws Exp $
     . $' /
C    ===================================================================
C
C
C***********************************************************************
C NTRIAL IS USED TO DEFINE THE NUMBER OF RUNS THROUGH OVER26.  A SECOND
C RUN WILL BE MADE IF THE FIRST ESTIMATE OF QOK1 IS TOO MUCH IN ERROR.
C ......................................................................
      IADJH=0
      NQG=NQGEN
      ESTQO2=(QO1+QIM)*0.5
      NTRIAL=0
   10 NTRIAL=NTRIAL+1
C ......................................................................
C WRITE TRACE AND DEBUG IF REQUIRED.
C***********************************************************************
      IF(IBUG-1)60,20,30
   20 WRITE(IODBUG,40)
      GO TO 60
   30 WRITE(IODBUG,40)
   40 FORMAT(1H0,10X,17H** OVER26 ENTERED)
      WRITE(IODBUG,50) IFCST,FCST,NS2,IUNC,CREST,STOCR,SFILL,SPILMX,
     $QI1,QI2,QIM,QO1,QO2,QOM,QOK1,QOK2,S1,S2,ELEV1,ELEV2,
     $TESTPK,NTOTPK,NTERPQ,QGENMX,SLUICE,NQG,PCTERG,NTERP,IBUG,NTRIAL
   50 FORMAT(1H0,'   IFCST,FCST,NS2,IUNC,CREST,STOCR,SFILL,SPILMX,QI1,',
     $'QI2,QIM,QO1,       QO2,       QOM,QOK1,QOK2,S1,     S2,ELEV1',
     $/I6,L5,2I6,8F12.3/1X,7F12.3/1H0,' ELEV2,TESTPK,NTOTPK,NTERPQ,',
     $'QGENMX,SLUICE,NQG,PCTERG,NTERP,IBUG,NTRIAL'/1X,2F12.3,2I6,2F12.3
     $,I6,F12.3,3I6)
   60 CONTINUE
      IF(IUNC .EQ. 0) SPASSI=SFILL
      IF(IUNC .EQ. 1) SPASSI=STOCR
      IF(IFCST.EQ.0.OR.FCST) GO TO 70
      IF(ICOMB .NE. 4) GO TO 51
      IADJH=1
      GO TO 70
   51 OBSQO2=QO2
C
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      IF(IGO.EQ.1) GO TO 280
      IF(OBSQO2.EQ.-999.0) GO TO 61
      QOM=(QO1+QO2)*0.5
      S2=S1+QIM-QOM
      GO TO 290
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; ADJUSTED BETWEEN OBSERVED
C VALUES USING SIMULATED AND OBSERVED VALUES; OR INTERPOLATED BETWEEN
C OBSERVED VALUES.  WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED
C FROM THE OBSERVED MEAN INFLOW, THE ADJUSTED MEAN INFLOW, AND THE
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
   61 IF(QOM.EQ.-999.0) GO TO 70
      S2=S1+QIM-QOM
      GO TO 280
C***********************************************************************
C COMPUTE DIFFERENCE IN INFLOWS FROM BEGINNING TO END OF TIME PERIOD.
C  IF A TOTAL DISCHARGE VS MAXIMUM GENERATION DISCHARGE RELATION IS
C USED, THE OUTFLOW AT THE END OF THE TIME PERIOD (QO2) IS
C ESTIMATED AS AN AVEAGE OF OUTFLOW AT BEGINNING OF TIME PERIOD (QO1)
C AND MEAN INFLOW (QIM) AND THEN GENERATION DISCHARGE AT THE END OF THE
C TIME PERIOD (QOK2) IS COMPUTED FROM THE RELATION.
C  A SECOND ESTIMATE OF QOK2 IS MADE AT THE END OF
C THE SUBROUTINE AND, IF THE DIFFERENCE IN THE TWO ESTIMATES IS GREATER
C THAN A SPECIFIED FRACTION OF QO2, A SECOND RUN WILL BE MADE THROUGH
C OVER26.
C***********************************************************************
   70 DIFQI=QI2-QI1
C      IF(QOK2.NE.-999.0) GO TO 90
      IF(NQG.EQ.0) GO TO 90
      CALL TERP26(ESTQO2,QGEN,TOTALQ,QG,NQG,IFLAG,IBUG)
      QOK2=QGEN+SLUICE
      GO TO 100
   90 NTRIAL=2
C
C NON-SPILLWAY DISCHARGE (QOK2) AT THE END OF THE TIME PERIOD IS NOT
C COMPUTED WHEN OBSERVED STORAGES ARE AVAILABLE AND,
C THEREFORE, QOK1 WOULD BE -999.0 FOR  THE FIRST TIME INTERVAL AFTER
C THE LAST OBSERVED STORAGE IF NON-SPILLWAY DISCHARGES ARE MISSING.  TO
C TAKE CARE OF THIS POSSIBILITY, SET QOK1=QOK2 WHEN QOK1 IS -999.0.
C
  100 IF(QOK1.EQ.-999.0) QOK1=QOK2
      DIFQOK=QOK2-QOK1
      QOKM=(QOK1+QOK2)*0.5
      IF(IADJH .EQ. 0) GO TO 1190
      IF(S2 .GE. SPASSI) GO TO 1130
      QO2=QOK2
      GO TO 1180
 1130 IF(IUNC .EQ. 1) GO TO 1150
      IF(S2 .GT. SPASSI) GO TO 1150
      IF(QI2 .GT. QPASMX) GO TO 1150
      QOM=0.5*(QO1+SPILMX)
      IF(QIM .GT. QOM) GO TO 1150
      QO2=QI2
      IF(QO2 .LE. QOK2) QO2=QOK2
      QOM=S1+QIM-S2
      GO TO 1180
 1150 CALL NTER26(ELEV2,QOS,HS,OH,NOHS,IFLAG,NTERPQ,IBUG)
      IF(IFLAG .EQ. 1) QOS=OH(NOHS)
      IF(IFLAG .EQ. -1) QOS=OH(1)
      QO2=QOS+QOK2
 1180 QOM=0.5*(QO1+QO2)
      IF(QOM .LE. 0.) QOM=0.
      GO TO 290
 1190 CONTINUE
      IF(IUNC.EQ.0) GO TO 160
C***********************************************************************
C TEST IF POOL IS ABOVE OR BELOW UNCONTROLLED SPILLWAY CREST AT THE
C BEGINNING OF THE TIME PERIOD.
C***********************************************************************
      IF(S1.GE.STOCR) GO TO 130
C***********************************************************************
C CHECK IF STORAGE AT END OF PERIOD (SS2) EXCEEDS STOCR ASSUMING NO
C SPILLWAY DISCHARGE.
C***********************************************************************
      SS2=S1+QIM-QOKM
      IF(SS2.GT.STOCR) GO TO 110
C***********************************************************************
C WHEN SS2.LE.STOCR, THE POOL DOES NOT EXCEED SPILLWAY CREST.  GO TO
C STATEMENT 170 TO DETERMINE S2, QOM, AND QO2.
C***********************************************************************
      GO TO 170
C***********************************************************************
C COMPUTE THE FRACTION OF THE TIME PERIOD THAT IS REQUIRED FOR THE POOL
C TO FILL TO THE SPILLWAY CREST.  FUNCTION FRAC26 WILL COMPUTE THIS
C FRACTION.
C***********************************************************************
C QSFRAC IS THE SPILLWAY DISCHARGE AT THE TIME THE POOL REACHES SPILL-
C WAY CREST AND WILL BE 0 IN THIS CASE.
C
  110 QSFRAC=0.
C     SFILL=STOCR
      FRAC=FRAC26(DIFQI,DIFQOK,QI1,QO1,QOK1,QSFRAC,S1,STOCR,IBUG)
C***********************************************************************
C THE FOLLOWING STATEMENTS DETERMINE QOKBGN,QOSBGN, AND SBGN.  THESE ARE
C ,RESPECTIVELY, THE NON-SPILLWAY AND SPILLWAY DISCHARGES AND THE POOL
C STORAGE AT TIME THE POOL REACHES THE SPILLWAY CREST.
C***********************************************************************
      QOKBGN=QOK1+DIFQOK*FRAC
      QOSBGN=0.
      SBGN=STOCR
      FRAC1=FRAC
      FRAC2=1.-FRAC
C***********************************************************************
C SUBROUTINE RUTE26 ROUTES INFLOW AND COMPUTES THE TOTAL DISCHARGE (QO2)
C AND THE STORAGE (S2) AT THE END OF THE TIME PERIOD AND THE AVERAGE
C TOTAL DISCHARGE (QOM) FOR THE TIME PERIOD.  IN THIS CASE THE ROUTING
C TIME STEP IS FRAC2*TIME PERIOD UNLESS A SHORTER TIME STEP IS NEEDED
C FOR ROUTING.  IN THAT CASE ADDITIONAL TIME STEPS ARE USED.
C***********************************************************************
  120 CALL RUTE26(FRAC1,FRAC2,SBGN,QOKBGN,QOSBGN,PEAKO,PKPOS,O,SOH,WORK)
C***********************************************************************
C GO TO STATEMENT 290 TO CHECK FOR OBSERVED ELEV2 OR TO COMPUTE ELEV2
C FROM S2.
C***********************************************************************
      GO TO 290
C***********************************************************************
C FOR FOLLOWING STATEMENT 4, THE POOL WAS ABOVE THE UNCONTROLLED SPILL-
C WAY CREST AT THE BEGINNING OF THE TIME PERIOD.  CHECK IF POOL IS ABOVE
C OR BELOW CREST AT END OF TIME PERIOD BY ASSUMING ZERO SPILLWAY DIS-
C CHARGE AT END OF PERIOD.
C***********************************************************************
  130 SS2=S1+QIM-(QO1+QOK2)*0.5
      IF(SS2.GE.STOCR) GO TO 140
C
C COMPUTE FRACTION (FRAC) OF TIME PERIOD REQUIRED FOR POOL TO FALL TO
C SPILLWAY CREST.
C***********************************************************************
C     SFILL=STOCR
      QSFRAC=0.
      FRAC=FRAC26(DIFQI,DIFQOK,QI1,QO1,QOK1,QSFRAC,S1,STOCR,IBUG)
C***********************************************************************
C IN THIS SITUATION, ROUTING STARTS AT THE BEGINNING OF THE TIME PERIOD
C AND ENDS AFTER FRAC PORTION OF THE TIME PERIOD.  SUBROUTINE RUTE26
C USES FRAC2 AS THE ROUTING PORTION OF THE TIME PERIOD.  FRAC1 IS SET TO
C 0. AS IT IS THE FRACTION OF THE TIME PERIOD BEFORE ROUTING BEGINS.
C***********************************************************************
      FRAC1=0.
      FRAC2=FRAC
      QOKBGN=QOK1
      QOSBGN=QO1-QOK1
      SBGN=S1
C***********************************************************************
C STATEMENT 120 CALLS RUTE26.
C***********************************************************************
      GO TO 120
C***********************************************************************
C FOR STATEMENT 140, POOL WAS ABOVE UNCONTROLLED SPILLWAY BOTH AT THE
C BEGINNING AND END OF THE TIME PERIOD.
C***********************************************************************
  140 FRAC2=1.0
      FRAC1=0.
  150 QOKBGN=QOK1
      SBGN=S1
      QOSBGN=QO1-QOK1
      GO TO 120
C***********************************************************************
C STATEMENT 160 STARTS THE COMPUTATIONS FOR GATED SPILLWAYS BY CHECKING
C IF BEGINNING STORAGE (S1) IS GREATER OR LESS THAN SFILL.  SFILL IS THE
C POOL STORAGE WHERE INFLOW IS PASSED.
C***********************************************************************
  160 IF(S1.GE.SPASSI)GO TO 230
C***********************************************************************
C THE SUPERVISORY ROUTINE SHOULD NOT HAVE SENT THE PROGRAM TO OVER26
C UNLESS ROUTING IS REQUIRED DURING THE TIME PERIOD.  HOWEVER, CHECK
C THAT THE FIRST APPROXIMATION OF S2 (SS2) IS GREATER THAN SFILL.  IN
C THE UNLIKELY EVENT THAT THERE IS SOME SPILLWAY DISCHARGE AT THE BEGIN-
C NING OF THE TIME PERIOD, (QO1-QOK1).GT.0.), IT WILL BE ASSUMED THAT
C THIS DISCHARGE WILL BE KEPT CONSTANT.
C***********************************************************************
      SS2=S1+QIM-QOKM-(QO1-QOK1)
      IF(SS2.GT.SPASSI) GO TO 190
C***********************************************************************
C ENDING POOL STORAGE (S2) IS LESS THAN SFILL SO NO ROUTING IS REQUIRED.
C***********************************************************************
  170 S2=SS2
      QOM=QOKM
      IF(IFCST.EQ.0) GO TO 180
      IF(FCST) GO TO 180
      IF(OBSQO2.NE.-999.0)GO TO 290
  180 QO2=QOK2
C***********************************************************************
C GO TO STATEMENT 290 TO COMPUTE ELEV. AT END OF TIME PERIOD.
C***********************************************************************
      GO TO 290
C***********************************************************************
C SS2 IS GREATER THAN SFILL.  COMPUTE FRACTION (FRAC) OF TIME PERIOD FOR
C POOL TO FILL TO SFILL.  THE GATED SPILLWAY DISCHARGE (QSFRAC) AT THE
C TIME THE POOL REACHES SPILL STORAGE (SFILL) SHOULD BE 0 AS THERE
C SHOULD BE NO SPILLWAY DISCHARGE UNTIL THE POOL FILLS TO SFILL.  HOW-
C EVER IN CASE THERE IS SPILLWAY DISCHARGE AT THE BEGINNING, QSFRAC WILL
C BE SET TO QO1-QOK1 (THE DIFFERENCE IN BEGINNING TOTAL DISCHARGE AND
C NON-SPILLWAY DISCHARGE).
C***********************************************************************
  190 QSFRAC=QO1-QOK1
      FRAC=FRAC26(DIFQI,DIFQOK,QI1,QO1,QOK1,QSFRAC,S1,SFILL,IBUG)
C***********************************************************************
C COMPUTE INFLOW AT TIME OF FILLING (QIFRAC) AND CHECK AGAINST MAXIMUM
C POSSIBLE OUTFLOW (QPASMX) AT SPILL ELEVATION.
C***********************************************************************
      QIFRAC=QI1+DIFQI*FRAC
      IF(QIFRAC.GT.QPASMX) GO TO 210
      IF(QI2.GT.QPASMX) GO TO 200
C***********************************************************************
C ROUTING IS NOT REQUIRED WHEN QIFRAC AND QI2 ARE BOTH LESS THAN QPASMX.
C QIFRAC IS INFLOW AT TIME OF FILLING AND QI2 IS INFLOW AT END OF TIME
C PERIOD.
C***********************************************************************
      S2=SFILL
      QOM=S1+QIM-S2
      IF(QOM .LT. 0.) QOM=0.
      S2=S1+QIM-QOM
      QO2=QI2
      IF(QO2.LT.0.) QO2=0.
      IF(QO2 .LE. QOK2) QO2=QOK2
      GO TO 290
C***********************************************************************
C FOR THE FOLLOWING STATEMENTS, ROUTING BEGINS BETWEEN THE FRACTION
C (FRAC) OF THE TIME PERIOD FOR SPILL ELEVATION TO BE REACHED AND THE
C END OF THE TIME PERIOD.  COMPUTE THE PROPORTION (X) OF THE TIME PERIOD
C FROM FRAC TO START OF ROUTING.  THE EQUATION FOR X TAKES CARE OF THE
C VARIATION OF NON-SPILLWAY DISCHARGE DURING THE TIME PERIOD.
C***********************************************************************
  200 QMAX1=SPILMX+QOK1
      X=(QMAX1*(1.-FRAC)+DIFQOK*(FRAC-FRAC*FRAC)+QIFRAC*(FRAC-1.))/(QI2-
     $QIFRAC+DIFQOK*(FRAC-1.))
      FRAC1=FRAC+X
      FRAC2=1.-FRAC1
      GO TO 220
C***********************************************************************
C STATEMENT 210 STARTS PROCEJURE WHEN ROUTING STARTS AT TIME OF FILLING
C OF POOL TO SFILL.
C***********************************************************************
  210 FRAC1=FRAC
      FRAC2=1.-FRAC1
  220 QOKBGN=QOK1+DIFQOK*FRAC1
      QOSBGN=SPILMX
      SBGN=SFILL
C***********************************************************************
C STATEMENT 120 CALLS RUTE26 FOR ROUTING.
C***********************************************************************
      GO TO 120
C***********************************************************************
C FOLLOWING STATEMENTS ARE FOR THE SITUATION WHEN BEGINNING STORAGE (S1)
C IS GREATER THAN SFILL.  HOWEVER, IF S1 IS OBSERVED, ROUTING IS NOT
C NECESSARILY REQUIRED SINCE THE OPERATOR MIGHT NOT HAVE ALL GATES FULLY
C OPEN.  COMPUTE SPILLWAY DISCHARGE (QOS) AT BEGINNING OF TIME PERIOD
C WITH ALL GATES FULLY OPEN.  THEN TEST QI1 AGAINST (QOS+QOK1) TO SEE IF
C ROUTING IS NEEDED.  QO1 AND QOK1 ARE BEGINNING TOTAL AND NON-SPILLWAY
C DISCHARGES.  ALSO, TEST  (QO1-QOK1) AGAINST SPILMX SINCE THE POOL MAY
C BE FALLING FROM ABOVE SFILL LEVEL DUE TO FALLING INFLOWS BUT ROUTING
C MAY BE OCCURRING WITH ALL  GATES FULLY OPEN.
C***********************************************************************
C  SPILLWAY IS NOW SPECIFIED BY ELEVATIONS VS Q, NOT HEIGHT ABOVE
C  SPILLWAY VS. Q.  (JTO - 8/83)
C------------------------------------------------------------------
  230 CONTINUE
      IF(S1 .GT. SPASSI) GO TO 260
      CALL NTER26(ELEV1,QOS,HS,OH,NOHS,IFLAG,NTERPQ,IBUG)
      IF(QI1.GT.QPASMX) GO TO 260
      IF(QO1.GT.QPASMX) GO TO 260
C***********************************************************************
C FOLLOWING STATEMENTS TAKE CARE OF THE SITUATION WHERE THE BEGINNING
C POOL IS ABOVE THE LEVEL FOR PASSING INFLOW BUT ALL GATES ARE NOT FULLY
C OPEN.  CHECK IF ROUTING IS NEEDED AT THE END OF THE TIME PERIOD ASSUM-
C ING INFLOW IS PASSED UNTIL INFLOW EXCEEDS MAXIMUM POSSIBLE DISCHARGE
C AT ELEVATION ELEV1.
C***********************************************************************
      IF(QI2.GT.QPASMX) GO TO 250
C***********************************************************************
C ROUTING IS NOT REQUIRED WHEN BOTH QI1 AND QI2 ARE LESS THAN OR EQUAL
C TO QPASMX AND SPILLWAY DISCHARGE IS LESS TNAN OR EQUAL TO SPILMX.
C PASS INFLOW FOR THE TIME PERIOD.
C
      QO2=QI2
      IF(QO2.LT.0.) QO2=0.
      IF(QO2 .LE. QOK2) QO2=QOK2
      QOM=QIM
      IF(QOM.LT.0.) QOM=0.
      IF(QOM .LE. QOKM) QOM=QOKM
      S2=SPASSI
C***********************************************************************
C GO TO 290 TO COMPUTE ELEV2.
C***********************************************************************
      GO TO 290
C***********************************************************************
C FOLLOWING STATEMENTS ARE FOR THE CASE WHERE BEGINNING STORAGE (S1)
C IS GREATER THAN FILL STORAGE (SFILL) AND ROUTING
C STARTS DURING BUT NOT AT THE BEGINNING OF THE TIME PERIOD.  ASSUME
C INFLOW IS PASSED AS LONG AS POSSIBLE.  ROUTING BEGINS WHEN THE INFLOW
C EXCEEDS THE MAXIMUM POSSIBLE OUTFLOW.  COMPUTE THE FRACTION FRAC1
C OF THE TIME PERIOD BEFORE ROUTING BEGINS TAKING IN ACCOUNT THE
C VARIATION OF NON-SPILLWAY DISCHARGE DURING THE TIME PERIOD.
C***********************************************************************
  250 QMAX1=QOS+QOK1
      FRAC1=(QMAX1-QI1)/(DIFQI-DIFQOK)
      FRAC2=1.-FRAC1
      QOKBGN=QOK1+DIFQOK*FRAC1
      SBGN=S1
      QOSBGN=QOS
C***********************************************************************
C GO TO STATEMENT 120 TO CALL THE ROUTING SUBROUTINE RUTE26.
C***********************************************************************
      GO TO 120
C***********************************************************************
C FOLLOWING STATEMENTS ARE FOR THE CASE WHERE ROUTING STARTS AT THE
C BEGINNING OF THE TIME PERIOD.  TEST TO SEE IF ROUTING ENDS DURING THE
C TIME PERIOD ASSUMING THE POOL WOULD BE ALLOWED TO DROP TO THE SFILL
C LEVEL WITH ALL GATES FULLY OPEN.  AT THE SFILL LEVEL ROUTING WOULD
C CEASE AND INFLOW WOULD BE PASSED TO THE END OF THE PERIOD.  USE SS2
C FOR A COMPUTED TEST VALUE FOR STORAGE (S2) AT THE END OF THE TIME
C PERIOD.
C***********************************************************************
  260 SS2=S1+QIM-(QO1+SPILMX)*0.5
      IF(SS2.GE.SPASSI) GO TO 270
C***********************************************************************
C FOLLOWING STATEMENTS ARE FOR THE CASE WHERE ROUTING STARTS AT THE
C BEGINNING BUT ENDS DURING THE TIME PERIOD.  COMPUTE THE FRACTION OF
C THE TIME PERIOD FROM START OF PERIOD TO END OF ROUTING.
C***********************************************************************
      QSFRAC=SPILMX
      FRAC=FRAC26(DIFQI,DIFQOK,QI1,QO1,QOK1,QSFRAC,S1,SFILL,IBUG)
      FRAC1=0.
      FRAC2=FRAC
      QOKBGN=QOK1
      SBGN=S1
      QOSBGN=QO1-QOK1
C***********************************************************************
C GO TO STATEMENT 120 TO CALL RUTE26.
C***********************************************************************
      GO TO 120
C***********************************************************************
C FOLLOWING STATEMENTS ARE FOR THE CASE WHERE ROUTING EXTENDS OVER THE
C ENTIRE TIME PERIOD FOR A FULLY-OPEN GATED SPILLWAY.
C***********************************************************************
  270 FRAC1=0.
      FRAC2=1.0
C***********************************************************************
C GO TO STATEMENT  150 TO DETERMINE SBGN, QOKBGN, AND QOSBGN AND THEN
C GO TO STATEMENT 120 TO CALL RUTE26.  SBGN, QOKBGN, AND QOSBGN ARE,
C RESPECTIVELY, STORAGE, NON-SPILLWAY DISCHARGE AND SPILLWAY DISCHARGE
C AT THE BEGINNING OF THE FIRST ROUTING STEP.
C***********************************************************************
      GO TO 150
C***********************************************************************
C CHECK IF ENDING DISCHARGE (QO2) WAS OBSERVED OR COMPUTED.  IF NOT, SET
C QO2 TO MEAN DISCHARGE (QOM) AS THIS USUALLY  GIVES A BETTER VALUE THAN
C COMPUTING QO2 FROM BEGINNING DISCHARGE (QO1) AND QOM.
C THE SUBROUTINE GOES TO 280 ONLY IF THE TIME PERIOD IS AT OR BEFORE RUN
C TIME.
C***********************************************************************
  280 QO2=QOM
      IF(OBSQO2.NE.-999.0) QO2=OBSQO2
  290 IF(IFCST.EQ.0) GO TO 300
      IF(FCST) GO TO 300
      IF(IADJH .EQ. 1) GO TO 300
      IF(ELEV2.NE.-999.0) GO TO 320
C ......................................................................
C IF THIS IS THE FIRST RUN THROUGH OVER26, COMPUTE A NEW VALUE OF QOK2
C (NON-SPILLWAY DISCHARGE AT THE END OF THE TIME PERIOD) IF THE TOTAL
C DISCHARGE VS MAXIMUM GENERATION DISCHARGE RELATION WAS USED.
C CHECK DIFFERENCE IN FIRST AND SECOND ESTIMATES AND, IF GREATER THAN
C PCTERG*QO2 AND IF THIS IS NOT THE SECOND RUN (NTRIAL=2), GO BACK TO
C THE FIRST OF OVER26 AND GO THROUGH AGAIN.  IF THE RELATION IS NOT USED
C USED, NTRIAL WILL HAVE BEEN PREVIOUSLY SET TO 2.  PCTERG IS A USER
C SPECIFIED FRACTION.
C ......................................................................
  300 IF(NTRIAL.EQ.2) GO TO 310
      QGENO=QGEN
      ESTQO2=QO2
      CALL TERP26(QO2,QGEN,TOTALQ,QG,NQG,IFLAG,IBUG)
      IF(ABS(QGEN-QGENO).GT.(PCTERG*QO2)) GO TO 10
C***********************************************************************
C COMPUTE ELEV2 WITH ARITHMETIC (NTERP=0) OR LOGARITHMIC INTERPOLATION
C (NTERP=1).
C***********************************************************************
  310 CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
  320 IF(IBUG-1)410,390,330
  330 WRITE(IODBUG,50) IFCST,FCST,NS2,IUNC,CREST,STOCR,SFILL,SPILMX,
     $QI1,QI2,QIM,QO1,       QO2,       QOM,QOK1,QOK2,S1,      S2,ELEV1,
     $ELEV2,TESTPK,NTOTPK,NTERPQ,QGENMX,SLUICE,NQG,PCTERG,NTERP,IBUG,
     $NTRIAL
C     WRITE(IODBUG,340)IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
  340 FORMAT(1H0,9H IFLAG IS,I6,43H.  NO OF POINTS ON ELEV. - STORAGE CU
     $RVE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION
     $ARE/(1X,5(F12.3,F9.3,3X)))
C     WRITE(IODBUG,350)NOSOH,(O(I),SOH(I),I=1,NOSOH)
  350 FORMAT(1H0,48HNO OF POINTS ON OUTFLOW VS(STORAGE+O/2) CURVE IS,I6/
     $1H0,54H ALTERNATE VALUES OF OUTFLOW AND STORAGE+OUTFLOW/2 ARE/(1X,
     $10F12.3))
C     WRITE(IODBUG,360)NOHS,(OH(I),HS(I),I=1,NOHS)
  360 FORMAT(1H0,42H NO. OF POINTS ON SPILLWAY RATING CURVE IS,I6,1H./1H
     $0,63H ALTERNATING VALUES OF DISCHARGE AND HEIGHT  ABOVE SPILLWAY A
     $RE/(1X,10F12.3))
      IF(NUMPKO.LE.0) GO TO 390
      WRITE(IODBUG,370) TESTPK,NUMPKO,(PEAKO(I),PKPOS(I),I=1,NUMPKO)
  370 FORMAT(1H0,70H NO. OF PEAK DISCHARGES BETWEEN REGULAR TIME INTERVA
     $L VALUES AND ABOVE,F12.3, 3H IS,I6/1H0,85H ALTERNATING VALUES OF P
     $EAK DISCHARGE AND POSITION IN INSTANTANEOUS OUTFLOW ARRAY ARE/(1X,
     $6(F12.3,F4.1,4X)))
      IF(NQG.EQ.0) GO TO 390
      WRITE(IODBUG,380) (TOTALQ(I),QG(I),I=1,NQG)
  380 FORMAT(1H0,75H ALTERNATING VALUES OF TOTAL DISCHARGE AND MAXIMUM G
     $ENERATION DISCHARGE ARE/(1X,10F12.3))
  390 WRITE(IODBUG,400)
  400 FORMAT(1H0,10X,17H** LEAVING OVER26)
  410 RETURN
      END
