C MEMBER XS0626
C  (from old member FCXS0626)
C
      SUBROUTINE XS0626(SUNUM,PO,W,D,LOCWS,LOCOWS,IDPT)
C------------------------------------------------------------------
C  SUBROUTINE TO GET PARAMETER AND TIME SERIES VALUES FOR THE
C  'SPILLWAY' SCHEME.
C------------------------------------------------------------------
C  WRITTEN BY - JOE OSTROWSKI - HRL - AUGUST 1983
C------------------------------------------------------------------
C
      INCLUDE 'common/resv26'
      INCLUDE 'common/exg26'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fila26'
      INCLUDE 'common/root26'
C
      DIMENSION PO(1),W(1),D(1),LOCWS(1),LOCOWS(1),IDPT(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/xs0626.f,v $
     . $',                                                             '
     .$Id: xs0626.f,v 1.1 1995/09/17 19:06:56 dws Exp $
     . $' /
C    ===================================================================
C
      DATA BLANK/4H    /
C
      IF (IBUG.GE.1) WRITE(IODBUG,1600)
 1600 FORMAT('   *** ENTER XS0626 ***')
C
C  GET POINTER INFO
C
      CALL XPTR26(SUNUM,PO,IORD,IBASE,LEVEL,LOCPM,LOCTS,LOCCO)
C
C  SET EXECUTION POINTER
C
      LOCEX = IORD*3
      W(LOCEX) = 1.01
C
C  SET CARRYOVER LOCATION AND GET THE ONE CARRYOVER VALUE
C
      LOCCOT = LOCWS(4) + LOCCO - 1
C
C  THE ONE CARRYOVER VALUE IS THE NON-SPILLWAY DISCHARGE
C
      QOK1 = W(LOCCOT)
C
C  SET DEFAULTS FOR MAX GEN Q AND SLUICE Q
C
      QGENMX = 0.00
      SLUICE = 0.00
C
C  GET SPILLWAY PARAMETERS (IF FIRST VALUE HERE IS NEGATIVE, THEN
C   ALL SPILLWAY PARMS ARE STORED ELSEWHERE)
C
      LOCTYP = LOCPM
      IF (PO(LOCTYP).LT.0.00) LOCTYP = -PO(LOCTYP) + IOFPRM
C
C  THE UNCONTROLLED/GATED SWITCH IN COMP. ROUTINES IS THE OPPOSITE OF
C  THAT HELD IN THE PO ARRAY
C
      IUNCT = PO(LOCTYP) + 1
      IUNC = MOD(IUNCT,2)
C
C  SET THE PEAK REPLACING DISCHARGE
C
      TESTPK = PO(LOCTYP+1)
C
C  SET CREST ELEVATION AND THE STORAGE CONTENTS AT THAT ELEVATION.
C
      CREST = PO(LOCTYP+2)
      CALL NTER26(CREST,STOCR,PO(LESELV),PO(LESSTO),NSE,IFLAG,NTERP,
     .  IBUG)
C
C  GET THE SPILLWAY RATING CURVE
C
      LOCSPW = LOCTYP + 3
      IF (PO(LOCSPW).LT.0.00) LOCSPW = -PO(LOCSPW) + IOFPRM
      NOHS = PO(LOCSPW)
      LOCSPE = LOCSPW+1
      LOCSPQ = LOCSPE + NOHS
C
C  GET THE INTERPOLATION SWITCH
C
      IADD = 1
      IF (LOCSPW.EQ.LOCTYP+3) IADD = 2*NOHS + 1
      LOCTRP = LOCTYP + IADD + 3
      NTERPQ = PO(LOCTRP)
C
C  GET THE PASS INFLOW ELEVATION, THE STORAGE AND THE SPILLWAY DISCHARGE
C  AT THAT ELEVATION. (FOR GATED SPILLWAY ONLY)
C
      IF (IUNC.EQ.1) GO TO 20
C
      PASSEL = PO(LOCTRP+1)
      CALL NTER26(PASSEL,SFILL,PO(LESELV),PO(LESSTO),NSE,IFLAG,NTERP,
     . IBUG)
      CALL NTER26(PASSEL,SPILMX,PO(LOCSPE),PO(LOCSPQ),NOHS,IFLAG,NTERPQ,
     . IBUG)
C
C  NEXT SEE IF WE NEED TO COMPUTE A TOTALQ VS. MAXGENQ CURVE AND AN
C  ELEV VS. MAXGEN CURVE
C
   20 CONTINUE
      LOCNXT = LOCTRP + IUNCT
      NQGEN = 0
      NHQ = PO(LOCNXT)
      NDAMQ = 0
      IF (NHQ.EQ.0) GO TO 50
      IF(NHQ .GT. 0) LOCNXT=LOCNXT+2*NHQ+3
      QGENMX = PO(LOCNXT+1)
      SLUICE = PO(LOCNXT+2)
C
C  COMPUTE THE TOTALQ VS. MAXGEN Q, AND THE ELEV VS MAXGEN Q CURVES.
C
      CALL XFTQ26(SUNUM,PO,W,LOCOWS,1)
      CALL XFMQ26(SUNUM,PO,LOCSPW,W,LOCOWS,ITYPE,LOCMQ,LOKELV,NDAMQ)
C
C  COMPUTE THE MAX Q AT THE PASS INFLOW ELEVATION
C
      IF (IUNC.EQ.1) GO TO 45
C
C  GET MAX Q FOR UNCONTROLLED SPILLWAY CASE.
C
      CALL NTER26(CREST,QPASMX,W(LOCMQ),W(LOCMQ+NDAMQ),
     . NDAMQ,IFLAG,0,IBUG)
      GO TO 60
C
C  GET MAX Q FOR GATED SPILLWAY CASE.
C
   45 CONTINUE
      CALL NTER26(PASSEL,QPASMX,W(LOCMQ),W(LOCMQ+NDAMQ),
     . NDAMQ,IFLAG,0,IBUG)
      GO TO 60
C
C  GET CONSTANT VALUES FOR MAX GENQ AND SLUICE Q
C
   50 CONTINUE
      QGENMX = PO(LOCNXT+1)
      SLUICE = PO(LOCNXT+2)
C
C  SET THE MAXIMUM DISCHARGE AT THE PASS INFLOW ELEVATION
C
      IF (IUNC.EQ.1) CALL NTER26(CREST,SPILMX,PO(LOCSPE),PO(LOCSPQ),
     .               NOHS,IFLAG,NTERPQ,IBUG)
      IF (IUNC.EQ.0) CALL NTER26(PASSEL,SPILMX,PO(LOCSPE),PO(LOCSPQ),
     .               NOHS,IFLAG,NTERPQ,IBUG)
      QXNOS=QGENMX+SLUICE
      IF (QLIMFL .GE. QXNOS) QXNOS=QLIMFL
      QPASMX = SPILMX + QXNOS
C
C--------------------------------------------------------------
C  NEXT, LOOK FOR ANY TIME SERIES SPECS THE MAX GEN Q
C  AND THE SLUICE Q (BUT ONLY FOR AN ADJUSTED RUN).
C
   60 CONTINUE
      QOK2 = QGENMX+SLUICE
C      IF (.NOT.ADJRUN) GO TO 500
C
C  SET DATA OFFSET
C
      IOFFTS = IDOFST * NTIM24
C  THE FOLLOWING CHANGE MADE ON 12/14/90 -- M624
      LOCPTS=W(LOCEX-1)
C  END OF CHANGE OF 12/14/90
C
C  LOOK FOR GENERATION AND SLUICE DISCHARGE
C
      NO1 = 1
      NO2 = 1
      NF1 = 0
      NF2 = 0
      JUMP = 2
      IF (PO(LOCTS).EQ.BLANK.AND.PO(LOCTS+1).EQ.BLANK) GO TO 250
C
C  TIME SERIES IS USED FOR GEN Q, SEE IF WE HAVE A VALID VALUE.
C
C  THE FOLLOWING CHANGE MADE ON 12/14/90 -- M624
      IF(IBUG.LT.1 .OR. NS2.GT.1) GO TO 202
      IST=IDPT(LOCPTS)+IOFFTS+NS2-1
      IEND=IST+NUM-1
      WRITE(IODBUG,1610)
 1610 FORMAT(5X,'POWER GENERATION TIME SERIES: ')
      WRITE(IODBUG,1630) (D(I),I=IST,IEND)
 1630 FORMAT(5X,10F10.2)
C      JUMP = 5
  202 JUMP = 5
C  END OF CHANGE OF 12/14/90
      NO1 = 0
C  THE FOLLOWING CHANGE MADE ON 12/14/90 -- M624
C      QKG = D(IDPT(LOCPTS+1)+IOFFTS+NS2-1)
      QKG = D(IDPT(LOCPTS)+IOFFTS+NS2-1)
C  END OF CHANGE OF 12/14/90
      IF (IFMSNG(QKG).EQ.1) NF1 = 1
      QKG = QKG * NTIM24
      IF(NF1 .EQ. 0) QGENMX=QKG
C
C  CHECK FOR SLUICE Q NOW
C
  250 CONTINUE
      IF (PO(LOCTS+JUMP).EQ.BLANK.AND.PO(LOCTS+JUMP+1).EQ.BLANK)
     . GO TO 300
C
C  TIME SERIES IS USED, SEE IF WE HAVE A VALUE.
C
      NO2 = 0
C  THE FOLLOWING CHANGE MADE ON 12/14/90 -- M624
C      QKSL = D(IDPT(LOCPTS+2)+IOFFTS+NS2-1)
C      IF (IFMSNG(QKSL).EQ.1) NF2 = 1
      QKSL = D(IDPT(LOCPTS+1)+IOFFTS+NS2-1)
      IF(IBUG.LT.1 .OR. NS2.GT.1) GO TO 252
      IST=IDPT(LOCPTS+1)+IOFFTS+NS2-1
      IEND=IST+NUM-1
      WRITE(IODBUG,1620)
 1620 FORMAT(5X,'SLUICE DISCHARGE TIME SERIES: ')
      WRITE(IODBUG,1630) (D(I),I=IST,IEND)
  252 IF (IFMSNG(QKSL).EQ.1) NF2 = 1
C  END OF CHANGE OF 12/14/90
      QKSL = QKSL * NTIM24
      IF(NF2 .EQ. 0) SLUICE=QKSL
C
C  NOW ASSIGN THE NON-SPILLWAY DISCHARGE FOR THE VARIOUS CONDITIONS
C
  300 CONTINUE
      IF (NO1.EQ.1.OR.NF1.EQ.1) GO TO 350
C
C  HERE WE FOUND THE GEN TS. ADD SLUICE TS IF IT WAS FOUND.
C
      QOK2 = QKG + SLUICE
      IF (NO2.EQ.0.AND.NF2.EQ.0) QOK2 = QKG + QKSL
      GO TO 500
C
C  HERE WE DIDN'T USE OR FIND A VALUE FOR THE GEN TS. ONLY NEED TO ADD
C  THE SLUICE Q IF IT WAS FOUND, AND THE MAX GEN Q IS A CONSTANT.
C
  350 CONTINUE
      IF (NO2.EQ.0.AND.NF2.EQ.0.AND.NQGEN.EQ.0) QOK2 = QGENMX + QKSL
C
C  LAST THING IS TO COMPUTE MOD PULS CURVE FOR THIS SPILLWAY.
C
  500 CONTINUE
C      CSTQ = QGENMX + SLUICE
      CSTQ=0.
      CALL XSOH26(SUNUM,PO,W,LOCOWS,PO(LOCSPE),PO(LOCSPQ),NOHS,CSTQ,1)
C
C----------------------------------------------------------------
C  NOW HAVE ALL PROPER VALUES; EXECUTE THE SCHEME.
C
      PCTERG = 0.01
      CALL OVER26(PO(LESSTO),PO(LESELV),W(LOCOWS(4)),W(LOCOWS(4)+NTOTPK)
     . ,W(LOCOWS(10)),W(LOCOWS(10)+NOSOH),PO(LOCSPQ),PO(LOCSPE),
     . W(LOCOWS(8)),W(LOCOWS(8)+NQGEN),W(LOCOWS(8)+2*NQGEN))
C
C----------------------------------------------------------------
C  UPDATE CARRYOVER
C
      W(LOCCOT) = QOK2
C
      IF (IBUG.GE.1) WRITE(IODBUG,1699)
 1699 FORMAT('    *** EXIT XS0626 ***')
      RETURN
      END
