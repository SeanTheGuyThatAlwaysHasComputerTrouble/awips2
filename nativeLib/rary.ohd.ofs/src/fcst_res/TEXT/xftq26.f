C MEMBER XFTQ26
C  (from old member FCXFTQ26)
C
C DESC GENERATE TOTAL Q VS MAX GENERATION Q CURVE.
C-----------------------------------------------------------------------
      SUBROUTINE XFTQ26(SUNUM,PO,W,LOCOWS,ICOMP)
C----------------------------------------------------------------------
C  SUBROUTINE TO COMPUTE THE TOTAL DISCHARGE VS. MAXIMUM GENERATION
C  CURVE. THIS CURVE IS NEEDED BY THE FILLSPILL, SPILLWAY AND FLASHBDS
C  SCHEMES. ALSO, THIS CURVE IS ONLY USED WHEN THE TAILWATER AFFECTS THE
C  GENERATION DISCHARGE.
C
C  FOR FLASHBOARDS, WE MUST COMPUTE THE TOTAL SPILLWAY RATING, AS SMALL
C  BOARDS AND GATES MAY EXIST ALONG WITH THE LARGE BOARDS.
C  (ALSO, FOR FLASHBOARDS, THE VALUE OF ICOMP TELLS US WHETHER WE MUST
C   COMPUTE THE CURVE OR JUST FILL THE DISCHARGES.)
C
C  NOTE: THIS ROUTINE IS ONLY CALLED WHEN THE TAILWATER AFFETS THE GEN.
C        DISCHARGE. THE CHECK FOR THIS MUST BE DONE PRIOR TO CALLING
C        THIS ROUTINE.
C-----------------------------------------------------------------------
C  WRITTEN BY - JOE OSTROWSKI - HRL - AUGUST 1983
C-----------------------------------------------------------------------
C
      INCLUDE 'common/resv26'
      INCLUDE 'common/exg26'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/genq26'
      INCLUDE 'common/root26'
C
      DIMENSION PO(1),W(1),LOCOWS(1),WHICH(3)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/xftq26.f,v $
     . $',                                                             '
     .$Id: xftq26.f,v 1.2 1999/04/23 16:54:17 page Exp $
     . $' /
C    ===================================================================
C
      DATA WHICH/4HLRGE,4HSMAL,4HGATE/
C
      IF (IBUG.GE.1) WRITE(IODBUG,1600)
 1600 FORMAT('   *** ENTER XFTQ26 ***')
C
C  GET PARM, TS, AND CARRYOVER POINTERS FOR THIS S/U
C
      CALL XPTR26(SUNUM,PO,IORD,IBASE,LEVEL,LOCPM,LOCTS,LOCCO)
C
C-------------------------------------------------------
C  SET POINTERS IN WORK ARRAY
C
      LOCTQ = LOCOWS(8)
C
C------------------------------------------------------------
C  IF WE LAST COMPUTE THE CURVE FOR THIS S/U, JUST RETURN.
C
      ISUNUM = IBASE*10 + LEVEL
      IF (MRLOC(3).EQ.ISUNUM) GO TO 9500
      MRLOC(3) = ISUNUM
C
      IF (IBASE.EQ.112) GO TO 500
C
C-------------------------------------------------------
C  SCHEME IS EITHER FILLSPILL OR SPILLWAY
C
      IADD = 0
      IF (IBASE.EQ.105) IADD = 1
      LOCSPW = LOCPM + IADD
C
C  IF ALL PARMS FOR THIS S/U WERE DEFINED ELSEWHERE, GO THERE.
C
      IF (PO(LOCSPW).LT.0.0) LOCSPW = -PO(LOCSPW) + IOFPRM
C
C  ITYPE INDICATES NON-GATED (0) OR GATED (1) SPILLWAY.
C
      ITYPE = PO(LOCSPW)
C
C  NOW GET THE SPILLWAY RATING CURVE
C
      LOCSPE = LOCSPW + 3
      IF (PO(LOCSPE).LT.0.0) LOCSPE = -PO(LOCSPE) + IOFPRM
      NOHS = PO(LOCSPE)
      LOCSPH = LOCSPE + 1
      LOCSPQ = LOCSPH + NOHS
C
C  NOW GET INTERPOLATION TYPE
C
      IADD = 1
      IF (LOCSPE.EQ.LOCSPW+3) IADD = 2*NOHS + 1
      LOCTRP = LOCSPW + IADD + 3
      NTERPQ = PO(LOCTRP)
C
C  NOW GET THE HEAD VS. DISCHARGE RELATION.
C
      IADD = ITYPE + 1
      LOCHDQ = LOCTRP + IADD
      IF (PO(LOCHDQ).LT.0.0) LOCHDQ = -PO(LOCHDQ) + IOFPRM
      NHQ = PO(LOCHDQ)
      LOCHD = LOCHDQ + 1
      LOCHQ = LOCHD + NHQ
C
C  SET RATING CURVE POINTER
C
      JUMP2 = 1
      IF (LOCHDQ.EQ.LOCTRP+IADD) JUMP2 = 2*NHQ + 1
      LOCRC = LOCTRP + IADD + JUMP2
C
C  THE CONVERGENCE CRITERION IS AFTER THE RATING CURVE ID
C
      PCTERG = PO(LOCRC + 2)
C
C  THE CONSTANT SLUICE Q IS NEXT
C
      SLUICE = PO(LOCRC+3)
C
C  GENERATE THE CURVE
C
      CALL XCTQ26(PO,W,LOCTQ,PO(LOCSPH),PO(LOCSPQ),NOHS,PO(LOCHD),
     .            PO(LOCHQ),NHQ,PO(LOCRC))
      NQGEN = NSE
C
      GO TO 9000
C
C------------------------------------------------------------------
C  THE CURVE IS NEEDED FOR FLASHBOARDS. IN THIS CASE, THE SPILLWAY
C  RATING IS THE COMBINATION OF ALL SPILLWAY RATINGS (LARGE BOARDS, AND
C  POTENTIALLY, SMALL BOARDS AND GATES).
C
C  THIS COMBINED RATING IS TO BE STORED IN OPTIONAL WORK SPACE (ITEM 9)
C
  500 CONTINUE
C
C  IF TOTAL SPILLWAY IS ALREADY DEFINED, JUST GO GET OTHER PARMS.
C
      IF (ISUNUM.EQ.MRLOC(4)) GO TO 560
      MRLOC(4) = ISUNUM
C
C  SET POINTERS AND OTHER INFO
C
      NSMALL = PO(LOCPM+1)
      ISM = (NSMALL-1)/(NSMALL+1) + 1
      LOCFBQ = LOCOWS(9)
C
C  INITIALIZE THE WORKING SPACE
C
      DO 510 I=1,NSE
      LOCQ = LOCFBQ + I - 1
      W(LOCQ) = 0.00
  510 CONTINUE
C
C  NOW ADD LARGE BOARD RATING TO WORKING SPACE
C
      LOCNLR = LOCPM + 7
      NRT = PO(LOCNLR)
C
      IF (IBUG.GE.2) WRITE(IODBUG,1620) WHICH(1)
 1620 FORMAT('  SPILLWAY CONTRIB FROM ',A4,/7X,'ELEV',8X,'Q',5X,
     .'TOTALQ'/)
      DO 520 I=1,NSE
      CALL NTER26(PO(LESELV+I-1),TQ,PO(LOCNLR+1),PO(LOCNLR+NRT+1),NRT,
     .            IFLAG,0,IBUG)
      W(LOCFBQ+I-1) = W(LOCFBQ+I-1) + TQ
      IF (IBUG.GE.2) WRITE(IODBUG,1630) PO(LESELV+I-1),TQ,W(LOCFBQ+I-1)
 1630 FORMAT(3X,3F12.3)
  520 CONTINUE
C
C  NOW ADD SMALL RATING, IF ENTERED.
C
      LOCSMR = LOCNLR + 2*NRT + 1
      NSMR = 0
      IF (ISM.EQ.0) GO TO 540
C
      LOCSMR = LOCSMR + 5
      NSMR = PO(LOCSMR)
C
      IF (IBUG.GE.2) WRITE(IODBUG,1620) WHICH(2)
      DO 530 I=1,NSE
      CALL NTER26(PO(LESELV+I-1),TQ,PO(LOCSMR+1),PO(LOCSMR+NSMR+1),NSMR,
     .            IFLAG,0,IBUG)
      W(LOCFBQ+I-1) = W(LOCFBQ+I-1) + TQ
      IF (IBUG.GE.2) WRITE(IODBUG,1630) PO(LESELV+I-1),TQ,W(LOCFBQ+I-1)
  530 CONTINUE
C
C  NOW ADD GATE RATING, IF ENTERED.
C
  540 CONTINUE
      LOCGAT = LOCSMR + ISM*(2*NSMR + 1)
      IGATE = PO(LOCGAT)
      IF (IGATE.EQ.0) GO TO 560
C
      LOCGTR = LOCGAT + 5
      NGTR = PO(LOCGTR)
      IF (IBUG.GE.2) WRITE(IODBUG,1620) WHICH(3)
      DO 550 I=1,NSE
      CALL NTER26(PO(LESELV+I-1),TQ,PO(LOCGTR+1),PO(LOCGTR+NGTR+1),NGTR,
     .            IFLAG,0,IBUG)
      W(LOCFBQ+I-1) = W(LOCFBQ+I-1) + TQ
      IF (IBUG.GE.2) WRITE(IODBUG,1630) PO(LESELV+I-1),TQ,W(LOCFBQ+I-1)
  550 CONTINUE
C
C------------------------------------------------------------------
C  NOW HAVE COMBINE SPILLWAY RATING; GET OTHERS PARMS.
C
  560 CONTINUE
      IF (ICOMP.EQ.0) GO TO 9000
C
C  GET THE HEAD VS. DISCHARGE RELATION.
C
      LOCHDQ = LOCGAT + IGATE*(2*NGTR + 6) + 1
      LOCHD1 = LOCHDQ
      IF (PO(LOCHDQ).LT.0.0) LOCHDQ = -PO(LOCHDQ) + IOFPRM
      NHQ = PO(LOCHDQ)
      LOCHD = LOCHDQ + 1
      LOCHQ = LOCHD + NHQ
      JUMP2 = 1
      IF (LOCHDQ.EQ.LOCHD1) JUMP2 = 2*NHQ + 1
C
C  SET RATING CURVE POINTER
C
      LOCRC = LOCHD1 + JUMP2
C
C  SET CONVERGENCE CRITERION
C
      PCTERG = PO(LOCRC + 2)
C
C  SET CONSTANT SLUICE Q
C
      SLUICE = PO(LOCRC+3)
C
C  GENERATE THE CURVE
C
      NTERPQ = 0
      CALL XCTQ26(PO,W,LOCTQ,PO(LESELV),W(LOCFBQ),NSE,PO(LOCHD),
     .            PO(LOCHQ),NHQ,PO(LOCRC))
      NQGEN = NSE
C
C-------------------------------------------
C  ALL DONE
C
 9000 CONTINUE
      IF (ICOMP .EQ. 0) GO TO 9500
      IF (IBUG.GE.2) WRITE(IODBUG,1690) (W(LOCTQ+I-1),I=1,NQGEN)
      IF (IBUG.GE.2) WRITE(IODBUG,1691) (W(LOCTQ+NQGEN+I-1),I=1,NQGEN)
 1690 FORMAT(/'   ** TOTAL Q VS. MAX GEN. Q CURVE :'//,(6F12.3))
 1691 FORMAT((6F12.3))
 9500 CONTINUE
      IF (IBUG.GE.1) WRITE(IODBUG,1699)
 1699 FORMAT('    *** EXIT XFTQ26 ***')
      RETURN
      END
