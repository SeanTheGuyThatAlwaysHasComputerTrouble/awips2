C MEMBER F3WAYZ
C  (from old member FCF3WAYZ)
C
      SUBROUTINE F3WAYZ(X,Y,Z,XX,YY,ZZ,NY,NZ,IBUG)
C
C SUBROUTINE F3WAYZ INTERPOLATES THE Z VALUE IN A THREE-WAY RELATION
C WHERE YY (ORDINATE VALUES) AND ZZ (PARAMETER VALUES) ARE SINGLE ARRAYS
C AND XX (ABSCISSA VALUES) IS A DOUBLE ARRAY.  EACH PARAMETER (ZZ) VALUE
C HAS AN ASSOCIATED CURVE DEFINED BY A SET OF YY VS XX VALUES.  YY
C VALUES MUST BE THE SAME FOR ALL ZZ CURVES.  X AND Y ARE KNOWN ABSCISSA
C AND ORDINATE VALUES.
C
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST.
C     DECEMBER, 1981
C
C  MODIFIED TO REARRANGE THE XX ARRAY FROM (NZ,NY) TO (NY,NZ) FOR MORE
C  STRAIGHTFORWARD INPUT AND DISPLAY PURPOSES - (JTO - 1/84)
C
C SUBROUTINE F3WAYZ IS IN
C
C VARIABLES IN THE ARGUMENT LIST ARE DEFINED AS FOLLOWS:
C     X -- KNOWN ABSCISSA VALUE.
C     Y -- KNOWN ORDINATE VALUE.
C     Z -- UNKNOWN PARAMETER VALUE TO BE COMPUTED.
C     XX -- DOUBLE ARRAY OF ABSCISSA VALUES WITH DIMENSIONS OF (NY,NZ).
C     YY -- SINGLE ARRAY OF ORDINATE VALUES.  YY VALUES ARE THE SAME FOR
C       ALL PARAMETER (ZZ) CURVES.
C     ZZ -- SINGLE ARRAY OF PARAMETER VALUES.  EACH ZZ CURVE IS DEFINED
C       BY THE YY VALUES AND DIFFERENT SETS OF XX VALUES.
C     NY -- NO. OF VALUES IN YY ARRAY.
C     NZ -- NO. OF VALUES IN ZZ ARRAY.
C
      DIMENSION ZZ(1),YY(1),XX(NY,1)
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/f3wayz.f,v $
     . $',                                                             '
     .$Id: f3wayz.f,v 1.1 1995/09/17 19:05:38 dws Exp $
     . $' /
C    ===================================================================
C
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF (IBUG.EQ.1) GO TO 10
      IF (IBUG.GE.5) GO TO 20
      GO TO 50
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** F3WAYZ ENTERED)
      WRITE(IODBUG,40) X,Y
   40 FORMAT(1H0,12H X AND Y ARE,2F12.3,22H.  Z WILL BE COMPUTED.)
C
C BRACKET Y WITHIN THE YY ARRAY.
C
   50 DO 60 II=2,NY
      I=II
      IF(YY(I).GT.Y) GO TO 70
   60 CONTINUE
      I=NY
   70 Y2=YY(I)
      Y1=YY(I-1)
C
C Y2 CAN BE EQUAL TO Y1 ONLY IF THE TWO HIGHEST VALUES OF YY ARE EQUAL.
C IN THAT CASE Z IS INDETERMINATE BUT RATIO WILL BE SET TO 1 TO PROVIDE
C A VALUE FOR Z.
C
      IF(Y2.NE.Y1) GO TO 71
      RATIO=1.0
      GO TO 72
   71 RATIO=(Y-Y1)/(Y2-Y1)
C
C COMPUTE XX VALUES ON ZZ LINES AT Y VALUE AND CHECK IF X IS BETWEEN
C SUCCESSIVE VALUES.
C
   72 DO 80 JJ=1,NZ
      J=JJ
      XX2=XX(I,J)
      XX1=XX(I-1,J)
      IF(J.GT.1) GO TO 73
      IF((XX2-XX1).EQ.0..AND.RATIO.EQ.0.) GO TO 76
      X1=XX1+(XX2-XX1)*RATIO
      GO TO 80
   73 IF((XX2-XX1).EQ.0..AND.RATIO.EQ.0.) GO TO 77
      X2=XX1+(XX2-XX1)*RATIO
      IF(X2.GT.X1) GO TO 74
      IF(X2.LT.X) GO TO 100
      GO TO 75
   74 IF(X2.GT.X) GO TO 100
   75 IF(J.EQ.NZ) GO TO 100
      X1=X2
      GO TO 80
   76 X1=XX1
      GO TO 80
   77 X2=XX1
   80 CONTINUE
  100 Z2 =ZZ(J)
      Z1=ZZ(J-1)
C
C X2 CAN BE EQUAL TO X1 ONLY IF THE TWO HIGHEST VALUES OF ZZ HAVE THE
C SAME XX1, XX2, Y1, AND Y2 VALUES.  IN THAT CASE Z IS INDETERMINATE BUT
C WILL BE SET TO Z2.
C
      IF(X2.NE.X1) GO TO 101
      Z=Z2
      GO TO 102
C
C COMPUTE THE UNKNOWN Z VALUE.
C
  101 Z=Z1+(Z2-Z1)*(X-X1)/(X2-X1)
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
  102 IF (IBUG.EQ.1) GO TO 170
      IF (IBUG.GE.5) GO TO 110
      GO TO 190
  110 WRITE(IODBUG,120) X,Y,Z
  120 FORMAT(1H0,43H X,Y, AND Z VALUES AT THE END OF F3WAYZ ARE,3F12.3)
      WRITE(IODBUG,130) (YY(I),I=1,NY)
  130 FORMAT(1H0,36H YY VALUES IN THE 3-WAY RELATION ARE/(1X,10F12.3))
      WRITE(IODBUG,140) (ZZ(I),I=1,NZ)
  140 FORMAT(1H0,14H ZZ VALUES ARE/(1X,10F12.3))
      DO 150 I=1,NZ
  150 WRITE(IODBUG,160) ZZ(I),(YY(J),XX(J,I),J=1,NY)
  160 FORMAT(1H0,48H ALTERNATING VALUES OF YY AND XX FOR ZZ VALUE OF,F12
     $.3, 4H ARE/(1X,4(2F12.3,4X)))
  170 WRITE(IODBUG,180)
  180 FORMAT(1H0,10X,17H** LEAVING F3WAYZ)
  190 RETURN
      END
