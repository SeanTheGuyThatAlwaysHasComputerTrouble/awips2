C MEMBER OSOH26
C  (from old member FCOSOH26)
C
      SUBROUTINE OSOH26(ANUMLG,ANUMSM,RTQQIM,SUMFRA,QQI1,QQI2,QQIM,QQO1,
     $QQO2,QQOK1,QQOK2,SS1,SS2,SIGELV,SIGSTO,ELVSOH,STOSOH,TMPSOH,O,SOH,
     $ELVLG,QLG,ELVSM,QSM,ELVFL,QFLUD,STOR,ELEV)
C
C THIS UTILITY SUBROUTINE IS USED IN FLSH26 FOR DEVELOPING (IF NEEDED)
C A NEW RELATION OF OUTFLOW (O) VS STORAGE + O/2(SOH) AND FOR ROUTING
C INFLOW IN EXCESS OF NON-SPILLWAY OUTFLOW THROUGH THE DAM.
C THE ROUTING STEP MAY BE FOR THE REGULAR ROUTING TIME (NSTEPS STEPS IN
C TIME INTERVAL) OR MAY BEGIN AFTER THE BEGINNING BUT WILL END AT THE
C END OF THE REGULAR ROUTING TIME STEP.  THE MODIFIED PULS METHOD IS
C USED FOR THE ROUTING.  IF THERE IS A FLOOD GATE AND THE GATE IS
C FULLY OPEN, GATE DISCHARGES ARE INCLUDED IN THE O VS SOH RELATION.
C IF THE GATE IS PARTLY OPEN, GATE DISCHARGE WILL BE HANDLED
C SEPARATELY WITH GATE OPENINGS VARIED TO CAUSE INFLOW TO BE PASSED
C (IF POSSIBLE).  NON-SPILLWAY DISCHARGE WILL BE HANDLED SEPARATELY.
C
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     JULY, 1982
C
C SUBROUTINE OSOH26 IS IN
C
C VARIABLES PASSED TO OR FROM OSOH26 ARE DEFINED IN FLSH26 EXCEPT FOR
C ANUMLG, ANUMSM, AND RTQQIM.  ANUMLG AND ANUMSM ARE THE REAL NUMBER
C EQUIVALENTS OF THE TOTAL NUMBER OF UNCONTROLLED LARGE AND SMALL
C BOARDS, RESPECTIVELY.  RTQQIM IS THE MEAN INFLOW FOR THE TIME STEP
C MINUS THE NON-SPILLWAY OUTFLOW.
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
      INCLUDE 'common/flas26'
C
      DIMENSION SIGELV(1),SIGSTO(1),ELVSOH(1),STOSOH(1),TMPSOH(1),
     $O(1),SOH(1),ELVLG(1),QLG(1),ELVSM(1),QSM(1),ELVFL(1),QFLUD(1),
     $STOR(1),ELEV(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/osoh26.f,v $
     . $',                                                             '
     .$Id: osoh26.f,v 1.1 1995/09/17 19:05:53 dws Exp $
     . $' /
C    ===================================================================
C
C
      STEPS=NOSTEP
      ANLGDN=NLGDN
      ANSMDN=NSMDN
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** OSOH26 ENTERED)
      WRITE(IODBUG,40) NUMLG,ANUMLG,NUMSM,ANUMSM,NLGDN,NSMDN,NGATE,    N
     $LGSOH,NSMSOH,NGASOH,NPOSOH,DISTLG,DISTSM,COEF,SUMFRA,QQI1,QQI2,  Q
     $QIM,QQO1,QQO2,QQOK1,QQOK2,SS1,SS2,NTERPQ,NTERP,RTQQIM,IBUG
   40 FORMAT(1H0,114H   NUMLG,ANUMLG,NUMSM,ANUMSM,NLGDN,NSMDN,NGATE,
     $NLGSOH,NSMSOH,NGASOH,NPOSOH,DISTLG,DISTSM,COEF,SUMFRA,QQI1,QQI2/1X
     $,I6,F6.0,I6,F6.0,7I6,4F7.2,2F12.3/1H0,63H QQIM,QQO1,QQO2,QQOK1,
     $QQOK2,SS1,SS2,NTERPQ,NTERP,RTQQIM,IBUG/1X,7F12.3,2I6,F12.3,I6)
C
C CHECK IF A NEW OUTFLOW (O) VS STORAGE+OUTFLOW/2 (SOH) RELATION IS
C NEEDED.  NLGSOH, NSMSOH, AND
C NGASOH ARE, RESPECTIVELY, THE NUMBER OF LARGE BOARDS DOWN, NUMBER OF
C SMALL BOARDS DOWN, AND INDICATION FOR GATE OPENING USED IN
C DEVELOPING THE LAST O VS SOH RELATION.
C
   50 IF(NLGSOH.NE.NLGDN) GO TO 80
      IF(NUMSM.EQ.0) GO TO 60
      IF(NSMSOH.NE.NSMDN) GO TO 80
      IF(NPOSOH.EQ.-999) GO TO 80
   60 IF(NGATE.EQ.-999) GO TO 70
      IF(NGASOH.NE.NGATE) GO TO 80
C
C IN NEXT STATEMENTS A NEW O VS SOH RELATION IS NOT NEEDED AND PROGRAM
C GOES TO STATEMENT 140 TO COMPUTE BEGINNING ELEVATION AND TO DO LOOP
C 230 TO COMPUTE SPILLWAY DISCHARGE AT THE BEGINNING OF THE ROUTING
C PERIOD.
C
   70 JEND=1
      STCRST=SIGSTO(NPOSOH)
      ELCRST=SIGELV(NPOSOH)
      GO TO 140
C
C DO LOOP 130 COMPUTES THE POSITION (NPOSOH) IN THE SIGNIFICANT ELEVA-
C TION (SIGELV) ARRAY OF THE SPILLWAY CREST ELEVAAION (OR LOWEST TOP OF
C BOARDS USED FOR THE OUTFLOW (O) VS STORAGE+OUTFLOW/2 (SOH) RELATION.
C
   80 DO 130 I=1,3
      GO TO (90,100,110),I
   90 IF(NLGDN.EQ.0) GO TO 91
      ELCRST=SIGELV(1)
      NPOSOH=1
      GO TO 130
   91 ELCRST=SIGELV(3)
      NPOSOH=3
      GO TO 130
  100 IF(NUMSM.EQ.0) GO TO 130
      IF(NSMDN.EQ.0) GO TO 101
      IF(ELCRST.LE.SIGELV(5)) GO TO 130
      ELCRST=SIGELV(5)
      NPOSOH=5
      GO TO 130
  101 IF(ELCRST.LE.SIGELV(7)) GO TO 130
      ELCRST=SIGELV(7)
      NPOSOH=7
      GO TO 130
  110 IF(NGATE.NE.2) GO TO 130
      IF(ELCRST.LE.SIGELV(9)) GO TO 130
      ELCRST=SIGELV(9)
      NPOSOH=9
  130 CONTINUE
      STCRST=SIGSTO(NPOSOH)
      O(1)=0.
      SOH(1)=0.
      J=1
      JEND=NELSOH+1
  140 QGATMX=0.
C
C COMPUTE ELEVATION (ELV1) FOR BEGINNING STORAGE (SS1) CONVERTED TO
C UNITS OF MEAN DISCHARGE FOR THE TIME INTERVAL.
C
      S=SS1/STEPS
      CALL NTER26(S,ELV1,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C
C DO LOOP 230 COMPUTES, IF NEEDED, A NEW RELATION OF OUTFLOW (O) VS
C STORAGE+OUTFLOW/2 (SOH) AND THE
C SPILLWAY DISCHARGE AT THE BEGINNING OF THE ROUTING PERIOD.  THE FIRST
C POSITION VALUE FOR BOTH O AND SOH MUST BE 0.  THE SECOND VALUE IS
C DETERMINED FROM THE FIRST VALUE OF ELVSOH THAT EXCEEDS ELCRST.
C THE LAST LOOP (AND THE ONLY LOOP IF A NEW O VS SOH RELATION IS NOT
C NEEDED) IN THE DO LOOP IS USED TO COMPUTE SPILLWAY DISCHARGE AT THE
C BEGINNING OF THE ROUTING STEP.
C
      DO 230 I=1,JEND
      QOS=0.
      IF(I.NE.JEND) GO TO 150
      ELVI=ELV1
      IF(NGATE.EQ.-999) GO TO 170
      IF(ELVI.LE.SIGELV(9)) GO TO 170
      GO TO 160
  150 ELVI=ELVSOH(I)
      IF(ELCRST.GE.ELVI) GO TO 230
      J=J+1
      IF(NGATE.NE.2) GO TO 170
      IF(ELVI.LE.SIGELV(9)) GO TO 170
C
C COMPUTE DISCHARGE FROM FLOOD GATE FULLY OPEN
C
  160 CALL NTER26(ELVI,QGATMX,ELVFL,QFLUD,NELVFL,IFLAG,NTERPQ,IBUG)
      IF(NGATE.NE.2) GO TO 170
      QOS=QOS+QGATMX
  170 IF(NUMSM.EQ.0) GO TO 180
      IF(NSMDN.EQ.0) GO TO 180
      IF(ELVI.LE.SIGELV(5)) GO TO 180
C
C COMPUTE DISCHARGE FROM SMALL-FLASHBOARD SPILLWAY.
C
      CALL NTER26(ELVI,QQ,ELVSM,QSM,NELVSM,IFLAG,NTERPQ,IBUG)
      QOS=QOS+QQ*ANSMDN/ANUMSM
  180 IF(NLGDN.EQ.0) GO TO 190
      IF(ELVI.LE.SIGELV(1)) GO TO 190
C
C COMPUTE DISCHARGE FROM LARGE-FLASHBOARD SPILLWAY.
C
      CALL NTER26(ELVI,QQ,ELVLG,QLG,NELVLG,IFLAG,NTERPQ,IBUG)
      QOS=QOS+QQ*ANLGDN/ANUMLG
C
C DETERMINE IF WATER IS FLOWING OVER TOP OF UPRIGHT BOARDS.  IF SO,
C COMPUTE DISCHARGE OVER BOARDS.
C
  190 IF(NUMSM.EQ.0.OR.NSMDN.EQ.NUMSM) GO TO 200
      IF(ELVI.LE.SIGELV(7)) GO TO 200
      QQ=COEF*DISTSM*(ELVI-SIGELV(7))**1.5*(ANUMSM-ANSMDN)/ANUMSM
      QOS=QOS+QQ
  200 IF(NLGDN.EQ.NUMLG) GO TO 210
      IF(ELVI.LE.SIGELV(3)) GO TO 210
      QQ=COEF*DISTLG*(ELVI-SIGELV(3))**1.5*(ANUMLG-ANLGDN)/ANUMLG
      QOS=QOS+QQ
  210 IF(I.LT.JEND) GO TO 220
      QQOS1=QOS
      GO TO 230
  220 O(J)=QOS
      STO=STOSOH(I)-STCRST
      SOH(J)=STO+QOS*0.5
  230 CONTINUE
      IF(JEND.EQ.1) GO TO 231
      NOSOH=J
C
C DEFINE VARIABLES FOR NO. OF BOARDS DOWN AND INDICATOR FOR FLOOD
C GATE OPENING USED IN DEVELOPING THE OUTFLOW (O) VS STORAGE+OUTFLOW/2
C RELATION.
C
  231 IF(JEND.EQ.1) GO TO 240
      NLGSOH=NLGDN
      IF(NUMSM.GT.0)NSMSOH=NSMDN
      IF(NGATE.GE.0)NGASOH=NGATE
C
C SINCE THE FLOOD GATE DISCHARGE IS NOT INCLUDED IN THE O VS SOH
C RELATION UNLESS NGATE IS 2, NGASOH WILL BE CHANGED TO 0 IF NGATE IS 1.
C NGASOH IS THE GATE OPENING (NGATE) USED IN COMPUTING THE O VS SOH
C RELATION.
C
      IF(NGASOH.EQ.1)NGASOH=0
C
C COMPUTE THE BEGINNING VALUE OF STORAGE MINUS OUTFLOW/2(SI).
C STORAGE MUST BE CONVERTED TO UNITS OF MEAN DISCHARGE OVER (1.-SUMFRA)
C FRACTION OF THE REGULAR ROUTING STEP(NOSTEP STEPS IN THE TIME
C INTERVAL).  THE STORAGE IN THE SOH VALUES MUST BE CONVERTED TO THE
C SAME UNITS.  AFTER CONVERSION, THE SOH VALUES ARE PUT IN THE
C TMPSOH WORKING ARRAY.
C
  240 SI=(SS1-STCRST)*1./(1.-SUMFRA)-QQOS1*0.5
      DO 250 I=1,NOSOH
      S=SOH(I)-O(I)*0.5
      S=S*1./(1.-SUMFRA)
  250 TMPSOH(I)=S+O(I)*0.5
C
C CHECK IF THERE IS A FLOOD GATE AND IF ONE, THE CONDITION OF THE
C GATE.  IF THE GATE IS PARTLY OPEN AND THE POOL ELEVATION IS ABOVE
C THE GATE SPILLWAY CREST, ADJUST GATE DISCHARGE TO PASS INFLOW IF
C POSSIBLE.
C
      IF(NGATE.NE.1) GO TO 280
      IF(ELV1.LE.SIGELV(9)) GO TO 280
      QTST1=QQI1-QQOS1-QQOK1
      QTST2=QQI2-QQOS1-QQOK2
      IF(QTST1.LT.0.)QTST1=0.
      IF(QTST2.LT.0.)QTST2=0.
C
C THE GATE WILL BE CLOSED IF QTST1 AND QTST2 ARE 0.
C
      IF(.NOT.(QTST1.EQ.0.AND.QTST2.EQ.0.)) GO TO 260
      NGATE=0
      GO TO 280
C
C IF THE GATE CANNOT PASS THE DESIRED OUTFLOW AT EITHER THE BEGINNING
C OR END OF THE ROUTING STEP, THE GATE IS OPENED FULLY AND THE
C PROGRAM GOES BACK TO 80 TO COMPUTE A NEW O VS SOH RELATION THAT
C INCLUDES GATE DISCHARGE.  O AND SOH ARE OUTFLOW AND STORAGE+OUTFLOW/2
C ARRAYS, RESPECTIVELY.
C
  260 IF(QTST1.LE.QGATMX.AND.QTST2.LE.QGATMX) GO TO 270
      NGATE=2
      GO TO 80
C
C RTQQIM IS THE MEAN INFLOW FOR THE TIME STEP MINUS THE NON-SPILLWAY
C OUTFLOW.
C
  270 RTQQIM=RTQQIM-(QTST1+QTST2)*0.5
  280 SO=RTQQIM+SI
      IF(SO.GT.0.)GO TO 290
      QQOS2=0.
      GO TO 300
  290 CALL TERP26(SO,QQOS2,TMPSOH,O,NOSOH,IFLAG,IBUG)
  300 QQO1=QQOK1+QQOS1
      IF(NGATE.EQ.1)QQO1=QQO1+QTST1
      QQO2=QQOK2+QQOS2
      IF(NGATE.EQ.1)QQO2=QQO2+QTST2
      QQOM=(QQO1+QQO2)*0.5
      SS2=SS1+(QQIM-QQOM)*(1.-SUMFRA)
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)420,400,310
  310 WRITE(IODBUG,40) NUMLG,ANUMLG,NUMSM,ANUMSM,NLGDN,NSMDN,NGATE,    N
     $LGSOH,NSMSOH,NGASOH,NPOSOH,DISTLG,DISTSM,COEF,SUMFRA,QQI1,QQI2,  Q
     $QIM,QQO1,QQO2,QQOK1,QQOK2,SS1,SS2,NTERPQ,NTERP,RTQQIM,IBUG
      WRITE(IODBUG,320)(SIGELV(I),SIGSTO(I),I=1,NSIGEL)
  320 FORMAT(1H0,64H ALTERNATING VALUES OF SIGNIFICANT ELEVATIONS AND
     $STORAGES ARE/(1X,10F12.3))
      WRITE(IODBUG,330)(ELVLG(I),QLG(I),I=1,NELVLG)
  330 FORMAT(1H0,101HALTERNATING VALUES OF POOL ELEVATION AND DISCHARGE
     $OVER LARGE-BOARD SPILLWAY WITH ALL BOARDS DOWN ARE/(1X,10F12.3))
      IF(NUMSM.EQ.0) GO TO 350
      WRITE(IODBUG,340)(ELVSM(I),QSM(I),I=1,NELVSM)
  340 FORMAT(1H0,101HALTERNATING VALUES OF POOL ELEVATION AND DISCHARGE
     $OVER SMALL-BOARD SPILLWAY WITH ALL BOARDS DOWN ARE/(1X,10F12.3))
  350 IF(NGATE.EQ.-999) GO TO 370
      WRITE(IODBUG,360)(ELVFL(I),QFLUD(I),I=1,NELVFL)
  360 FORMAT(1H0,94H ALTERNATING VALUES OF POOL ELEVATION AND
     $DISCHARGE THROUGH FLOOD GATE FULL OPEN ARE/(1X,10F12.3))
  370 WRITE(IODBUG,380)(O(I),SOH(I),TMPSOH(I),I=1,NOSOH)
  380 FORMAT(1H0,110HSETS OF VALUES (3 VALUES TO A SET) OF OUTFLOW,
     $CORRESPONDING STORAGE +O/2 WITH STORAGE IN UNITS OF REGULAR/  109H
     $ROUTING STEP, AND STORAGE +O/2 WITH STORAGES IN UNITS OF A
     $FRACTION(1.-SUMFRA) OF THE ROUTING STEP ARE/(1X,10F12.3))
      WRITE(IODBUG,390)(ELVSOH(I),STOSOH(I),I=1,NELSOH)
  390 FORMAT(1H0,112HALTERNATING VALUES OF SPECIFIED ELEVATIONS FOR
     $DEVELOPING THE ROUTING RELATION AND CORRESPONDING STORAGES IN/ 43H
     $UNITS OF MEAN FLOW FOR THE ROUTING STEP ARE/(1X,10F12.3))
  400 WRITE(IODBUG,410)
  410 FORMAT(1H0,10X,17H** LEAVING 0S0H26)
  420 RETURN
      END
