C MEMBER USMN26
C  (from old member FCUSMN26)
C ......................................................................
      SUBROUTINE USMN26(USQO,USTAGE,POOLEL,USQ,PEAK,DRAWDN,RATE,
     $STGOPT,STOPEV,ELEVQ,DAMQ,STOR,ELEV)
C ......................................................................
C THIS SUBROUTINE MINIMIZES THE CREST STAGE DURING FLOOD CONDITIONS AT
C AN UPSTREAM CONTROL POINT IN THE RESERVOIR.  THE POOL ELEVATIOM AT THE
C DAM IS LOWERED IN ADVANCE OF THE FLOOD PEAK IN ORDER TO LOWER THE
C CREST STAGE AT THE CONTROL POINT.  THE REQUIRED DRAWDOWN ELEVATION IS
C DETERMINED IN EACH TIME PERIOD BY THE FORECAST HIGHEST DISCHARGE
C (USQHI) AT THE UPSTREAM CONTROL POINT FOR A SPECIFIED NUMBER OF FUTURE
C TIME PERIODS.  (THE REQUIRED DRAWDOWN ELEVATION MAY CHANGE IN THE NEXT
C TIME PERIOD.) VALUES IN CORRESPONDING POSITIONS IN THE PEAK, DRAWDN,
C RATE,  STGOPT, AND STOPEV ARRAYS DENOTE, RESPECTIVELY, PEAK DISCHARGES
C AT THE UPSTREAM GAGE, REQUIRED DRAWDN POOL ELEVATION, POOL EVACUATION
C RATES FOR TIME INCREMENT (AS DEFINED BY THE NUMBER OF INCREMENTS,
C NTIMIN, IN THE TIME PERIOD), LIMITING STAGES AT THE UPSTREAM
C GAGE FOR OPTIONAL EVACUATION RATES, AND LIMITING STAGES AT THE
C UPSTREAM GAGE FOR STOPPING EVACUATION.  WHEN STGOPT (I) IS 0., THE
C RATE(I) VALUE IS THE EVACUATION RATE.  WHEN STGOPT(I) IS A VALUE
C GREATER THAN 0., THE EVACUATION RATE BELOW STGOPT(I) STAGE IS THE
C MINIMUM OF RATE(I) OR A FACTOR (FAC) TIMES THE RATE OF RISE AT THE
C UPSTREAM GAGE FOR THE PREVIOUS TIME INCREMENT.  EVACUATION STOPS AND
C REFILL BEGINS WHEN THE UPSTREAM STAGE IS FALLING AND IS BELOW STAGE
C REFILL (USUALLY FLOOD STAGE).  PEAK DISCHARGE FROM THE DAM IS
C RESTRICTED TO A MAXIMUM VALUE (PERMXQ) DUE TO FLOODING DOWNSTREAM.  A
C THREE-WAY RELATION BETWEEN UPSTREAM STAGE AND DISCHARGE WITH POOL
C ELEVATION AS A PARAMETER IS NEEDED FOR THIS SUBROUTINE.  A SIMULATED
C RUN IS MADE FIRST USING NO OBSERVED OR ADJUSTED VALUES EXCEPT FOR THE
C BEGINNING STORAGE.  AN ADJUSTED RUN IS THEN MADE  USING OBSERVED AND
C ADJUSTED VALUES.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C ......................................................................
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     DECEMBER, 1981
C ......................................................................
C SUBROUTINE USMN26 IS IN
C ......................................................................
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IFCST -- SIMULATED RUN (IFCST=0) AND ADJUSTED RUN (IFCST=1).
C     FCST -- LOGICAL VARIABLE INDICATING THAT TIME PERIOD IS IN THE
C       FCST PERIOD IF TRUE OR IN THE OBSERVED PERIOD IF FALSE.
C     QI1 -- INFLOW AT BEGINNING OF TIME PERIOD.
C     QI2 -- INFLOW AT END OF TIME PERIOD.
C     QIM -- MEAN INFLOW FOR TIME PERIOD.
C     QO1 -- OUTFLOW AT BEGINNING OF TIME PERIOD.
C     QO2 -- OUTFLOW AT END OF TIME PERIOD.
C     QOM -- MEAN OUTFLOW FOR TIME PERRFD#
C     S1 -- STORAGE AT BEGINNING OF TIME PERIOD IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME PERIOD.
C     S2 -- STORAGE AT END OF TIME PERIOD IN UNITS OF MEAN DISCHARGE FOR
C       THE TIME PERIOD.
C     ELEV1 -- ELEVATION AT BEGINNING OF TIME PERIOD.
C     ELEV2 -- ELEVATION AT END OF TIME PERIOD.
C     ELVNRM -- NORMAL POOL ELEVATION.  WILL BE RULE CURVE ELEVATION IF
C       -999.0.
C     USQO -- COMPUTED DISCHARGE ARRAY FOR UPSTREAM GAGE WITH BEGINNING
C       POSITION AT END OF FIRST TIME PERIOD.
C     NUM -- NO. OF VALUES IN USQO ARRAY.  SAME AS NUMBER OF VALUES IN
C       RESERVOIR INFLOW AND DAM OUTFLOW ARRAYS.
C     NTIME -- NO. OF FUTURE TIME PERIODS USED FOR COMPUTING HIGHEST
C       DISCHARGE.  INCLUDES PRESENT TIME PERIOD.
C     USQ1 -- DISCHARGE AT UPSTREAM GAGE AT BEGINNING OF TIME PERIOD.
C     USTAGB -- STAGE AT UPSTREAM GAGE ONE TIME PERIOD PRIOR TO USTAG1.
C     USTAG1 -- STAGE AT UPSTREAM GAGE AT BEGINNING OF TIME PERIOD.
C     USTAG2 -- STAGE AT UPSTREAM GAGE AT END OF TIME PERIOD.
C     USTAGE -- ARRAY OF UPSTREAM STAGES FOR STAGE VS DISCHARGE RELATION
C       WITH POOL ELEVATION AS A PARAMETER.  STAGES ARE THE SAME FOR ALL
C       POOL ELEVATION CURVES.
C     NUSTAG -- NO. OF STAGE VALUES.
C     POOLEL -- ARRAY OF POOL ELEVATIONS AS PARAMETER IN STAGE VS DIS-
C       CHARGE RELATION.  EACH POOL ELEVATION HAS AN ASSOCIATED CURVE
C       WHICH IS DEFINED BY DISCHARGE VALUES FOR THE ARRAY OF STAGES.
C     NPOOLU -- NO. OF POOL ELEVATION VALUES.
C     USQ -- DOUBLE ARRAY (NUSTAG,NPOOLU) OF DISCHARGE VALUES FOR THE
C       UPSTREAM STAGE VS DISCHARGE RELATION (PARAMETER OF POOL
C       ELEVATION).
C     RULEL1 -- RULE CURVE ELEVATION AT BEGINNING OF TIME PERIOD.
C     RULEL2 -- RULE CURVE ELEVATION AT END OF TIME PERIOD.
C     AGVDEV -- AVERAGE DEVIATION OF OBSERVED POOL ELEVATIONS FROM
C       RULE CURVE ELEVATIONS FOR A SPECIFIED NUMBER OF TIME
C       INTERVALS PRIOR TO THE BEGINNING OF THE FIRST TIME INTERVAL.
C       WILL BE USED FOR THE SIMULATED RUN IF AN ADJUSTMENT IS USED.
C     AVGDEV -- AVERAGE DEVIATION OF POOL ELEVATIONS FROM RULE CURVE
C       ELEVATIONS PRIOR TO RUN TIME FOR A SPECIFIED NUMBER OF TIME
C       INTERVALS.  WILL BE USED FOR ADJUSTED RUN IF AVGDEV IS USED.
C     PEAK -- ARRAY OF PEAK DISCHARGE VALUES FOR PEAK VS DRAWDN VS RATE
C       VS ELVOPT VS STOPEV RELATION.  PEAK VALUES MUST BE IN ASCENDING
C       ORDER.
C     DRAWDN -- ARRAY OF REQUIRED DRAWDOWN POOL ELEVATIONS CORRESPONDING
C       TO PEAK VALUES.  THESE VALUES ARE IN DESCENDING ORDER IN THE
C       ARRAY.
C     RATE -- VALUES OF EVACUATION RATE PER TIME INCREMENT
C       EFFECTIVE BETWEEN THE CORRESPONDING DRAWDN VALUE AND THE NEXT
C       HIGHER VALUE (NEXT LOWER POSITION IN ARRAY).
C     STGOPT -- ARRAY OF ZERO OR UPSTREAM LIMITING STAGE VALUES
C       CORRESPONDING TO RATE VALUES.  WHEN STGOPT(I) IS 0., RATE(I) IS
C       THE EVACUATION RATE.  WHEN STGOPT(I) IS GREATER THAN 0.,
C       STGOPT(I) IS A LIMITING STAGE AT THE UPSTREAM GAGE.  BELOW THE
C       LIMITING STAGE, THE EVACUATION RATE IS THE LESSER OF RATE(I) OR
C       A FACTOR (FAC) TIMES THE RATE OF RISE AT THE UPSTREAM GAGE FOR
C       THE PREVIOUS TIME INCREMENT.
C     STOPEV -- ARRAY OF ZERO OR UPSTREAM STAGES.  IF STOPEV(I) IS
C       GREATER THAN ZERO, IT IS THE UPSTREAM STAGE BELOW WHICH
C       EVACUATION CAN STOP WHEN THE UPSTREAM STAGE STOPS RISING.  WHEN
C       STOPEV(I) IS 0., EVACUATION IS CONTINUED UNTIL THE DRAWNDN
C       POOL ELEVATION IS REACHED.  VALUES OF STOPEV ARE EFFECTIVE
C       BETWEEN THE CORRESPONDING DRAWDN VALUE AND THE NEXT HIGHER VALUE
C       (NEXT LOWER POSITION IN ARRAY).
C     NUMM -- NO. OF VALUES IN EACH OF THE PEAK, DRAWDN, RATE, STGOPT,
C       AND STOPEV ARRAYS.
C     NTIMIN -- NO. OF TIME INCREMENTS IN TIME PERIOD.
C     RISE -- RATE OF RISE IN STAGE AT UPSTREAM STATION IN PREVIOUS TIME
C       INCREMENT.
C     FAC -- FACTOR TO MULTIPLY BY THE HOURLY RATE OF RISE AT THE
C       UPSTREAM GAGE AS EXPLAINED IN THE STGOPT DEFINITION.
C     REFILL -- UPSTREAM STAGE FOR DECIDING WHEN TO REFILL POOL.  WHEN
C       UPSTREAM STAGE IS FALLIMG AND IS BELOW REFILL, FILLING CAN BEGIN
C     ELEVQ -- POOL ELEVATIONS FOR ELEVATION VS TOTAL DAM DISCHARGE
C       RELATION.  (SPILLWAY OR SPILLLWAY + TURBINE).
C     DAMQ -- SPILLWAY (OR SPILLWAY+TURBINE) DISCHARGES FOR ELEVATION
C       VS DISCHARGE RELATION.  ( ALL GATES MUST BE FULLY OPEN.)
C     NDAMQ -- NO. OF PAIRS OF ELEVQ AND DAMQ VALUES.
C     NTERPQ -- ARITHMETIC (NTERQ=0) OR LOGARITHMIC INTERPOLATION
C       (NTERPQ=1) FOR THE ELEVQ VS DAMQ RELATION.
C     PERMXQ -- MAXIMUM PERMISSIBLE DISCHARGE FROM THE DAM DUE TO
C       FLOODING DOWNSTREAM.
C     QLIMNF -- DESIRED LIMIT TO DISCHARGE IN NON-FLOOD SITUATIONS.
C       QLIMNF IS USED IF THE PROGRAM SHOULD NOT BE IN USMN26 OR AFTER
C         FILLING OF THE RESERVOIR.
C       THIS IS USUALLY MAXIMUM TURBINE DISCHARGE IF A POWER DAM.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION CURVE.  MUST BE IN UNITS
C       OF MEAN DISCHARGE FOR THE TIME PERIOD.
C     ELEV -- ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NS2 -- POSITION IN ARRAYS AT END OF THIS TIME PERIOD.
C     NTERP -- INDICATES ARITHMETIC (NTERP=0) OR LOGARITHMIC
C       INTERPOLATION (NTERP=1) FOR THE ELEV VS STOR RELATION.
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1),TRACE AND
C       DEBUG (IBUG=2).
C ......................................................................
C QO2,QOM,S2,ELEV2,USTAG2, AND RISE WILL BE COMPUTED IN THIS SUBROUTINE
C       EXCEPT IN THOSE TIME PERIODS IN THE ADJUSTED RUN WHEN THE VALUES
C       ARE OBSERVED.
C ......................................................................
C
C  MODIFIED TO REARRANGE THE USQ ARRAY FROM (NPOOLU,NUSTAG) TO
C  (NUSTAG,NPOOLU) FOR STRAIGHTFORWARD INPUT AND DISPLAY PURPOSES -
C  (JTO -1/84)
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
      INCLUDE 'common/usmi26'
      INCLUDE 'common/rulc26'
C
      DIMENSION USTAGE(1),POOLEL(1),USQ(NUSTAG,1),PEAK(1),DRAWDN(1),
     $RATE(1),STGOPT(1),ELEVQ(1),DAMQ(1),STOR(1),ELEV(1),STOPEV(1),
     $USQO(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/usmn26.f,v $
     . $',                                                             '
     .$Id: usmn26.f,v 1.1 1995/09/17 19:06:16 dws Exp $
     . $' /
C    ===================================================================
C
C
C ......................................................................
C WRITE DEBUG AND TRACE IF REQUIRED.
C ......................................................................
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** USMN26 ENTERED)
      WRITE(IODBUG,40)IFCST,FCST,IGO,NS2,QI1,QI2,QIM,QO1,QO2,QOM,
     $S1,S2,ELEV1,ELEV2,ELVNRM,NTIME,USQ1,USTAGB,USTAG1,USTAG2,RULEL1,
     $RULEL2,NTIMIN,RISE,FAC,REFILL,NTERPQ,PERMXQ,QLIMNF,NTERP,IBUG
   40 FORMAT(1H0,108H IFCST,FCST,IGO,NS2,QI1,QI2,QIM,QO1,QO2,QOM,S1,S2,
     $ELEV1,ELEV2,ELVNRM,NTIME,USQ1,USTAGB,USTAG1,USTAG2,RULEL1/1X,
     $I6,L5,2I6,8F12.3/1X,3F12.3,I6,5F12.3/1H0,64H RULEL2,NTIMIN,RISE,
     $FAC,REFILL,NTERPQ,PERMXQ,QLIMNF,NTERP,IBUG/1X,F12.3,I6,3F12.3,I6,
     $2F12.3,2I6)
   50 IGO=0
      USQ2=USQO(NS2)
      IF (IFCST.EQ.0) GO TO 60
      IF(FCST) GO TO 60
      OBSQO2=QO2
C
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      IF(IGO.EQ.1) GO TO 350
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; ADJUSTED BETWEEN OBSERVED
C VALUES USING SIMULATED AND OBSERVED VALUES; OR MISSING.
C WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED IN STATEMENT 341
C FROM THE OBSERVED MEAN INFLOW, THE ADJUSTED MEAN INFLOW, AND THE
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
      IF(QOM.EQ.-999.0) GO TO 51
      GO TO 341
C
C IF THE UPSTREAM STAGE (USTAG2) AT THE END OF THE TIME PERIOD IS
C OBSERVED AND POOL ELEVATION (STORAGE) IS NOT OBSERVED, POOL ELEVATION
C CAN BE COMPUTED FROM THE THREE-WAY RELATION OF UPSTREAM STAGE, POOL
C ELEVATION, AND UPSTREAM DISCHARGE.
C
   51 IF(USTAG2.NE.-999.0) GO TO 223
C ......................................................................
C COMPUTE DIFFERENCE IN DISCHARGE AT UPSTREAM GAGE
C FROM BEGINNING TO END OF TIME PERIOD AND COMPUTE HIGHEST DISCHARGE IN
C NEXT NTIME PERIODS.  COMPUTE DIFFERENCE IN INFLOW AT BEGINNING AND
C END OF TIME PERIOD.
C ......................................................................
   60 DIFQUS=USQ2-USQ1
      DIFQI=QI2-QI1
      USQHI=USQ2
C ......................................................................
C COMPUTE HIGHEST UPSTREAM DISCHARGE IN NEXT NTIME PERIODS (INCLUDING
C PRESENT PERIOD).
C ......................................................................
      IF(NS2.EQ.NUM) GO TO 80
      JBGN=NS2+1
      JEND=NS2+NTIME-1
      IF(JEND.GT.NUM) JEND=NUM
      DO 70 J=JBGN,JEND
      IF(J.GT.NUM) GO TO 70
      IF(USQHI.GE.USQO(J)) GO TO 70
      USQHI=USQO(J)
   70 CONTINUE
C
C COMPUTE STORAGE FOR NORMAL ELEVATION AT BEGINNING AND ENDING OF TIME
C PERIOD.  USE RULST1 AND RULST2 EVEN IF A CONSTANT ELEVATION  IS USED.
C
   80 IF(ELVNRM.EQ.-999.0) GO TO 81
      CALL NTER26(ELVNRM,RULST1,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      GO TO 90
   81 ADJ=AGBDEV
      IF(IFCST.EQ.1) ADJ=AVGDEV
      RUL1=RULEL1+ADJ
      RUL2=RULEL2+ADJ
      CALL NTER26(RUL1,RULST1,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      IF(RULEL2.EQ.RULEL1) GO TO 90
      CALL NTER26(RUL2,RULST2,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      GO TO 91
   90 RULST2=RULST1
C
C COMPUTE MAXIMUM POSSIBLE OUTFLOW AT BEGINNING OF TIME PERIOD.
C
   91 CALL NTER26(ELEV1,QMAX1,ELEVQ,DAMQ,NDAMQ,IFLAG,NTERPQ,IBUG)
      QMAX=QMAX1
C
C IF THE UPSTREAM STAGE IS FALLING AND DROPS TO THE REFILL LEVEL
C (REFILL), KEEP AT REFILL UNTIL THE POOL IS FILLED TO NORMAL LEVEL.
C
      IF(USTAG1.EQ.REFILL.AND.QI1.GT.QI2) GO TO 93
      IF(USTAG1.GT.REFILL.AND.QI1.GT.QI2) GO TO 92
      GO TO 100
C
C COMPUTE UPSTREAM STAGE (USTAG2) AT END OF TIME PERIOD ASSUMING OUTFLOW
C IS EQUAL TO INFLOW BUT NOT ALLOWING OUTFLOW TO EXCEED MAXIMUM
C PERMISSIBLE OUTFLOW (PERMXQ) OR MAXIMUM POSSIBLE (QMAX).
C
   92 QOM=QIM
      IF(QOM.GT.PERMXQ) QOM=PERMXQ
      IF(QOM.GT.QMAX) QOM=QMAX
      S2=S1+QIM-QOM
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      CALL F3WAYY(USQ2,USTAG2,ELEV2,USQ,USTAGE,POOLEL,NUSTAG,NPOOLU,
     $IBUG)
      IF(USTAG2.GE.REFILL) GO TO 350
   93 USTAG2=REFILL
      GO TO 223
C ......................................................................
C CHECK HIGHEST UPSTREAM DISCHARGE (USQHI) IN NEXT NTIME PERIODS TO SEE
C IF DRAWNDOWN  IS NEEDED OR IS OCCURRING.  IT IS ASSUMED THAT DRAWNDOWN
C STARTS AT THE BEGINNING OF THE PERIOD IF NOT ALREADY STARTED.  THIS
C ASSUMES THAT DRAWNDOWN WILL BE STARTED FROM AN ANALYSIS OF RAINFALL
C AND RIVER REPORTS BEFORE THERE IS TIME TO MAKE A FORECAST.
C ......................................................................
  100  IF(USQHI.GE.PEAK(1)) GO TO 120
C ......................................................................
C DRAWNDOWN IS NOT NEEDED FOR FOLLOWING STATEMENTS BUT FILLING OR NORMAL
C OPERATION MAY BE OCCURRING.  CHECK IF MEAN OUTFLOW HAS BEEN COMPUTED
C IN A PREVIOUS SCHEME.  IF NOT,
C COMPUTE OUTFLOW AND STORAGE BY BRINGING POOL TO NORMAL STORAGE BUT
C LIMIT OUTFLOW TO LIMITING DISCHARGE (QLIMNF) FOR NON-FLOOD SITUATIONS.
C  LIMITING DISCHARGE IS USUALLLY NORMAL MAXIMUM TURBINE FLOW FOR POWER
C DAMS.
C ......................................................................
      IF(QOM.NE.-999.0) GO TO 102
      QOM=S1+QIM-RULST2
  101 IF(QOM.GT.QLIMNF) QOM=QLIMNF
C
C A CHECK WILL BE MADE LATER TO INSURE THAT UPSTREAM STAGE IS NOT INCREA
C ED ABOVE REFILL STAGE IF THE COMPUTED OUTFLOW IS PASSED.
C
      IF(QOM.LT.0.) QOM=0.
  102 S2=S1+QIM-QOM
C
C COMPUTE ENDING ELEVATION (ELEV2) FOR TIME INTERVAL AND THEN GO TO 170
C TO COMPUTE MAXIMUM POSSIBLE OUTFLOW.
C
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      GO TO 170
C ......................................................................
C DRAWMDOWN IS NEEDED OR HAS BEEN OCCURRING IN THE FOLLOWING STATEMENTS.
C COMPUTE DRAWNDOWN ELEVATION (DRAWEL) AND CHECK IF IT HAD BEEN REACHED
C AT THE BEGINNING OF THE TIME PERIOD.
C ......................................................................
  120 DO 130 I=2,NUMM
      IF(USQHI.GE.PEAK(I)) GO TO 130
      DRAWEL=DRAWDN(I-1)
      GO TO 140
  130 CONTINUE
      DRAWEL=DRAWDN(NUMM)
  140 IF(ELEV1.GT.DRAWEL) GO TO 250
C ......................................................................
C FOR FOLLOWING STATEMENTS, ELEVATION WAS DOWN TO OR BELOW DRAWDOWN
C ELEVATION.  MEAN INFLOW WILL BE A TRIAL VALUE OF OUTFLOW FOR THE TIME
C PERIOD.  FIRST CHECK QIM AGAINST MAXIMUM PERMISSIBLE DISCHARGE
C (PERMXQ).
C ......................................................................
      IF(QIM.LE.PERMXQ) GO TO 150
      QOM=PERMXQ
      GO TO 160
  150 QOM=QIM
      IF(QOM.LT.0.) QOM=0.
  160 S2=S1+QIM-QOM
C ......................................................................
C COMPUTE ELEV2 FROM S2 USING ARITHMETIC OR LOGARITHMIC INTERPOLATION.
C ......................................................................
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C ......................................................................
C COMPUTE MAXIMUM POSSIBLE  DISCHARGE AT END OF TIME
C PERIOD AND CHECK IF MEAN OUTFLOW (QOM) EXCEEDS MAXIMUM.  USE
C ARITHMETIC (NTERPQ=0) OR LOGARITHMIC INTERPOLATION (NTERPQ=1).
C QMAX1 HAS BEEN COMPUTED.
C ......................................................................
  170 CALL NTER26(ELEV2,QMAX2,ELEVQ,DAMQ,NDAMQ,IFLAG,NTERPQ,IBUG)
      IF(QMAX2.LE.QMAX1) GO TO 190
      QMAX=QMAX2
      GO TO 200
  190 QMAX=QMAX1
  200 IF(QOM.LE.QMAX) GO TO 210
C ......................................................................
C TEST MEAN DISCHARGE (QOM) FOR THE TIME PERIOD IS GREATER THAN MAX.
C POSSIBLE (QMAX) IN THE FLLLOWING STATEMENTS.  SET QOM=QMAX AND COMPUTE
C NEW VALUES FOR S2 AND ELEV2.
C ......................................................................
      QOM=QMAX
      S2=S1+QIM-QOM
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C ......................................................................
C CHECK IF UPSTREAM STAGE (USTAG2) HAS BEEN COMPUTED.  IF IT HAS NOT,
C COMPUTE UPSTREAM STAGE    AT END OF TIME PERIOD AND CHECK IF RISING
C OR FALLING.  USE SUBROUTINE F3WAYY.
C ......................................................................
  210 IF(USTAG2.EQ.-999.0) GO TO 220
C ......................................................................
C FOR FOLLOWING STATEMENTS USTAG2 HAS BEEN ALREADY COMPUTED.  COMPUTE
C POOL ELEVATION FROM THREE WAY RELATION.
C ......................................................................
      CALL F3WAYZ(USQ2,USTAG2,ELEV2,USQ,USTAGE,POOLEL,NUSTAG,NPOOLU,
     $IBUG)
C ......................................................................
C COMPUTE STORAGE (S2) FROM ELEVATION (ELEV2) AT END OF TIME PERIOD.
C ......................................................................
      CALL NTER26(ELEV2,S2,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      QOM=S1+QIM-S2
      IF(QOM.GT.QMAX) QOM = QMAX
      IF(QOM.LT.0.) QOM=0.
      S2=S1+QIM-QOM
      GO TO 221
  220 CALL F3WAYY(USQ2,USTAG2,ELEV2,USQ,USTAGE,POOLEL,NUSTAG,NPOOLU,
     $IBUG)
  221 RISE=USTAG2-USTAG1
      IF(USQHI.GE.PEAK(1)) GO TO 222
C
C WHEN PEAK UPSTREAM DISCHARGE OVER THE SPECIFIED NUMBER OF FUTURE TIME
C INTERVALS IS LESS THAN THE LOWEST PEAK DISCHARGE FOR DRAWDOWN, THE
C COMPUTED OUTFLOW WILL BE ADJUSTED DOWNWARD IF IT CAUSES THE UPSTREAM
C STAGE TO EXCEED REFILL STAGE BY SETTING UPSTREAM STAGE EQUAL
C TO REFILL.
C
      IF(USTAG2.LE.REFILL) GO TO 350
      IF(USTAG2.GE.USTAG1) GO TO 350
      USTAG2=REFILL
      GO TO 223
  222 IF(USTAG2.GE.USTAG1) GO TO 350
C ......................................................................
C WHEN UPSTREAM STAGE IS FALLING, FILLING CAN BEGIN IF STAGE IS LESS
C THAN REFILL AT BEGINNING OF TIME PERIOD OR IF STAGE DROPS TO REFILL
C DURING THE TIME PERIOD.
C ......................................................................
      IF(USTAG1.GE.REFILL) GO TO 350
C ......................................................................
C SINCE BEGINNING STAGE (USTAG1)IS LESS THAN REFILL (OR STAGE DROPS
C TO REFILL DURING THE TIME PERIOD) AND THE STAGE IS
C FALLING, THE POOL CAN START TO FILL OR CONTINUE TO FILL.
C ASSUMING THE UPSTREAM STAGE WILL BE HELD STEADY,
C COMPUTE THE POOL ELEVATION (ELEV2) AT THE END OF THE TIME PERIOD.
C USE SUBROUTINE F3WAYZ.
C ......................................................................
      USTAG2=USTAG1
  223 CALL F3WAYZ(USQ2,USTAG2,ELEV2,USQ,USTAGE,POOLEL,NUSTAG,NPOOLU,
     $IBUG)
C ......................................................................
C COMPUTE NEW STORAGE (S2) FROM ELEVATION (ELEV2) AT END OF TIME PERIOD.
C ......................................................................
      CALL NTER26(ELEV2,S2,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      QOM=S1+QIM-S2
      IF(QOM.LT.0.) QOM=0.
      IF(QOM.GT.QMAX) QOM=QMAX
      IF(QOM.GT.PERMXQ) QOM=PERMXQ
      S2=S1+QIM-QOM
C
C COMPUTE NEW ENDING ELEVATION (ELEV2) AND THEN COMPUTE A NEW UPSTREAM
C STAGE (USTAG2).
C
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      CALL F3WAYY(USQ2,USTAG2,ELEV2,USQ,USTAGE,POOLEL,NUSTAG,NPOOLU,
     $IBUG)
      GO TO 350
C ......................................................................
C FOLLOWING STATEMENTS ARE FOR SITUATION WHERE ELEVATION AT BEGINNING OF
C TIME PERIOD IS GREATER THAN THE DRAWNDOWN ELEVATION.  DRAWNDOWN MUST
C BE CONTINUED UNTIL DRAWNDOWN ELEVATION IS REACHED OR UPSTREAM STAGE
C STOPS RISING AND IS BELOW STOP EVACUATION VALUE.  COMPUTE IN
C TIME INCREMENTS (NTIMIN INCREMENTS IN TIME PERIOD) TO MAKE SURE THAT
C REQUIRED OUTFLOWS DO NOT EXCEED MAXIMUM PERMISSIBLE (PERMXQ) DUE TO
C DOWNSTREAM FLOODING OR MAX. POSSIBLE (QMAX) DUE TO SPILLWAY PLUS
C TURBINE DISCHARGE CAPACITY.  FIRST, DEFINE VALUES OF POOL ELEVATION,
C UPSTREAM STAGE, UPSTREAM DISCHARGE,INFLOW, AND STORAGE AT THE BEGIN-
C NING OF THE FIRST TIME INCREMENT.
C ......................................................................
  250 ELRES1=ELEV1
      UST1=USTAG1
      USQQ1=USQ1
      QQI1=QI1
      SS1=S1
C ......................................................................
C COMPUTE INCREMENTAL CHANGES IN RESERVOIR INFLOW (DELQI) AND UPSTREAM
C DISCHARGE (DELUSQ).
C ......................................................................
      TIMIN=NTIMIN
      DELQI=DIFQI/TIMIN
      DELUSQ=DIFQUS/TIMIN
C ......................................................................
C COMPUTE VALUES AT END OF EACH TIME INCREMENT IN DO LOOP 330.
C ......................................................................
      DO 330 I=1,NTIMIN
      QQI2=QQI1+DELQI
      USQQ2=USQQ1+DELUSQ
      QQIM=(QQI1+QQI2)*0.5
      IF(ELRES1.LE.DRAWEL) GO TO 310
C ......................................................................
C STATEMENT 310 WILL TAKE CARE OF SITUATION WHERE EVACUATION HAS STOPPED
C FOLLOWING DO LOOP 260 DETERMINES EVACUATION RATE (DRATE), LIMITING
C UPSTREAM STAGE FOR STOPPING EVACUATION (EVAC), AND LIMITING UPSTREAM
C STAGE (STAGOP) FOR USE OF ALTERNATE METHOD OF COMPUTING EVACUATION
C RATE.
C ......................................................................
      DO 260 K=1,NUMM
      J=NUMM-K+1
      IF(ELRES1.GT.DRAWDN(J)) GO TO 260
      J=J+1
      GO TO 261
  260 CONTINUE
  261 DRATE=RATE(J)
      EVAC=STOPEV(J)
      STAGOP=STGOPT(J)
      IF(RISE.EQ.-999.0) RISE=USTAG1-USTAGB
      IF(STAGOP.EQ.0.) GO TO 270
      IF(UST1.GE.STAGOP) GO TO 270
C ......................................................................
C COMPUTE EVACUATION RATE (TEST) FROM MULTIPLYING A FACTOR (FAC) TIMES
C THE PREVIOUS INCREMENTAL CHANGE (RISE) IN THE UPSTREAM STAGE AND TAKE
C THE MINIMUM OF TEST AND DRATE AS THE EVACUATION RATE FOR THIS TIME
C INCREMENT.
C ......................................................................
      TEST=RISE*FAC
      IF(TEST.GE.DRATE) GO TO 270
      IF(TEST.LT.0.) TEST=0.
      DRATE=TEST
C
C EVACUATION WILL CEASE IF THE UPSTREAM DISCHARGE IS DROPPING AND THE
C UPSTREAM STAGE IS LESS THAN THE STAGE FOR STOPPING EVACUATION.
C
  270 IF(USQQ2.LT.USQQ1.AND.UST1.LT.EVAC) GO TO 310
C ......................................................................
C  COMPUTE REQUIRED POOL ELEVATION (ELRES2) AT END OF TIME INCRE-
C MENT AND CHECK IF ELRES2 IS ABOVE REQUIRED DRAWNDOWN (DRAWEL).
C ......................................................................
      ELRES2=ELRES1-DRATE
      IF(ELRES2.GE.DRAWEL) GO TO 280
      ELRES2=DRAWEL
C ......................................................................
C COMPUTE STORAGE AT END OF TIME INCREMENT.
C ......................................................................
  280 CALL NTER26(ELRES2,SS2,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
C ......................................................................
C COMPUTE MEAN OUTFLOW (QQOM) FOR TIME INCREMENT AND TEST QQOM AGAINST
C MAX. PERMISSIBLE OUTFLOW.  NQQOM IS USED TO INDICATE WHETHER QQOM IS
C LATER CHANGED.
C ......................................................................
      QQOM=SS1+QQIM-SS2
      IF(QQOM.LT.0.) QQOM=0.
      NQQOM=0
      IF(QQOM.LE.PERMXQ) GO TO 290
      QQOM=PERMXQ
      NQQOM=1
C ......................................................................
C COMPUTE MAX. POSSIBLE OUTFLOW (QMAX) AT END OF TIME INCREMENT AND
C CHECK AGAINST QQOM.
C ......................................................................
  290 CALL NTER26(ELRES2,QMAX,ELEVQ,DAMQ,NDAMQ,IFLAG,NTERPQ,IBUG)
      IF(QQOM.LE.QMAX) GO TO 300
      QQOM=QMAX
      NQQOM=1
  300 IF(NQQOM.EQ.0) GO TO 320
C ......................................................................
C COMPUTE NEW VALUES OF SS2 AND ELRES2 AS QQOM HAS BEEN CHANGED.
C SS2 AND ELRES2 ARE, RESPECTIVELY, STORAGE AND ELEVATION AT THE END OF
C THE COMPUTATIONAL TIME INCREMENT.  QQOM IS THE MEAN OUTFLOW FOR THE
C TIME INCREMENT.
C ......................................................................
      SS2=SS1+QQIM-QQOM
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      GO TO 320
C ......................................................................
C FOR THE FOLLOWING STATEMENTS, EVACUATION HAS STOPPED DUE TO DRAWDOWN
C ELEVATION BEING REACHED OR DUE TO FALLING UPSTREAM STAGES AND STAGE
C LESS THAN SPECIFIED LIMITING EVACUATION STAGE (EVAC).
C ......................................................................
  310 ELRES2=ELRES1
      SS2=SS1
C ......................................................................
C COMPUTE USSTREAM STAGE (UST2) AT END OF TIME INCREMENT.
C ......................................................................
  320 CALL F3WAYY(USQQ2,UST2,ELRES2,USQ,USTAGE,POOLEL,NUSTAG,NPOOLU,
     $IBUG)
C
C WRITE DEBUG IF REQUIRED.
C
      IF(IBUG.NE.2) GO TO 322
      WRITE(IODBUG,321) I, IGO,NS2,UST1,UST2,RISE,DRAWEL,QI1,QI2,QQI1,
     $QQI2,QO1,QQOM,QMAX,USQ1,USQ2,USQQ1,USQQ2,S1,SS1,SS2,ELEV1,
     $ELEV2,ELRES1,ELRES2,DRATE,EVAC,STAGOP
  321 FORMAT(1H0,108H  I, IGO,NS2,UST1,UST2,RISE,DRAWEL,QI1,QI2,QQI1,
     $QQI2,QO1,QQOM,QMAX,USQ1,USQ2,USQQ1,USQQ2,S1,SS1,SS2,ELEV1/1X,3I6,
     $8F12.3/1X,10F12.3/1X,F12.3/1H0,38H ELEV2,ELRES1,ELRES2,DRATE,EVAC,
     $STAGOP/1X,6F12.3)
C ......................................................................
C COMPUTE CHANGE IN UPSTREAM STAGE (RISE) FOR THE TIME INCREMENT AND
C RESET VALUES FOR NEXT LOOP IN DO LOOP.
C ......................................................................
  322 RISE=UST2-UST1
      QQI1=QQI2
      USQQ1=USQQ2
      SS1=SS2
      UST1=UST2
      ELRES1=ELRES2
  330 CONTINUE
      QO2=QQOM
      USTAG2=UST1
      S2=SS1
      QOM=S1+QIM-S2
      IF(QOM.GE.0.) GO TO 331
      QOM=0.
      S2=S1+QIM
      GO TO 340
  331 ELEV2=ELRES1
  340 IF(IFCST.EQ.0) GO TO 381
      IF(FCST) GO TO 381
      GO TO 360
  341 S2=S1+QIM-QOM
  350 QO2=QOM
      IF(IFCST.EQ.0) GO TO 370
      IF(FCST) GO TO 370
  360 IF(OBSQO2.NE.-999.0) QO2=OBSQO2
C ......................................................................
C COMPUTE ELEV2 IF MISSING FROM S2.  ELEV2 AND S2 ARE ELEVATION AND
C STORAGE AT THE END OF THE TIME PERIOD.
C ......................................................................
      IF(ELEV2.NE.-999.0) GO TO 380
  370 CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C
C COMPUTE UPSTREAM STAGE IF IT HAS NOT ALREADY BEEN COMPUTED.
C
  380 IF(USTAG2.NE.-999.0) GO TO 381
      CALL F3WAYY(USQ2,USTAG2,ELEV2,USQ,USTAGE,POOLEL,NUSTAG,NPOOLU,
     $IBUG)
      IF(IGO.EQ.1) RISE=USTAG2-USTAG1
C ......................................................................
C WRITE TRACE AND DEBUG IF REQUIRED.
C ......................................................................
  381 IF(IBUG-1) 500,480,390
  390 WRITE(IODBUG,400)
  400 FORMAT(1H0,53H FOLLOWING ARE VARIABLES VALUES AT THE END OF USMN26
     $.)
      WRITE(IODBUG,40)IFCST,FCST,IGO,NS2,QI1,QI2,QIM,QO1,QO2,QOM,
     $S1,S2,ELEV1,ELEV2,ELVNRM,NTIME,USQ1,USTAGB,USTAG1,USTAG2,RULEL1,
     $RULEL2,NTIMIN,RISE,FAC,REFILL,NTERPQ,PERMXQ,QLIMNF,NTERP,IBUG
      IF(NS2.NE.1.AND.NS2.NE.NUM) GO TO 480
      WRITE(IODBUG,410) (USQO(I),I=1,NUM)
  410 FORMAT(1H0,30H UPSTREAM DISCHARGE VALUES ARE/(1X,10F12.3))
      WRITE(IODBUG,420) IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
  420 FORMAT(1H0,9H IFLAG IS,I6,42H.  NO OF POINTS ON ELEV - STORAGE CUR
     $VE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION A
     $RE/(1X,10F12.3))
      WRITE(IODBUG,430) NDAMQ,(ELEVQ(I),DAMQ(I),I=1,NDAMQ)
  430 FORMAT(1H0,58H NO. OF POINTS ON POOL ELEVATION VS MAX DISCHARGE CU
     $RVE IS,I4,1H./1H0,58H ALTERNATING VALUES OF ELEVATION AND MAXIMUM
     $DISCHARGE ARE/(1X,10F12.3))
      WRITE(IODBUG,440) NUMM,(PEAK(I),DRAWDN(I),RATE(I),STGOPT(I),STOPEV
     $(I),I=1,NUMM)
  440 FORMAT(1H0,65H NO. OF VALUES IN PEAK, DRAWDN, RATE,STGOPT,AND  STO
     $PEV ARRAYS IS,I4/1H0,85H FOLLOWING ARE SETS OF CORRESPONDING VALUE
     $S OF PEAK, DRAWDN, RATE, STGOPT, AND STOPEV/(1X,2(F12.3,F10.3,F6.3
     $,2F10.3,10X)))
      WRITE(IODBUG,470) RULST1,RULST2
  470 FORMAT(1H0,60H RULE CURVE STORAGES AT BEGINNING AND END OF TIME PE
     $RIOD ARE,2F10.3)
  480 WRITE(IODBUG,490)
  490 FORMAT(1H0,10X,17H** LEAVING USMN26)
  500 RETURN
      END
