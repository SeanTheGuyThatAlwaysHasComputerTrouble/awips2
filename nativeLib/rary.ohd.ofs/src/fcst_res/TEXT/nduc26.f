C MEMBER NDUC26 C (from old member FCNDUC26) C
      SUBROUTINE NDUC26(ELVSUR,QISUR,ELVFAL,PEAKQ,PEAKEL,OPTEV,EVACEL,
     $GATE,EVACQ,SCEL,SCQI,SCQO,STOR,ELEV)
C
C THIS SUBROUTINE COMPUTES OUTFLOW FROM AN INDUCED SURCHARGE RELATION
C WHEN THE POOL IS RISING AND OUTFLOW FROM VARIOUS EVACUATION PROCEDURES
C WHEN THE POOL IS FALLING.  THE INDUCED SURCHARGE RELATION IS A THREE
C VARIABLE RELATION BETWEEN POOL ELEVATION, OUTFLOW, AND INFLOW
C PARAMETER.  IF THE DAM OPERATOR USES SEPARATE INDUCED SURCHARGE CURVES
C AND EVACUATION INSTRUCTIONS FOR WINTER AND SUMMER,  THE PARAMETER FILE
C WILL HAVE TWO SURCHARGE CURVES AND TWO SETS OF EVACUATION INSTRUCTIONS
C THE SUPERVISORY EXECUTION ROUTINE WILL PASS THE PROPER SURCHARGE CURVE
C AND EVACUATION INSTRUCTIONS TO NDUC26.
C     SOME DAMS FOLLOW THE INDUCED SURCHARGE CURVES UNTIL THE POOL
C CRESTS.  OTHER DAMS DO NOT PERMIT THE GATE SETTINGS TO BE DECREASED AS
C LONG AS THE POOL IS RISING EVEN THOUGH THE SURCHARGE CURVES INDICATE A
C DECREASE IN OUTFLOW AND GATE SETTINGS.  THE SUBROUTINE WILL USE THE
C OPTION (IOPTND) SPECIFIED BY THE USER.
C     WHEN THE POOL STARTS TO FALL, A VARIETY OF INSTRUCTIONS ARE USED
C FOR EVACUATIONG THE SURCHARGE STORAGE.  GENERALLY, THE MAXIMUM GATE
C SETTING OR THE MAXIMUM OUTFLOW WILL BE MAINTAINTED UNTIL THE POOL
C DROPS TO A SPECIFIED ELEVATION.  BELOW THE SPECIFIED ELEVATION A
C VARIETY OF INSTRUCTIONS ARE USED.  THE EVACUATION OPTIONS IN THE
C SUBROUTINE DEPEND ON ONE OF THE FOLLOWING CONDITIONS:  (1) THE POOL
C ELEVATION.  (2) THE PEAK OUTFLOW.  (3) THE PEAK POOL ELEVATION.  (4)
C POOL ELEVATION AND PEAK OUTFLOW.  (5) PEAK OUTFLOW AND PEAK POOL
C ELEVATION.  A DECISION TABLE (INPUT BY USER) BASED ON EXCEEDANCE
C CRITERIA OF THE SELECTED CONDITION DETERMINES WHICH OF TEN EVACUATION
C OPTIONS WILL BE USED.   THESE OPTIONS ARE DEFINED IN SUBROUTINES
C EVAA26, EVAB26, AND EVAC26.
C     TWO RUNS WILL BE MADE. THE FIRST WILL BE A SIMULATED RUN WITH
C NO OBSERVED DATA (EXCEPT INITIAL STORAGE AT BEGINNING OF FIRST TIME
C PERIOD).  THE SECOND RUN WILL BE AN ADJUSTED RUN THAT WILL USE ALL
C OBSERVED DATA.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     FEBRUARY, 1982
C
C  MODIFIED TO REARRANGE THE OUTFLO ARRAY FROM (NSCQI,NSCEL) TO
C  (NSCEL,NSCQI) STRAIGHTFORWARD INPUT AND DISPLAY PURPOSES -
C   (JTO - 1/84)
C
C SUBROUTINE NDUC26 IS IN
C
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C
C     ELBAC1 -- POOL ELEVATION ONE TIME PERIOD BEFORE ELEV1.
C     ELEV -- ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     ELEV1 -- ELEVATION AT BEGINNING OF TIME PERIOD.
C     ELEV2 -- ELEVATION AT END OF TIME PERIOD.
C     ELRES1 -- ELEVATION AT BEGINNING OF TIME STEP.
C     ELRES2 -- ELEVATION AT END OF TIME STEP.
C     ELVFAL -- ARRAY OF POOL ELEVATIONS OR SETS OF ELEVATIONS IN
C       ASCENDING ORDER.  MAY BE USED ALONE OR COMBINED WITH PEAK
C       OUTFLOW OR PEAK POOL ELEVATION IN A DECISION  TABLE TO DEFINE
C       THE EVACUATION OPTION.  RULE CURVE OR RULE CURVE ELEVATIONS
C       PLUS AN ADJUSTMENT ARE DEFINED BY ADDING THE ADJUSTMENT TO
C       -999.0.  FOR EXAMPLE, RULE CURVE ELEVATION WITH AN ADJUSTMENT
C       OF +1.0 WOULD BE DEFINED AS -998.0.
C     ELVMAX -- MAXIMUM ELEVATION DURING A SURCHARGE OPERATION.  WILL BE
C       RESET TO -999.0 WHEN ELEVATION FALLS BELOW FALELB.
C     ELVSUR -- ELEVATION VALUES FOR ELEVATION VS REQUIRED INFLOW
C       RELATION.
C     EUPRND -- SPECIFIED UPPER ELEVATION ABOVE WHICH THE OPERATION IS
C       ALWAYS IN INDUCED SURCHARGE OR IN EVACUATION OF SURCHARGE
C       STORAGE.  USE RULE CURVE ELEVATION IF ELWRND IS -999.0.
C       RULE CURVE PLUS AN ADDITION IS DEFINED BY ADDING THE ADDITION
C       TO -999.0.
C     ELWRND -- SPECIFIED LOWER ELEVATION FOR RISING POOL.  BETWEEN
C       ELWRND AND EUPRND, INDUCED SURCHARGE WILL BE REQUIRED ONLY IF
C       THE INFLOW EXCEEDS THE VALUE COMPUTED FROM THE ELVSUR VS QISUR
C       RELATION.  RULE CURVE ELEVATION IS DEFINED AS -999.0 AND RULE
C       CURVE PLUS AN ADDITION IS DEFINED BY ADDING THE ADDITION TO
C       -999.0.
C     EVACEL -- ARRAY OF POOL ELEVATIONS FOR THREE-WAY RELATION OF
C       ELEVATION, GATE OPENINGS AND DISCHARGES.
C     EVACQ -- DOUBLE ARRAY (NGAT,NELEV) OF DISCHARGE VALUES
C       CORRESPONDING TO EVACEL AND GATE VALUES.  DISCHARGE WILL INCLUDE
C       MAXIMUM GENERATION AND/OR SLUICE DISCHARGES IF USED.
C     FCST -- LOGICAL VARIABLE.  IF TRUE, TIME PERIOD IS IN THE FORECAST
C       PERIOD.
C     FRACT -- FRACTION OF TIME PERIOD OVER WHICH MEAN INFLOW IS
C       COMPUTED FOR INDUCED SURCHARGE RELATION.
C     GATE -- GATE OPENINGS FOR THREE-WAY RELATION.  ALL GATES ARE
C       ASSUMED TO BE OPEN THE SAME AMOUNT.
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1),TRACE AND
C       DEBUG (IBUG=2).
C     IFCST -- SIMULATED RUN (IFCST=0) AND ADJUSTED RUN (IFCST=1).
C     IOPTND -- OPTION FOR FOLLOWING SURCHARGE CURVES (IOPTND=0) IN
C       INDUCED SURCHARGE OPERATION UNTIL POOL CRESTS OR FOR NOT
C       DECREASING GATE SETTINGS AS LONG AS POOL IS RISING (IOPTND=1)
C       EVEN THOUGH THE SURCHARGE RELATION INDICATES A DECREASE.
C     ISURG -- = 1 TO INDICATE FIRST TIME INTO INDSRCHGE SCHEME
C     ISTEP -- INDICATOR FOR STEP IN EVACUATION OPTION WHEN MORE THAN
C       ONE SET OF INSTRUCTIONS IS USED FOR THE OPTION.  THE INDICATOR
C       IS 1 FOR THE FIRST SET OF INSTRUCTIONS, 2 FOR THE SECOND SET,
C       ETC.  WHEN THE INDICATOR HAS NOT BEEN PREVIOUSLY DEFINED, IT
C       WILL BE -999.0 BUT WILL BE SET TO 1 WHEN FIRST ENTERING ONE OF
C       THE EVACUATION SUBROUTINES.  IT WILL BE SET TO -999.0 WHEN
C       EVACUATION CEASES.
C     NCON -- DEFINES THE VARIABLE OR COMBINATION OF VARIABLES ON WHICH
C       THE EVACUATION OPTION IS DEPENDENT.  VALUES OF NCON DEFINE THE
C       FOLLOWING CONDITIONS:  (1) POOL ELEVATION  , (2) PEAK OUTFLOW,
C       (3) PEAK POOL ELEVATION, (4) POOL ELEVATION AND PEAK OUTFLOW,
C       AND (5) PEAK OUTFLOW AMD PEAK POOL ELEVATION.
C     NELEV -- NO. OF VALUES IN EVACEL ARRAY.
C     NEQSUR -- NO. OF PAIRS OF ELVSUR AND QISUR VALUES.
C     NGAT -- NO. OF GATE OPENING VALUES.
C     NSCEL -- NO. OF POOL ELEVATION VALUES IN SURCHARGE REALTION.
C     NSCQI -- NO. OF INFLOW (SCQI) VALUES IN SURCHARGE REALTION.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NSTEPS -- NO. OF COMPUTATIONAL TIME STEPS IN A TIME PERIOD.
C     NTERP -- INDICATES ARITHMETIC (NTERP=0) OR LOGARITHMIC
C       INTERPOLATION (NTERP=1).
C     NUMVAL -- NO. OF VALUES IN OPTEV AND THE OTHER ARRAYS USED IN
C       THE DECISION TABLE.
C     OPTEV -- ARRAY OF EVACUATION OPTIONS DEFINED BY THE DECISION TABLE
C       INVOLVING ONE OR MORE OF THE ELVFAL,PEAKQ, OR PEAKEL ARRAYS.
C       OPTION VALUES RANGE FROM 1 TO 10 AND ARE DEFINED IN UTILITY
C       SUBROUTINES EVAA26, EVAB26, AND EVAC26.
C     OUTMAX -- MAXIMUM OUTFLOW DURING A SURCHARGE OPERATION.  WILL BE
C       RESET TO -999.0 UNDER SAME CONDIRIONS AS ELVMAX.
C     PCTERN -- SPECIFIED TOLERANCE IN SUCCESSIVE ITERATED DISCHARGES AS
C       A FRACTION OF THE DISCHARGE.
C     PEAKEL -- ARRAY OF PEAK ELEVATIONS OR SETS OF PEAK ELEVATIONS IN
C       ASCENDING ORDER  USED ALONE OR COMBINED WITH PEAK OUTFLOW IN A
C       DECISION TABLE TO DEFINE THE EVACUATION OPTION.  WILL BE -999.0
C       IF NOT USED.
C     PEAKQ -- ARRAY OF PEAK OUTFLOWS OR SETS OF PEAK OUTFLOWS IN
C       ASCENDING ORDER USED ALONE OR CONBINED WITH ELEVATION OR PEAK
C       ELEVATION IN A DECISION TABLE TO DEFINE THE EVACUATION OPTION.
C       WILL BE -999.0 IF NOT USED.
C     QELVMX -- OUTFLOW AT ELVMAX DURING A SURCHARGE OPERATION.  WILL BE
C       RESET TO -999.0 UNDER SAME CONDIRIONS AS ELVMAX.
C     QGENMX -- MAXIMUM ALLOWABLE POWER GENERATION DISCHARGE.
C     QIBAC1 -- INSTANTANEOUS INFLOW ONE TIME PERIOD BEFORE QI1.
C     QI1 -- INFLOW AT BEGINNING OF TIME PERIOD.
C     QI2 -- INFLOW AT END OF TIME PERIOD.
C     QIM -- MEAN INFLOW FOR TIME PERIOD.
C     QISUR -- REQUIRED INFLOWS FOR ENTERING THE SURCHARGE RELATION WHEN
C       ELEVATION IS LESS THAN EUPRND.
C     QO1 -- OUTFLOW AT BEGINNING OF TIME PERIOD.
C     QO2 -- OUTFLOW AT END OF TIME PERIOD.
C     QOM -- MEAN OUTFLOW FOR TIME PERIOD.
C     QOMAX -- SEE OUTMAX
C     QQI1 -- INFLOW AT BEGINNING OF TIME STEP.
C     QQI2 -- INFLOW AT END OF TIME STEP.
C     QQIM -- MEAN INFLOW FOR TIME STEP.
C     QQO1 -- OUTFLOW AT BEGINNING OF TIME STEP.
C     QQO2 -- OUTFLOW AT END OF TIME STEP.
C     QQOM -- MEAN OUTFLOW FOR TIME STEP.
C     RULEL1 -- RULE CURVE ELEVATION AT BEGINNING OF TIME PERIOD.
C     RULEL2 -- RULE CURVE ELEVATION AT END OF TIME PERIOD.
C     S1 -- STORAGE AT BEGINNING OF TIME PERIOD IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME PERIOD.
C     S2 -- STORAGE AT END OF TIME PERIOD IN UNITS OF MEAN DISCHARGE FOR
C       THE TIME PERIOD.
C     SCEL -- ARRAY OF POOL ELEVATIONS FOR INDUCED SURCHARGE RELATION.
C     SCEL1 -- LOWEST POOL ELEVATION IN INDUCED SURCHARGE RELATION.
C     SCQI -- ARRAY OF INFLOW VALUES USED AS PARAMETER IN INDUCED SUR-
C       CHARGE RELATION.
C     SCQIMN -- LOWEST INFLOW VALUE IN INDUCED SURCHARGE RELATION.
C     SCQO -- DOUBLE ARRAY (NSCEL,NSCQI) OF OUTFLOW VALUES FOR THE
C       INDUCED SURCHARGE RELATION.
C     SCQOMN -- MINIMUM OUTFLOW SHOWN ON INDUCED SURCHARGE RELATION.
C       MAY BE MAXIMUM GENERATION DISCHARGE FOR A POWER DAM BUT COULD
C       BE A GREATER OR LESSER VALUE.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION CURVE.  MUST BE IN UNITS
C       OF MEAN DISCHARGE FOR THE TIME PERIOD.
C     SURCHG -- LOGICAL VARIABLE.  WHEN TRUE, THE PROGRAM SHOULD BE IN
C       NDUC26.
C     TENDCY -- CHANGE IN POOL ELEVATION FROM PREVIOUS ELEVATION TO 
C        ELEV1. TENDCY WILL NEED TO BE SAVED FROM ONE TIME PERIOD TO THE
C       NEXT PERIOD SINCE IT CAN CAHNGE DURING THE TIME PERIOD.  HOWEVER
C       IF THE PROGRAM WAS NOT IN NDUC26 IN THE PREVIOUS TIME PERIOD,
C       TENDCY SHOULD BE SET TO -999.0 AND IT WILL BE COMPUTED IN
C       NDUC26 FROM ELBAC1 AND ELEV1.
C
C QO2, QOM, S2, AND ELEV2 WILL BE COMPUTED IN THIS SUBROUTINE EXCEPT
C IN THOSE TIME PERIODS IN THE ADJUSTED RUN WHEN THE VALUES ARE OBSERVED
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
      INCLUDE 'common/srcg26'
      INCLUDE 'common/evak26'
C
      DIMENSION SCEL(1), SCQI(1), SCQO(NSCQI,1), ELVFAL(1), PEAKQ(1)
     $, PEAKEL(1), OPTEV(1),  EVACEL(1), GATE(1),
     $EVACQ(NELEV,1), STOR(1), ELEV(1),ELVSUR(1),QISUR(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/nduc26.f,v $
     . $',                                                             '
     .$Id: nduc26.f,v 1.3 2000/03/13 21:05:54 page Exp $
     . $' /
C    ===================================================================
C
      IF(NSTEPS .LE. 0) NSTEPS=1
C
C COMPUTE TENDENCY (TENDCY) FROM ELEVATION AT START OF TIME INTERVAL
C AND ELEVATION ONE TIME INTERVAL PREVIOUS IF TENDENCY IS MISSING.
C
      IF(IFMSNG(TENDCY).EQ.1) TENDCY=ELEV1-ELBAC1
C
C CHECK IF PROGRAM SHOULD BE IN NDUC26.  IF SURCHG IS FALSE, THE PROGRAM
C MIGHT NOT HAVE GONE THROUGH SURC26  THEREFORE, CALL SURC26 TO CHECK
C ON SURCHG.  IF SURCHG IS STILL FALSE, GO TO RETURN.
C
      IF(SURCHG) GO TO 3
C
C FALELB IN SURC26 IS DEFINED IN EVAB26 AS THE LOWEST ELEVATION FOR
C SURCHARGE EVACUATION.  RULE CURVE ELEVATION FOR FALELB
C IS DEFINED AS -999.0 AND RULE CURVE PLUS AN ADDITION IS DEFINED AS
C -999.0 PLUS THE ADDITION.
C
    1 CALL SURC26(ELVSUR,QISUR,STOR,ELEV)
      IF(SURCHG)  GO TO 3
      IF(IBUG.NE.2) GO TO 600
      WRITE(IODBUG,2)
    2 FORMAT(1H0,48H PROGRAM SHOULD NOT BE IN NDUC26.  GO TO RETURN.)
      GO TO 600
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
    3 IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** NDUC26 ENTERED)
      WRITE(IODBUG,40) IFCST,FCST,SURCHG,FRACT,QIBAC1,QI1,QI2,
     $QIM,QO1,QO2,QOM,S1,S2,ELBAC1,ELEV1,ELEV2, RULEL1,
     $RULEL2,PCTERN,TENDCY,ELVMAX,OUTMAX,HUPPER,HCHECK,HLOWER,
     $NSTEPS,NCON,IOPTND,ISTEP,NTERP,IBUG,NS2
   40 FORMAT(1H0,' IFCST,FCST,SURCHG,FRACT,QIBAC1,QI1,QI2,QIM,QO1,QO2,',
     $'QOM,',/5X,'S1,S2,ELBAC1,ELEV1,ELEV2,RULEL1,RULEL2,PCTERN,TENDCY',
     $/1X,I6,2L5,8F12.3/1X,9F12.3,
     $/1H0,'ELVMAX,OUTMAX,HUPPER,HCHECK,HLOWER,NSTEPS',
     $',NCON,IOPTND,ISTEP,NTERP,IBUG,NS2',
     $/1X,5F12.3,7I6)
   50 CHANGE=TENDCY
C  SURCHARGE CURVE IS IN TERMS AMOUNT OF INFLOW
      IROR=0
C  SURCHARGE CURVE IS IN TERMS OF RATE OF RISE OF POOL
      IF (SCQI(NSCQI).LE.100.0) IROR=1
      IF(IFCST.EQ.0) GO TO 60
      IF(FCST) GO TO 60
C
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      IF(IGO.EQ.1) GO TO 360
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; ADJUSTED BETWEEN OBSERVED
C VALUES USING SIMULATED AND OBSERVED VALUES; OR INTERPOLATED BETWEEN
C OBSERVED VALUES.  WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED IN STATEMENT 340
C FROM THE OBSERVED MEAN INFLOW, THE ADJUSTED MEAN INFLOW, AND THE
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
      IF(QOM.EQ.-999.0) GO TO 60
      GO TO 340
C
   60 CONTINUE
C
C SET BEGINNING VALUES FOR THE FIRST TIME STEP IN THE TIME PERIOD.
C
      ELRES1=ELEV1
      QQI1=QI1
      SS1=S1
      QQO1=QO1
C
C COMPUTE TIME PERIOD CHANGES IN RESERVOIR INFLOW FOR PREVIOUS TIME
C PEIIOD (DIFQIB) AND FOR THIS TIME PERIOD (DIFQI).  COMPUTE INCREMENTAL
C CHANGES FOR EACH TIME STEP IN PREVIOUS TIME PERIOD (DELQIB) AND IN
C THIS TIME PERIOD (DELQI).
C
      IF(NSTEPS .LE. 0) NSTEPS=1
      DIFQIB=QI1-QIBAC1
      DIFQI=QI2-QI1
      STEPS=NSTEPS
      DELQIB=DIFQIB/STEPS
      DELQI=DIFQI/STEPS
C
C IN THE FOLLOWING STATEMENTS,
C         FRACT IS THE FRACTION OF THE TIME PERIOD OVER WHICH SURCHARGE
C INFLOW IS AVERAGED, NSTEPS IS THE NO. OF COMPUTATIONAL TIME STEPS IN
C THE TIME PERIOD, FRPD IS THE FRACTION OF THE TIME PERIOD FOR EACH TIME
C STEP, AND SFRPD WILL BE USED IN DO LOOP  10 TO SUM FRPD AND GET THE
C FRACTION OF THE TIME PERIOD THROUGH I LOOPS IN DO LOOP 10.  MULTIPLY
C FRACT BY 100 AND CONVERT TO INTEGER FOR LATER TESTING.. COMPUTE QIBGN,
C THE BEGINNING INSTANTANEOUS INFLOW FOR COMPUTING THE MEAN INFLOW FOR
C SURCHARGE AND EVACUATION RELATIONS FOR THE FIRST TIME STEP IN THE
C TIME PERIOD.
C
      IFRACT=FRACT*100.+0.05
      FRPD=1./STEPS
      QIBGN=QIBAC1+DIFQIB*(1.-FRACT+FRPD)
      SFRPD=0.
      QIBGN0=QIBAC1+DIFQIB*(1.-FRACT)
      QIMS0=(QIBGN0+QI1)*0.5
      DO 310 I=1,NSTEPS
      I10=I
      SFRPD=SFRPD+FRPD
      RULELX=RULEL1+(RULEL2-RULEL1)*SFRPD
C
C MULTIPLY SFRPD BY 100. AND CONVERT TO INTEGER FOR TESTING AGAINST
C IFRACT.  QIEND IS THE INSTANTANEOUS INFLOW AT THE END OF THE SURCHARGE
C INFLOW TIME PERIOD.
C
      QIEND=QI1+DIFQI*SFRPD
      ISFRPD=SFRPD*100.+0.05
      IF(ISFRPD.GE.IFRACT) GO TO 70
C
C THE TIME UNIT FOR COMPUTING SURCHARGE INFLOW BEGINS IN THE PREVIOUS
C TIME PERIOD FOR THE FOLLOWING STATEMENTS.  COMPUTE SURCHARGE MEAN
C INFLOW (QIMSUR) AND NEW VALUE OF QIBGN FOR NEXT TIME STEP.
C
      QIMSUR=(QIBGN+QI1)*0.5*(FRACT-SFRPD)/FRACT+(QI1+QIEND)*0.5*SFRPD/F
     $RACT
      QIBGN=QIBGN+DELQIB
      GO TO 80
C
C FOLLOWING TWO STATEMENTS ARE FOR CASE WHERE SURCHARGE INFLOW TIME UNIT
C STARTS AND ENDS IN THIS TIME PERIOD.
C
   70 QIMSUR=(QIBGN+QIEND)*0.5
      QIBGN=QIBGN+DELQI
C
C COMPUTE INFLOW AT END OF TIME STEP  AND MEAN INFLOW IN UNITS OF MEAN
C DISCHARGE FOR THE TIME PERIOD.
C
   80 QQI2=QQI1+DELQI
      QQIM=(QQI1+QQI2)*0.5*FRPD
      CHANGQ=QIMSUR-QIMS0
C
C FOLLOWING STATEMENTS ARE FOR RISING POOL AND INDUCED SURCHARGE RELA-
C TION WILL BE USED TO COMPUTE OUTFLOW.  GATE SETTINGS ARE CHANGED AT
C THE BEGINNING OF EACH TIME STEP AND THE OUTFLOW CHANGES IN A SERIES
C OF STEPS RATHER THAN A SMOOTH CURVE.  HOWEVER, ASSUMING A STRAIGHT
C LINE BETWEEN OUTFLOW POINTS WILL NOT CAUSE ANY SIGNIFICANT CHANGE IN
C RESULTS AND THIS ASSUMPTION WILL BE USED.  THE ENDING STEP OUTFLOW
C (QQO2) WILL BE ITERATED IN DO LOOP 90.  ASSUME THE BEGINNING OUTFLOW
C (QQO1) IS THE STEP AVERAGE FOR THE FIRST APPROXIMATION.  THE ENDING
C ELEVATION (ELRES2) WILL HAVE TO BE CHECKED AGAINST SCEL1 IN EACH LOO
C AND INDUCED SURCHARGE INFLOW (QIMSUR) AGAINST SCQIMN TO SEE IF THE
C VALUES ARE LESS THAN THE BEGINNING VALUES IN THE INDUCED SURCHARGE
C RELATION.  IF THIS SITUATION OCCURS, THE OUTFLOW (QQO2) WILL BE THE
C LESSER OF INFLOW (QQI2) OR MINIMUM INDUCED SURCHARGE OUTFLOW (SCQOMN).
C SCEL1 SHOULD BE THE SAME AS SCQOMN BUT A LESER VALUE MAY HAVE BEEN
C PUT IN.
C
C
C  IF BOTH RES. ELEV. > MIN. SURCHARGE ELEV
C          AND INFLOW > MIN SURCHARGE OUTFLOW
C  GO TO 81 TO FOLLOW SURCHARGE CURVE => IF(.NOT. .FALSE.)
C  IF (.TRUE. .OR. .FALSE.) = .TRUE.
C  IF (.FALSE. .OR. .TRUE.) = .TRUE.
C      IF(NOT.(ELRES1.LT.HUPPEX.OR.QIMSUR.LT.SCQIMN)) GO TO 81
      HUPPEX=HUPPER
      IF(HUPPEX.LT.-900.0) HUPPEX=RULELX+HUPPEX+999.0
      HCHECX=HCHECK
      IF(HCHECX.LT.-900.0) HCHECX=RULELX+HCHECX+999.0
      HLOWEX=HLOWER
      IF(HLOWEX.LT.-900.0) HLOWEX=RULELX+HLOWEX+999.0
C
      IF(IFMSNG(OUTMAX).EQ.1) GO TO 82
      IF(CHANGE.LT.0.0.OR.CHANGQ.LT.0.1)GO TO 140
 82   IF(ELRES1.GT.HUPPEX.AND.QIMSUR.GT.SCQIMN) GO TO 81
C
      CALL NTER26(HLOWEX,SSLW,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
C
C  HOLDING ELEVATION AT HLOWER BY PASSING INFLOW
      QQO2=(SS1+QQIM-SSLW)*2./FRPD-QQO1
      IF(QQO2.GT.QGENMX) GO TO 884
      IF(ELRES1.GT.HLOWEX) GO TO 1882
      IF(QQO2.LT.QQI2) GO TO 1882
      QQO2=QQI2
      SS2=SS1
      ELRES2=ELRES1
      GO TO 290
 1882 ELRES2=HLOWEX
      SS2=SSLW
      GO TO 290
 884  CONTINUE
      QQO2=QGENMX
      IF(QQO2.GE.QQO1 .AND. QQO2.LE.QQI2) GO TO 1884
      IF(QQO2.GE.QQO1) GO TO 1883
        QQO2=QQO1
        SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
        CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      IF(ELRES2.GE.HCHECX) GO TO 885
      GO TO 290
 1883 IF(QQI2.GT.QGENMX) GO TO 290
      IF(QQO2.LT.QQI2) GO TO 290
      QQO2=QQI2
      SS2=SS1
      ELRES2=ELRES1
      GO TO 290
 1884 SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      IF(ELRES2.LT.HCHECX) GO TO 290
 885  CONTINUE
C
C  IF POOL ELEVATION IS ABOVE HCHECK AND QIMSUR IS BETWEEN QCHECK AND
C  SCQIMN, THEN MAKE A DISCHARGE THAT IS QDIFF LESS THAN QIMSUR
C
      IF(IFMSNG(QCHECK).EQ.1) GO TO 890
      IF(QIMSUR.GT.SCQIMN) GO TO 81
      IF(QIMSUR.LT.QCHECK) GO TO 890
      QQO2=QIMSUR-QDIFF
      SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      GO TO 290
 890  CONTINUE
      QQO2=QGENMX
      IF(QQO2.GE.QQO1 .AND. QQO2.LE.QQI2) GO TO 1891
      IF(QQO2.GE.QQO1) GO TO 1890
        QQO2=QQO1
        SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
        CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      IF(ELRES2.GE.HCHECX) GO TO 895
      GO TO 290
 1890 IF(QQI2.GT.QGENMX) GO TO 290
      IF(QQO2.LT.QQI2) GO TO 290
      QQO2=QQI2
      SS2=SS1
      ELRES2=ELRES1
      GO TO 290
 1891 SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      IF(ELRES2.LT.HUPPEX) GO TO 290
 895  CONTINUE
      CALL NTER26(HUPPEX,SSUP,ELEV,STOR,NSE,IFLAG,NTERP,IBUG)
      QQO2=(SS1+QQIM-SSUP)*2./FRPD-QQO1
      IF(QQO2.GE.QQO1 .AND. QQO2.LE.QQI2) GO TO 1895
      IF(QQO2.GE.QQO1) GO TO 1896
        QQO2=QQO1
        SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
        CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      IF(ELRES2.GT.HUPPEX) GO TO 81
      GO TO 290
 1895 IF(QQI2.GT.SCQOMN) GO TO 290
      IF(QQO2.LT.QQI2) GO TO 290
      QQO2=QQI2
      SS2=SS1
      ELRES2=ELRES1
      GO TO 290
 1896 IF(QQO2.GT.SCQOMN) GO TO 81
      ELRES2=HUPPEX
      SS2=SSUP
      GO TO 290
C
C  OTHERWISE, PASS INFLOW
C      QQO2=SCQOMN
C      IF(QQO2.GT.QQI2) QQO2=QQI2
C      SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
C      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C      GO TO 130
   81 CONTINUE
      SS2=SS1+QQIM-QQO1*FRPD
      PREVQ2=QQO1
      DO 90 J=1,5
      J90=J
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      SURZ=QIMSUR
      IF (IROR.EQ.1) THEN
        SURZ=(ELEV1-ELBAC1)/MINODT
        IF (SURZ.LE.SCQI(1)) SURZ=SCQI(1)
      ENDIF
      CALL F3WAYX(QQO2,ELRES2,SURZ,SCQO,SCEL,SCQI,NSCEL,NSCQI,
     $IBUG)
      IF(ABS(QQO2-PREVQ2).LE.(PCTERN*QQO2)) GO TO 100
      PREVQ2=QQO2
   90 SS2=SS1+QQIM-(QQO1+PREVQ2)*0.5*FRPD
      GO TO 101
  100 SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
  101 CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C
C WRITE DEBUG IF REQUIRED.
C
      IF(IBUG.NE.2) GO TO 115
      WRITE(IODBUG,110) QQO1,PREVQ2,QQO2,SS1,SS2,ELRES1,ELRES2
  110 FORMAT(1H0,'QQO1,PREVQ2,QQO2,SS1,SS2,ELRES1,ELRES2',
     & /1X,5F13.0,2F12.3)
      WRITE(IODBUG,111) J90,I,I10,ISFRPD,IFRACT,FRACT,FRPD,SFRPD,QIBGN,
     $QIEND,QIMSUR,QI1,QI2,QQI1,QQI2,S1,SS1,SS2
  111 FORMAT(1H0, 63H J90,I,I10,ISFRPD,IFRACT,FRACT,FRPD,SFRPD,QIBGN,
     $QIEND,QIMSUR/1X,5I6,6F12.3/1H0, 29H QI1,QI2,QQI1,QQI2,S1,SS1,SS2/
     $1X,7F12.3)
C
 115  CONTINUE
C      IF(ELRES2.LT.ELRES1) GO TO 140
C
C THE INDUCED SURCHARGE CURVES CAN BE FOLLOWED STRICTLY UNTIL THE POOL
C CRESTS (IOPTND=0) OR THE GATE SETTING CAN NOT BE ALLOWED TO DECREASE
C (IOPTND=1) AS LONG AS THE POOL IS RISING.  IF THE GATE SETTING CANNOT
C BE ALLOWED TO DECREASE, THE GATE OPENING IS COMPUTED FROM THE ELEVA-
C TION AND OUTFLOW AT THE FIRST OF THE TIME INTERVAL.  THE OUTFLOW AT
C THE END OF THE TIME INTERVAL IS COMPUTED FROM THE GATE OPENING AND THE
C ELEVATION AT THE END OF THE TIME INTERVAL.  THE HIGHER OF THE OUTFLOWS
C COMPUTED FROM THE GATE OPENING AND THE SURCHARGE CURVES IS USED FOR
C THE OUTFLOW  AT THE END OF THE TIME INTERVAL.
C
 120  IF(IOPTND.EQ.0) GO TO 130
      IF(NGAT.LE.0 .OR. NELEV.LE.0) GO TO 130
C
C COMPUTE GATE OPENING FROM POOL ELEVATION AND OUTFLOW AT BEGINNING OF
C TIME INTERVAL.
C
      CALL F3WAYZ(QQO1,ELRES1,OPEN,EVACQ,EVACEL,GATE,NELEV,NGAT,IBUG)
C
C COMPUTE OUTFLOW (QQO22) AT END OF TIME INTERVAL FROM ELEVATION AND
C GATE OPENING.
C
      CALL F3WAYX(QQO22,ELRES2,OPEN,EVACQ,EVACEL,GATE,NELEV,NGAT,IBUG)
      IF(QQO22.LE.QQO2) GO TO 130
C
C COMPUTE OUTFLOW (QQ022) AT THE END OT THE TIME STEP KEEPING THE GATE
C OPENING CONSTANT.  USE ITERATION TECHIQUE.
C
      SS2=SS1+QQIM-(QQO1+QQO22)*0.5*FRPD
      PREVQ2=QQO22
      DO 121 J=1,5
      JJ=J
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      CALL F3WAYX(QQO22,ELRES2,OPEN,EVACQ,EVACEL,GATE,NELEV,NGAT,IBUG)
      IF(ABS(QQO22-PREVQ2).LE.(PCTERN*QQO22)) GO TO 122
      PREVQ2=QQO22
  121 SS2=SS1+QQIM-(QQO1+QQO22)*0.5*FRPD
  122 QQO2=QQO22
      SS2=SS1+QQIM-(QQO1+QQO22)*0.5*FRPD
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      IF(IBUG.NE.2) GO TO 130
      WRITE(IODBUG,123) QQO2,PREVQ2,JJ
  123 FORMAT(1H0,108H GATE SETTING IS NOT ALLOWED TO DECREASE AS POOL IS
     $STILL RISING.  QQO2 HAS BEEN SET TO THE ITERATED VALUE OF,F12.3/1H
     $0,61H COMPUTED FROM THE GATE SETTING.  PREVIOUS ITERATED VALUE WAS
     $,F12.3,32H.  NUMBER OF ITERATIONS MADE WAS,I6)
  130 IF(QQO2.LE.QQI2 .AND. QQO2.GE.QQO1) GO TO 290
C
C THE FOLLOWING STATEMENT PREVENTS A SUDDEN LARGE INCREASE IN OUTFLOW
C OVER INFLOW (BUT DOES NOT ALLOW QQO2 TO BE LESS THAN QQO1) WHEN THE
C OBSERVED POOL ELEVATION IS ABOVE THE SURCHARGE CURVES.
C
      IF(QQO2.GE.QQO1) GO TO 135
      QQO2=QQO1
      SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      GO TO 290
 135  IF(QQO2.LT.QQI2) GO TO 290
      QQO2=QQI2
      SS2=SS1
      ELRES2=ELRES1
      GO TO 290
C
C FOLLOWING STATEMENTS START COMPUTATIONS FOR EVACUATION FROM INDUCED
C SURCHARGE STORAGE.  TEN EVACUATION OPTIONS ARE PROVIDED.  THE OPTION
C USED WILL BE DEPENDENT ON ONE OR A COMBINATIOM OF VARIABLES AS FOLLOWS
C (1) POOL ELEVATION, (2) PEAK OUTFLOW, (3) PEAK POOL ELEVATION, (4)
C POOL ELEVATION AND PEAK OUTFLOW, AND (5) PEAK OUTFLOW AND PEAK POOL
C ELEVATION.  THE VARIABLE NCON DEFINES THE PROPER CONDITION OF
C VARIABLES OR COMBINATION OF VARIABLES.  DO LOOPS 30 THROUGH 70 DETER-
C MINE THE EVACUATION OPTION FROM A DECISION TABLE BASED ON EXCEEDANCE
C CRITERIA OF THE SELECTED CONDITIONS.
C
  140 CONTINUE
      OPT = OPTEV(1)
      GO TO(150,170,190,210,230),NCON
  150 DO 160 J=1,NUMVAL
C
C ELVFAL(J) CAN BE A SPECIFIED ELEVATION, RULE CURVE ELEVATION, OR RULE
C CURVE ELEVATION PLUS OR MINUS A SPECIFIED DEVIATION FROM THE RULE
C CURVE ELEVATION.  RULE CURVE ELEVATION IS SPECIFIED AS
C -999.0.  DEVIATION FROM THE RYLE CURVE IS SPECIFIED IN ELVFAL(J) AS
C (-999.0+DEVIATION).  FOR EXAMPLE, A VALUE OF -998.0 IN ELVFAL(J)
C WOULD BE RULE CURVE ELEVATION + 1.0.  IN THE EVENT OF AN ACTUAL
C NEGATIVE ELEVATION, ELVFAL(J) WILL BE TESTED AGAINST -900.0 TO SEE
C IF THE RULE CURVE ELEVATION IS INVOLVED.
C
      ELV=ELVFAL(J)
      IF(ELV.LT.-900.0) ELV=ELV+999.0+RULEL1
  160 IF(ELRES1.GT.ELV) OPT=OPTEV(J)
      GO TO 250
  170 DO 180 J=1,NUMVAL
  180 IF(OUTMAX.GT.PEAKQ(J)) OPT=OPTEV(J)
      GO TO 250
  190 DO 200 J=1,NUMVAL
  200 IF(ELVMAX.GT.PEAKEL(J)) OPT=OPTEV(J)
      GO TO 250
  210 DO 220 J=1,NUMVAL
      ELV=ELVFAL(J)
      IF(ELV.LT.-900.0)   ELV=ELV+999.0+RULEL1
  220 IF(ELRES1.GT.ELV.AND.OUTMAX.GT.PEAKQ(J)) OPT=OPTEV(J)
      GO TO 250
  230 DO 240 J=1,NUMVAL
  240 IF(OUTMAX.GT.PEAKQ(J).AND.ELVMAX.GT.PEAKEL(J)) OPT=OPTEV(J)
  250 IOPTEV=OPT+0.05
      GO TO(260,260,260,260,260,260,270,280,260,260,260),IOPTEV
  260 CALL EVAA26(EVACEL,GATE,EVACQ,STOR,ELEV)
C
C QELVMX IS TEMPORARILY PUT IN THE ARGUMENT LIST FOR EVAA26 AND NDUC26
C BUT SHOULD BE LATER PUT IN COMMON BLOCK SRCG26.
C
      IF(IOUT.EQ.0) GO TO 300
      GO TO 320
  270 CALL EVAB26(STOR,ELEV)
      IF(IOUT.EQ.0) GO TO 300
      GO TO 320
  280 CONTINUE
C
      IX=HPREVQ
      IF(IX.EQ.-9998) GO TO 285
      HPREVX=HPREVQ
      IF(HPREVX.LT.-900.0) HPREVX=RULELX+HPREVX+999.0
      IF(ELRES1.LE.HPREVX) GO TO 285
      IF(QIMSUR.LE.QQO2) GO TO 285
C
C  IF AT ANY ELEVATION ABOVE HPREVQ AND QIMSUR EXCEEDS THE OUTFLOW VALUE
C  THEN DISCHARGE THE PREVIOUS OUTFLOW AND CONTINUE UNTIL INFLOW IS
C  LESS THAN OUTFLOW.
C
      QQO2=QQO1
      SS2=SS1+QQIM-(QQO1+QQO2)*0.5*FRPD
      CALL NTER26(SS2,ELRES2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
 285  CONTINUE
      TWOQMS=2.*QIMSUR
      IF(OUTMAX.LE.TWOQMS) CALL EVAC26(STOR,ELEV)
      IF(OUTMAX.GT.TWOQMS) CALL EV3C26(STOR,ELEV)
C
C WHEN IOUT IS 0, THE PROGRAM CONTINUES COMPUTATIONS IN DO LOOP 10.
C WHEN IOUT IS 1, EVACUATION IS NO LONGER NEEDED AND THE PROGRAM GOES
C OUT OF DO LOOP 10 TO STATEMENT 25.  IN THIS CASE THE POOL IS HELD
C CONSTANT THROUGH THE REMAINDER OF THE TIME PERIOD AND QO2 IS SET EQUAL
C TO QI2.  OTHER VARIABLES WILL BE THE SAME AS COMPUTED AT THE END OF DO
C LOOP 10.
C
      IF(IOUT.EQ.0) GO TO 300
      GO TO 320
C
C SET NEW VALUES OF MAXIMUM ELEVATION AND CORRESPONDING OUTFLOW FOR
C RISING POOL.  SINCE QELVMX AND ELVMAX ARE USED IN COMPUTING THE GATE
C OPENINGS IN EVAA26, QELVMX WILL BE THE OUTFLOW AT CREST POOL ELEVATION
C WHICH IS NOT NECESSARILY THE MAXIMUM OUTFLOW (OUTMAX).
C
  290 IF(QQO2.GT.OUTMAX) OUTMAX=QQO2
      IF(ELRES2.LT.ELVMAX) GO TO 300
      ELVMAX=ELRES2
      QELVMX=QQO2
C
C COMPUTE NEW VALUES OF CHANGE AND RESET VALUES FOR NEXT LOOP.
C
  300 CHANGE=ELRES2-ELRES1
      ELRES1=ELRES2
      QQI1=QQI2
      QIMS0=QIMSUR
      SS1=SS2
      QQO1=QQO2
  310 CONTINUE
      GO TO 330
  320 ISTEP=-999
      ELVMAX=-999.0
      OUTMAX=-999.0
      SURCHG=.FALSE.
  330 S2=SS2
      ELEV2=ELRES2
      QO2=QQO2
C
C IF THE PROGRAM BRANCHES OUT OF DO LOOP 310 DUE TO EVACUATION STOPPING,
C THE POOL ELEVATION IS HELD CONSTANT UNTIL THE END OF THE TIME PERIOD
C BY PASSING INFLOW.
C
      IF(I10.LT.NSTEPS) QO2=QI2
      QOM=S1+QIM-S2
      IF(QOM.LT.0.) QOM=0.
  340 S2=S1+QIM-QOM
  360 IF(IFCST.EQ.0) GO TO 370
      IF(FCST) GO TO 370
C
C SET INSTANTANEOUS OUTFLOW AT THE END OF THE TIME INTERVAL TO MEAN
C OUTFLOW FOR TIME INTERVAL IF INSTANTANEOUS OUTFLOW (QO2) HAS NOT BEEN
C COMPUTED OR WAS NOT OBSERVED.
C
      IF(QO2.EQ.-999.0) QO2=QOM
  370 TENDCY=CHANGE
C
C COMPUTE NEW ELEVATION AT END OF TIME INTERVAL FROM POOL STORAGE AS
C STORAGE MIGHT HAVE BEEN CHANGED OR ELEVATION MIGHT NOT HAVE BEEN
C COMPUTED PREVIOUSLY OR OBSERVED.  USE ARITHMETIC (NTERP=0) OR
C LOGARITHMIC (NTERP=1) INTERPOLATION.
C
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
C
C WRITE TRACE AND DEBUG IF REQUIRED.
C
      IF(IBUG-1)600,580,380
  380 WRITE(IODBUG,390)
  390 FORMAT(1H0,51H FOLLOWING ARE VARIABLE VALUES AT THE END OF NDUC26)
      WRITE(IODBUG,40) IFCST,FCST,SURCHG,FRACT,QIBAC1,QI1,QI2,
     $QIM,QO1,QO2,QOM,S1,S2,ELBAC1,ELEV1,ELEV2, RULEL1,
     $RULEL2,PCTERN,TENDCY,ELVMAX,OUTMAX,HUPPEX,HCHECX,HLOWEX,
     $NSTEPS,NCON,IOPTND,ISTEP,NTERP,IBUG,NS2
C      IF(NEQSUR.LE.0) GO TO 420
C      WRITE(IODBUG,400)(ELVSUR(I),I=1,NEQSUR)
C  400 FORMAT(1H0,64H ELEVATION VALUES FOR REQUIRED INFLOW FOR SURCHARGE
C     $RELATION ARE/(1X,10F12.3))
C      WRITE(IODBUG,410) (QISUR(I),I=1,NEQSUR)
C  410 FORMAT(1H0,27H REQUIRED INFLOW VALUES ARE/(1X,10F12.3))
  420 IF(NCON.NE.1.OR.NCON.NE.4) GO TO 440
      WRITE(IODBUG,430) (ELVFAL(I),I=1,NUMVAL)
  430 FORMAT(1H0,72H ELEVATION VALUES USED IN DECISION TABLE TO DEFINE E
     $VACUATION OPTION ARE/(1X,10F12.3))
  440 IF(NCON.EQ.1.OR.NCON.EQ.3) GO TO 460
      WRITE(IODBUG,450) (PEAKQ(I),I=1,NUMVAL)
  450 FORMAT(1H0,75H PEAK OUTFLOW VALUES USED IN DECISION TABLE TO DEFIN
     $E EVACUATION OPTION ARE/(1X,10F12.3))
  460 IF(NCON.NE.3.OR.NCON.NE.5) GO TO 480
      WRITE(IODBUG,470)(PEAKEL(I),I=1,NUMVAL)
  470 FORMAT(1H0,77H PEAK ELEVATION VALUES USED IN DECISION TABLE TO DEF
     $INE EVACUATION OPTION ARE/(1X,10F12.3))
  480 WRITE(IODBUG,490)(OPTEV(I),I=1,NUMVAL)
  490 FORMAT(1H0,37H OPTION VALUES FOR DECISION TABLE ARE/(1X,10F12.3))
      IF(ISURG.NE.1)  GO TO 580
      IF(NGAT.LE.1 .OR. NELEV.LE.1) GO TO 545
      DO 530 J=1,NGAT
  530 WRITE(IODBUG,540)GATE(J),(EVACEL(K),EVACQ(J,K),K=1,NELEV)
  540 FORMAT(1H0,71H ALTERNATING VALUES OF POOL ELEVATION AND OUTFLOW FO
     $R A GATE OPENING OF,F8.3, 4H ARE/(1X,5(F8.3,F10.0,2X)))
 545  DO 550 J=1,NSCQI
  550 WRITE(IODBUG,560)SCQI(J),(SCEL(K),SCQO(K,J),K=1,NSCEL)
  560 FORMAT(1H0,94H ALTERNATING VALUES OF POOL ELEVATIONS AND OUTFLOW F
     $OR THE SURCHARGE RELATION FOR AN INFLOW OF,F10.0,  4H ARE,
     & /(1X,5(F8.3,F10.0,2X)))
      WRITE(IODBUG,570)(ELEV(I),STOR(I),I=1,NSE)
  570 FORMAT(1H0,' ALTERNATING VALUES OF ELEVATION AND STORAGE FOR THE',
     $ 'ELEVATION VS STORAGE RELATION ARE'/(1X,5(F8.3,F10.0,2X)))
  580 WRITE(IODBUG,590)
  590 FORMAT(1H0,10X,17H** LEAVING NDUC26)
  600 RETURN
      END
