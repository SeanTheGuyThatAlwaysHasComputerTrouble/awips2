C MEMBER FILL26
C  (from old member FCFILL26)
C **********************************************************************
      SUBROUTINE FILL26(STOR,ELEV,PEAKO,PKPOS,O,SOH,OH,HS,TOTALQ,QGEN,
     $WORK)
C **********************************************************************
C SUBOUTINE FILL26 ALLOWS THE RESERVOIR TO FILL UNTIL A SPECIFIED POOL
C ELEVATION IS REACHED.  IF THE DAM IS GATED, THE SPECIFIED POOL ELEVA-
C TION WOULD BE THE TOP OF GATES OR A LEVEL SLIGHTLY BELOW THE TOP OF
C GATES.  FOR A DAM WITH AN UNCONTROLLED SPILLWAY, THE SPECIFIED POOL
C ELEVATION WOULD BE THE SPILLWAY CREST ELEVATION.  AFTER THE SPECIFIED
C POOL ELEVATION IS REACHED, INFLOW IS PASSED UNTIL INFLOW EXCEEDS THE
C MAXIMUM POSSIBLE DISCHARGE THROUGH THE DAM AT THE SPECIFIED ELEVATION.
C THE MAXIMUM POSSIBLE DISCHARGE IS THE NON-SPILLWAY DISCHARGE AT SPILL-
C WAY CREST ELEVATION FOR AN UNCONTROLLED SPILLWAY DAM AND SPILLWAY
C DISCHARGE AT THE SPECIFIED ELEVATION PLUS NON-SPILLWAY DISCHARGE FOR
C THE GATED DAM.  THE ROUTING SUBROUTINE OVER26 IS THEN CALLED AS ROUT-
C ING IS NECESSARY.  SPECIFIED OUTFLOW IS SUBTRACED FROM THE INFLOW WHEN
C PERMITTING THE RESERVOIR TO FILL.  SPECIFIED OUTFLOW MIGHT BE MAXIMUM
C GENERATION DISCHARGE FOR A POWER DAM, MAXIMUM NON-SPILLWAY DISCHARGE
C FOR A NON-POWER DAM OR A SPECIFIED LOWER VALUE.  A SIMULATED RUN IS
C MADE FIRST FOR ALL TIME PERIODS STARTING WITH THE BEGINNING OBSERVED
C OR ADJUSTED STORAGE.  AFTER THE SIMULATED RUN, MISSING STORAGES ARE
C DETERMINED WITH THE ADJUST-Q SUBROUTINE USING SIMULATED AND OBSERVED
C STORAGES.  A SECOND ADJUSTED RUN IS THEN MADE USING THE ADJUSTED AND
C OBSERVED STORAGES.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C **********************************************************************
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX--CONSULTING HYDROLOGIST
C     NOVEMBER, 1981
C **********************************************************************
C SUBROUTINE FILL26 IS IN
C **********************************************************************
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IFCST -- SIMULATED RUN (IFCST=0) AND ADJUSTED RUN (IFCST=1).
C     FCST -- LOGICAL VARIABLE.  IF TRUE, TIME PERIOD IS IN FORECAST
C       PERIOD.
C     SPASSI -- POOL STORAGE WHERE INFLOW IS PASSED.
C     QPASMX -- MAXIMUM OUTFLOW AT POOL STORAGE WHERE INFLOW IS PASSED.
C     QLIMFL -- LIMITING DISCHARGE WHEN POOL IS FILLING.  COULD BE
C       MAXIMUM TURBINE FLOW, MINIMUM REQUIRED FLOW OR SOME OTHER DESIR-
C       ED LIMITING FLOW BUT WOULD NOT EXCEED FLOW DISCHARGE.
C     QI1 -- INFLOW AT BEGINNING OF TIME PERIOD.
C     QI2 -- INFLOW AT END OF TIME PERIOD.
C     QIM -- MEAN INFLOW FOR TIME PERIOD.
C     QO1 -- OUTFLOW AT BEGINNING OF TIME PERIOD.
C     QO2 -- OUTFLOW AT END OF TIME PERIOD.
C     QOM -- MEAN OUTFLOW FOR TIME PERIOD.
C     S1 -- STORAGE AT BEGINNING OF TIME PERIOD IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME PERIOD.
C     S2 -- STORAGE AT END OF TIME PERIOD IN UNITS OF MEAN DISCHARGE
C       FOR THE TIME PERIOD.
C     ELEV1 -- POOL ELEVATION AT BEGINNING OF TIME PERIOD.
C     ELEV2 -- POOL ELEVATION AT END OF TIME PERIOD.
C     STOR -- STORAGE VALUES FOR STORAGE VS ELEVATION CURVE.
C     ELEV -- ELEVATION VALUES FOR STORAGE VS ELEVATION CURVE.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NTERP -- ARITHMETIC (NTERP=0) OR LOGARITHMIC INTERPOLATION (NTERP
C       =0) FOR ELEVATION VS STORAGE RELATION.
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1), TRACE AND
C       DEBUG (IBUG=2).
C VARIABLES NEEDED FOR OVER26 ROUTING SUBROUTINE MUST ALSO BE PASSED TO
C FILL26.
C **********************************************************************
C QO2, QOM, S2, AND ELEV2 WILL BE COMPUTED IN FILL26 EXCEPT WHEN THE
C VALUES ARE OBSERVED IN THE ADJUSTED RUN.  ALL OTHER VARIABLES IN THE
C ARGUMENT LIST WILL BE FURNISHED BY THE SUPERVISORY EXECUTION ROUTINE.
C **********************************************************************
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
      INCLUDE 'common/fila26'
      INCLUDE 'common/root26'
C
      DIMENSION STOR(1),ELEV(1),O(1),SOH(1),OH(1),HS(1),WORK(1),PEAKO(1)
     $,PKPOS(1),TOTALQ(1),QGEN(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/fill26.f,v $
     . $',                                                             '
     .$Id: fill26.f,v 1.2 1996/05/07 11:29:06 page Exp $
     . $' /
C    ===================================================================
C
C
C **********************************************************************
C WRITE TRACE AND DEBUG IF REQUIRED.
C **********************************************************************
      IADJH=0
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** FILL26 ENTERED)
      WRITE(IODBUG,40) IFCST,FCST,SPASSI,QPASMX,QLIMFL,QI1,QI2,QIM,QO1,
     $QO2,QOM,S1,S2,ELEV1,ELEV2,NTERP,IBUG,NS2,IUNC,CREST,STOCR,SFILL,
     $SPILMX,QOK1,QOK2,TESTPK,NUMPKO,NTOTPK,NTERPQ,QGENMX,SLUICE,PCTERG
   40 FORMAT(1H0,114H   IFCST,FCST,SPASSI,QPASMX,QLIMFL,QI1,QI2,QIM,QO1,
     $QO2,QOM,S1,S2,ELEV1,ELEV2,NTERP,IBUG,NS2,IUNC,CREST,STOCR,SFILL/
     $1X,I6,L5,9F12.3/1X,4F12.3,4I6,3F12.3/1H0,70H SPILMX,QOK1,QOK2,
     $TESTPK,NUMPKO,NTOTPK,NTERPQ,QGENMX,SLUICE,PCTERG/1X,4F12.3,3I6,
     $3F12.3)
   50 SPASSI=SFILL
      IF(IUNC.EQ.1) SPASSI=STOCR
      SS=S1
      QOKM=(QOK1+QOK2)*0.5
      IF(IFCST.EQ.0.OR.FCST)GO TO 60
      IF(ICOMB .NE. 4) GO TO 51
      SS=S2
      IADJH=1
      GO TO 60
   51 OBSQO2=QO2
C **********************************************************************
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      IF(IGO.EQ.1) GO TO 110
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; ADJUSTED BETWEEN OBSERVED
C VALUES USING SIMULATED AND OBSERVED VALUES; OR INTERPOLATED BETWEEN
C OBSERVED VALUES.  WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED IN STATEMENT 100
C FROM THE OBSERVED MEAN INFLOW, THE ADJUSTED MEAN INFLOW, AND THE
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
      IF(QOM.EQ.-999.0) GO TO 60
      GO TO 100
   60 IF(SS.GT.SPASSI)GO TO 80
      QOM=QLIMFL
      QO2=QLIMFL
C
C TO KEEP FROM PULLING THE RESERVOIR DOWN WHEN MEAN INFLOW (QIM) IS LESS
C THAN SPECIFIED LIMITING OUTFLOW (QLIMFL) WHILE POOL IS FILLING, THE
C OUTFLOW WILL NOT BE ALLOWED TO EXCEED QIM UNLESS QIM IS NEGATIVE.
C
      IF(QLIMFL.LE.QIM) GO TO 70
C
C MEAN INFLOW (QIM) MAY BE NEGATIVE DUE TO LAKE EVAPORATION.
C
      QOM=AMAX1(QIM,QOKM)
      IF(QOM.LT.0.) QOM=0.
      QO2=AMAX1(QI2,QOK2)
      IF(QO2.LT.0.) QO2=0.
   70 IF(IADJH .EQ. 1) GO TO 120
      S2=S1+QIM-QOM
      IF(S2.LT.0.)S2=0.
      QOM=S1+QIM-S2
      QOM=AMAX1(QOM,QOKM)
      IF(QO2.NE.QI2) QO2=QOM
      IF(S2.LT.SPASSI)GO TO 110
      S2=SPASSI
      QOM=S1+QIM-S2
C      IF(QOM.LT.0.)QOM=0.
C **** REVISION NO1 BY ED FOX 12/14/88 **** BEGIN ****
      IF(QOM .GE. 0.) GO TO 71
      QOM=0.
      S2=S1+QIM
C **********************************************************************
C INSTANTANEOUS OUTFLOW (QO2) AT THE END OF THE TIME PERIOD IS SET EQUAL
C TO INSTANTANEOUS INFLOW (QI2) BUT A CHECK MUST BE MADE TO SEE IF QO2
C OR MEAN OUTFLOW (QOM) EXCEEDS THE MAXIMUM POSSIBLE DISCHARGE.  IF SO,
C THE PROGRAM WILL CALL THE ROUTING SUBROUTINE OVER26.
C **********************************************************************
   71 QO2=AMAX1(QI2,QOK2)
      IF(QO2.LT.0.) QO2=0.
      IF(QI2.GT.QPASMX.OR.QOM.GT.QPASMX) GO TO 130
      GO TO 110
C **********************************************************************
C IF STORAGE AT THE BEGINNING OF THE TIME PERIOD (S1) IS GREATER THAN
C THE SPECIFIED STORAGE (AT SPECIFIED ELEVATION) FOR PASSING
C INFLOW, EITHER INFLOW IS BEING ROUTED THROUGH THE DAM OR THE OPERATOR
C IS ALLOWING THE POOL TO EXCEED THE ELEVATION FOR PASSING INFLOW.
C FIRST, CHECK IF INSTANTANEOUS INFLOWS AT BEGINNING (QI1) OR END (QI2)
C OF THE TIME PERIOD OR INSTANTANEOUS OUTFLOW (QO1) AT THE BEGINNING
C OF THE PERIOD IS GREATER THAN MAXIMUM POSSIBLE DISCHARGE (QPASMX).  IF
C SO, CALL SUBROUTINE OVER26.  IF NOT, THE GREATER OF INFLOW OR LIMITING
C DISCHARGE      (QLIMFL) WILL BE USED AS THE OUTFLOW  AT THE END OF THE
C TIME PERIOD.
C **********************************************************************
   80 IF(QI1.GT.QPASMX.OR.QI2.GT.QPASMX) GO TO 130
      IF(QO1.GT.QPASMX) GO TO 130
C ......................................................................
C THE CHECK OF QO1 AGAINST QPASMX IS NEEDED SINCE THE POOL MAY BE
C FALLING DUE TO FALLING INFLOW BUT ROUTING MAY BE OCCURRING.
C ......................................................................
      IF(QIM.GE.QLIMFL)GO TO 90
      QOM=QLIMFL
      QO2=QLIMFL
      GO TO 100
   90 QOM=QIM
      QO2=QI2
  100 S2=S1+QIM-QOM
  110 IF(IFCST.EQ.0) GO TO 120
      IF(FCST) GO TO 120
      IF(IADJH .EQ. 1) GO TO 140
      IF(QO2.NE.QI2) QO2=QOM
      IF(OBSQO2.NE.-999.0) QO2=OBSQO2
      IF(ELEV2.NE.-999.0) GO TO 140
C **********************************************************************
C COMPUTE ELEV2 WITH ARITHMETIC (NTERP=0) OR LOGARITHMIC INTERPOLATION
C (NTERP=1).
C **********************************************************************
  120 CONTINUE
      IF(IADJH .EQ. 0) GO TO 125
      S2=SS
      GO TO 140
  125 CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      GO TO 140
  130 CALL OVER26(STOR,ELEV,PEAKO,PKPOS,O,SOH,OH,HS,TOTALQ,QGEN,WORK)
  140 IF (IBUG-1)200,180,150
  150 WRITE(IODBUG,160)
  160 FORMAT(1H0,40H VALUES AT END OF FILL26 ARE AS FOLLOWS:)
      WRITE(IODBUG,40) IFCST,FCST,SPASSI,QPASMX,QLIMFL,QI1,QI2,QIM,QO1,
     $QO2,QOM,S1,S2,ELEV1,ELEV2,NTERP,IBUG,NS2,IUNC,CREST,
     $STOCR,SFILL,SPILMX,QOK1,QOK2,TESTPK,NUMPKO,NTOTPK,NTERPQ,
     $QGENMX,SLUICE,PCTERG
      WRITE(IODBUG,170)IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
  170 FORMAT(1H0,9H IFLAG IS,I6,43H.  NO OF POINTS ON ELEV. - STORAGE CU
     $RVE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION
     $ARE/(1X,5(F12.3,F9.3,3X)))
  180 WRITE(IODBUG,190)
  190 FORMAT(1H0,10X,17H** LEAVING FILL26)
  200 RETURN
      END
