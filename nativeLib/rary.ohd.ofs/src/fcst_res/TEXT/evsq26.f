C MEMBER EVSQ26
C  (from old member FCEVSQ26)
C ......................................................................
      SUBROUTINE EVSQ26(STORAG,ELVOUT,OUTFLO,STOR,ELEV)
C ......................................................................
C THIS SUBROUTINE DETERMINES OUTFLOW FOR THE TIME PERIOD WHEN THE OUT-
C FLOW VARIES WITH THE POOL ELEVATION (STORAGE) FOR SPECIFIED RANGES OF
C ELEVATIONS.  OUTFLOW WILL BE CONSTANT BETWEEN SPECIFIED LOWER AND
C UPPER POOL ELEVATIONS AND THEN WILL CHANGE TO OTHER VALUES BETWEEN
C OTHER SETS OF ELEVATIONS.  THIS TYPE OF REGULATION MAY NOT GO INTO
C EFFECT UNTIL A SPECIFIED POOL ELEVATION IS EXCEEDED.  A SIMULATED RUN
C IS MADE FIRST, STARTING WITH THE BEGINNING OBSERVED OR ADJUSTED
C STORAGE BUT USING NO OTHER OBSERVED STORAGE OR OUTFLOW VALUES.  AFTER
C THE SIMULATED RUN, MISSING STORAGES PRIOR TO THE LAST OBSERVED STORAGE
C ARE COMPUTED WITH THE ADJUST-Q SUBROUTINE.  AN ADJUSTED RUN IS THEN
C MADE BY USING THE ADJUSTED STORAGES AND ALL OBSERVED STORAGES AND
C OUTFLOWS.
C FOR THE ADJUSTED RUN PRIOR TO RUN TIME, POOL STORAGES PASSED TO
C THIS SUBROUTINE MAY BE OBSERVED, COMPUTED FROM OBSERVED MEAN OUTFLOWS
C AND ADJUSTED MEAN INFLOWS, ADJUSTED FROM SIMULATED AND OBSERVED VALUES
C OR MISSING.  POOL ELEVATIONS WILL BE OBSERVED OR MISSING.  MEAN OUT-
C FLOWS MAY BE OBSERVED OR COMPUTED FROM ADJUSTED MEAN INFLOWS AND
C OBSERVED OR ADJUSTED STORAGES.  MEAN OUTFLOWS FOR THE TIME INTERVAL,
C POOL STORAGE, POOL ELEVATION, AND INSTANTANEOUS OUTFLOW AT THE END OF
C THE TIME INTERVAL ARE PASSED TO THIS SUBROUTINE IN THE VARIABLES QOM,
C S2, ELEV2, AND QO2, RESPECTIVELY.  MISSING VALUES ARE PASSED AS -999.0
C ......................................................................
C THIS SUBROUTINE WAS ORIGINALLY PROGRAMMED BY
C     WILLIAM E. FOX -- CONSULTING HYDROLOGIST
C     DECEMBER, 1981
C ......................................................................
C SUBROUTINE EVSQ26 IS IN
C ......................................................................
C VARIABLES PASSED TO OR FROM THIS SUBROUTINE ARE DEFINED AS FOLLOWS:
C     IFCST -- SIMULATED RUN (IFCST=0) OR ADJUSTED RUN (IFCST=1).
C     FCST -- LOGICAL VARIABLE.  IF TRUE, TIME PERIOD IS IN THE FORECAST
C       PERIOD.
C     QI1 -- INFLOW AT BEGINNING OF TIME PERIOD.
C     QI2 -- INFLOW AT END OF TIME PERIOD.
C     QIM -- MEAN INFLOW FOR TIME PERIOD.
C     QO1 -- OUTFLOW AT BEGINNING OF TIME PERIOD.
C     QO2 -- OUTFLOW AT END OF TIME PERIOD.
C     QOM -- MEAN OUTFLOW FOR TIME PERIOD.
C     S1 -- STORAGE AT BEGINNING OF TIME PERIOD IN UNITS OF MEAN DIS-
C       CHARGE FOR THE TIME PERIOD.
C     S2 -- STORAGE AT END OF TIME PERIOD IN UNITS OF MEAN DISCHARGE FOR
C       THE TIME PERIOD.
C     ELEV2 -- ELEVATION AT END OF TIME PERIOD.
C     RULEL2 -- RULE CURVE ELEVATION AT END OF TIME PERIOD.  IT IS
C       ASSUMED THAT THIS ELEVATION APPLIES THROUGHOUT THE TIME PERIOD.
C     STORAG -- ARRAY OF STORAGE VALUES ( IN UNITS OF MEAN DISCHARGE FOR
C       TIME PERIOD) FOR STORAGE VS OUTFLOW RELATION.  THESE VALUES ARE
C       COMPUTED IN THE SUPERVISORY EXECUTION ROUTINE.
C     ELVOUT -- ARRAY OF ELEVATION VALUES FOR THE ELEVATION VS OUTFLOW
C       RELATION.  A VALUE OF -999.0 INDICATES RULE CURVE ELEVATION.
C       VALUES MUST BE IN ASCENDING ORDER.
C     OUTFLO -- ARRAY OF OUTFLOW VALUES FOR THE ELEVATION VS OUTFLOW
C       RELATION.  EACH OUTFLOW VALUE REPRESENTS THE SPECIFIED DISCHARGE
C       FROM THE DAM BETWEEN THE CORRESPONDING STORAG VALUE AND THE NEXT
C       HIGHER STORAG VALUE.
C     NSO -- NO. OF PAIRS OF ELVOUT AND OUTFLO VALUES.  ALSO, IT IS THE
C       NUMBER OF STORAG VALUES.
C     STOR -- STORAGES FOR STORAGE VS ELEVATION CURVE IN UNITS OF MEAN
C       DISCHARGE FOR THE TIME PERIOD.
C     ELEV -- ELEVATIONS FOR STORAGE VS ELEVATION CURVE.
C     NSE -- NO. OF PAIRS OF STOR AND ELEV VALUES.
C     NTERP -- INDICATES ARITHMETIC (NTERP=0) OR LOGARITHMIC
C       INTERPOLATION (NTERP=1).
C     NTERPQ -- =0, LINEAR INTERPOLATION IN POOLQ CURVE
C              =-1, CONSTANT FLOW INTERPOLATION IN POOLQ CURVE
C     IADJH -- =1, ONLY OBSERVED ELEV; SPECIAL ADJUST CASE IN EFFECT
C              =0, OTHERWISE
C     IBUG -- NO TRACE OR DEBUG (IBUG=0), TRACE ONLY (IBUG=1),TRACE AND
C       DEBUG (IBUG=2).
C ......................................................................
C QO2, QOM, S2, AND ELEV2 WILL BE COMPUTED IN THIS SUBROUTINE EXCEPT AT
C THOSE TIME PERIODS IN THE ADJUSTED RUN WHEN THE VALUES ARE OBSERVED.
C OTHER VARIABLES IN THE ARGUMENT LIST WILL BE FURNISHED BY THE
C SUPERVISORY EXECUTION ROUTINE.
C ......................................................................
      INCLUDE 'common/fdbug'
      INCLUDE 'common/resv26'
C
      DIMENSION STORAG(1),OUTFLO(1),STOR(1),ELEV(1),ELVOUT(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_res/RCS/evsq26.f,v $
     . $',                                                             '
     .$Id: evsq26.f,v 1.2 1996/07/12 14:46:15 dws Exp $
     . $' /
C    ===================================================================
C
C
C ......................................................................
C WRITE TRACE AND DEBUG IF REQUIRED.
C ......................................................................
      IADJH=0
      IF(IBUG-1)50,10,20
   10 WRITE(IODBUG,30)
      GO TO 50
   20 WRITE(IODBUG,30)
   30 FORMAT(1H0,10X,17H** EVSQ26 ENTERED)
      WRITE(IODBUG,40) IFCST,FCST,QI1,QI2,QIM,QO1,QO2,QOM,S1,S2,
     $ELEV2,RULEL2,NTERP,NTERPQ,ICOMB,IBUG
   40 FORMAT(1H0,'   IFCST,FCST,QI1,QI2,QIM,QO1,QO2,QOM,S1,S2,',
     $'ELEV2,RULEL2,NTERP,NTERPQ,ICOMB,IBUG '/1X,I6,L5,9F12.3/
     $1X,F12.3,4I6)
C ......................................................................
C COMPUTE STORAGE ARRAY FROM ELEVATION ARRAY.  USE RULE CURVE ELEVATION
C WHEN VALUE IS -999.0.
C ......................................................................
   50 IF(IFCST.EQ.0) GO TO 70
      IF(FCST) GO TO 70
      IF (ICOMB .NE. 4) GO TO 51
      IADJH=1
      GO TO 70
   51 OBSQO2=QO2
C ......................................................................
C USE FUNCTION OBSV26 TO CHECK FOR A VALUE OF STORAGE AT THE END OF THE
C TIME INTERVAL.  THE TIME INTERVAL IS PRIOR TO OR AT RUN TIME.
C
      IGO=OBSV26(S2,IBUG)
      IF(IGO.EQ.1) GO TO 230
C
C WHEN IGO IS 1, THE POOL STORAGE IS OBSERVED; COMPUTED FROM OBSERVED
C MEAN OUTFLOWS AND ADJUSTED MEAN INFLOWS; ADJUSTED BETWEEN OBSERVED
C VALUES USING SIMULATED AND OBSERVED VALUES; OR INTERPOLATED BETWEEN
C OBSERVED VALUES.  WHEN IGO IS 0, THE STORAGE IS MISSING.  IF THE MEAN
C OUTFLOW IS OBSERVED BUT THE STORAGE AT THE END OF THE TIME INTERVAL IS
C MISSING, THE ENDING STORAGE WILL BE COMPUTED IN STATEMENT 220
C FROM THE OBSERVED MEAN INFLOW, THE ADJUSTED MEAN INFLOW, AND THE
C COMPUTED STORAGE AT THE BEGINNING OF THE TIME INTERVAL.
C
      IF(QOM.EQ.-999.0) GO TO 70
      GO TO 220
C ......................................................................
C SET THE DIFFERENCE IN ENDING AND BEGINNING INFLOW FOR LATER USE.
C
   70 DIFQI=QI2-QI1
C ......................................................................
C COMPUTE POSITION OF BEGINNING STORAGE (S1) IN STORAG ARRAY.  FIRST,
C CHECK AGAINST STORAG(1) AND THEN AGAINST EACH VALUE OF STORAG IN DO
C LOOP 100.  IF S1 IS LESS THAN STORAG(1), S2 (STORAGE COMPUTED IN A
C PREVIOUS SUBROUTINE) SHOULD BE GREATER OR THE PROGRAM SHOULD NOT BE
C IN EVSQ26.
C ......................................................................
      IF (NTERPQ .NE. 0) GO TO 790
C LINEAR INTERPOLATION IN POOLQ CURVE -- STORAG(ELVOUT) VS OUTFLO CURVE
      IF (IADJH .EQ. 0) GO TO 600
C  GIVEN S2, FIND QO2 BY LINEAR INTERPOLATION
      CALL NTER26(S2,QO2,STORAG,OUTFLO,NSO,IFLAG,0,IBUG)
      IF (S2 .LE. STORAG(1)) QO2=OUTFLO(1)
      IF (S2 .GE. STORAG(NSO)) QO2=OUTFLO(NSO)
      QOM=0.5*(QO1+QO2)
      GO TO 250
C  SOLVE V2 AND QO2 SIMULTANEOUSLY
C  USING CONTINUITY AND Q VS STORAGE
  600 V1=S1
      ITER=0
      CALL ITRP26(V1,ISO,STORAG,NSO)
  700 IS=ISO
      IF (IS .GE. 1) GO TO 710
      QO2=OUTFLO(1)
      GO TO 720
  710 IF (IS .LE. NSO) GO TO 730
      QO2=OUTFLO(NSO)
  720 QOM=0.5*(QO1+QO2)
      V2=V1+QIM-QOM
      GO TO 740
  730 SLOPE=(OUTFLO(IS+1)-OUTFLO(IS))/(STORAG(IS+1)-STORAG(IS))
      UP=V1+QIM-0.5*(QO1+OUTFLO(IS)-SLOPE*STORAG(IS))
      DN=1+0.5*SLOPE
      V2=UP/DN
      QO2=OUTFLO(IS)+SLOPE*(V2-STORAG(IS))
  740 CALL ITRP26(V2,ISN,STORAG,NSO)
      IF (ISO .EQ. ISN) GO TO 750
      ISNMO=ISN-ISO
      ISO=ISO+ISIGN(1,ISNMO)
      IF (ISO .GT. NSO) ISO=NSO+1
      IF (ISO .LT. 1) ISO=0
      ITER=ITER+1
      IF (ITER .LE. 20) GO TO 700
  750 S2=V2
      CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
      QOM=0.5*(QO1+QO2)
      GO TO 250
  790 CONTINUE
C CONSTANT FLOW IN POOLQ CURVE -- STORAG(ELVOUT) VS OUTFLO CURVE
      IF (IADJH .EQ. 0) GO TO 800
      CALL ITRP26(S2,IS,STORAG,NSO)
      IF (IS .LE. 1) IS=1
      IF (IS .GE. NSO) IS=NSO
      QO2=OUTFLO(IS)
      QOM=QO2
      GO TO 250
  800 CONTINUE
      IF(S1.LT.STORAG(1)) GO TO 120
      DO 100 I=2,NSO
      IF(S1.GE.STORAG(I)) GO TO 100
      QO=OUTFLO(I-1)
C ......................................................................
C COMPUTE TEST VALUE OF STORAGE (STEST) AT END OF TIME PERIOD ASSUMING
C QO IS CONSTANT OUTFLOW DURING THE TIME PERIOD.
C ......................................................................
      STEST=S1+QIM-QO
      IF(STEST.GE.S1) GO TO 90
C ......................................................................
C POOL IS FALLING.  NEXT LOWER STORAG VALUE IS STORAG(I-1).  SET IRISE
C=0 FOR FALLING POOL.  CHECK IF STEST IS EQUAL TO OR GREATER THAN
C STORAG(I-1).  IF SO, SET MEAN OUTFLOW (QOM)=QO IN STATEMENT 110 AND
C COMPUTE STORAGE AT END OF TIME PERIOD (S2) IN STATEMENT 220.
C ......................................................................
      K=I-1
      IF(STEST.GE.STORAG(K)) GO TO 110
      IRISE=0
      GO TO 130
C ......................................................................
C POOL IS RISING OR STEADY FOR FOLLOWING STATEMENTS.  IRISE IS SET TO 1
C FOR RISING OR STEADY POOL.      NEXT HIGHER STORAG VALUE IS STORAG(I).
C CHECK IF STEST IS EQUAL TO OR LESS THAN STORAG(I).  IF SO, SET MEAN
C OUTFLOW (QOM)=QO IN STATEMENT 110 AND COMPUTE S2 IN STATEMENT 220.
C ......................................................................
   90 IF(STEST.LE.STORAG(I)) GO TO 110
      K=I
      IRISE=1
      GO TO 130
  100 CONTINUE
C ......................................................................
C FOLLOWING STATEMENTS ARE FOR THE CASE WHERE BEGINNING POOL STORAGE IS
C GREATER THAN HIGHEST STORAG VALUE.  IF COMPUTED STORAGE (STEST) AT END
C OF PERIOD IS ALSO ABOVE THE HIGHEST STORAG VALUE, THE MEAN OUTFLOW
C IS SET TO THE HIGHEST STORAG VALUE.
C ......................................................................
      K=NSO
      QO=OUTFLO(NSO)
      STEST=S1+QIM-QO
      IRISE=0
      IF (STEST .GE. S1) IRISE=1
      IF(STEST.LT.STORAG(NSO)) GO TO 130
  110 QOM=QO
      QO2=QOM
C ......................................................................
C GO TO STATEMENT 220 TO COMPUTE STORAGE (S2) AT END OF TIME PERIOD..
C ......................................................................
      GO TO 220
C ......................................................................
C FOLLOWING STATEMENTS WILL COMPUTE FRACTION (FRAC) OF TIME PERIOD FOR
C POOL STORAGE TO RISE ABOVE OR FALL BELOW STORAG VALUES.  FUNCTION
C FRAC26 IS USED TO COMPUTE FRAC.
C FOR STATEMENT 120 BEGINNING STORAGE (S1) IS LESS THAN STORAG(1).
C CHECK IF COMPUTED STORAGE (STEST) AT END OF TIME PERIOD USING FIRST
C VALUE OF OUTFLO AS MEAN OUTFLOW IS LESS THAN OR GREATER THAN
C STORAG(1).  MEAN OUTFLOW WILL NOT BE ALLOWED TO EXCEED MEAN INFLOW.
C
  120 QO=OUTFLO(1)
      IF(QO.GT.QIM) QO=QIM
      STEST=S1+QIM-QO
      IF(STEST.GT.STORAG(1)) GO TO 121
C
C STEST DOES NOT EXCEED STORAG(1).  USE STEST FOR STORAGE (S2) AT END
C OF TIME PERIOD.
C
      QOM=QO
      QO2=QO
      S2=STEST
      GO TO 230
  121 K=1
      IRISE=1
  130 QI=QI1
      SS1=S1
      SFRAC=0.
      SQOM=0.
      DIFQOK=0.
      QSFRAC=0.
  140 SFILL=STORAG(K)
      FRAC=FRAC26(DIFQI,DIFQOK,QI,QO,QO,QSFRAC,SS1,SFILL,IBUG)
C ......................................................................
C COMPUTE SFRAC, THE TOTAL FRACTION OF TIME, AND SQOM, THE OUTFLOW,
C SINCE THE BEGINNING OF THE TIME PERIOD.  SQOM IS CONVERTED TO UNITS OF
C MEAN DISCHARGE OVER THE ENTIRE TIME PERIOD.
C ......................................................................
      SFRAC=SFRAC+FRAC
      SQOM=SQOM+QO*FRAC
      IF(IBUG.NE.2) GO TO 160
      WRITE(IODBUG,150) DIFQI,QI,QO,SS1,K,STORAG(K),FRAC,SFRAC,SQOM
  150 FORMAT(1H0,51H    DIFQI,QI,QO,SS1,K,STORAG(K),FRAC,SFRAC,SQOM ARE/
     $1X,4F12.3,I6,4F12.3)
C ......................................................................
C CHECK FOR POOL STORAGE EXCEEDING OR FALLING BELOW ADDITIONAL VALUES OF
C STORAG DURING THE TIME PERIOD.  USE SS2 FOR A TEST VALUE OF STORAGE.
C COMPUTE MEAN INFLOW, QIMEAN, AND MEAN OUTFLOW, QOMEAN, FOR REMAINDER
C OF TIME PERIOD AND CONVERT TO MEAN DISCHARGE FOR ENTIRE TIME PERIOD.
C ......................................................................
  160 QI=QI1+DIFQI*SFRAC
      QIMEAN=(QI+QI2)*0.5*(1.-SFRAC)
      IF(IRISE.EQ.0)  GO TO 170
C  POOL IS RISING
      QO=OUTFLO(K)
      QOMEAN=QO*(1.-SFRAC)
      SS2=STORAG(K)+QIMEAN-QOMEAN
C  IF SS2<SS1, POOL RISES AND FALLS IN A SINGLE TIME PERIOD
C  PASS INFLOW AND EXIT
      IF(SS2.LT.SS1) THEN
        QOM=QIM
        QO2=QO
        GO TO 220
      ENDIF
      K=K+1
      IF(SS2.LE.STORAG(K) .OR. K.GE.NSO) GO TO 210
      SS1=STORAG(K-1)
      GO TO 140
  170 K=K-1
C  POOL IS FALLING
      IF(K.GE.1) GO TO 190
C ......................................................................
C IF COMPUTED STORAGE DROPS TO STORAG(1), THE STORAGE IS HELD AT
C STORAG(1) THROUGH THE END OF THE TIME PERIOD.
C ......................................................................
      S2=STORAG(1)
      QOM=S1+QIM-S2
      IF(QOM.GE.0.) GO TO 180
      QOM=0.
      S2=S1+QIM
  180 QO2=QI2
      IF(QO2.LT.0.) QO2=0.
      GO TO 230
  190 QO=OUTFLO(K)
      QOMEAN=QO*(1.-SFRAC)
      SS2=STORAG(K+1)+QIMEAN-QOMEAN
      SS1=SS2
      IF(SS2.LT.STORAG(K)) GO TO 140
C ......................................................................
C THE LOWER OUTFLOW USED IN COMPUTING SS2 (STORAGE AT END OF COMPUTA-
C TIONAL TIME STEP) TO RISE INSTEAD OF FALL SO CHECK SS2 AGAINST
C STORAG(K+1).  IF SS2 EXCEEDS STORAG(K+1), KEEP THE POOL AT STORAG(K+1)
C WHEN IT REACHES THAT STORAGE UNTIL THE END OF THE TIME PERIOD.
C ......................................................................
      IF(SS2.LE.STORAG(K+1)) GO TO 210
      S2=STORAG(K+1)
      QOM=S1+QIM-S2
      IF(QOM.GE.0.) GO TO 200
      QOM=0.
      S2=S1+QIM
  200 QO2=QI2
      IF(QO2.LT.0.) QO2=0.
      GO TO 230
C ......................................................................
C COMPUTE QOM, S2, AND DEFINE QO2.  QOM IS MEAN OUTFLOW FOR TIME PERIOD,
C S2 IS POOL STORAGE AT END OF TIME PERIOD, AND QO2 IS INSTANTANEOUS
C OUTFLOW AT END OF TIME PERIOD.
C ......................................................................
  210 QOM=SQOM+QOMEAN
      QO2=QO
  220 CONTINUE
      S2=S1+QIM-QOM
  230 IF(IFCST.EQ.0) GO TO 240
      IF(FCST) GO TO 240
      IF(IGO.EQ.1) QO2=QOM
      IF(OBSQO2.NE.-999.0) QO2=OBSQO2
      IF(ELEV2.NE.-999.0) GO TO 250
C ......................................................................
C COMPUTE ELEV2 (ELEVATION) FROM S2 (POOL STORAGE AT END OF TIME PERIOD)
C WITH ARITHMETIC (NTERP=0) OR LOGARITHMIC INTERPOLATION (NTERP=1).
C ......................................................................
  240 CALL NTER26(S2,ELEV2,STOR,ELEV,NSE,IFLAG,NTERP,IBUG)
  250 IRISE=0
      IF(S2 .GT. S1) IRISE=1
      IF(IBUG-1)320,300,260
  260 WRITE(IODBUG,270) QO2, QOM, S2, ELEV2, IRISE
  270 FORMAT(1H0,' QO2,QOM,S2,ELEV2,IRISE ',4F12.3,I5)
C      WRITE(IODBUG,280)IFLAG,NSE,(STOR(I),ELEV(I),I=1,NSE)
C  280 FORMAT(1H0,9H IFLAG IS,I6,43H.  NO OF POINTS ON ELEV. - STORAGE CU
C     $RVE IS,I4,1H./1H0,48H ALTERNATING VALUES OF STORAGE AND ELEVATION
C     $ARE/(1X,10F12.3))
      WRITE(IODBUG,290)(ELVOUT(I),STORAG(I),OUTFLO(I),I=1,NSO)
  290 FORMAT(1H0,118HFOLLOWING ARE SETS OF ELEVATION, CORRESPONDING STOR
     $AGE, AND SPECIFIED OUTFLOW VALUES FOR ELEVATION VS OUTFLOW RELATIO
     $N/(1X,3(3F12.3,3X)))
  300 WRITE(IODBUG,310)
  310 FORMAT(1H0,10X,17H** LEAVING EVSQ26)
  320 RETURN
      END
