C MODULE PIN33
C-----------------------------------------------------------------------
C
      SUBROUTINE PIN33 (PO,LEFTP,IUSEP,CO,LEFTC,IUSEC)
C
C  THIS SUBROUTINE READS AND CHECKS ALL CARD INPUT FOR SEGMENT
C  DEFINITION FOR THE API-CIN OPERATION.  IT THEN FILLS THE PO AND CO
C  ARRAYS.
C
C  SUBROUTINE INITIALLY WRITTEN BY - TIM SWEENEY - OHRFC
C
C***********************************************************************
C  PRINCIPAL VARIABLES...
C
C  FOR DEFINITION OF VARIABLES IN SUBROUTINE ARGUMENT LIST, SEE
C  SECTION VIII.4.2-PIN OF THE NWSRFS USER'S MANUAL.
C
C  FOR DEFINITION OF VARIABLES IN COMMON BLOCKS, SEE SECTION IX.3.3C
C  OF THE NWSRFS USER'S MANUAL.
C
C     AI             CURRENT AI VALUE
C     AIADJ          INITIAL AI ADJUSTMENT FACTOR
C     AICO           STORM AI VALUE
C     API            CURRENT API VALUE
C     ARECF          API RECESSION FACTOR-NO SNOW
C     ASRF           API RECESSION FACTOR-SNOW
C     AT             CURRENT UNCORRECTED SYNTHETIC TEMPERATURE
C     AVGT           CURRENT AVERAGE TEMPERATURE
C     CO(1)          INPUT CARRYOVER DATA FROM C ARRAY
C     DTCAP          DATA TYPE CODE OF API TIME SERIES
C     DTCAI          DATA TYPE CODE OF AI TIME SERIES
C     DTCRF          DATA TYPE CODE FOR TIME SERIES FOR RAINFALL/MELT
C     DTCRO          DATA TYPE CODE FOR TIME SERIES FOR RUNOFF
C     DTCTE          DATA TYPE CODE FOR TIME SERIES FOR TEMPERATURE
C     DTCWE          DATA TYPE CODE FOR TIME SERIES FOR WATER EQUIVALENT
C     ICHKDM         SWITCH TO INDICATE CHECK DIMENSION OF TIME SERIES
C                      0 = NO, 1 = YES
C     IDELT          DELTA-T OF TIME SERIES FOR RAINFALL/MELT AND RUNOFF
C     IFATAL         FATAL ERROR FLAG, 0 = NO FATAL ERRORS, 1 = ERRORS
C     IFDEB          DEBUG OUTPUT SWITCH, 0 = OFF, 1 = ON
C     INCOFL         READ/NO READ CARRYOVER FLAG, 0 = NO READ, 1 = READ
C     IOUTFL         API/AI OUTPUT TIME SERIES FLAG,
C                      0 = NO READ, 1 = READ
C     IUSEC          NUMBER OF WORDS REQUIRED IN THE C ARRAY FOR
C                      THIS OPERATION
C     IUSEP          NUMBER OF WORDS REQUIRED IN THE P ARRAY FOR
C                      THIS OPERATION
C     IVERS          VERSION NUMBER
C     LAT            LATITUDE OF RUNOFF ZONE   (DEG MIN)
C     LEFTC          NUMBER OF WORDS AVAILABLE IN THE C ARRAY
C     LEFTP          NUMBER OF WORDS AVAILABLE IN THE P ARRAY
C     LON            LONGITUDE OF RUNOFF ZONE    (DEG MIN)
C     MISS           SWITCH INDICATING IF TIME SERIES CAN CONTAIN
C                      MISSING VALUES, 0 = NO, 1 = YES
C     NALZN          ALTERNATE RUNOFF ZONE NUMBER
C     NEWSTM         STORM PERIOD COUNTER
C     NOP            NUMBER OF OPERATION ASSIGNED TO THIS OPERATION
C     NOZON          RUNOFF ZONE NUMBER
C     PO(1)          INPUT PARAMETRIC DATA FROM P ARRAY
C     PRECF          PRECIPITATION RECESSION FACTOR
C     RANCO          STORM RAINFALL VALUE
C     ROCO           STORM RUNOFF VALUE
C     SUBNAM(2)      SUBROUTINE NAME
C     TC             CURRENT SYNTHETIC TEMPERATURE (CORRECTED)
C     TCADJ          SYNTHETIC TEMPERATURE ADJUSTMENT FACTOR
C     TSIDAP(2)      ID OF API TIME SERIES
C     TSIDAI(2)      ID OF AI TIME SERIES
C     TSIDRF(2)      ID OF TIME SERIES FOR RAINFALL/MELT
C     TSIDRO(2)      ID OF TIME SERIES FOR RUNOFF
C     TSIDTE(2)      ID OF TIME SERIES FOR TEMPERATURE
C     TSIDWE(2)      ID OF TIME SERIES FOR WATER EQUIVALENT
C     WE             CURRENT WATER EQUIVALENT VALUE
C     WERCR          CRITERIA WE FOR API RECESSION FACTOR WITH SNOW
C     ZONAME(6)      RUNOFF ZONE NAME
C
C***********************************************************************
C  CONTENTS OF THE PO ARRAY...
C
C       WORD   ITEM
C     -------  ---------------------------------------------------------
C        1     VERSION NUMBER
C      2 - 7   RUNOFF ZONE NAME
C        8     RUNOFF ZONE NUMBER
C        9     ALTERNATE RUNOFF ZONE NUMBER
C       10     LATITUDE OF ZONE
C       11     LONGITUDE OF ZONE
C       12     INITIAL AI ADJUSTMENT FACTOR
C       13     DELTA-T OF RAINFALL/MELT AND RUNOFF TIME SERIES
C     14 - 15  ID OF RAINFALL/MELT TIME SERIES
C       16     DATA TYPE CODE OF RAINFALL/MELT TIME SERIES
C     17 - 18  ID OF RUNOFF TIME SERIES
C       19     DATA TYPE CODE OF RUNOFF TIME SERIES
C     20 - 21  ID OF WATER EQUIVALENT TIME SERIES
C       22     DATA TYPE CODE OF WATER EQUIVALENT TIME SERIES
C     23 - 24  ID OF TEMPERATURE TIME SERIES
C       25     DATA TYPE CODE OF TEMPERATURE TIME SERIES
C       26     SYNTHETIC TEMPERATURE ADJUSTMENT FACTOR
C       27     PRECIPITATION RECESSION FACTOR
C       28     API RECESSION FACTOR - NO SNOW
C       29     API RECESSION FACTOR - WITH SNOW
C       30     WE CRITERIA TO USE SNOW API RECESSION FACTOR
C       31     CARRYOVER INPUT READ/NO READ FLAG  0 = NO READ, 1 = READ
C     32 - 33  ID OF API TIME SERIES
C       34     DATA TYPE CODE OF API TIME SERIES
C     35 - 36  ID OF AI TIME SERIES
C       37     DATA TYPE CODE OF AI TIME SERIES
C     38 - 42  EMPTY SPACES (ZEROES)
C
C***********************************************************************
C  CONTENTS OF THE CO ARRAY...
C
C       WORD   ITEM
C     -------  ---------------------------------------------------------
C        1     STORM PERIOD COUNTER
C        2     STORM RAINFALL VALUE
C        3     STORM AI VALUE
C        4     STORM RUNOFF VALUE
C        5     CURRENT API VALUE
C        6     CURRENT AI VALUE
C        7     CURRENT WATER EQUIVALENT VALUE
C        8     24-HOUR RAINFALL/MELT ENDING 12Z
C        9     AVERAGE TEMPERATURE
C       10     CURRENT UNCORRECTED SYNTHETIC TEMPERATURE
C       11     CURRENT CORRECTED SYNTHETIC TEMPERATURE
C
C***********************************************************************
C
      CHARACTER*80 CARD
C
      DIMENSION PO(*),CO(*)
      DIMENSION ZONAME(6),SUBNAM(2),TSIDRF(2),TSIDRO(2),TSIDWE(2)
      DIMENSION TSIDAP(2),TSIDAI(2),TSIDTE(2)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin33.f,v $
     . $',                                                             '
     .$Id: pin33.f,v 1.3 2002/02/11 19:01:46 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SUBNAM/4hPIN3,4h3   /,NOP/33/
      DATA EMPTY/4h    /,DIM/4hL   /,TMP/4hTEMP/,DLS/4hDLES/
C
C
C  CALL DEBUG CHECK ROUTINE AND SET INITIAL VARIABLES.
C
      CALL FPRBUG (SUBNAM,1,NOP,IFDEB)
      IVERS=2
      IFATAL=0
      IUSEP=42
      IUSEC=11
      ICHKDM=1
      MISS=0
      NOVAL=1
      IF(IFDEB) 110,110,810
C
C  READ ZONE NAME AND NUMBER, ALTERNATE ZONE NUMBER, LATITUDE,
C  LONGITUDE, AI ADJUSTMENT FACTOR, SYNTHETIC TEMP ADJUSTMENT FACTOR,
C  PRECIP RECESSION FACTOR, API RECESSION FACTOR (NO SNOW),
C  API RECESSION FACTOR (WITH SNOW), WATER EQUIV CRITERIA
C   VALUE, AND INPUT CARRYOVER FLAG.
C
  110 READ(IN,10) ZONAME,NOZON,NALZN,LAT,LON,AIADJ,TCADJ,
     1            PRECF,ARECF,ASRF,WERCR,IOUTFL,INCOFL
      IF(IFDEB) 120,120,812
C
C  READ TIME SERIES INFORMATION.
C
  120 READ(IN,11) IDELT,TSIDRF,DTCRF,TSIDRO,DTCRO,TSIDWE,DTCWE,
     1            TSIDTE,DTCTE
C
C  CHECK IF OPTIONAL API AND/OR AI OUTPUT TIME SERIES ARE DESIRED
C
      TSIDAP(1)=EMPTY
      TSIDAP(2)=EMPTY
      DTCAP=EMPTY
      TSIDAI(1)=EMPTY
      TSIDAI(2)=EMPTY
      DTCAI=EMPTY
C
      IF(IOUTFL) 140,140,130
  130 READ(IN,12) TSIDAP,DTCAP,TSIDAI,DTCAI
  140 IF(IFDEB) 160,160,150
C
C  IF PRINTING TIME SERIES DEBUG OUTPUT, CHECK FOR DEFAULT DELTA-T.
C
  150 IF(IDELT) 814,814,816
C
C  CHECK TO INSURE MANDATORY TIME SERIES ARE AVAILABLE
C
  160 CALL CHEKTS (TSIDRF,DTCRF,IDELT,ICHKDM,DIM,MISS,NOVAL,IERR)
      CALL CHEKTS (TSIDRO,DTCRO,IDELT,ICHKDM,DIM,MISS,NOVAL,IERR)
      CALL CHEKTS(TSIDWE,DTCWE,24,ICHKDM,DIM,MISS,NOVAL,IERR)
      CALL CHEKTS (TSIDTE,DTCTE,IDELT,ICHKDM,TMP,MISS,NOVAL,IERR)
C
C  IF TIME SERIES FOR API AND AI VALUES ARE TO BE WRITTEN, CHECK
C  TO SEE IF THOSE TIME SERIES ARE AVAILABLE.
C
      IF(DTCAP-EMPTY) 180,200,180
  180 IF(IFDEB) 190,190,820
  190 CALL CHEKTS(TSIDAP,DTCAP,24,ICHKDM,DIM,MISS,NOVAL,IERR)
  200 IF(DTCAI-EMPTY) 210,230,210
  210 IF(IFDEB) 220,220,822
  220 CALL CHEKTS(TSIDAI,DTCAI,24,ICHKDM,DLS,MISS,NOVAL,IERR)
C
C  CHECK TO SEE IF CARRYOVER VALUES ARE BEING INPUT FROM CARDS.
C  IF SO, READ CARDS WITH VALUES.
C
  230 IF(INCOFL) 250,250,240
  240 READ(IN,'(A)') CARD
      READ(CARD,13,ERR=241) NEWSTM,RANCO,AICO,ROCO,API,AI,WE,AVGT,AT,TC
      GO TO 242
241   CALL FRDERR (IPR,' ',CARD)
242   IF(IFDEB) 260,260,826
C
C  SET DEFAULT CARRYOVER VALUES.
C
  250 NEWSTM=4
      RANCO=0.0
      AICO=0.0
      ROCO=0.0
      API=0.0
      AI=0.0
      WE=0.0
      AVGT=50.0
      AT=51.0
      TC=51.0
      IF(IFDEB) 260,260,824
C
C  SET INPUT CARRYOVER VALUES TO DEFAULTS
C
  260 IF(AVGT.LT.1.0) AVGT=50.0
      IF(AT.LT.1.0) AT=51.0
      IF(TC.LT.1.0) TC=51.0
C
C  SET DEFAULT RECESSION FACTORS
C
      IF(PRECF.LT.0.10) PRECF=0.90
      IF(ARECF.LT.0.10) ARECF=0.90
      IF(ASRF.LT.0.10) ASRF=0.90
C
C  IF INPUT DELTA-T OF TIME SERIES IS OMITTED, DEFAULT IS 6 HOURS.
C
      IF(IDELT) 290,280,290
  280 IDELT=6
C
C  CHECK VALIDITY OF INPUT DATA.
C
  290 IF(NOZON.LT.101.OR.NOZON.GT.4092) GO TO 992
  300 IF(LAT.LT.3515.OR.LAT.GT.4230) GO TO 993
  320 IF(LON.LT.7745.OR.LON.GT.8930) GO TO 994
  330 ADJ=ABS(AIADJ)
      IF(ADJ-4.0) 340,340,995
  340 IF(IDELT-6) 996,350,996
  350 ADJ=ABS(TCADJ)
      IF(ADJ-30.0) 360,360,997
  360 IF(PRECF.LT.0.50.OR.PRECF.GT.1.00) GO TO 998
  370 IF(ARECF.LT.0.50.OR.ARECF.GT.1.00) GO TO 999
  390 IF(ASRF.LT.0.50.OR.ASRF.GT.1.00) GO TO 1000
  400 IF(WERCR.LT.0.0.OR.WERCR.GT.12.00) GO TO 1001
C
C  IF DEFAULT CARRYOVERS USED, DO NOT CHECK VALIDITY.
C
  410 IF(INCOFL) 450,450,420
  420 IF(NEWSTM.LT.0.OR.NEWSTM.GT.4) GO TO 1005
      IF(RANCO.LT.0.0.OR.RANCO.GT.50.00) GO TO 1005
      IF(AICO.LT.1.2.OR.AICO.GT.7.6) GO TO 1005
      IF(ROCO.LT.0.0.OR.ROCO.GT.50.00) GO TO 1005
      IF(API.LT.-4.00.OR .API.GT.4.00) GO TO 1005
      IF(AI.LT.1.2.OR.AI.GT.7.6) GO TO 1005
      IF(WE.LT.0.0.OR.WE.GT.20.00) GO TO 1005
      IF(AVGT.LT.-20.0.OR.AVGT.GT.105.0) GO TO 1005
      IF(AT.LT.15.0.OR.AT.GT.100.0) GO TO 1005
      IF(TC.LT.15.0.OR.TC.GT.100.0) GO TO 1005
C
C  CHECK P ARRAY TO MAKE SURE SPACE IS AVAILABLE FOR PO ARRAY.
C
  450 CALL CHECKP (IUSEP,LEFTP,IERR)
      IF(IERR) 470,470,460
  460 IFATAL=1
      GO TO 480
C
C  STORE PARAMETRIC DATA IN PO ARRAY.
C
  470 PO(1)=IVERS+0.01
      DO 2001 I=1,6
 2001 PO(I+1)=ZONAME(I)
      PO(8)=NOZON+0.01
      PO(9)=NALZN+0.01
      PO(10)=LAT+0.01
      PO(11)=LON+0.01
      PO(12)=AIADJ
      PO(13)=IDELT+0.01
      PO(14)=TSIDRF(1)
      PO(15)=TSIDRF(2)
      PO(16)=DTCRF
      PO(17)=TSIDRO(1)
      PO(18)=TSIDRO(2)
      PO(19)=DTCRO
      PO(20)=TSIDWE(1)
      PO(21)=TSIDWE(2)
      PO(22)=DTCWE
      PO(23)=TSIDTE(1)
      PO(24)=TSIDTE(2)
      PO(25)=DTCTE
      PO(26)=TCADJ
      PO(27)=PRECF
      PO(28)=ARECF
      PO(29)=ASRF
      PO(30)=WERCR
      PO(31)=INCOFL+0.01
      PO(32)=TSIDAP(1)
      PO(33)=TSIDAP(2)
      PO(34)=DTCAP
      PO(35)=TSIDAI(1)
      PO(36)=TSIDAI(2)
      PO(37)=DTCAI
      DO 2002 I=38,42
 2002 PO(I)=0.01
C
C  CHECK C ARRAY TO MAKE SURE SPACE IS AVAILABLE FOR CO ARRAY.
C
  480 CALL CHECKC (IUSEC,LEFTC,IERR)
      IF(IERR) 490,490,8888
C
C  STORE CARRYOVER DATA IN CO ARRAY.
C
  490 CO(1)=NEWSTM+0.01
      CO(2)=RANCO
      CO(3)=AICO
      CO(4)=ROCO
      CO(5)=API
      CO(6)=AI
      CO(7)=WE
      CO(8)=0.0
      CO(9)=AVGT
      CO(10)=AT
      CO(11)=TC
      IF(IFATAL) 9999,9999,8888
C
C  DEBUG OUTPUT STATEMENTS.
C
  810 WRITE(IODBUG,14) SUBNAM,IVERS
      GO TO 110
  812 WRITE(IODBUG,15) ZONAME,NOZON,NALZN,LAT,LON,AIADJ,TCADJ,
     1                 PRECF,ARECF,ASRF,WERCR,IOUTFL,INCOFL
      GO TO 120
  814 WRITE(IODBUG,16) TSIDRF,DTCRF,TSIDRO,DTCRO,TSIDWE,DTCWE,
     1                 TSIDTE,DTCTE
      GO TO 160
  816 WRITE(IODBUG,17) TSIDRF,DTCRF,IDELT,TSIDRO,DTCRO,IDELT,TSIDWE,
     1                 DTCWE,TSIDTE,DTCTE,IDELT
      GO TO 160
  820 WRITE(IODBUG,18) TSIDAP,DTCAP
      GO TO 190
  822 WRITE(IODBUG,19) TSIDAI,DTCAI
      GO TO 220
  824 WRITE(IODBUG,20)
      GO TO 260
  826 WRITE(IODBUG,21) NEWSTM,RANCO,AICO,ROCO,API,AI,WE,AVGT,AT,TC
      GO TO 260
C
C  ERROR STATEMENTS.
C
  992 WRITE(IPR,22) NOZON
      IFATAL=1
      GO TO 300
  993 WRITE(IPR,23) LAT
      IFATAL=1
      GO TO 320
  994 WRITE(IPR,24) LON
      IFATAL=1
      GO TO 330
  995 WRITE(IPR,25) AIADJ
      IFATAL=1
      GO TO 340
  996 WRITE(IPR,26) IDELT
      IFATAL=1
      GO TO 350
  997 WRITE(IPR,27) TCADJ
      IFATAL=1
      GO TO 360
  998 WRITE(IPR,28) PRECF
      IFATAL=1
      GO TO 370
  999 WRITE(IPR,29) ARECF
      IFATAL=1
      GO TO 390
 1000 WRITE(IPR,30) ASRF
      IFATAL=1
      GO TO 400
 1001 WRITE(IPR,31) WERCR
      IFATAL=1
      GO TO 450
 1005 WRITE(IPR,35) NEWSTM,RANCO,AICO,ROCO,API,AI,WE,AVGT,AT,TC
      IFATAL=1
      GO TO 450
 8888 IUSEP=0
      IUSEC=0
      CALL ERROR
 9999 RETURN
C
C
   10 FORMAT(6A4,I6,3I5,2F5.1,2F5.2,/,2F5.2,2I5)
   11 FORMAT(I5,4(2X,2A4,1X,A4))
   12 FORMAT(5X,2(2X,2A4,1X,A4))
   13 FORMAT(I5,F5.2,F5.1,2F5.2,F5.1,F5.2,3F5.1)
   14 FORMAT(/5X,2A4,' DEBUG OUTPUT.',5X,'VERSION:',I4)
   15 FORMAT(/12X,'ZONE NAME:',2X,6A4,5X,'ZONE NUMBER:',I9,
     1       5X,'ALT ZONE NUMBER:',I5,/
     2       53X,'LATITUDE:',8X,I4,5X,'LONGITUDE:',6X,I5,/
     3       53X,'AI ADJ FACTOR:',F7.1,5X,'TC ADJ FACTOR:',F7.1,/
     4       53X,'PRECIP REC FAC:',F6.2,5X,'AP REC FAC:',5X,F5.2,/
     5       53X,'AP SNOW REC FAC:',F5.2,5X,'WE REC CRITERIA:',F5.2,/
     6       53X,'OUTPUT TS FLAG:',I6,5X,'C/O INPUT FLAG:',I6)
   16 FORMAT(/20X,'TIME SERIES USED BY THIS OPERATION...'/
     1       25X,'DELTA-T WAS ZERO OR BLANK.  DEFAULT VALUE OF 6 ',
     2       'HOURS SUBSTITUTED.'/
     3       25X,'CONTENTS',15X,'TS I.D.',5X,'TYPE',5X,'TIME INTERVAL'/
     4       25X,20('-'),2X,8('-'),5X,4('-'),5X,13('-')/
     5       25X,'RAINFALL/MELT',9X,2A4,5X,A4,8X,'6 HOURS'/
     6       25X,'RUNOFF',16X,2A4,5X,A4,8X,'6 HOURS'/
     7       25X,'WATER EQUIVALENT',6X,2A4,5X,A4,7X,'24 HOURS'/
     5       28X,'TEMPERATURE',11X,2A4,5X,A4,8X,'6 HOURS')
   17 FORMAT(/20X,'TIME SERIES USED BY THIS OPERATION...'/
     1       25X,'CONTENTS',15X,'TS I.D.',5X,'TYPE',5X,'TIME INTERVAL'/
     2       25X,20('-'),2X,8('-'),5X,4('-'),5X,13('-')/
     3       25X,'RAINFALL/MELT',9X,2A4,5X,A4,I9,' HOURS'/
     4       25X,'RUNOFF',16X,2A4,5X,A4,I9,' HOURS'/
     5       25X,'WATER EQUIVALENT',6X,2A4,5X,A4,7X,'24 HOURS'/
     6       25X,'TEMPERATURE',11X,2A4,5X,A4,I9,' HOURS')
   18 FORMAT(25X,'CURRENT API',11X,2A4,5X,A4,7X,'24 HOURS')
   19 FORMAT(25X,'CURRENT AI',12X,2A4,5X,A4,7X,'24 HOURS')
   20 FORMAT(//20X,'INPUT CARRYOVER VALUES...'/
     1       25X,'DEFAULT VALUES WERE USED.')
   21 FORMAT(//20X,'INPUT CARRYOVER VALUES...'/
     1       25X,'INITIAL 24-HOUR RAINFALL/MELT VALUE SET TO ZERO'/
     2       25X,'NEWSTM   RANCO      AICO     ROCO      API       ',
     3       'AI       WE     AVGT       AT       TC'/
     4       27X,I4,3X,F6.2,4X,F5.1,3X,F6.2,4X,F5.2,4X,F5.1,3X,F6.2,
     5       3(4X,F5.1))
   22 FORMAT(/10X,'**ERROR** ILLEGAL RUNOFF ZONE NUMBER:',I6/
     1       22X,'LIMITS ARE...  101 THRU 4092')
   23 FORMAT(/10X,'**ERROR** ILLEGAL LATITUDE:',I6/
     1       22X,'LATITUDE MUST BE BETWEEN 3515 AND 4230 ',
     2       'DEG MIN)')
   24 FORMAT(/10X,'**ERROR** ILLEGAL LONGITUDE:',I6/
     1       22X,'LONGITUDE MUST BE BETWEEN 7745 AND 8930 ',
     2       '(DEG MIN)')
   25 FORMAT(/10X,'**ERROR** ILLEGAL INITIAL AI ADJ FACTOR:',F6.1/
     1       22X,'LIMITS ARE...  -4.0 THRU +4.0')
   26 FORMAT(/10X,'**ERROR** ILLEGAL TIME STEP (DELTA-T):',I6/
     1       22X,'VALUE MUST BE 6 HOURS.  IF LEFT BLANK, OR ZERO, ',
     2       'DEFAULT IS 6 HOURS.')
   27 FORMAT(/10X,'**ERROR** ILLEGAL INITIAL TC ADJ FACTOR:',F6.1/
     1       22X,'LIMITS ARE...  -30.0 THRU 30.0')
   28 FORMAT(/10X,'**ERROR** ILLEGAL PRECIP RECESSION FACTOR:',F5.2/
     1       22X,'LIMITS ARE...  0.50 THRU 1.00',/
     2       22X,'DEFAULT IS 0.90')
   29 FORMAT(/10X,'**ERROR** ILLEGAL API RECESSION FACTOR:',F5.2/
     1       22X,'LIMITS ARE...  0.50 THRU 1.00',/
     2       22X,'DEFAULT IS 0.90')
   30 FORMAT(/10X,'**ERROR** ILLEGAL API RECESSION FACTOR (SNOW):',F5.2/
     1       22X,'LIMITS ARE...  0.50 THRU 1.00',/
     2       22X,'DEFAULT IS 0.90')
   31 FORMAT(/10X,'** ERROR ** ILLEGAL CRITERIA WE TO USE API ',
     1       'RECESSION FACTOR WITH SNOW PACK:',F6.2/
     2       22X,'LIMITS ARE...  0.00 THRU 12.00')
   35 FORMAT(/10X,'**ERROR** ILLEGAL INITIAL CARRYOVER VALUE.'/
     1       22X,'ONE OF THE FOLLOWING VALUES IS OUTSIDE LIMITS.'//
     2       22X,'C/O ITEM',5X,'LIMITS',5X,'VALUE PUNCHED'/
     3       22X,8('-'),3X,11('-'),2X,13('-')/
     4       22X,'NEWSTM       0 - 4',6X,I7/
     5       22X,'RANCO        0 - 50.00',4X,F6.2/
     6       22X,'AICO       1.2 - 7.6',6X,F5.1/
     7       22X,'ROCO         0 - 50.00',4X,F6.2/
     8       22X,'API      -4.00 - 4.00',6X,F5.2/
     9       22X,'AI         1.2 - 7.6',6X,F5.1/
     1       22X,'WE           0 - 20.00',4X,F6.2/
     2       22X,'AVGT     -20.0 - 105.0',3X,F6.1/
     3       22X,'AT        15.0 - 100.0',3X,F6.1/
     4       22X,'TC        15.0 - 100.0',3X,F6.1)
      END
