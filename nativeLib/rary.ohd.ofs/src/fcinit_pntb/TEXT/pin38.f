C MODULE PIN38
C-----------------------------------------------------------------------
C
      SUBROUTINE PIN38(PO,LEFTP,IUSEP,CO,LEFTC,IUSEC)
C
C#######################################################################
C
C  THIS SUBROUTINE READS AND CHECKS ALL CARD INPUT TO THE BASEFLOW
C  SIMULATION OPERATION AT INITIALIZATION TIME.  IT THEN FILLS THE
C  PO AND CO ARRAYS.
C
C#######################################################################
C
C  CONTENTS OF THE PO ARRAY:
C
C
C     WORD     NAME      DESCRIPTION                             UNITS
C   ________  ________   _______________________________________________
C      1       IVERS     VERSION NUMBER FOR THIS OPERATION
C      2       METENG    METRIC/ENGLISH UNIT INPUT SWITCH
C                        0 = ENGLISH
C                        1 = METRIC
C    3 - 4     TSIDBF    BASEFLOW TIME SERIES ID
C      5       DTCBF     BASEFLOW TIME SERIES TYPE
C      6       IDELTA    BASEFLOW TIME SERIES TIME INTERVAL      HOURS
C      7       AREA      AREA ASSOCIATED WITH THIS OPERATION     KM**2
C      8       CBFLOW    CONSTANT BASEFLOW AMOUNT                CMS
C      9       IVBF      VARIABLE BASEFLOW OPTION INDICATOR
C                        = 0   NO VARIABLE BASEFLOW
C                        = 1   SINGLE BASEFLOW RECESSION COEFF
C                        = 2   RECESSION COEFF SUPPLIES AS T.S.
C     10       NVBFP     NUMBER OF PO ARRAY SPACES SPECIFICALLY
C                        NEEDED FOR VARIABLE BASEFLOW PARAMETERS
C                        = 0   IF PO(9)=ZERO
C                        = 1   IF PO(9)=1
C                        = 3   IF PO(9)=2
C     11       IUSEP     TOTAL NUMBER OF PO ARRAY SPACES REQUIRED
C     12       IUSEC     TOTAL NUMBER OF CO ARRAY SPACES REQUIRED
C   13 - 17              EMPTY FOR FUTURE OPTIONS
C     18       BFREC     BASEFLOW RECESSION COEFF
C                        (USED IF PO(9) = 1)
C   18 - 19    TSIDRC    BASEFLOW REC COEFF TIME SERIES ID
C                        (USED IF PO(9) = 2)
C     20       DTCRC     DATA TYPE CODE FOR BASEFLOW REC T.S.
C                        (USED IF PO(9) = 2)
C
C#######################################################################
C
C  CONTENTS OF THE CO ARRAY:
C
C    NOTE:  CO ARRAY IS NOT NEEDED IF PO(9) = ZERO.
C
C     WORD    NAME      DESCRIPTION                             UNITS
C   ________  ________  ________________________________________________
C      1      TBFLOW    PREVIOUS TOTAL BASEFLOW                 CMS
C                       (USED IF PO(9) = 1 OR 2)
C      2      BFREC     PREVIOUS BASEFLOW RECESSION COEFF
C                       (USED ONLY IF PO(9) = 2)
C
C#######################################################################
C
      CHARACTER*80 CARD
C
      DIMENSION TSIDBF(2),TSIDRC(2),SUBNAM(2)
      INCLUDE 'common/fdbug'
      INCLUDE 'common/ionum'
C
      DIMENSION PO(*),CO(*)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin38.f,v $
     . $',                                                             '
     .$Id: pin38.f,v 1.4 2002/02/11 19:02:11 dws Exp $
     . $' /
C    ===================================================================
C
      DATA DIML3T/4hL3/T/,DLES/4hDLES/,SUBNAM/4hPIN3,4h8   /,NOP/38/
      DATA PCTD/4hPCTD/
C
C  CALL DEBUG CHECK ROUTINE AND SET INITIAL VARIABLES.
C
      CALL FPRBUG(SUBNAM,1,NOP,IFDEB)
      IVERS=1
      IF(IFDEB)110,110,4110
C
C  READ IN METRIC/ENGLISH SWITCH, BASEFLOW TIME SERIES ID,BASEFLOW TIME
C  SERIES DATA TYPE, BASEFLOW TIME SERIES TIME INTERVAL, AREA, CONSTANT
C  BASEFLOW VALUE AND VARIABLE BASEFLOW OPTION INDICATOR.
C
110   READ(IN,7110)METENG,TSIDBF,DTCBF,IDELTA,AREA,CBFLOW,IVBF
      IF(IFDEB)115,115,4115
C
C  CHECK TO SEE IF TIME SERIES JUST READ IN IS DEFINED FOR THIS OPERATIO
C  NOTE:  NO MISSING VALUES ALLOWED, AND ONLY ONE VALUE PER TIME INTERVA
C
115   ICHKDM=1
      MISS=0
      NOVAL=1
      IERR=0
      CALL CHEKTS(TSIDBF,DTCBF,IDELTA,ICHKDM,DIML3T,MISS,NOVAL,IERR)
C
C  CHECK TO SEE IF OTHER VARIABLES READ IN ARE WITHIN ALLOWED LIMITS.
C
      IF((METENG.LT.0).OR.(METENG.GT.1))GO TO 5130
135   IF(METENG)140,140,160
140   IF(AREA.LE.0.)GO TO 5150
145   IF(CBFLOW.LT.0.)GO TO 5155
      GO TO 170
160   IF(AREA.LE.0.)GO TO 5170
165   IF(CBFLOW.LT.0.)GO TO 5175
170   IF((IVBF.LT.0).OR.(IVBF.GT.2))GO TO 5180
C
C  READ IN OTHER PARAMETRIC DATA, IF NEEDED, AND CHECK FOR ERRORS.
C
175   IF(IVBF-1)180,190,200
180   NVBFP=0
      IUSEC=0
      GO TO 220
190   READ(IN,7190)BFREC
      IF(IFDEB)195,195,4195
195   IF((BFREC.LT.0.5).OR.(BFREC.GE.1.0)) THEN
         if (bfrec.lt.0.5) goto 5195
         if (abs(bfrec-0.999).gt.0.001) goto 5195
         temp=bfrec
         bfrec=0.999
         write(ipr,9196) temp,bfrec
         call warn
         endif
197   NVBFP=1
      IUSEC=1
      GO TO 220
200   READ(IN,7200)TSIDRC,DTCRC
      IF(IFDEB)205,205,4205
C
C  CHECK TO SEE IF TIME SERIES JUST READ IN IS DEFINED FOR THIS OPERATIO
C  NOTE:  MISSING VALUES ARE ALLOWED, 24 HOUR TIME INTERVAL.
C
205   ICHKDM=1
      IDEL=24
      MIS=1
      NOVAL=1
      IERR=0
      CALL CHEKTS(TSIDRC,DTCRC,IDEL,ICHKDM,DLES,MIS,NOVAL,IERR)
      CALL FDCODE(DTCRC,SUNITS,SDIMS,MISS,NOVALS,TSCALE,IADD,IER)
      IF(SUNITS.NE.PCTD)GO TO 5217
217   NVBFP=3
      IUSEC=2
220   IUSEP=17+NVBFP
C
C  NOW READ IN CARRYOVER VALUES (IF NECESSARY)
C
      IF(IUSEC-1)400,350,300
300   READ(IN,'(A)') CARD
      READ(CARD,7300,ERR=302)TBFLOW,BFREC
      GO TO 303
302   CALL FRDERR (IPR,' ',CARD)
303   IF(IFDEB)301,301,4310
C
C  CHECK VALIDITY OF VALUES JUST READ IN.
C
301   IF(METENG-1)305,310,310
305   IF(TBFLOW.LT.CBFLOW)GO TO 5305
      GO TO 320
310   IF(TBFLOW.LT.CBFLOW)GO TO 5310
320   IF((BFREC.LT.0.5).OR.(BFREC.GE.1.0)) THEN
         if (bfrec.lt.0.5) goto 5320
         if (abs(bfrec-0.999).gt.0.001) goto 5320
         temp=bfrec
         bfrec=0.999
         write(ipr,9196) temp,bfrec
         call warn
         endif
      GO TO 400
350   READ(IN,7350)TBFLOW
      IF(IFDEB)360,360,4350
C
C  CHECK VALIDITY OF VALUE JUST READ IN.
C
360   IF(METENG)365,365,370
365   IF(TBFLOW.LT.CBFLOW)GO TO 5365
      GO TO 400
370   IF(TBFLOW.LT.CBFLOW)GO TO 5370
C
C  LOAD PO AND CO ARRAYS.
C
C  CHECK P ARRAY TO MAKE SURE SPACE IS AVAILABLE TO STORE PARAMETRIC
C  DATA.
C
400   CALL CHECKP(IUSEP,LEFTP,IERR)
      IF(IERR)420,420,410
410   IUSEP=0
      GO TO 500
C
C  NOW STORE PARAMETRIC DATA IN THE PO ARRAY.  FIRST, CHECK TO SEE
C  IF AREA AND CBFLOW VARIABLES SHOULD BE CONVERTED FROM ENGLISH
C  TO METRIC UNITS.
C
420   IF(METENG)421,421,425
421   AREA=AREA*2.58999
      CBFLOW=CBFLOW*0.028317
425   PO(1)=IVERS+0.01
      PO(2)=METENG+0.01
      PO(3)=TSIDBF(1)
      PO(4)=TSIDBF(2)
      PO(5)=DTCBF
      PO(6)=IDELTA+0.01
      PO(7)=AREA
      PO(8)=CBFLOW
      PO(9)=IVBF+0.01
      PO(10)=NVBFP+0.01
      PO(11)=IUSEP+0.01
      PO(12)=IUSEC+0.01
      PO(13)=0.01
      PO(14)=0.01
      PO(15)=0.01
      PO(16)=0.01
      PO(17)=0.01
      IF(IVBF-1)500,430,440
430   PO(18)=BFREC
      GO TO 500
440   PO(18)=TSIDRC(1)
      PO(19)=TSIDRC(2)
      PO(20)=DTCRC
C
C  CHECK TO SEE IF ANY CARRYOVER IS TO BE STORED
C
500   IF(IUSEC)10000,10000,501
C
C  CHECK C ARRAY TO MAKE SURE SPACE IS AVAILABLE TO STORE
C  CARRYOVER DATA
C
501   CALL CHECKC(IUSEC,LEFTC,IERR)
      IF(IERR)515,515,510
510   IUSEC=0
      GO TO 10000
C
C  CHECK TO SEE IF THE TOTAL BASEFLOW VALUE SHOULD BE CONVERTED
C  FROM ENGLISH TO METRIC UNITS.
C
515   IF(METENG-1)516,520,520
516   TBFLOW=TBFLOW*0.028317
520   IF(IUSEC-1)10000,530,540
530   CO(1)=TBFLOW
      GO TO 10000
540   CO(1)=TBFLOW
      CO(2)=BFREC
      GO TO 10000
C
C######################################################################
C
C  DEBUG WRITE STATEMENTS
C
C######################################################################
C
4110  WRITE(IODBUG,8110)SUBNAM,IVERS
      GO TO 110
4115  WRITE(IODBUG,8115)METENG,TSIDBF,DTCBF,IDELTA,AREA,CBFLOW,IVBF
      GO TO 115
4195  WRITE(IODBUG,8195)BFREC
      GO TO 195
4205  WRITE(IODBUG,8205)TSIDRC,DTCRC
      GO TO 205
4310  WRITE(IODBUG,8310)TBFLOW,BFREC
      GO TO 301
4350  WRITE(IODBUG,8350)TBFLOW
      GO TO 360
C
C#######################################################################
C
C  ERROR AND WARNING WRITE STATEMENTS
C
C#######################################################################
C
5130  WRITE(IPR,9130)METENG
      CALL ERROR
      GO TO 135
5150  WRITE(IPR,9150)AREA
      CALL ERROR
      GO TO 145
5155  WRITE(IPR,9155)CBFLOW
      CALL ERROR
      GO TO 170
5170  WRITE(IPR,9170)AREA
      CALL ERROR
      GO TO 165
5175  WRITE(IPR,9175)CBFLOW
      CALL ERROR
      GO TO 170
5180  WRITE(IPR,9180)IVBF
      CALL ERROR
      GO TO 175
5195  WRITE(IPR,9195)BFREC
      CALL ERROR
      GO TO 197
5217  WRITE(IPR,9217)PCTD,DTCRC,SUNITS
      CALL ERROR
      GO TO 217
5305  WRITE(IPR,9305)CBFLOW,TBFLOW
      CALL WARN
      TBFLOW=CBFLOW
      GO TO 320
5310  WRITE(IPR,9310)CBFLOW,TBFLOW
      CALL WARN
      TBFLOW=CBFLOW
      GO TO 320
5320  WRITE(IPR,9320)BFREC
      CALL ERROR
      GO TO 400
5365  WRITE(IPR,9305)CBFLOW,TBFLOW
      CALL WARN
      TBFLOW=CBFLOW
      GO TO 400
5370  WRITE(IPR,9310)CBFLOW,TBFLOW
      CALL WARN
      TBFLOW=CBFLOW
      GO TO 400
C
C####################################################################
C
C  INPUT FORMAT STATEMENTS
C
C####################################################################
C
7110  FORMAT(I1,3X,2A4,2X,A4,1X,I2,3X,F9.0,1X,F9.0,1X,I1)
7190  FORMAT(F5.3)
7200  FORMAT(2A4,1X,A4)
7300  FORMAT(F9.0,5X,F5.3)
7350  FORMAT(F9.0)
C
C####################################################################
C
C  DEBUG FORMAT STATEMENTS
C
C####################################################################
C
8110  FORMAT(/5X,'DEBUG OUTPUT FOR SUBROUTINE ',2A4,' VERSION ',I2)
8115  FORMAT(/20X,'METENG = ',I4,/20X,'TSIDBF = ',2A4,
     1       /20X,'DTCBF  = ',A4,/20X,'IDELTA = ',I4,
     2       /20X,'AREA   = ',F9.1,/20X,'CBFLOW = ',F9.1,
     3       /20X,'IVBF   = ',I4)
8195  FORMAT(/20X,'BFREC = ',F5.3)
8205  FORMAT(/20X,'TSIDRC = ',2A4,/20X,'DTCRC  = ',A4)
8310  FORMAT(/20X,'TBFLOW = ',F9.1,/20X,'BFREC  = ',F5.3)
8350  FORMAT(/20X,'TBFLOW = ',F9.1)
C
C####################################################################
C
C  ERROR AND WARNING FORMAT STATEMENTS
C
C####################################################################
C
9130  FORMAT(/10X,'** ERROR **   THE METRIC/ENGLISH SWITCH MUST ',
     1       'EQUAL 0 OR 1.',/24X,'THE VALUE ENTERED WAS ',I4)
9150  FORMAT(/10X,'** ERROR **   AREA MUST EXCEED ZERO SQUARE MILES.',
     1       /24X,'THE VALUE ENTERED WAS ',F9.0)
9155  FORMAT(/10X,'** ERROR **   CONSTANT BASEFLOW MUST BE GREATER ',
     1       'THAN OR EQUAL TO ZERO CFS.',/24X,'THE VALUE ENTERED WAS ',
     2       F9.0)
9170  FORMAT(/10X,'** ERROR **   AREA MUST EXCEED ZERO SQUARE ',
     1       'KILOMETERS.',/24X,'THE VALUE ENTERED WAS ',F9.0)
9175  FORMAT(/10X,'** ERROR **   CONSTANT BASEFLOW MUST BE GREATER ',
     1       ' THAN OR EQUAL TO ZERO CMS.',/24X,'THE VALUE ENTERED ',
     2       'WAS ', F9.0)
9180  FORMAT(/10X,'** ERROR **   THE VARIABLE BASEFLOW OPTION ',
     1       'INDICATOR MUST EQUAL',/24X,'ONE OF THE FOLLOWING ',
     2       'VALUES:  0, 1 OR 2.',/24X,'THE VALUE ENTERED WAS ',
     3        I4)
9195  FORMAT(/10X,'** ERROR **   CONSTANT RECESSION COEFFICIENT FOR ',
     1       'BASEFLOW MUST EQUAL',/24X,'A VALUE BETWEEN 0.50 AND ',
     2       '0.999 INCLUSIVELY.',/24X,'THE VALUE ENTERED WAS ',F5.3)
9196  FORMAT(/10X,'** WARNING **   CONSTANT RECESSION COEFFICIENT FOR ',
     1       'BASEFLOW MUST EQUAL',/24X,'A VALUE BETWEEN 0.50 AND ',
     2       '0.999 INCLUSIVELY.',/24X,'THE VALUE ENTERED WAS ',F5.3,/,
     3       24X,'THIS VALUE WILL BE RESET TO ',F5.3)
9217  FORMAT(/10X,'** ERROR **   STANDARD UNITS OF THE RECESSION ',
     1       'COEFFICIENT DATA TYPE MUST BE ',A4,/24X,'DATA TYPE ',
     2       A4,' HAS UNITS OF ',A4)
9305  FORMAT(/10X,'** WARNING ** TOTAL PREVIOUS BASEFLOW MUST EQUAL ',
     1       'OR EXCEED THE CONSTANT BASEFLOW OF ',F9.0,' CFS.',/24X,
     2       'THE TOTAL PREVIOUS BASEFLOW VALUE ENTERED WAS ',F9.0,
     3       ' CFS.',/24X,'TOTAL PREVIOUS BASEFLOW WAS SET EQUAL TO ',
     4       'CONSTANT BASEFLOW BEFORE BEING STORED AS CARRYOVER.')
9310  FORMAT(/10X,'** WARNING ** TOTAL PREVIOUS BASEFLOW MUST EQUAL ',
     1       'OR EXCEED THE CONSTANT BASEFLOW OF ',F9.0,' CMS.',/24X,
     2       'THE TOTAL PREVIOUS BASEFLOW VALUE ENTERED WAS ',F9.0,
     3       ' CMS.',/24X,'TOTAL PREVIOUS BASEFLOW WAS SET EQUAL TO ',
     4       'CONSTANT BASEFLOW BEFORE BEING STORED AS CARRYOVER.')
9320  FORMAT(/10X,'** ERROR **   BASEFLOW RECESSION COEFFICIENT ',
     1       'LIMITS ARE 0.5 AND 0.999 INCLUSIVELY.',/24X,
     2       'THE VALUE ENTERED WAS ',F9.5)
10000 RETURN
      END
