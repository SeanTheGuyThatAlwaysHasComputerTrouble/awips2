C MEMBER PIN36
C  (from old member FCPIN36)
C
      SUBROUTINE PIN36(PL,LEFTP,IUSEP,CL,LEFTC,IUSEC)
C.......................................
C     THIS IS THE INPUT SUBROUTINE FOR THE XINANJIANG BASIN MODEL
C       OPERATION.                 THIS SUBROUTINE
C        INPUTS ALL CARDS FOR THE OPERATION AND FILLS THE
C        PL ARRAY AND PLACES INITIAL VALUES OF THE
C        STATE VARIABLES INTO THE CL ARRAY.
C.......................................
C     SUBROUTINE INITIALLY WRITTEN BY. . .
C            QINGPING ZHU  -YRCC CHINA  JUNE 1988   VERSION 1
C                MODIFIED BY . . .
C            ERIC ANDERSON - HRL        JULY 1988
C.......................................
      DIMENSION PL(1),CL(1)
      DIMENSION ANAME(5),PXID(2),ROID(2),PEID(2),CO(7)
      DIMENSION NDAYS(12),SNAME(2)
C
C
      REAL KG,KSS,IMP,K
C     COMMON BLOCKS.
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
      COMMON/FPROG/MAINUM,VERS,VDATE(2),PNAME(5),NDD
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin36.f,v $
     . $',                                                             '
     .$Id: pin36.f,v 1.1 1995/09/17 18:48:32 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA DL/4HL   /
      DATA PRST/4HPRST/
      DATA BLANK,SUMS/4H    ,4HSUMS/
      DATA SNAME/4HPIN3,4H6   /
      DATA NDAYS/31,28,31,30,31,30,31,31,30,31,30,31/
C.......................................
C     CHECK TO SEE IF DEBUG OUTPUT IS NEEDED FOR THIS OPERATION.
      CALL FPRBUG (SNAME,1,36,IBUG)
C.......................................
C     INITIALIZE VAILABLES
      IVER=1
      IUSEP=0
      IUSEC=0
      NOFILL=0
      IET=0
      ICLX=1
      IPLPM=26
      IPLET=IPLPM+13
      IPXRO=IPLET+24
      NWORD=6
      JDT=24
C.......................................
C     READ INPUT CARDS AND MAKE CHECKS
C
C     INPUT CARD NO. 1.
      READ (IN,900) ANAME,NUNIT,IDT,PROT,STORE
  900 FORMAT (5A4,3X,I2,3X,I2,10X,A4,1X,A4)
      IPRINT=0
      IF(PROT.NE.PRST) GO TO 100
      IPRINT=1
  100 ISUM=0
      IF (MAINUM.LT.3) GO TO 101
      IF(STORE.NE.SUMS) GO TO 101
      ISUM=1
C     CHECK NUMBER OF UNITS
  101 IF(NUNIT.GE.1.AND.NUNIT.LE.25) GOTO 102
      WRITE(IPR,901) NUNIT
  901 FORMAT(1H0,10X,32H**ERROR** NUMBER OF SUB-BASINS =,2X,I2,2H. ,
     139H ONLY NUMBERS FROM 1 TO 25 ARE ALLOWED.)
      CALL ERROR
       NOFILL=1
C     CHECK COMPUTATIONAL TIME INTERVAL
  102 IF (((24/IDT)*IDT).EQ.24) GO TO 103
      WRITE (IPR,902) IDT
  902 FORMAT (1H0,10X,24H**ERROR** TIME INTERVAL=,I2,5X,
     158HONLY INTERVALS OF 1,2,3,4,6,8,12 AND 24 HOURS ARE ALLOWED.)
      CALL ERROR
C     INPUT NO. 2. CARD (MODEL PARAMETERS).
  103 READ(IN,903) K,IMP,WUM,WLM,WDM,SM,B,EX,C,KSS,KG,CI,CG,IOPTET
  903 FORMAT(F5.2,F5.3,4F5.0,2F5.2,5F5.3,4X,I1)
C     INSURE THAT CAPACITIES ARE NOT EQUAL TO 0.0
      IF(WUM.LE.0.1) WUM=0.1
      IF(WLM.LE.0.1) WLM=0.1
      IF(WDM.LE.0.1) WDM=0.1
      IF(KSS.LE.0.01) KSS=0.01
      IF(SM.LE.0.1) SM=0.1
      IF(IOPTET.NE.1) IOPTET=0
C
      ICHGE=0
C     PARAMETER CHECKS -- TO ELIMINATE IMPOSSIBLE VALUES.
      IF (C.LE.1.0) GO TO 104
      C=1.0
      ICHGE=ICHGE+1
  104 IF (IMP.LT.0.50) GO TO 106
      IMP=0.00
      ICHGE=ICHGE+1
  106 IF(B.GT.0.0001) GO TO 121
      B=0.1
      ICHGE=ICHGE+1
  121 IF(CI.LE.1.0) GO TO 122
      CI=1.0
      ICHGE=ICHGE+1
  122 IF(CG.LE.1.0) GOTO 123
      CG=1.0
      ICHGE=ICHGE+1
  123 IF(KG+KSS.LT.1.0) GO TO 125
      KG=0.001
      KSS=0.01
      ICHGE=ICHGE+1
  125 IF(ICHGE.EQ.0) GO TO 126
      WRITE (IPR,910) ICHGE
  910 FORMAT (1H0,10X,  11H**WARNING**,1X,I2,1X,69HPARAMETERS WERE CHANG
     1ED BECAUSE THEY WERE ASSIGNED IMPOSSIBLE VALUES.)
      CALL WARN
C
C.......................................
C     STORAGE REQUIREMENTS KNOWN . CHECK SPACE IN P ARRAY TO SEE IF
C     ET-DEMAND OR PE-ADJUSTMENT VALUES CAN BE PUT IN DIRECTLY AS
C     CAN PRECIP. AND CHANNEL INFLOW INFORMATION.
  126 NEEDP=IPLET+23
      IF(NOFILL.EQ.1) GO TO 127
      NEEDP=NEEDP+NUNIT*NWORD
      IF ((MAINUM.GT.2).AND.(IPRINT.GT.0)) NEEDP=NEEDP+15
  127 CALL CHECKP(NEEDP,LEFTP,IERR)
      IF (IERR.EQ.0) GO TO 135
      NOFILL=1
      READ (IN,933) PEID,PETYPE
      GO TO 140
C
C
C     INPUT NO. 3. CARD (PE INFORMATION )
  135 J=IPLET+11
      READ (IN,933) PEID,PETYPE,(PL(I),I=IPLET,J)
  933 FORMAT (2X,2A4,1X,A4,5X,12F4.2)
      IF ((PEID(1).EQ.BLANK).AND.(PEID(2).EQ.BLANK)) GO TO 136
      IET=1
      CALL CHEKTS(PEID,PETYPE,JDT,1,DL,0,1,IERR)
C     COMPUTE DAILY INCREMENT IN DEMAND CURVE OR
C        PE-ADJUSTMENT CURVE.
  136 DO 138 I=2,13
      ND=NDAYS(I-1)
      M=IPLET+I-1
      L=M-1
      IF (I.EQ.13) M=IPLET
      J=L+12
      PL(J)=(PL(M)-PL(L))/ND
  138 CONTINUE
C ......................................
C     INPUT CARD NO .4. - NO. 3+UNIT.
C     PRECIP. & CHANNEL INFLOW INFORM. CARD (REPEAT FOR EACH UNIT).
C
  140 DO 105 I=1,NUNIT
      READ(IN,906) PXID(1),PXID(2),PXTYPE,ROID(1),ROID(2),ROTYPE
  906 FORMAT(2X,2A4,1X,A4,5X,2A4,1X,A4)
      CALL CHEKTS(PXID,PXTYPE,IDT,1,DL,0,1,IERR)
      CALL CHEKTS(ROID,ROTYPE,IDT,1,DL,0,1,IERR)
      IF (NOFILL.EQ.1) GO TO 105
      J=IPXRO+(I-1)*NWORD
      PL(J)=PXID(1)
      PL(J+1)=PXID(2)
      PL(J+2)=PXTYPE
      PL(J+3)=ROID(1)
      PL(J+4)=ROID(2)
      PL(J+5)=ROTYPE
  105 CONTINUE
C......................................
C     CHECK SPACE IN C ARRAY TO SEE IF STATE VARIABLES
C        CAN BE PUT IN DIRECTLY.
      NEEDC=NUNIT*7
      IF (ISUM.EQ.1) NEEDC=NEEDC+10*NUNIT
      CALL CHECKC(NEEDC,LEFTC,IERR)
      IF(IERR.EQ.0) GO TO 130
      NOFILL=1
      CALL ERROR
C     INPUT NO. 4+NUNIT - NO. 3+2*NUNIT.CARD (STATE VARIABLES )
C     REPEAT FOR EACH UNIT.
  130 DO 115 I=1,NUNIT
      READ(IN,920) WUC,WLC,WDC,SC,QIC,QGC,FRC
  920 FORMAT (4F5.1,2F5.2,F5.3)
C     CHECK STATE VARIABLES FOR ILLEGITIMATE VALUES.
      CO(1)=WUC
      CO(2)=WLC
      CO(3)=WDC
      CO(4)=SC
      CO(5)=QIC
      CO(6)=QGC
      CO(7)=FRC
      L=0
      IF (WUC.LE.WUM) GO TO 20
      WUC=WUM
      L=1
   20 IF (WLC.LE.WLM) GO TO 21
      WLC=WLM
      L=1
   21  IF (WDC.LE.WDM) GO TO 22
      WDC=WDM
      L=1
   22 IF(SC.LE.SM) GO TO 23
      SC=SM
      L=1
   23 IF(FRC.LE.(1.0-IMP)) GO TO 24
      FRC=1.0-IMP
      L=1
   24 IF (L.EQ.0) GO TO 141
      WRITE(IPR,922) I
  922 FORMAT (1H0,10X,46H**WARNING**  INITIAL STATE VARIABLES FOR UNIT ,
     1 I2,27H CONTAIN IMPOSSIBLE VALUES.)
      WRITE(IPR,923)
  923 FORMAT(1H0,41X,5H WUC ,2X,5H WLC ,2X,5H WDC ,2X,5H SC  ,2X,
     15H QIC ,2X,5H QGC ,1X,5H FRC )
      WRITE (IPR,924) CO
  924 FORMAT (1H0,16X,19HINITIAL VALUES WERE,3X,4F7.1,2F7.2,F7.2)
      WRITE (IPR,925) WUC,WLC,WDC,SC,QIC,QGC,FRC
  925 FORMAT (1H0,25X,10HCHANGED TO,3X,4F7.1,2F7.2,F7.2)
      CALL WARN
C
  141 IF(NOFILL.EQ.1)  GO TO 115
C     STORE  C ARRAY IF SPACE IS AVAILABLE   .
      CL(ICLX)=WUC
      CL(ICLX+1)=WLC
      CL(ICLX+2)=WDC
      CL(ICLX+3)=SC
      CL(ICLX+4)=QIC
      CL(ICLX+5)=QGC
      CL(ICLX+6)=FRC
      ICLX=ICLX+7
  115 CONTINUE
      IF(NOFILL.EQ.1) GO TO 152
      IF (ISUM.EQ.0) GO TO 152
      M=10
C     INITIALIZE SPACES FOR STORING SUMS.
      DO 116 I=1,NUNIT
      L=ICLX-1+(I-1)*M
      DO 116 J=1,M
  116 CL(L+J)=0.0
C.......................................
C     READ CARD NO. 4+2*NUNIT
  152 IF((IPRINT.EQ.0).OR.(MAINUM.LT.3)) GO TO 154
      IPRINT=IPXRO+NWORD*NUNIT
      PL(IPRINT)=IPRINT+1
      IF(NOFILL.EQ.1) GO TO 155
      J=IPRINT
      READ (IN,926) (PL(I+J),I=1,14)
  926 FORMAT(14I5)
      GO TO 154
  155 READ(IN,926) IDUMMY
C     STORE REMAINING INFORMATION IN PL ARRAYS.
  154 IF (NOFILL.EQ.1) GO TO 190
      PL(1)=IVER+0.01
      PL(2)=IDT+0.01
      PL(3)=NUNIT+0.01
      DO 153 I=1,5
  153 PL(I+3)=ANAME(I)
      PL(9)=ISUM+0.01
      PL(10)=IPRINT+0.01
      PL(11)=IET+0.01
      PL(12)=PEID(1)
      PL(13)=PEID(2)
      PL(14)=PETYPE
      PL(15)=IPXRO+0.01
      PL(16)=NWORD+0.01
      PL(17)=NEEDP+0.01
      PL(18)=IPLET+0.01
      PL(19)=IPLPM+0.01
      PL(20)=IOPTET+0.01
      PL(21)=0.01
      PL(22)=0.01
      PL(23)=0.01
      PL(24)=0.01
      PL(25)=0.01
C
C      STORE PARAMETER VALUE
      PL(IPLPM)=K
      PL(IPLPM+1)=IMP
      PL(IPLPM+2)=WUM
      PL(IPLPM+3)=WLM
      PL(IPLPM+4)=WDM
      PL(IPLPM+5)=SM
      PL(IPLPM+6)=B
      PL(IPLPM+7)=EX
      PL(IPLPM+8)=C
      PL(IPLPM+9)=KSS
      PL(IPLPM+10)=KG
      PL(IPLPM+11)=CI
      PL(IPLPM+12)=CG
C.......................................
      IUSEP=NEEDP
      IUSEC=NEEDC
      IF (IBUG.EQ.0) GO TO 190
C.......................................
C     DEBUG OUTPUT - PRINT PL( ) AND CL( ).
      WRITE (IODBUG,919) IUSEP,IUSEC
  919 FORMAT(1H0,29HCONTENTS OF PL AND CL ARRAYS.,5X,
     121HNUMBER OF VALUES--PL=,I3,2X,3HCL=,I2)
      WRITE (IODBUG,916) (PL(I),I=1,IUSEP)
  916 FORMAT (1H0,15F8.3)
      WRITE(IODBUG,918) (PL(I),I=1,IUSEP)
  918 FORMAT(1H0,15(4X,A4))
      WRITE (IODBUG,916) (CL(I),I=1,IUSEC)
      WRITE (IODBUG,917) NOFILL,NUNIT,LEFTP,LEFTC
  917 FORMAT (1H0,7HNOFILL=,I1,2X,6HNUNIT=,I2,2X,6HLEFTP=,I5,2X,6HLEFTC=
     1,I5)
C.......................................
C     DEFINITION OF SELECTED VARIABLES.
C        IPLET=FIRST STORAGE POSITION FOR ET-CURVE IN PL( )
C        IPXRO=FIRST STORAGE POSITION FOR RAINFALL INFORMATION IN PL( )
C        IPLPM=FIRST STORAGE POSITION FOR PARAMETERS IN PL( )
C        IVER=VERSION NUMBER FOR THIS OPERATION-NOT FOR THE PROGRAM
C        NOFILL=1 IF PL( ) AND CL( ) WERE NOT FILLED, OTHERWISE=0
C.......................................
C
C     CONTENTS OF THE PL ARRAY. -XINANJIANG BASIN MODEL OPERATION.
C
C     POSITION                    CONTENTS
C        1         VERSION NUMBER FOR THE OPERATION - INTEGER
C        2         COMPUTATIONAL TIME INTERVAL IN HOURS
C        3         COMPUTATIONAL NUMBER OF UNITS
C        4-8       GENERAL NAME FOR AREA OR POINT.
C         9       CONTROL FLAG FOR PRINTING WATER BALANCE OUTPUT
C                   =1   WATER BALANCE PRINTED ;   =0   OTHERWISE.
C        10        CONTROL FOR PRINTING DETAILED OUTPUT.
C                =0 DO NOT PRINT.
C               >0 PRINT DETAILED OUTPUT.   OPERATIONAL PROGRAM =1.
C                CALIBRATION - LOCATION IN PL OF MONTHS TO BE PRINTED.
C        11        CONTROL FLAG FOR USING PE TIME SERIES .
C                      =1 USE PE DATA ,=0 DO NOT USE PE DATA
C        12-13     I.D FOR EVAPORATION TIME SERIES
C        14        TYPE CODE FOR EVAPORATION TIME SERIES
C        15        LOCATION IN PL OF PRECITATION TIME SERIES INFOR.
C        16        NUMBER OF WORDS OF PREC. AND INFLOW T.S FOR EACH UNIT
C        17        NUMBER OF WORDS USED IN PL.
C        18         LOCATION IN PL OF SEASONAL PE CURVE.
C        19        LOCATION IN PL OF PARAMETERS.
C        20        DAILY ET DISTRIBUTION OPTION .
C                      =1   DIURNAL   .    =0   UNIFORM.
C        21-25     RESERVED FOR FUTURE USE.
C
C     ORDER FOR PARAMETER VALUES IS...
C                      1. K         2. IMP       3. WUM       4. WLM
C                      5. WDM       6  SM        7. B         8. EX
C                      9  C        10. KSS      11. KG       12. CI
C                     13  CG
C     SEASONAL ET CURVE INFORMATION IS...
C        1-12      SEASONAL ET-DEMAND OR PE-ADJUSTMENT CURVE (JAN-DEC)
C        13-24     DAILY INCREMENTS IN SEASONAL ET-DEMAND OR
C                        PE-ADJUSTMENT CURVE (FIRST VALUE IS FOR MID
C                        JAN. TO MID FEB.)
C               DETAIL OUTPUT INFORMATION -CALIBRATION.
C           1          LOCATION OF CURRENT POSION TO CHECK
C           2-15       MONTH +YEAR TO PRINT  (MAX OF 7 MONTHS ).
C
  190 CONTINUE
      RETURN
      END
