C MEMBER PIN27
C  (from old member FCPIN27)
C
C    THIS IS THE INPUT SUBROUTINE FOR THE WGRFC TABULAR OUTPUT
C    OPERATION TITLES 'LIST-FTW'
C
C    DAVE REED WGRFC 4/87
C
C
      SUBROUTINE PIN27(PO,LEFTP,IUSEP)
C
C    THIS IS THE INPUT SUBROUTINE FOR WGRFC TABULAR OUTPUT SUBROUITNE
C
C    THIS ROUTINE WILL READ IN ALL INFORMATION FOR EXECUTION OF THIS
C    OPERATION FOR NWSRFS
C
C    ARGUMENT LIST
C    ARG        DIM      TYPE      DESC
C    PO         (1)      REAL*4    PARAMETER ARRAY
C    LEFTP       1       I*4       NUMBER OF SPACES LEFT IN THE
C                                    PO ARRAY
C    IUSEP       1       I*4       NUMBER OF SPACES REQUIRED BY
C                                    THIS OPERATION
C
C
C    PROCEDURE
C    1. DIMENSION AND COMMON
C    2. CALL FPRBUG TO CHECK IF DEBUG OUTPUT IS NEEDED
C    3. CHECK TRACE LEVEL AND PRINT IF NEEDED
C    4. READ CARD NUMBER 1
C    5. CHECK THAT NUMBER OF TITLE LINES IS BETWEEN 1 AND 10
C    6. CHECK THAT NUMBER OF TIME SERIES IS GREATER THAN 0
C    7. CHECK THAT NUMBER OF COLUMNS IS BETWEEN 80 AND 132
C    8. IF NECESSARY,CALL CHEKRC TO SEE IF RATING CURVE INPUT IS DEFINED
C    9. CHECK IF NUMBER OF BLANK LINES IN PRINT OUT IS LESS THAN 10
C   10. CHECK THAT ALL BLANK LINE INDICATORS ARE BETWEEN 0 AND
C            NUMBER OF TIME SERIES
C   11. COMPUTE SPACE REQUIRED
C   12. CHECK IF THERE IS SUFFICIENT SPACE FOR OPERATION
C   13. READ TITLE LINES AND STORE IF POSSIBLE
C   14. BEGIN LOOP TO READ IN ALL TIME SERIES INFORMATION
C   15. READ IN TIME SERIES INFORMATION
C   16. CALL CHEKTS TO SEE IF TIME SERIES IS VALID
C   17. CHECK IF TSPRINT IS VALID AMOUNT
C   18. CHACK IF TSPRT IS EVEN MULTIPLE OF TSINT
C   19. CHACK IF CHAR LEFT OF DECIMAL IS =>1
C   20. CHECK IF PRINT CONTROL OPTION IS 0, 1, OR 2.
C   21. READ IN CONTROL CARD IF PRINT CONTROL OPTION IS 1 OR 2
C   22. CALL FCONVT AND FDCODE TO ACCESS UNITS INFORMATION
C   23. END TIME SERIES INPUT LOOP
C   24. CHECK THAT ALL TSPRT ARE MULTIPLE OF MIN TSPRT
C   25. CHECK TRACE AND PRINT IF NEEDED
C   26 SET IUSEP IF NO ERRORS
C   27. RETURN
C
C
C    CONTENTS OF THE PO ARRAY
C
C    POSITION     CONTENTS
C     1           VERSION NUMBER
C     2           SPACE NEEDED
C     3           NUMBER OF TITLE LINES
C     4           NUMBER OF TIME SERIES TO DISPLAY
C     5           NEW PAGE OPTION
C                  =0 , DO NOT BEGIN OUTPUT ONN A NEW PAGE
C                  =1, BEGIN OUTPUT ON A NEW PAGE
C     6           NUMBER OF COLUMNS TO USE FOR PRINT
C     7           4 CHARACTER FLAG TO REPRESENT THE END
C                   OF PREVIOUS HYDROLOGIC DAYS
C     8           4 CHARACTER FLAG TO BE USED TO MARK THE
C                   LAST DAY AND TI E OF DATA
C     9           4 CHARACTER FLAG TO REPRESENT THE END OF
C                   HYDROPLOGIC DAYS INTO THE FUTURE
C     10,11       8 CHARACTER RATUNG CURVE DEFINITION
C                        INFORMATION ABOUT THIS RATING CURVE
C                        ARE PRNOITED WHEN THE OPERATION IS EXECUTED
C     12          NUMBER OF BLANK LINES WHICH SOULD BE INCLUDED IN THE
C                   TABULAR OUTPUT
C     13-22       THE RELATIVE LINE NUMBER AFTER WHICH A BLANK
C                 LINE SHOULD APPEAR IN THE TABULAR OUTPUT
C     23-30      UNUSED
C     31-48      72 CHARACTER TITLE ;INES
C                  THIS IS REPEATED FOR EACH TITLE LINE WHICH
C                  SHOULD BE PRINTED OUT. THE NUMBER OF TITLE
C                  LINES IS CONTAINED IN PO(3)
C
C    BEGINNING AT LOCATION IPT=30+(NUM TITLE LINES*18)
C    IPT+1,IPT+2   TIME SERIES ID TO BE TABULATED
C    IPT+3         TIME SERIES TYPE
C    IPT+4         TIME SERIES TIME INTERVAL
C    IPT+5         TIME INTERVAL TO PRINT TIME SERIES
C    IPT+6         PRINT MISSING DATA FLAG
C                   =0, DO NOT PRINT MISSING DATA
C                   =1, PRINT MISSING DATA
C    IPT+7         4 CHARACTER RFC IDENTIFIER OF TIE SERIES
C    IPT+8         NUMBER OF ORDINATES BASED ON TIME SERIES
C                   PRINT INTERVAL TO CARRY OUT TO THE FUTURE
C    IPT+9         NUMBER OF CHARACTERS TO PRINT TO THE RIGHT
C                   OF THE DECIMAL
C    IPT+10        NUMBER OF DIGITS TO PRINT TO THE LEFT OF THE DRCIMAL
C    IPT+11        TIME SERIES PRINT CONTROL TYPE
C                   =0, ALWAYS PRINT TIME SERIES
C                   =1, PRINT THIS TIME SERIES IF IT EXCEEDS A
C                      SPECIFIED LIMIT
C                   =2, A CONTROL TIME SERIES WHICH IS USED TO TURN
C                      ON OR OFF ALL OUTPUT FOR A SPECIFIED RUN
C    IPT+12        VOLUME PRINT OPTION
C                   =0, EXPRESS VOLUMES A CUBIC FEET OR M**3
C                   =1, EXPRESS VOLUMES AS VOLUMETRIC RATES PER UNIT
C                       TIME
C    IPT+13        CRITERI A LIMIT VALUE
C    IPT+14        UNITS OF CRITERIA VALUE
C                   =0, ENGLISH UNITS
C                   =1, METRIV UNITS
C    IPT+15          4 CHARACTER CODE REPRESENTING METRIC UNITS
C    IPT+16         4 CHARACTER CODE REPRESENTING ENGLIST UNITS
C    IPT+17         MULTIPLICATION CONVERSION FACTOR TO
C                 GO FROM METRIC TO ENGLISH
C    IPT+18          OFFSET FACTOR TO GO FROM METRIC TO ENGLISH UNITS
C
C    THIS SEQUENCE IS REPEATED FOR EACH TIME SERIES . THE
C    INFORMATION FOR THE SECOND TIME SERIES IMMEDIATELY FORLLOWS
C    INFORMATION ON THE FIRST. IPT IS INCREMENTED NY 18.
C
C    THE TOTAL SPACE REQUIRED IS
C     SPACE=30+NTITL*18+NTS*18
C
C    COMMON AND DIMENSION STATEMENTS
C
      DIMENSION RTMP(2),TSID(2),IBLKA(10),PO(1),SNAME(2)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin27.f,v $
     . $',                                                             '
     .$Id: pin27.f,v 1.1 1995/09/17 18:48:20 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA RBLNK,SNAME/4H    ,4HPIN2,4H7   /
      DATA RL3/4HL3  /
C
C    SET IVER TO VERSION NUMBER - IT WILL BE LOADED INTO PO ARRAY IF NO
C
C
C    CURRENT VERSION IS VERSION 1 - APRIL 1987
C
      IVER=1
C
C
C    2 CALL FPRBUG TO CHECK FOR DEBUG OUTPUT
C
      I1=1
      I2=27
      CALL FPRBUG(SNAME,I1,I2,IBUG)
C
C    3 PRINT TRACE MESSAGE IF REQUIRED
C
      IF(ITRACE.GE.1)WRITE(IODBUG,900)
  900 FORMAT(1H0,19H** PIN27 ENTERED **)
C
C    4 READ IN CARD 1
C
      READ(IN,901)NTITL,NTS,NP,NCOL,YDAFLG,TDAFLG,TMAFLG,RTMP,NBLKLN,
     1IBLKA
  901 FORMAT(3X,I2,3X,I2,4X,I1,2X,I3,1X,A4,1X,A4,1X,A4,2X,2A4,
     13X,I2,10I2)
      IF(IBUG.EQ.1)WRITE(IODBUG,1900)NTITL,NTS,NP,NCOL,YDAFLG,
     1TDAFLG,TMAFLG,RTMP,NBLKLN,IBLKA
 1900 FORMAT(1H0,10X,25HNTITL NTS NP NCOL YDAFLG    ,
     142HTDAFLG TMAFLG RATING CURVE  NBLKLN  IBLKA  ,/,
     211X,I5,I4,I3,I5,1X,A4,3X,A4,3X,A4,3X,2A4,6X,I6,2X,10I3)
C
C    CHECK IF NTITL IN RANGE    1<=NTITL<=10
C
      IER=0
      IF(NTITL.GE.1.AND.NTITL.LE.10)GOTO 100
C
      IER=IER+1
      WRITE(IPR,902)NTITL
  902 FORMAT(1H0,10X,33H**ERROR** NUMBER OF TITLE LINES =,I2,
     157H. IT MUST BE GREATER THAN 0 AND LESS THAN OR EQUAL TO 10.)
      CALL ERROR
C
C    6 CHECK IF NTS >0
C
  100 IF(NTS.GT.0)GOTO 110
      WRITE(IPR,903)NTS
  903 FORMAT(1H0,10X,44H**ERROR** NUMBER OF TIME SERIES TO TABULATE=,
     1I3,28H. IT MUST BE GREATER THAN 0.)
      IER=IER+1
      CALL ERROR
C
C    7 CHECK THAT NCOL IS WITHIN LIMITS    80<=NCOL<=132
C
  110 IF(NCOL.LE.132)GOTO 120
      WRITE(IPR,904)NCOL
  904 FORMAT(1H0,10X,35H**ERROR** NUMBER OF PRINT COLUMNS =,I3,
     136H. IT MUST BE LESS THAN OR EQUAL 132.)
      IER=IER+1
      CALL ERROR
C
C    CALL CHEKRC TO SEE IF RATING CURVE IS VALID
C
  120 IF(RTMP(1).EQ.RBLNK.AND.RTMP(2).EQ.RBLNK)GOTO 130
      CALL CHEKRC(RTMP,IERFLG)
      IF(IERFLG.NE.0)IER=IER+1
C
C    9 CHECK NUMBER OF BLANK LINES
C
  130 IF(NBLKLN.LE.10.AND.NBLKLN.GE.0)GOTO 140
      WRITE(IPR,905)NBLKLN
  905 FORMAT(1H0,10X,43H**ERROR** NUMBER OF BLANK LINES IN DISPLAY=,
     1I2,53H. IT MUST BE GREATER THAN OR EQUAL 0 AND LESS THAN OR,
     210H EQUAL 10.)
      IER=IER+1
      CALL ERROR
C
C    10 CHECK THAT ALL BLANK LINES ARE => 0  AND <=NTS
C
  140 DO 150 I=1,NBLKLN
      IF(IBLKA(I).LE.NTS.AND.IBLKA(I).GE.0)GOTO 150
      WRITE(IPR,906)I,IBLKA(I),NTS
  906 FORMAT(1H0,10X,42H**ERROR** OPTION TO PRINT BLANK LINE AFTER,
     123H A TIME SERIES - ENTRY ,I2,4H, IS ,I3,19H IT MUST BE BETWEEN,
     27H 0 AND ,I3,11H INCLUSIVE.)
      CALL ERROR
      IER=IER+1
C
C    11 COMPUTE APACE REQUIRED IF IER=1
C
C       SPACE REQUIRED=IUSEP=30+18*NTITL+18*NTS
C
C
  150 IF(IER.GT.0)GOTO 162
C
      NSP=30+18*NTITL+18*NTS
C
C    12 CHECK IF SUFFICIENT SPACE
C
      CALL CHECKP(NSP,LEFTP,IERFLG)
      IF(IERFLG.NE.0)IER=IER+1
C
C    IF NO ERRROS STORE DATA IN PO ARRAY
C
      IF(IER.NE.0)GOTO 162
C
      PO(1)=IVER+0.01
      PO(2)=NSP+0.1
      PO(3)=NTITL+.01
      PO(4)=NTS+0.01
      PO(5)=NP+0.01
      PO(6)=NCOL+0.1
      PO(7)=YDAFLG
      PO(8)=TDAFLG
      PO(9)=TMAFLG
      PO(10)=RTMP(1)
      PO(11)=RTMP(2)
      PO(12)=NBLKLN+0.1
      DO 161 I=1,10
  161 PO(I+12)=IBLKA(I)+0.1
C
C    INITIALIZE EXTRA SPACE IN ARRAY
C
      DO 163 I=23,30
  163 PO(I)=0.01
C
C
C    IF NO ERRORS FOUND, READIN  TITLE LINES AND STORE IN ARRAY
C
  162 DO 170 I=1,NTITL
      IF(IER.EQ.0)GOTO 159
      READ(IN,907)J
  907 FORMAT(18A4)
      GOTO 170
  159 I1=(I-1)*18+31
      I2=I1+17
      READ(IN,907)(PO(J),J=I1,I2)
  170 CONTINUE
C
C    14 BEGIN LOOP TO READ IN ALL TIME SERIES INFORMATION
C
      IPT=30+NTITL*18
      DO 200 I=1,NTS
C
C    READ IN INFO
C
      READ(IN,908)TSID,TSTYPE,ITSINT,ITSPRT,IPMSG,RFCID,IFUTR,ICRIT,
     1ICLFT,IPOPT,IVOL
  908 FORMAT(2A4,2X,A4,4X,I2,3X,I2,4X,I1,1X,A4,3X,I2,3X,I2,3X,I2,
     14X,I1,4X,I1)
      IF(IBUG.EQ.1)WRITE(IODBUG,1901)TSID,TSTYPE,ITSINT,ITSPRT,
     1IPMSG,RFCID,IFUTR,ICRIT,ICLFT,IPOPT,IVOL
 1901 FORMAT(1H0,10X,26HTSID      TSTYPE TSINT PRT  ,
     145HINT MSG RFCID  IFUTR ICRIT ICLFT IPOPT IVOL  ,
     2/,11X,2A4,2X,A4,I8,I7,I4,2X,A4,I7,I6,2I6,I5)
C
C    CALL CHEKTS TO CHECK TIME SERIES
      I1=0
      I2=1
      CALL CHEKTS(TSID,TSTYPE,ITSINT,I1,DCODE,I2,I2,IERFLG)
      IF(IERFLG.NE.0)IER=IER+1
C
C    17 CHEK THAT ITSPRT=1,2,3,6,12, OR 24
C
      IF(ITSPRT.EQ.1.OR.ITSPRT.EQ.2.OR.ITSPRT.EQ.3)GOTO 210
      IF(ITSPRT.EQ.6.OR.ITSPRT.EQ.12.OR.ITSPRT.EQ.24) GOTO 210
      WRITE(IPR,909)ITSPRT
  909 FORMAT(1H0,10X,46H**ERROR** TIME SERIES PRINT INTERVAL (ITSPRT)=,
     1I4,43H HRS. IT MUST BE 1, 2, 3, 6, 12, 0R 24 HRS.)
      CALL ERROR
      IER=IER+1
C
C    CHECK THET ITSPRTIS AN EVEN MULTIPLE OF ITSINT
C
  210 IF(((ITSPRT/ITSINT)*ITSINT).EQ.ITSPRT)GOTO 220
C
      WRITE(IPR,910)ITSPRT,ITSINT
  910 FORMAT(1H0,10X,38H**ERROR** TIME SERIES PRINT INTERVAL (,I2,
     154H) MUST BE AS LARGE AS AND AN EVEN MULTIPLE OF THE TIME,
     218H SERIES INTERVAL (,I2,2H).)
      IER=IER+1
      CALL ERROR
C
C   CHECK IF CHARACTERS LEFT OF DECIMAL IS GREATER THAN 0
C
  220 IF(ICLFT.LE.10.AND.ICLFT.GE.1)GOTO 230
      WRITE(IPR,911)ICLFT
  911 FORMAT(1H0,10X,36H**ERROR** DIGITS LEFT OF DECIMAL IS ,I2,
     142H. IT SHOULD BE BETWEEN 1 AND 10 INCLUSIVE.)
      CALL ERROR
      IER=IER+1
C
C    CHECK IF PRINT OPTION IS 0,1, OR 2 AND IF SO READ SECOND CARD
C    CONTAINING PRINT CRITERIA VARIABLES
C
  230 PCRIT=0.0
      IUNT=0
      IF(IPOPT.GE.0.AND.IPOPT.LE.2)GOTO 240
C
      WRITE(IPR,912)IPOPT
  912 FORMAT(1H0,10X,32H**ERROR** PRINT CRITERIA OPTION=,I2,
     122H. IT MUST BE 0,1,OR 2.)
      CALL ERROR
      IER=IER+1
C
  240 IF(IPOPT.NE.1.AND.IPOPT.NE.2)GOTO 241
      READ(IN,915)PCRIT,IUNT
  915 FORMAT(F15.5,4X,I1)
C
C    CHECK IF ENG/MET SWITCH IUNT =0 OR 1
C
      IF(IUNT.EQ.0.OR.IUNT.EQ.1)GOTO 241
      WRITE(IPR,916)IUNT
  916 FORMAT(1H0,10X,39H**ERROR** ENGLISH/METRIC UNITS OF PRINT,
     112H CRITERIA IS,I2,22H. IT SHOULD BE 0 OR 1.)
C
C    CALL FDCODE AND FCONVT TO OBTAIN ENGLISH AND METRIC UNITS
C
  241 CALL FDCODE(TSTYPE,RMDIM,DCODE,IMSG,INUM,SCAL,INUM,IERFLG)
      IF(IERFLG.NE.0)IER=IER+1
      CALL FCONVT(RMDIM,DCODE,REDIM,R1,R2,IERFLG)
      IF(IERFLG.NE.0)IER=IER+1
C
C    SET VOLUME PARAMETER IF L3
C
       IF(DCODE.NE.RL3)IVOL=0
      IF(DCODE.NE.RL3)GOTO 242
      IF(IVOL.NE.0) IVOL=1
      IF(IVOL.EQ.0)IVOL=-1
  242 CONTINUE
C
C    CHECK IF THERE WERE ANY ERRORS - IF SO DO NOT LOAD PO ARRAY
C
      IF(IER.NE.0)GOTO 200
C
      PO(IPT+1)=TSID(1)
      PO(IPT+2)=TSID(2)
      PO(IPT+3)=TSTYPE
      PO(IPT+4)=ITSINT+0.01
      PO(IPT+5)=ITSPRT+0.01
      PO(IPT+6)=IPMSG+0.01
      PO(IPT+7)=RFCID
      PO(IPT+8)=IFUTR+0.01
      PO(IPT+9)=ICRIT+0.01
      PO(IPT+10)=ICLFT+0.01
      PO(IPT+11)=IPOPT+0.01
      PO(IPT+12)=IVOL+0.01
      PO(IPT+13)=PCRIT
      PO(IPT+14)=IUNT+0.01
      PO(IPT+15)=RMDIM
      PO(IPT+16)=REDIM
      PO(IPT+17)=R1
      PO(IPT+18)=R2
      IP1=IPT+1
C
      IPT=IPT+18
C
C  THATS ALL FOR THE TIME SERIES LOOP
C
  200 CONTINUE
C
C    CHECK THAT LL TIME SERIES TSPRT ARE AN EVEN MULTIPLE OF THE MIN
C
      MINTS=24
      IPT=30+NTITL*18
      DO 300 I=1,NTS
      IF(INT(PO(IPT+5)).LT.MINTS)MINTS=INT(PO(IPT+5))
      IPT=IPT+18
  300 CONTINUE
C
      IF(MINTS.LT.1)GOTO 320
      IPT=30+NTITL*18
      DO 310 I=1,NTS
      ITS=INT(PO(IPT+5))
      IF(((ITS/MINTS)*MINTS).EQ.ITS)GOTO 310
      WRITE(IPR,913)MINTS,I,ITS
  913 FORMAT(1H0,10X,45H**ERROR** MINIMUM TIME SERIES PRINT INTERVAL=,
     1I2,14H. TIME SERIES ,I2,20H HAS AN INTERVAL OF ,I2,
     231H. THESE MUST BE EVEN MULTIPLES.)
      CALL ERROR
      IER=IER+1
  310 CONTINUE
  320 CONTINUE
C
C    SET IUSEP IF NO ERRORS OCCURRED
C
      IUSEP=-1
      IF(IER.EQ.0)IUSEP=NSP
      IF(ITRACE.GE.1)WRITE(IODBUG,914)
  914 FORMAT(1H0,18H** PIN27 EXITED **)
      RETURN
      END
