C MEMBER PIN11
C  (from old member FCPIN11)
C
      SUBROUTINE PIN11(P,LEFTP,IUSEP,C,LEFTC,IUSEC)
C
C     THIS THE INPUT SUBROUTINE FOR LAYERED COEFFICIENT ROUTING.
C     THIS SUBROUTINE INPUTS ALL CARDS FOR THE OPERATION
C     AND FILLS THE P ARRAY AND PLACES INITIAL VALUES OF THE
C     STATE VARIABLES IN THE C ARRAY.
C
C     THIS SUBROUTINE ORIGINALLY WRITTEN BY
C          DAVID REED -HRL   OCT 1979
C
C
C
      DIMENSION P(1),C(1),TITLE(5),QIN(2),QOUT(2)
C
C
C     DEFINITION OF VARIABLES
C
C     QIN         INTERNAL IDENTIFIER FOR INFLOW TIME SERIES
C     QITYPE      DATA TYPE CODE FOR INFLOW TIME SERIES
C     IDTQI       TIME INTERVAL FOR INFLOW TIME SERIES
C     QOUT        INTERNAL IDENTIFIER FOR OUTFLOW TIME SERIES
C     QOTYPE      DATA TYPE CODE FOR OUTFLOW TIME SERIES
C     IDTQO       TIME INTERVAL FOR OUTFLOW TIME SERIES
C     TITLE       GENERAL TITLE OF REACH OR PROBLEM
C     NCOEFF      NUMBER OF LAYERS FOR ROUTING
C     ICVAL       CARRYOVER DECISION PARAMETER
C                 IF ICVAL=0,ALL RESIDUAL CARRYOVER SET TO 0 BY DEFAULT
C                 IF ICVAL=1,ALL RESIDUAL CARRYOVER IS READ FROM
C                      NEXT DATA CARD
C     COEFF       COEFFICIENTS FOR LAYERED COEFFICIENT ROUTING
C     QRANGE      MAXIMUM VALUE OF FLOW FOR A GIVEN LAYER
C     RES         RESIDUAL LAYER CARRYOVER
C
C
C     COMMON BLOCKS
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin11.f,v $
     . $',                                                             '
     .$Id: pin11.f,v 1.1 1995/09/17 18:48:03 dws Exp $
     . $' /
C    ===================================================================
C
      DATA DIMQI/4HL3/T/,DIMQO/4HL3/T/
C
C     CHECK TRACE LEVEL-- FOR THIS SUBROUTNE =1
C
      IF(ITRACE.GE.1) WRITE(IODBUG,900)
  900 FORMAT(1H0,16H** PIN11 ENTERED)
C
C     CHECK TO SEE IF DEBUG OUTPUT IS NEEDED
C
      IBUG=0
      IF(IDBALL.GT.0)IBUG=1
      IF(NDEBUG.EQ.0)GO TO 12
      DO 10 I=1,NDEBUG
      IF(IDEBUG(I).EQ.11) GO TO 11
   10 CONTINUE
      GO TO 12
   11 IBUG=1
C
C     READ IN CARD #1-TITLE,INFLOW AND OUTFLOW TIME SERIES IDENTIFIER,
C                    NUMBER OF LAYERS, AND CARRYOVER DECISION PARAMETERS
C
   12 READ(IN,901)TITLE,QIN,QITYPE,IDTQI,QOUT,QOTYPE,IDTQO,NCOEFF,ICVAL
  901 FORMAT(5A4,2X,2A4,1X,A4,3X,I2,2X,2A4,1X,A4,3X,I2,2X,I3,4X,I1)
      NM=NCOEFF-1
      IVER=1
      NOFILL=0
      NQST=17+NCOEFF
      NQSTM1=NQST-1
      NQLST=NQST+NM-1
      NM2=NM-1
      IF(NCOEFF.GT.0) GO TO 209
      WRITE(IPR,927)
  927 FORMAT(1H0,10X,44H**ERROR**THE NUMBER OF COEFFICIENTS IS ZERO.)
      CALL ERROR
      IUSEP=0
      IUSEC=0
      NOFILL=1
C
C     CHECK INFLOW TIME SERIES USING SUBROUTINE CHEKTS
C
  209 CALL CHEKTS(QIN,QITYPE,IDTQI,1,DIMQI,0,1,IERFLG)
C
C     CHECK OUTFLOW TIME SERIES
C
      IF(IDTQO.EQ.0) GO TO 103
      IF(IDTQO.GE.IDTQI) GO TO 102
      WRITE(IPR,905)IDTQO,IDTQI
  905 FORMAT(1H0,10X,33H**ERROR** OUTFLOW TIME INTERVAL =,I3,6H HOURS,
     136H IS LESS THAN INFLOW TIME INTERVAL =,I3,6H HOURS,
     239H THIS IS NOT ALLOWED FOR THIS OPERATION)
C
      CALL ERROR
C
C     CHECK OUTFLOW TIME SERIES BY USING SUBROUTINE CHEKTS
C
  102 CALL CHEKTS(QOUT,QOTYPE,IDTQO,1,DIMQO,0,1,IERFLG)
C
C
C     CALCULATE THE SPACES NEEDED IN THE P AND C ARRAYS
  103 IF(NOFILL.EQ.1) GO TO 114
      IUSEC=NCOEFF
      IUSEP=16+NCOEFF+NM
C
C     CHECK TO SEE IF THERE IS ENOUGH ROOM IN THE P AND C ARRAYS
C
      IF(IUSEP.LE.LEFTP) GO TO 111
      WRITE(IPR,908)
  908 FORMAT(1H0,10X,74H**ERROR** THIS OPERATION NEEDS MORE SPACE THAN I
     1S AVAILABLE IN THE P ARRAY)
      CALL ERROR
      NOFILL=1
  111 IF(IUSEC.LE.LEFTC) GO TO 112
      WRITE(IPR,909)
  909 FORMAT(1H0,10X,74H**ERROR** THIS OPERATION NEEDS MORE SPACE THAN I
     1S AVAILABLE IN THE C ARRAY)
      CALL ERROR
      NOFILL=1
C
C
  112 IF(NOFILL.EQ.0) GO TO 113
      IUSEC=0
      IUSEP=0
      GO TO 114
  113 CONTINUE
      READ(IN,917)(P(I),I=17,NQSTM1)
  917 FORMAT(7F10.0)
      IERFLG=0
      DO 104 I=1,NCOEFF
      IF(P(I+16).LE.1.) GO TO 104
      IERFLG=IERFLG+1
      P(I+16)=1.
  104 CONTINUE
      IF(IERFLG.EQ.0) GO TO 105
      WRITE(IPR,906)IERFLG
  906 FORMAT(1H0,10X,11H**WARNING**,I2,63H COEFFICIENTS HAD VALUES GREAT
     1ER THAN 1. VALUES SET EQUAL TO 1.)
      CALL WARN
C
C     CHECK QRANGE DATA FOR ERRORS
C        THESE VALUES MUST BE IN INCREASING ORDER
  105 IF(NCOEFF.LE.1) GO TO 107
      READ(IN,917)(P(I),I=NQST,NQLST)
      IERFLG=0
      IF(NCOEFF.LE.2) GO TO 107
      NM2=NM-1
      DO 106 I=1,NM2
  106 IF(P(NQST+I-1).GE.P(NQST+I))IERFLG=1
      IF(IERFLG.EQ.0)GO TO 107
      WRITE(IPR,907)
  907 FORMAT(1H0,10X,56H**ERROR** FLOW RANGES FOR LAYERS NOT IN INCREASI
     1NG ORDER)
      CALL ERROR
C
C     CHECK TO SEE IF CARRYOVER SHOULD BE READ IN INITIALLY OR
C           SET EQUAL TO ZERO BY DEFAULT
C
  107 IF(ICVAL.EQ.0) GO TO 108
      READ(IN,917)(C(I),I=1,NCOEFF)
      GO TO 109
  108 DO 110 I=1,NCOEFF
  110 C(I)=0.
C
C     PUT VALUES IN P AND C ARRAYS
C
  109 P(1)=IVER+0.01
      DO 116 I=2,6
  116 P(I)=TITLE(I-1)
      P(7)=QIN(1)
      P(8)=QIN(2)
      P(9)=QITYPE
      P(10)=IDTQI+0.01
      P(11)=QOUT(1)
      P(12)=QOUT(2)
      P(13)=QOTYPE
      P(14)=IDTQO+0.01
      P(15)=ICVAL+0.01
      P(16)=NCOEFF+0.01
      IF(IBUG.EQ.0) GO TO 114
C
C     PRINT DEBUG OUTPUT  P AND C ARRAYS
C
      WRITE(IODBUG,910)IUSEP,IUSEC
  910 FORMAT(1H0,10X,26HCONTENTS OF P AND C ARRAYS,5X,
     124HNUMBER OF VARIABLES---P=,I3,5H  C =,I3)
      WRITE(IODBUG,911)(P(I),I=1,IUSEP)
  911 FORMAT(1H0,14(F8.3,1X))
      WRITE(IODBUG,911)(C(I),I=1,IUSEC)
C
C     POSITION         CONTENTS OF P ARRAY
C     1                VERSION NUMBER OF OPERATION
C     2-6              GENERAL NAME OR TITLE
C     7-8              INFLOW TIME SERIES I.D.
C     9                INFLOW TIME SERIES DATA TYPE
C     10               INFLOW TIME SERIES TIME INTERVAL
C     11-12            OUTFLOW TIME SERIES I.D.
C     13               OUTFLOW TIME SERIES DATA TYPE
C     14               OUTFLOW TIME SERIES TIME INTERVAL
C     15               CARRYOVER DECISION PARAMETER
C                        =0--RESIDUALS =0 BY DEFAULT
C                        =1--RESIDUALS ARE READ IN FROM NEXT DATA CARD
C     16               NUMBER OF LAYERS
C
C
C                STARTING LOCATION OF COEFFICIENTS IS POSITION 17
C
C                STARTING LOCATION OF FLOW RANGES IS POSITION (17+NCOEFF
C
  114 CONTINUE
      RETURN
      END
