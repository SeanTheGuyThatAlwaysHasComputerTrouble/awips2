C MEMBER PIN9
C  (from old member FCPIN9)
C
      SUBROUTINE PIN9(PO,LEFTP,IUSEP,CO,LEFTC,IUSEC)
C     THIS IS THE INPUT SUBROUTINE FOR MUSKINGUM ROUTING
C     METHOD.  THIS SUBROUTINE INPUTS ALL CARDS FOR THE OPERATION
C     AND FILLS THE PO ARRAY AND PLACES INITIAL VALUES OF THE
C     STATE VARIABLES IN THE CO ARRAY.
C
C     THIS SUBROUTINE ORIGINALLY WRITTEN BY
C        DAVID REED - HRL   OCT. 1979
C
C
      DIMENSION PO(1),CO(1),QID(2),TITLE(5),QOID(2)
C
C     DEFINITION OF VARIABLES
C     QID        INTERNAL IDENTIFIER FOR INFLOW TIME SERIES
C     QTYPE      DATA TYPE CODE FOR INFLOW TIME SERIES
C     IDT        COMPUTATIONAL TIME INTERVAL
C     RK         STORAGE COEFFICIENT IN HOURS FOR MUSKINGUM ROUTING
C     X          PARAMETER FOR MUSKINGUM ROUTING
C     CO         MUSKINGUM ROUTING COEFFICIENT
C     C1         MUSKINGUM ROUTING COEFFICIENT
C     C2         MUSKINGUM ROUTING COEFFICIENT
C     ICVAL      CARRYOVER DECISION PARAMETER
C                IF ICVAL=0, QIN=0 AND QOUT=0 BY DEFAULT
C                IF ICVAL=1, QIN AND QOUT ARE READ FROM NEXT DATA CARD
C     QIN        INITIAL CARRYOVER FOR INFLOW
C     QOUT       INITIAL CARRYOVER FOR OUTFLOW
C     QOID       INTERNAL IDENTIFIER FOR OUTFLOW TIME SERIES
C     QOTYPE     DATA TYPE CODE FOR OUTFLOW TIME SERIES
C     IDTQO      COMPUTATIONAL TIME INTERVAL FOR OUTFLOW TIME SERIES
C
C     COMMON BLOCKS
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin9.f,v $
     . $',                                                             '
     .$Id: pin9.f,v 1.1 1995/09/17 18:48:52 dws Exp $
     . $' /
C    ===================================================================
C
      DATA DQ/4HL3/T/,DQOUT/4HL3/T/
C
C     CHECK TRACE LEVEL- FOR THIS SUBROUTINE =1
C
      IF(ITRACE.GE.1) WRITE(IODBUG,900)
  900 FORMAT(1H0,15H** PIN9 ENTERED)
C
C     CHECK TO SEE IF DEBUG OUTPUT IS NEEDED FOR THIS OPERATION
C
C
      IBUG=0
      IF(IDBALL.GT.0) IBUG=1
      IF(NDEBUG.EQ.0) GO TO 12
      DO 10 I=1,NDEBUG
      IF(IDEBUG(I).EQ.9) GO TO 11
   10 CONTINUE
      GO TO 12
   11 IBUG=1
C
C     INITIALIZE VARIABLES
   12 NOFILL=0
      IUSEP=20
      IVER=1
      IUSEC=2
C
C
C
C     READ IN DATA
C
      READ(IN,901)(TITLE(I),I=1,5),QID,QTYPE,IDT,QOID,QOTYPE,IDTQO,RK,X,
     1ICVAL
  901 FORMAT(5A4,2X,2A4,1X,A4,3X,I2,2X,2A4,1X,A4,3X,I2,2F5.0,1X,I1)
C
C      CHECK INFLOW TIME SERIES
C
C
C     CHECK THE INFLOW TIME SERIES USING SUBROUTINE CHEKTS
C
      CALL CHEKTS(QID,QTYPE,IDT,1,DQ,0,1,IERFLG)
C
C     CHECK TO SEE IF OUTFLOW TIME SERIES EXISTS
C
      IF(IDTQO.EQ.0) GO TO 303
C
      IF(IDTQO.GE.IDT) GO TO 203
      WRITE(IPR,970) IDTQO,IDT
  970 FORMAT(1H0,10X,33H**ERROR** OUTFLOW TIME INTERVAL =,I3,
     135HIS LESS THAN INFLOW TIME INTERVAL =,I3,
     138HTHIS IS NOT ALLOWED FOR THIS OPERATION)
      CALL ERROR
C
C     CHECK THE OUTFLOW TIME SERIES USING SUBROUTINE CHEKTS
C
  203 CALL CHEKTS(QOID,QOTYPE,IDTQO,1,DQOUT,1,1,IERFLG)
C
C     CHECK ICVAL TO SEE IF INITIAL INFLOW AND OUTFLOW SHOULD BE READ IN
C
  303 IF(ICVAL.EQ.0) GO TO 104
      READ (IN,906) QIN,QOUT
  906 FORMAT(2F10.0)
      GO TO 105
  104 QIN=0.0
      QOUT=0.0
C
C     CALCULATE MUSKINGUM ROUTINE COEFFICIENTS
C
  105 XK=RK*X
      HDT=IDT*0.5
      DENOM=RK-XK+HDT
      C0=(HDT-XK)/DENOM
      C1=(XK+HDT)/DENOM
      C2=(RK-XK-HDT)/DENOM
C
C
C     CHECK VALIDITY OF RANGE OF VARIABLES AND TIME INTERVALS
C
C
      IF(X.GE.0.5) GO TO 290
      BLIM=2.*XK
      TINC=FLOAT(IDT)
      IF((TINC.LE.RK).AND.(TINC.GE.BLIM)) GO TO 300
      WRITE(IPR,922)IDT
  922 FORMAT(1H0,10X,35H**WARNING** ROUTING TIME INTERVAL =,I2,
     127H IS NOT WITHIN THE RANGE OF ,15HOF 2.*K*X TO K.)
      CALL WARN
      GO TO 300
  290 WRITE(IPR,924)
  924 FORMAT(1H0,10X, 99H**WARNING**X IS GREATER THAN OR EQUAL 0.5. PROB
     1LEMS COULD ARISE IN THIS MUSKINGUM ROUTING OPERATION)
      CALL WARN
C
C     CHECK TO SEE IF PO AND CO ARRAYS HAVE ENOUGH ROOM TO STORE VALUES
C
  300 IF(IUSEP.LE.LEFTP) GO TO 401
      NOFILL=1
      WRITE(IPR,910)
  910 FORMAT(1H0,10X,74H**ERROR** THIS OPERATION NEEDS MORE SPACE THAN I
     1S AVAILABLE IN THE P ARRAY)
      CALL ERROR
  401 IF(IUSEC.LE.LEFTC) GO TO 402
      NOFILL=1
      WRITE(IPR,911)
  911 FORMAT(1H0,10X,74H**ERROR** THIS OPERATION NEEDS MORE SPACE THAN I
     1S AVAILABLE IN THE C ARRAY)
      CALL ERROR
  402 IF(NOFILL.EQ.0) GO TO 111
C
C     IF NOFILL =1 EITHER PO OR CO DOES NOT HAVE ENOUGH
C     ROOM TO HOLD ALL VALUES REQUIRED
C
      IUSEC=0
      IUSEP=0
      GO TO 106
C
C     PUT VALUES IN CO AND PO ARRAYS
C
  111 CO(1)=QIN
      CO(2)=QOUT
      PO(1)=IVER+0.01
      DO 107 I=2,6
  107 PO(I)=TITLE(I-1)
      PO(7)=QID(1)
      PO(8)=QID(2)
      PO(9)=QTYPE
      PO(10)=IDT+0.01
      PO(11)=QOID(1)
      PO(12)=QOID(2)
      PO(13)=QOTYPE
      PO(14)=IDTQO+0.01
      PO(15)=RK
      PO(16)=X
      PO(17)=C0
      PO(18)=C1
      PO(19)=C2
      PO(20)=ICVAL+0.01
      IF(IBUG.EQ.0) GO TO 106
C
C
C     PRINT DEBUG OUTPUT--PO AND CO ARRAYS
C
      WRITE(IODBUG,907) IUSEP,IUSEC
  907 FORMAT(1H0,20X,28HCONTENTS OF PO AND CO ARRAYS,5X,23HNUMBERS OF VA
     1LUES---PO=,I3,2X,3HCL=,I2)
      WRITE(IODBUG,908)(PO(I),I=1,IUSEP)
  908 FORMAT(1H0,14(F8.3,1X))
C
      WRITE(IODBUG,908)(CO(I),I=1,IUSEC)
C    POSITION           CONTENTS OF PO ARRAY
C     1       VERSION NUMBER FOR OPERATION
C     2-6     GENERAL NAME FOR TITLE OR POINT
C     7-8     INFLOW TIME SERIES I.D.
C     9       INFLOW TIME SERIES DATA TYPE
C     10      INFLOW TIME SERIES INTERVAL--HOURS
C     11-12   OUTFLOW TIME SERIES I.D.
C     13      OUTFLOW TIME SERIES DATA TYPE
C     14      OUTFLOW TIME SERIES TIME INTERVAL--HOURS
C     15      MUSKINGUM ROUTING COEFFICIENT K --HOURS
C     16      MUSKINGUN ROUTING PARAMETER X --
C     17      COEFFICIENT C0
C     18      COEFFICIENT C1
C     19      COEFFOCIENT C2
C     20      CARRYOVER DECISION PARAMETER
C                  =0 -- QIN=0 AND QOUT=0 BY DEFAULT
C                  =1 -- QIN AND QOUT ARE READ FROM DATA CARDS
C
C
  106 RETURN
      END
