C MEMBER PIN34
C  (from old member FCPIN34)
C  VERSION 1
C
C***********************************************************************
C
C THIS SUBROUTINE READS AND CHECKS ALL CARD INPUT FOR SEGMENT DEFINITION
C FOR THE API-SLC OPERATION. IT THEN FILLS THE PO (PARAMETER) AND CO
C (CARRYOVER) ARRAYS.
C
C THE USER HAS THE OPTION OF CREATING OUTPUT TIME SERIES FOR CURRENT
C API VALUES.
C
C***********************************************************************
C
C SUBROUTINE INITIALLY WRITTEN BY
C        DON LAURINE - CBRFC   MARCH 1985
C
C***********************************************************************
C
C PRINCIPAL VARIABLES
C
C FOR DEFINITION OF VARIABLES IN COMMON BLOCKS, SEE SECTION IX.3.3C
C OF THE NWSRFS USER'S MANUAL
C
C     AA            API RELATIONSHIP CONSTANT (A)
C     AB            API RELATIONSHIP CONSTANT (I)
C     AK            API RELATIONSHIP CONSTANT (K)
C     AM            API RELATIONSHIP CONSTANT (M)
C     ANAME         AREA NAME
C     API           CURRENT API VALUE
C     APILMT        API LOWER LIMIT
C     APIREC        API RECESSION CONSTANT
C     CO            CARRYOVER ARRAY
C     CP            WEEK OF YEAR RELATION MODIFIER
C     DTCAP         DATA TYPE CODE OF API TIME SERIES
C     DTCRF         DATA TYPE CODE OF RAIN+MELT TIME SERIES
C     DTCRO         DATA TYPE CODE OF RUNOFF TIME SERIES
C     DTCSC         DATA TYPE CODE OF % AREAL SNOW COVER
C     E1            API RELATIONSHIP CONSTANT (E1)
C     E2            API RELATIONSHIP CONSTANT (E2)
C     IAPFLG        FLAG TO INDICATE NEED FOR API TIME SERIES
C     ICHKDM        SWITCH INDICATING WHETHER DIMENSION OF TIME SERIES
C                     SHOULD BE CHECK - 0=NO 1=YES
C     ICOFLG        CARRYOVER FLAG - 0=DEFAUT 1=USER SUPPLIED
C     IDELAP        DELTA-T FOR API TIME SERIES (24 HOURS)
C     IDELRF        DELTA-T FOR RAIN+MELT TIME SERIES (6 HOURS)
C     IDELRO        DELTA-T  FOR RUNOFF TIME SERIES (6 HOURS)
C     IDELSC        DELTA-T FOR % SNOW COVER TIME SERIES (24 HOURS)
C     IDUR          CARRYOVER - STORM DURATION
C     IFATAL        FATAL ERROR FLAG - 0=NO ERRORS 1=ERRORS
C     IFDEB         DEBUG OUTPUT SWITCH - 0=OFF 1=ON
C     ISCFLG        FLAG TO INDICATE NEED FOR % SNOW COVER TIME SERIES
C     ISFG          CARRYOVER - STORM FLAG
C     IUSEC         NUMBER OF WORDS REQUIRED IN THE C ARRAY FOR THIS
C                     OPERATION
C     IUSEP         NUMBER OF WORDS REQUIRED IN THE P ARRAY FOR THIS
C                     OPERATION
C     IVERS         VERSION NUMBER FOR THIS OPERATION
C     IWN           API RELATIONSHIP CONSTANT (WN)
C     IWX           API RELATIONSHIP CONSTANT (WX)
C     LEFTC         NUMBER OF WORDS AVAILABLE IN THE C ARRAY
C     LEFTP         NUMBER OF WORDS AVAILABLE IN THE P ARRAY
C     NOVAL         NUMBER OF VALUES PER TIME INTERVAL THAT THE TIME
C                     SERIES CAN HAVE FOR THIS OPERATION
C     PO            INPUT PARAMETRIC DATA FOR THE P ARRAY
C     POW           API RELATIONSHIP CONSTANT (POW)
C     PSC,PS        CARRYOVER - CURRENT % SNOW COVER
C     PXSIG         SIGNIFICANT PRECIP LEVEL
C     SUBNAM        SUBROUTINE NAME
C     TSIDAP        ID OF API TIME SERIES
C     TSIDRF        ID OF RAIN+MELT TIME SERIES
C     TSIDRO        ID OF RUNOFF TIME SERIES
C     TSIDSC        ID OF % SNOW COVER TIME SERIES
C
C
C***********************************************************************
C
C CONTENTS OF THE PO ARRAY:
C
C      WORD    ITEM
C    -------   -----------------------------------------------------
C       1      VERSION NUMBER
C     2 - 7    AREA NAME
C       8      API RECESSION CONSTANT
C       9      API LOWER LIMIT
C      10      INITIAL CARRYOVER FLAG 0=DEFAULT, 1=CARD INPUT
C      11      TIME INTERVAL RAIN+MELT TIME SERIES
C    12 - 13   INTERNAL ID RAIN+MELT TIME SERIES
C      14      DATA TYPE CODE RAIN+MELT TIME SERIES
C      15      TIME INTERVAL RUNOFF TIME SERIES
C    16 - 17   INTERNAL ID RUNOFF TIME SERIES
C      18      DATA TYPE CODE RUNOFF TIME SERIES
C      19      TIME INTERVAL PRECENT AREAL SNOW COVER TIME SERIES
C    20 - 21   INTERNAL ID PERCENT AREAL SNOW COVER TIME SERIES
C      22      DATA TYPE CODE PERCENT AREAL SNOW COVER TS
C      23      TIME INTERVAL API TIME SERIES
C    24 - 25   INTERNAL ID API TIME SERIES
C      26      DATA TYPE CODE API TIME SERIES
C      27      API CONSTANT - A
C      28      API CONSTANT - I
C      29      API CONSTANT - WN
C      30      API CONSTANT - WX
C      31      API CONSTANT - E1
C      32      API CONSTANT - E2
C      33      API CONSTANT - CP
C      34      API CONSTANT - K
C      35      API CONSTANT - M
C      36      API CONSTANT - POW
C      37      SIGNIFICANT PRECIP LEVEL
C    38 - 40   EMPTY SPACE
C
C***********************************************************************
C
C CONTENTS OF THE CO ARRAY:
C
C      WORD    ITEM
C    -------   -----------------------------------------------------
C       1      CURRENT API VALUE
C       2      CURRENT PERCENT AREAL SNOW COVER
C       3      STORM FLAG
C       4      STORM DURATION VALUE IN HOURS
C
C***********************************************************************
C
      SUBROUTINE PIN34(PO,LEFTP,IUSEP,CO,LEFTC,IUSEC)
C
      DIMENSION ANAME(6),SUBNAM(2),TSIDRF(2),TSIDRO(2),TSIDSC(2),
     *          TSIDAP(2),PO(1),CO(1)
C
      COMMON /IONUM/ IN,IPR,IPU
      COMMON /FDBUG/ IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin34.f,v $
     . $',                                                             '
     .$Id: pin34.f,v 1.1 1995/09/17 18:48:29 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SUBNAM/4hPIN3,4h4   /,NOP/34/,EMPTY/4h    /,
     *     DIML/4hL   /,DLES/4hDLES/
C
C CALL DEBUG CHECK ROUTINE AND SET INITIAL VARIABLES
C
      CALL FPRBUG(SUBNAM,1,NOP,IFDEB)
C
      IVERS=1
      IFATAL=0
      IUSEP=40
      IUSEC=4
      ICHKDM=1
      MISS=0
      NOVAL=1
C
      DO 1000 I=1,2
      TSIDSC(I)=EMPTY
      TSIDAP(I)=EMPTY
1000  CONTINUE
      IDELSC=0
      IDELAP=0
      DTCSC=EMPTY
      DTCAP=EMPTY
C
      IF(IFDEB.GT.0) GO TO 201
C
C READ AREA NAME, API RECESSION CONSTANT, LOWER API LIMITING VALUE,
C CARRYOVER INPUT FLAG, OUTPUT API TIME SERIES FLAG, AREAL SNOW COVER
C FLAG AND SIGNIFICANT PRECIP LEVEL.
C
101   READ(IN,900) ANAME,APIREC,APILMT,PXSIG,ICOFLG,IAPFLG,ISCFLG
      IF(IFDEB.GT.0) GO TO 202
C
C CHECK APIREC VALUES
C
102   IF(APIREC.GE.1.0.OR.APIREC.LT.0.0) GO TO 500
      IF(APIREC.EQ.0.0) APIREC=0.9
C
C CHECK APILMT VALUES
C
103   IF(APILMT.LT.-0.99.OR.APILMT.GT.0.0) GO TO 501
C
C CHECK SIGNIFICANT PRECIP LEVEL
C
104   IF(PXSIG.LT.0.0.OR.PXSIG.GT.2.0) GO TO 503
      IF(PXSIG.EQ.0.0) PXSIG=0.10
C
C READ IN TIME SERIES DESCRIPTIONS
C
105   READ(IN,901) IDELRF,TSIDRF,DTCRF,IDELRO,TSIDRO,DTCRO
C
      CALL CHEKTS(TSIDRF,DTCRF,IDELRF,ICHKDM,DIML,MISS,NOVAL,IERR)
      CALL CHEKTS(TSIDRO,DTCRO,IDELRO,ICHKDM,DIML,MISS,NOVAL,IERR)
C
      IF(ISCFLG.LE.0) GO TO 106
      READ(IN,901) IDELSC,TSIDSC,DTCSC
C
      CALL CHEKTS(TSIDSC,DTCSC,IDELSC,ICHKDM,DLES,MISS,NOVAL,IERR)
C
106   IF(IAPFLG.LE.0) GO TO 108
      READ(IN,901) IDELAP,TSIDAP,DTCAP
C
      CALL CHEKTS(TSIDAP,DTCAP,IDELAP,ICHKDM,DIML,MISS,NOVAL,IERR)
C
108   IF(IFDEB.GT.0) GO TO 203
C
C READ IN API CONSTANTS
C
109   READ(IN,902) AA,AB,IWN,IWX,E1,E2,CP,AK,AM,POW
      IF(IFDEB.GT.0) GO TO 204
C
C CHECK API CONSTANTS
C
110   IF(AA.LT.0.01.OR.AA.GT.10.) GO TO 502
      IF(AB.LT.0.01.OR.AB.GT.20.) GO TO 502
      IF(IWN.LT.1.OR.IWN.GT.52) GO TO 502
      IF(IWX.LT.1.OR.IWX.GT.52) GO TO 502
      IF(E1.LT.0.0.OR.E1.GT.1.0) GO TO 502
      IF(E2.LT.0.0.OR.E2.GT.1.0) GO TO 502
      IF(CP.LT.0.0.OR.CP.GT.5.0) GO TO 502
      IF(AK.LT.0.01.OR.AK.GT.0.99) GO TO 502
      IF(AM.LT.0.0.OR.AM.GT.100.0) GO TO 502
      IF(POW.LT.0.0.OR.POW.GT.5.0) GO TO 502
C
C   CHECK FOR INVALID WET/DRY WEEKS
C
  300 IF(IWN.EQ.IWX) GO TO 509
C
C READ IN CARRYOVER IF REQUESTED
C
111   IF(ICOFLG.LE.0) GO TO 115
      READ(IN,902) API,PSC,ISFG,IDUR
      IF(IFDEB.GT.0) GO TO 205
C
C CHECK CARRYOVER VALUES
C
112   IF(API.LT.APILMT.OR.API.GT.5.00) GO TO 504
113   IF(PSC.LT.0.0.OR.PSC.GE.1.0) GO TO 505
114   IF(ISFG.LT.0.OR.IDUR.LT.0) GO TO 506
      IF(ISFG.EQ.0.AND.IDUR.GT.0) GO TO 508
      IF(ISFG.GT.0.AND.IDUR.EQ.0) GO TO 508
      GO TO 116
C
C SET CARRYOVER DEFAULTS
C
115   API=APILMT
      PSC=0.0
      ISFG=0
      IDUR=0
      IF(IFDEB.GT.0) GO TO 206
C
C CHECK DELTA-T FOR EACH OF THE TIMME SERIES DEFINED
C
116   IF(IDELRF.NE.6.OR.IDELRO.NE.6) GO TO 507
      IF(ISCFLG.LE.0) GO TO 117
      IF(IDELSC.NE.24) GO TO 507
117   IF(IAPFLG.LE.0) GO TO 118
      IF(IDELAP.NE.24) GO TO 507
C
C CHECK SPACE IN P ARRAY
C
118   CALL CHECKP(IUSEP,LEFTP,IERR)
      IF(IERR.LE.0) GO TO 119
      IFATAL=1
      GO TO 120
C
C STORE PARAMETERS INTO P ARRAY
C
119   PO(1)= IVERS+0.01
      DO 1002 I=1,6
      PO(I+1)= ANAME(I)
1002  CONTINUE
      PO(8)= APIREC
      PO(9)= APILMT
      PO(10)= ICOFLG
      PO(11)= IDELRF+0.01
      PO(12)= TSIDRF(1)
      PO(13)= TSIDRF(2)
      PO(14)= DTCRF
      PO(15)= IDELRO+0.01
      PO(16)= TSIDRO(1)
      PO(17)= TSIDRO(2)
      PO(18)= DTCRO
      PO(19)= IDELSC+0.01
      PO(20)= TSIDSC(1)
      PO(21)= TSIDSC(2)
      PO(22)= DTCSC
      PO(23)= IDELAP+0.01
      PO(24)= TSIDAP(1)
      PO(25)= TSIDAP(2)
      PO(26)= DTCAP
      PO(27)= AA
      PO(28)= AB
      PO(29)= IWN+0.01
      PO(30)= IWX+0.01
      PO(31)= E1
      PO(32)= E2
      PO(33)= CP
      PO(34)= AK
      PO(35)= AM
      PO(36)= POW
      PO(37)=PXSIG
      DO 1003 I=38,40
      PO(I)=0
1003  CONTINUE
C
C CHECK SPACE OF C ARRAY
C
120   CALL CHECKC(IUSEC,LEFTC,IERR)
      IF(IERR.GT.0) GO TO 555
C
C STORE CARRYOVER VAUES IN CO ARRAY
C
      CO(1)= API
      CO(2)= PSC
      CO(3)= ISFG+.01
      CO(4)= (IDUR/6.0)+0.01
C
C CHECK FOR ANY FATAL ERRORS
C
      IF(IFATAL) 9999,9999,555
C
C-----------------------------------------------------------------------
C DEBUG STATEMENTS
C
201   WRITE(IODBUG,920) SUBNAM,IVERS
      GO TO 101
202   WRITE(IODBUG,921) ANAME,ICOFLG,APILMT,ISCFLG,APIREC,IAPFLG,
     *PXSIG
      GO TO 102
203   WRITE(IODBUG,922) TSIDRF,DTCRF,IDELRF,TSIDRO,DTCRO,IDELRO
      IF(ISCFLG.GT.0) WRITE(IODBUG,923) TSIDSC,DTCSC,IDELSC
      IF(IAPFLG.GT.0) WRITE(IODBUG,924) TSIDAP,DTCAP,IDELAP
      GO TO 109
204   WRITE(IODBUG,925) AA,AB,IWN,IWX,E1,E2,CP,AK,AM,POW
      GO TO 110
205   PS=PSC*100.00
      WRITE(IODBUG,926) API,PS,ISFG,IDUR
      GO TO 112
206   PS=PSC*100.00
      WRITE(IODBUG,927) API,PS,ISFG,IDUR
      GO TO 116
C
C-----------------------------------------------------------------------
C ERROR STATEMENTS
C
500   WRITE(IPR,950) APIREC
      IFATAL=1
      GO TO 103
501   WRITE(IPR,951) APILMT
      IFATAL=1
      GO TO 104
502   WRITE(IPR,952) AA,AB,IWN,IWX,E1,E2,CP,AK,AM,POW
      IFATAL=1
      GO TO 300
503   WRITE(IPR,959) PXSIG
      IFATAL=1
      GO TO 105
504   APIT=API
      IF(API.GT.5.00) APIT=5.0
      IF(API.LT.APILMT) APIT=APILMT
      WRITE(IPR,953) API,APIT
      CALL WARN
      API=APIT
      GO TO 113
505   PS=PSC*100.
      WRITE(IPR,954) PS
      CALL WARN
      PSC=0.0
      GO TO 114
506   WRITE(IPR,955) ISFG,IDUR
      CALL WARN
      ISFG=0
      IDUR=0
      GO TO 116
507   WRITE(IPR,956) IDELRF,IDELRO
      IF(ISCFLG.GT.0) WRITE(IPR,957) IDELSC
      IF(IAPFLG.GT.0) WRITE(IPR,958) IDELAP
      IFATAL=1
      GO TO 118
508   WRITE(IPR,960) ISFG,IDUR
      ISFG=0
      IDUR=0
      CALL WARN
      GO TO 116
  509 WRITE(IPR,961)
      IFATAL=1
      GO TO 111
555   IUSEP=0
      IUSEC=0
      CALL ERROR
C
9999  RETURN
C
C-----------------------------------------------------------------------
C I/O FORMATS
C
900   FORMAT(6A4,1X,3F5.2,3I5)
901   FORMAT(2(I5,5X,2A4,3X,A4))
902   FORMAT(2F5.2,2I5,6F5.2)
C
920   FORMAT(/5X,2A4,' DEBUG OUTPUT.',5X,'VERSION:',I4)
921   FORMAT(/10X,'AREA NAME: ',6A4,7X,'C/O INPUT FLAG:',I3,
     *7X,'API LOWER LIMIT:',F7.2,/52X,'% SNOW TS FLAG:',I3,
     *7X,'API REC CONSTANT:',F6.2,/52X,'API TS FLAG:',I6,
     *7X,'SIG. PRECIP VALUE:',F5.2)
922   FORMAT(/35X,'TIME SERIES USED BY THIS OPERATION'/
     *25X,'CONTENTS',10X,'TS I.D.',5X,'TYPE',5X,'TIME INTERVAL'/
     *25X,16('-'),1X,8('-'),5X,4('-'),5X,13('-')/
     *25X,'RAINFALL/MELT',4X,2A4,5X,A4,I9,' HOURS'/
     *25X,'RUNOFF',11X,2A4,5X,A4,I9,' HOURS')
923   FORMAT(25X,'% SNOW COVER',5X,2A4,5X,A4,I9,' HOURS')
924   FORMAT(25X,'API',14X,2A4,5X,A4,I9,' HOURS')
925   FORMAT(/15X,'API CONSTANTS:',
     *//17X,'A',7X,'I',7X,'WN',6X,'WX',6X,'E1',6X,'E2',6X,'CP',6X,'K',
     *7X,'M',6X,'POW',/12X,10(3X,'-----')
     */15X,F5.2,3X,F5.2,4X,I3,5X,I3,1X,4(3X,F5.2),3X,F5.1,3X,F5.2)
926   FORMAT(/15X,'CARRYOVER VALUES:',
     *//16X,'API',7X,'%SC',6X,'STORM',6X,'DUR HRS',
     */15X,3('-----',5X),'--------',/14X,F6.2,6X,F4.0,6X,I2,9X,I3)
927   FORMAT(/15X,'DEFAULT CARRYOVER VALUES:',
     */15X,'NOTE: DEFAULT VALUES WERE USED'
     *//16X,'API',7X,'%SC',6X,'STORM',6X,'DUR HRS',
     */15X,3('-----',5X),'--------',/14X,F6.2,6X,F4.0,6X,I2,9X,I3)
C
950   FORMAT(/10X,'**ERROR** ILLEGAL API RECESSION CONSTANT:',F7.2,
     */20X,'VALUES MUST BE GREATER THAN 0.0 AND LESS THAN 1.0',
     */20X,'IF VALUE IS 0.0, CONSTANT IS SET TO DEFAULT OF 0.9')
951   FORMAT(/10X,'**ERROR** ILLEGAL API LOWER LIMIT:',F7.2,
     */20X,'VALUES RANGE FROM -0.99 TO 0.0',
     */20X,'DEFAULT IS 0.0')
952   FORMAT(/10X,'**ERROR** ILLEGAL API CONSTANTS',
     *//20X,'ONE OF THE FOLLOWING PARAMETERS DOES NOT FALL WITHIN ',
     *'PROGRAM LIMITS:',
     *//20X,'ITEM',6X,'LIMITS',10X,'VALUE ENTERED',
     */20X,4('-'),3X,13('-'),6X,13('-'),
     */20X,'A      .01 THRU 10.0',7X,F8.2,
     */20X,'I      .01 THRU 20.0',7X,F8.2,
     */20X,'WN       1 THRU   52',7X,I8,
     */20X,'WX       1 THRU   52',7X,I8,
     */20X,'E1     0.0 THRU 1.0',7X,F8.2,
     */20X,'E2     0.0 THRU 1.0',7X,F8.2,
     */20X,'CP     0.0 THRU  5.0',7X,F8.2,
     */20X,'K      .01 THRU 0.99',7X,F8.2,
     */20X,'M      0.0 THRU 100.',7X,F8.2,
     */20X,'POW    0.0 THRU  5.0',7X,F8.2)
953   FORMAT(/10X,'**WARNING** API CARRYOVER IS OUTSIDE OF LIMITS:',
     *F8.2,/22X,'VALUE IS SET TO',F8.2)
954   FORMAT(/10X,'**WARNING** % SNOW COVER CARRYOVER IS OUTSIDE',
     *' OF LIMITS:',F6.0,'%',/22X,'VALUE SET TO 0')
955   FORMAT(/10X,'**WARNING** STORM FLAG OR DURATION WAS NEGITIVE  ',
     *' STORM FLAG:',I4,'  DURATION:',I4,
     */22X,'BOTH VALUES ARE SET TO ZERO')
956   FORMAT(/10X,'**ERROR** ILLEGAL TIME INTERVAL FOR ONE OF THE',
     *' FOLLOWING TIME SERIES',
     */22X,'RAINFALL/MELT INTERVAL IS (',I3,') SHOULD BE  6 HOURS',
     */22X,'RUNOFF        INTERVAL IS (',I3,') SHOULD BE  6 HOURS')
957   FORMAT(22X,'% SNOW COVER  INTERVAL IS (',I3,') SHOULD BE 24 ',
     *'HOURS')
958   FORMAT(22X,'API           INTERVAL IS (',I3,') SHOULD BE 24 ',
     *'HOURS')
959   FORMAT(/10X,'**ERROR** ILLEGAL SIGNIFICANT PRECIPITATION ',
     *'LEVEL:',F6.2,/20X,'VALUES RANGE FROM .01 TO 2.0',
     */20X,'DEFAULT IS 0.10')
960   FORMAT(/10X,'**WARNING** STORM FLAG AND DURATION BOTH MUST ',
     *'BE GREATER THAN ZERO OR BOTH MUST BE EQUAL TO ZERO',
     */22X,'STORM FLAG:',I4,5X,'DURATION:',I4,
     */22X,'VALUES ARE SET TO ZERO')
  961 FORMAT(/,10X,'**ERROR** ILLEGAL WET/DRY WEEKS',
     *   /,20X,'VALUES OF WN AND WX CAN NOT BE EQUAL')
      END
