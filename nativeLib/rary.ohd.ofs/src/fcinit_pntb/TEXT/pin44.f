C MEMBER PIN44
C  (from old member FCPIN44)
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 06/23/95.16:29:19 BY $WC30RE
C
C @PROCESS LVL(77)
C
      SUBROUTINE PIN44 (P,LEFTP,IUSEP,C,LEFTC,IUSEC)

C     THIS IS THE INPUT ROUTINE FOR SSARR ROUTING.  THIS
C     ROUTINE INPUTS ALL CARDS FOR THE OPERATION AND FILLS THE
C     P ARRAY AND PLACES INITIAL VALUES OF THE STATE VARIABLES
C     IN THE C ARRAY

C     THIS ROUTINE ORIGINALLY WRITTEN BY
C        RAY FUKUNAGA - NWRFC   SEPTEMBER 1993

C     DEFINITION OF VARIABLES
C     IVERSN = I4     VERSION NUMBER OF OPERATION
C     CTITLE = C72    GENERAL NAME OR TITLE FOR OPERATION

C     START INFLOW TIME SERIES USED TO CONSERVE INFLOW HYDROGRAPH VOLUME
C     CIDINS = C8     START INFLOW TIME SERIES IDENTIFIER
C     CODINS = C4     START INFLOW DATA TYPE CODE 
C     CODINS MAY BE CODED AS 'NONE' IN THE CASE THAT ONLY THE END INFLOW
C     TIME SERIES IS SPECIFIED TO DESCRIBE THE INFLOW HYDROGRAPH IN THIS
C     CASE THE INITIAL START INFLOW MUST COME FROM THE CARRYOVER ARRAY

C     END INFLOW TIME SERIES
C     CIDINE = C8     END INFLOW TIME SERIES IDENTIFIER
C     CODINE = C4     END INFLOW DATA TYPE CODE 

C     START OUTFLOW TS USED TO CONSERVE OUTFLOW ROUTED HYDROGRAPH VOLUME
C     CIDOTS = C8     START OUTFLOW TIME SERIES IDENTIFIER
C     CODOTS = C4     START OUTFLOW DATA TYPE CODE 
C     CODOTS MAY BE CODED AS 'NONE' IN THE CASE THAT ONLY THE END OUTFLOW
C     TIME SERIES IS SPECIFIED TO DESCRIBE THE OUTFLOW HYDROGRAPH 

C     END OUTFLOW TIME SERIES IS THE ROUTED HYDROGRAPH
C     CIDOTE = C8     END OUTFLOW TIME SERIES IDENTIFIER
C     CODOTE = C4     END OUTFLOW DATA TYPE CODE 

C     NPHASE = I4     NUMBER OF ROUTING PHASES (MUST BE > 0)
C     RN     = R4     N VALUE OF KTS/Q**N COMPUTATION
C     RKTS   = R4     KTS VALUE IN HOURS IF N IS NONZERO
C     NPQTST = I4     NUMBER OF POINTS IN THE DISCHARGE-TIME OF
C                     STORAGE TABLE (MAX 50 POINTS)
C     NCOMP  = I4     COMPUTATIONAL TIME INTERVAL
C     QTABLE = R4(1)  DISCHARGE POINTS IN DISCHARGE-TIME OF STORAGE
C                     TABLE
C     TSTORE = R4     TIME OF STORAGE CALCULATED
C     TSTBLE = R4(1)  TIME OF STORAGE POINTS IN DISCHARGE-TIME OF
C                     STORAGE TABLE


C     POSITION     CONTENTS OF P ARRAY
C      1           VERSION NUMBER OF OPERATION
C      2-19        GENERAL NAME OR TITLE

C     USED TO CONSERVE INFLOW HYDROGRAPH VOLUME
C     20-21        START INFLOW TIME SERIES IDENTIFIER
C     22           START INFLOW DATA TYPE CODE (SARINS)
C     WHEN POSITIONS 20 AND 22 ARE 'NONE', ONLY THE END INFLOW
C     TIME SERIES IS SPECIFIED

C     INFLOW HYDROGRAPH
C     23-24        END INFLOW TIME SERIES IDENTIFIER
C     25           END INFLOW DATA TYPE CODE (SARINE)

C     USED TO CONSERVE ROUTED HYDROGRAPH VOLUME
C     26-27        START OUTFLOW TIME SERIES IDENTIFIER
C     28           START OUTFLOW DATA TYPE CODE (SAROTS)
C     WHEN POSITIONS 26 AND 28 ARE 'NONE', ONLY THE END OUTFLOW
C     TIME SERIES IS SPECIFIED

C     ROUTED OUTFLOW HYDROGRAPH
C     29-30        END OUTFLOW TIME SERIES IDENTIFIER
C     31           END OUTFLOW DATA TYPE CODE (SAROTE)

C     32           1 OR 2 INFLOW TIME SERIES SPECIFIED FLAG
C                  = 1, ONLY THE END INFLOW TIME SERIES IS SPECIFIED
C                  = 2, BOTH THE START AND END INFLOW TIME SERIES ARE
C                       SPECIFIED
C     33           1 OR 2 OUTFLOW TIME SERIES SPECIFIED FLAG
C                  = 1, ONLY THE END OUTFLOW TIME SERIES IS SPECIFIED
C                  = 2, BOTH THE START AND END OUTFLOW TIME SERIES ARE
C                       SPECIFIED
C     34           NUMBER OF ROUTING PHASES (MUST BE > 0)
C     35           N VALUE OF KTS/Q**N COMPUTATION
C                  IF N=0, THE TIME OF STORAGE IS EXTRACTED FROM THE
C                           DISCHARGE-TIME OF STORAGE TABLE
C     36           KTS VALUE IN HOURS IF N IS NONZERO
C     37           THE NUMBER OF POINTS ON THE DISCHARGE-TIME OF
C                  STORAGE TABLE IF N=0 (=0 IF N IS NONZERO)
C     38           COMPUTATIONAL TIME INTERVAL (HOURS)
C     39+          THE POINTS OF THE DISCHARGE-TIME OF STORAGE TABLE
C
C     THEREFORE THE NUMBER OF ELEMENTS REQUIRED IN THE P ARRAY IS
C        38 +
C         2 * NUMBER OF POINTS OF THE DISCHARGE-TIME OF STORAGE TABLE

C     POSITION     CONTENTS OF C ARRAY
C      1           INITIAL START INFLOW, IF ONLY END INFLOW TIME SERIES  
C                  IS SPECIFIED
C      2+          PHASE FLOW VALUES FROM REACH


      PARAMETER (MAXPH = 99)
      PARAMETER (MAXTBL = 50)

      CHARACTER *72 CTITLE
      CHARACTER * 8 CIDINS,CIDINE,CIDOTS,CIDOTE
      CHARACTER * 4 CODINS,CODINE,CODOTS,CODOTE

      DIMENSION RTITLE(18)
      DIMENSION RIDINS(2),RIDINE(2),RIDOTS(2),RIDOTE(2)
      DIMENSION P(*),C(*)
      DIMENSION QTABLE(MAXTBL),TSTBLE(MAXTBL)
      DIMENSION QPHASE(MAXPH)

      EQUIVALENCE (CTITLE,RTITLE)
      EQUIVALENCE (CIDINS,RIDINS),(CIDINE,RIDINE)
      EQUIVALENCE (CIDOTS,RIDOTS),(CIDOTE,RIDOTE)
      EQUIVALENCE (CODINS,RODINS),(CODINE,RODINE)
      EQUIVALENCE (CODOTS,RODOTS),(CODOTE,RODOTE)

C     COMMON BLOCKS

      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin44.f,v $
     . $',                                                             '
     .$Id: pin44.f,v 1.3 1996/03/21 14:45:21 page Exp $
     . $' /
C    ===================================================================
C

C     CHECK TRACE LEVEL
      CALL FPRBUG('PIN44   ',1,44,IBUG)


C                   NWRFS CARDS
C                   FREE FORMAT - NO TABLE - EQUATION ROUTING
C SSARR ROUT - HILLS CREEK ROUTED TO LOOKOUT POINT
C HCR SINS HCR SINE HCR2 SOTS HCR2 SOTE
C 2 0.1 1.5 0 6
C 1200.
C 1000.
C 1000.

C                   FREE FORMAT - DISCHARGE-TIME OF STORAGE TABLE
C SSARR ROUT - EUGO + COBURG ROUT TO HARRISBURG
C EUGO5 SINS EUGO5 SINE EUGO52 SOTS EUGO52 SOTE
C 7 0. 0. 4 6
C 1200.
C 1000.
C 1000.
C 1000.
C 1000.
C 1000.
C 1000.
C 1000.
C       1.       2.3
C    1000.       1.4
C   18000.       0.7
C 9999999. 9999999.

C     CARD 1 - GENERAL USER SUPPLIED INFORMATION
C     CARD 2 - TIME SERIES DEFINITION
C        FIELD 1 - START INFLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 2 - START INFLOW DATA TYPE CODE  - 4 CHAR
C                  IF 'NONE' THEN ONLY END INFLOW TIME SERIES
C                            IDENTIFIED
C        FIELD 3 - END INFLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 4 - END INFLOW DATA TYPE CODE  - 4 CHAR
C        FIELD 5 - START OUTFLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 6 - START OUTFLOW DATA TYPE CODE  - 4 CHAR
C                  IF 'NONE' THEN ONLY END OUTFLOW TIME SERIES
C                            IDENTIFIED
C        FIELD 7 - END OUTFLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 8 - END OUTFLOW DATA TYPE CODE  - 4 CHAR
C     CARD 3 - SSARR ROUTING CHARACTERISTICS
C        FIELD 1 - NUMBER OF ROUTING PHASES (MUST BE > 0)
C        FIELD 2 - N VALUE OF KTS/Q**N COMPUTATION
C                - IF N=0, THE TIME OF STORAGE IS EXTRACTED FROM THE
C                - DISCHARGE-TIME OF STORAGE TABLE
C        FIELD 3 - KTS VALUE IN HOURS IF N IS NONZERO
C        FIELD 4 - THE NUMBER OF POINTS ON THE DISCHARGE-TIME OF
C                  STORAGE TABLE
C        FIELD 5 - THE COMPUTATIONAL TIME INTERVAL
C                  MUST BE = INFLOW & OUTFLOW TIME INTERVAL
C     CARD 4+ - DISCHARGE-TIME OF STORAGE TABLE
C             - PAIRINGS OF DISCHARGE-TIME OF STORAGE POINTS
C             - ONLY ONE PAIR PER LINE AND A TOTAL MAXIMUM OF 50
C               POINTS ALLOWED
C             - MUST BE IN ASCENDING DISCHARGE ORDER
C             - THESE CARDS NECESSARY ONLY IF N=0
C               STORAGE IS EXTRACTED FROM THE DISCHARGE-TIME
C        FIELD 1 - DISCHARGE (CFS)
C        FIELD 2 - TIME OF STORAGE (HRS)
C
C     CARD 5  - INITIAL INFLOW
C     CARD 6+ - INITIAL PHASE FLOW VALUES

C     READ IN CARDS IN FREE FORMAT
      IUSEP = 0
      IUSEC = 0
      IERROR = 0

      READ(IN,FMT='(A)',ERR=9991) CTITLE
      IF (IBUG.GE.1) WRITE(IODBUG,801) CTITLE
 801  FORMAT('PIN44: CTITLE: ',A72)

      READ(IN,*,ERR=9992) CIDINS,CODINS,CIDINE,CODINE,
     +           CIDOTS,CODOTS,CIDOTE,CODOTE
      IF (IBUG.GE.1) WRITE(IODBUG,802)
     +                  CIDINS,CODINS,CIDINE,CODINE,
     +                  CIDOTS,CODOTS,CIDOTE,CODOTE
 802  FORMAT('PIN44: CIDINS,CODINS: ',2A8,/,
     +       '         CIDINE,CODINE: ',2A8,/,
     +       '         CIDOTS,CODOTS: ',2A8,/,
     +       '         CIDOTE,CODOTE: ',2A8)

      IF (CODINS.EQ.'NONE') THEN
         NINTS = 1
      ELSE
         NINTS = 2
      ENDIF
      IF (IBUG.GE.1) WRITE(IODBUG,8025) NINTS
 8025 FORMAT('PIN44: NINTS: ',I5)

      IF (CODOTS.EQ.'NONE') THEN
         NOTTS = 1
      ELSE
         NOTTS = 2
      ENDIF
      IF (IBUG.GE.1) WRITE(IODBUG,8026) NOTTS
 8026 FORMAT('PIN44: NOTTS: ',I5)

      READ(IN,*,ERR=9993) NPHASE,RN,RKTS,NPQTST,NCOMP
      IF (IBUG.GE.1) WRITE(IODBUG,803)
     +                    NPHASE,RN,RKTS,NPQTST,NCOMP
 803  FORMAT('PIN44: NPHASE: ',I6,/,
     +       '             RN: ',F8.3,/,
     +       '           RKTS: ',F8.3,/,
     +       '         NPQTST: ',I6,/,
     +       '          NCOMP: ',I6)

C     CHECK TS - CIDINS,CODINS
C                CIDINE,CODINE
C                CIDOTS,CODOTS
C                CIDOTE,CODOTE
      IF (NINTS.EQ.2) THEN
         CALL CHEKTS (CIDINS,CODINS,NCOMP,1,'L3/T',0,1,IERFLG)
         IF (IERFLG.NE.0) IERROR = 1
      ENDIF

      CALL CHEKTS (CIDINE,CODINE,NCOMP,1,'L3/T',0,1,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      IF (NOTTS.EQ.2) THEN
         CALL CHEKTS (CIDOTS,CODOTS,NCOMP,1,'L3/T',0,1,IERFLG)
         IF (IERFLG.NE.0) IERROR = 1
         CALL CHEKTS (CIDOTE,CODOTE,NCOMP,1,'L3/T',0,1,IERFLG)
         IF (IERFLG.NE.0) IERROR = 1
      ELSE
         CALL CHEKTS (CIDOTE,CODOTE,NCOMP,1,'L3  ',0,1,IERFLG)
         IF (IERFLG.NE.0) IERROR = 1
      ENDIF

C     IF USING THE TIME OF STORAGE - DISCHARGE TABLE
      IF (RN.EQ.0) THEN
C        NPQTST SHOULD BE LESS THAN MAXTBL
         IF (NPQTST.GT.MAXTBL) THEN
            WRITE(IPR,FMT='(1H0,10X,''**ERROR**  NUMBER OF POINTS IN '',
     +                      ''THE DISCHARGE-TIME OF STORAGE TABLE '',
     +                      ''GREATER MAX ALLOWED ('',I2,'')'')') MAXTBL
            IERROR = 1
            CALL ERROR
         ENDIF

C        NPQTST SHOULD BE GE 2

C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012
         IF (NPQTST.LT.2) THEN
            WRITE(IPR,FMT='(1H0,10X,''**ERROR** NUMBER OF POINTS IN '',
     +                    ''THE DISCHARGE-TIME OF STORAGE TABLE '',
     +                    ''LESS THAN 2'')')
            IERROR = 1
            CALL ERROR
         ENDIF

         DO 101 I=1,NPQTST
            READ(IN,*,ERR=9994) QTABLE(I),TSTBLE(I)
            IF (IBUG.GE.1) WRITE(IODBUG,810) I,QTABLE(I),TSTBLE(I)
 810        FORMAT('PIN44: I,QTABLE,TSTBLE: ',I4,2F10.2)
 101     CONTINUE

      ENDIF

C     NPHASE SHOULD BE LESS THAN MAXPH
      IF (NPHASE.GT.MAXPH) THEN
       WRITE(IPR,FMT='(1H0,10X,''**ERROR** NUMBER OF ROUTING PHASES '',
     +                   ''GREATER THAN MAX ALLOWED ('',I2,'')'')')
     +      MAXPH
         IERROR = 1
         CALL ERROR
      ENDIF

C     NPHASE SHOULD BE GREATER THAN ZERO
      IF (NPHASE.LE.0) THEN
       WRITE(IPR,FMT='(1H0,10X,''**ERROR** NUMBER OF ROUTING PHASES '',
     +                 ''LESS THAN OR EQUAL TO ZERO'')')
         IERROR = 1
         CALL ERROR
      ENDIF

      IF (NINTS.EQ.1) THEN
         READ(IN,*,ERR=9995) QI1
         IF (IBUG.GE.1) WRITE(IODBUG,811) QI1
 811     FORMAT('PIN44: QI1: ',F10.2)
      ENDIF

      DO 100 I=1,NPHASE
         READ(IN,*,ERR=9996) QPHASE(I)
         IF (IBUG.GE.1) WRITE(IODBUG,812) I,QPHASE(I)
 812     FORMAT('PIN44: I,QPHASE: ',I4,F10.2)
 100  CONTINUE

C     CHECK SPACE IN P AND C ARRAYS
C     NUMBER POINTS REQUIRED IN THE P ARRAY IS 39 +
C     THE NUMBER OF POINTS IN THE DISCHARGE-TIME OF STORAGE TABLE
C     NUMBER POINTS REQUIRED IN THE C ARRAY IS THE NUMBER OF PHASES
C     IF ICARRY IS NONZERO

      IF (RN.EQ.0.) THEN
         IUSEP = NPQTST*2 + 38
      ELSE
         IUSEP = 38
      ENDIF

      CALL CHECKP (IUSEP,LEFTP,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

C     IF ONLY END INFLOW TIME SERIES SPECIFIED,
C      THE INITIAL START INFLOW VALUE MUST BE INCLUDED IN THE CARRYOVER
      IF (NINTS.EQ.1) THEN
         IUSEC = NPHASE + 1

C     ELSE IF THE START AND END INFLOW TIME SERIES ARE SPECIFIED,
C             THE INITIAL START INFLOW VALUE NEED NOT BE IN CARRYOVER
      ELSE
         IUSEC = NPHASE
      ENDIF

      CALL CHECKC (IUSEC,LEFTC,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      IF (IERROR.GE.1) GO TO 990

      IVERSN = 1
      P(1) = REAL(IVERSN)
      DO 500 I=1,18
 500  P(I+1) = RTITLE(I)

      P(20) = RIDINS(1)
      P(21) = RIDINS(2)
      P(22) = RODINS

      P(23) = RIDINE(1)
      P(24) = RIDINE(2)
      P(25) = RODINE

      P(26) = RIDOTS(1)
      P(27) = RIDOTS(2)
      P(28) = RODOTS

      P(29) = RIDOTE(1)
      P(30) = RIDOTE(2)
      P(31) = RODOTE

      P(32) = REAL(NINTS)
      P(33) = REAL(NOTTS)
      P(34) = REAL(NPHASE)
      P(35) = RN
      P(36) = RKTS
      P(37) = REAL(NPQTST)
      P(38) = REAL(NCOMP)

      IF (RN.EQ.0.) THEN
         DO 510 I=1,NPQTST
         P(I*2+37) = QTABLE(I)
 510     P(I*2+38) = TSTBLE(I)
      ENDIF

      IF (NINTS.EQ.1) THEN
         C(1) = QI1
         DO 120 I=1,NPHASE
 120     C(I+1) = QPHASE(I)
      ELSE
         DO 121 I=1,NPHASE
 121     C(I) = QPHASE(I)
      ENDIF

      GO TO 990

 9991 IERROR = 1
      WRITE(IPR,9901) 
 9901 FORMAT(//,10X,'*** SARROUTE INPUT ERROR ***',/,                 
     +          10X,'CARD 1 READ: USER SUPPLIED INFORMATION')
C        *** SARROUTE INPUT ERROR *** 
C        CARD 1 READ: USER SUPPLIED INFORMATION 
      GO TO 990

 9992 IERROR = 1
      WRITE(IPR,9902)
 9902 FORMAT(//,10X,'*** SARROUTE INPUT ERROR ***',/,                   
     +          10X,'CARD 2 READ: TIME SERIES INFORMATION')
C        *** SARROUTE INPUT ERROR ***                                    
C        CARD 2 READ: TIME SERIES INFORMATION 
      GO TO 990

 9993 IERROR = 1
      WRITE(IPR,9903)
 9903 FORMAT(//,10X,'*** SARROUTE INPUT ERROR ***',/,          
     +          10X,'CARD 3 READ: CARRYOVER, ROUTING CHARACTERISTICS ',
     +              'INFORMATION')
C        *** SARROUTE INPUT ERROR ***                             
C        CARD 3 READ: CARRYOVER, ROUTING CHARACTERISTICS INFORMATION 
      GO TO 990

 9994 IERROR = 1
      WRITE(IPR,9904)
 9904 FORMAT(//,10X,'*** SARROUTE INPUT ERROR ***',/,         
     +          10X,'CARD 4 READ: DISCHARGE-TIME OF STOREAGE TABLE')
C        *** SARROUTE INPUT ERROR ***                               
C        CARD 4 READ: DISCHARGE-TIME OF STORAGE TABLE
      GO TO 990

 9995 IERROR = 1
      WRITE(IPR,9905)
 9905 FORMAT(//,10X,'*** SARROUTE INPUT ERROR ***',/,          
     +          10X,'CARD 5 READ: INITIAL BEGIN INCREMENT INFLOW')
C        *** SARROUTE INPUT ERROR ***                                  
C        CARD 5 READ: INITIAL BEGIN INCREMENT INFLOW
      GO TO 990

 9996 IERROR = 1
      WRITE(IPR,9906)
 9906 FORMAT(//,10X,'*** SARROUTE INPUT ERROR ***',/,            
     +          10X,'CARD 6 READ: CARRYOVER PHASE FLOW VALUES')
C        *** SARROUTE INPUT ERROR ***                               
C        CARD 6 READ: CARRYOVER PHASE FLOW VALUES
      
 990  IF (IERROR.GE.1) THEN
         IUSEP = 0
         IUSEC = 0
      ENDIF

      IF (ITRACE.GE.1) WRITE(IODBUG,999)
 999  FORMAT('PIN44 (SSARR ROUTING) EXITED')

      RETURN
      END
