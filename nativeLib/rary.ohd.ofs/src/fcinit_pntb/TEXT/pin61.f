C MEMBER PIN61
C-----------------------------------------------------------------------
C
C                             LAST UPDATE:  4/29/1998 TLS        
C
C @PROCESS LVL(77)
C
      SUBROUTINE PIN61 (P,LEFTP,IUSEP)

C     THIS IS THE STAGE REVIEW ROUTINE FOR DWOPER.  THIS
C     ROUTINE INPUTS ALL CARDS FOR THE OPERATION AND FILLS THE
C     P ARRAY.

C     THIS ROUTINE ORIGINALLY WRITTEN BY
C        JOANNE R. SALERNO - NWRFC   JAN 1998

C     DEFINITION OF VARIABLES
C     IVERSN = I1     VERSION NUMBER OF OPERATION
C     CTITLE = C72    GENERAL NAME OR TITLE FOR OPERATION

C  INPUT
C     ITIME  = I2     INPUT TIME SERIES INTERVAL

C     OBSERVED STAGE
C     CSTAO  = C8     OBSERVED STAGE TIME SERIES IDENTIFIER
C     STGO   = C4     OBSERVED STAGE DATA TYPE CODE

C     DW FORECASTED STAGE
C     CSTAP  = C8     DW FCST  STAGE TIME SERIES IDENTIFIER
C     STGP   = C4     DW FCST  STAGE DATA TYPE CODE

C  OUTPUT
C     OTIME  = I2     OUTPUT TIME SERIES INTERVAL
C 
C     TIDAL RANGE/SLICE DEFINITION
C     RANGE  = C8     RANGE1 LOWER LIMIT
C     OUTTC  = C4     OUTPUT SERIES TYPE CODE
C
C        RANGE(1)     RANGE1 LOWER LIMIT
C        RANGE(2)     RANGE2 LOWER LIMIT
C        RANGE(3)     RANGE3 LOWER LIMIT
C        RANGE(4)     RANGE4 LOWER LIMIT
C        RANGE(5)     RANGE4 UPPER LIMIT
C
C     OBSERVED AVERAGE BALANCE TIME SERIES PER RANGE
C     SBALR1 = C8     RANGE1 AVE TIDE BALANCE TIME SERIES IDENTIFIER
C     SBALR2 = C8     RANGE2 AVE TIDE BALANCE TIME SERIES IDENTIFIER
C     SBALR3 = C8     RANGE3 AVE TIDE BALANCE TIME SERIES IDENTIFIER
C     SBALR4 = C8     RANGE4 AVE TIDE BALANCE TIME SERIES IDENTIFIER



C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012

C     POSITION     CONTENTS OF P ARRAY

C      1           VERSION NUMBER OF OPERATION
C      2-19        GENERAL NAME OR TITLE

C  INPUT
C     20           INPUT TIME SERIES INTERVAL - ITIME
C     21-23        OBSERVED STAGE      - CSTAO (STGO)
C     24-26        DW FCST  STAGE      - CSTAP (STGP)
C  OUTPUT
C     27           OUTPUT TIME INTERVAL            -  OTIME
C     TIDAL RANGE/SLICE DEFINITION
C     28-29        RANGE LIMIT SERIES IDENTIFIER   -  RANGE
C     30           OUTPUT SERIES TYPE CODE         -  OUTTC
C                  RANGE(1) R1 LOWER LIMIT
C                  RANGE(2) R2 LOWER LIMIT
C                  RANGE(3) R3 LOWER LIMIT
C                  RANGE(4) R4 LOWER LIMIT
C                  RANGE(5) R4 UPPER LIMIT
C
C     OBSERVED PRTO3 AVERAGE  BALANCE TIME SERIES PER RANGE
C     31-32        RANGE1 AVE TIDE BALANCE TIME SERIES ID  -  SBALR1
C     33-34        RANGE2 AVE TIDE BALANCE TIME SERIES ID  -  SBALR2
C     35-36        RANGE3 AVE TIDE BALANCE TIME SERIES ID  -  SBALR3
C     37-38        RANGE4 AVE TIDE BALANCE TIME SERIES ID  -  SBALR4
C
C     39-40        RESERVED
C**********************************************************************

C     THE NUMBER OF ELEMENTS REQUIRED IN THE P ARRAY IS  40

C     THE NUMBER OF ELEMENTS REQUIRED IN THE C ARRAY IS   0

C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012

      CHARACTER *72 CTITLE
      CHARACTER * 8 CSTAO,CSTAP,RANGE,SBALR1,SBALR2,SBALR3,SBALR4
      CHARACTER * 4 STGO,STGP,OUTTC

      INTEGER ITIME, OTIME
      DIMENSION RTITLE(18)
      DIMENSION RCSTAO(2),RCSTAP(2),RRANGE(2),
     +          RSBALR1(2),RSBALR2(2),RSBALR3(2),RSBALR4(2)
      DIMENSION P(*)

      EQUIVALENCE (CTITLE,RTITLE)
      EQUIVALENCE (CSTAO,RCSTAO),(STGO,RSTGO)
      EQUIVALENCE (CSTAP,RCSTAP),(STGP,RSTGP)
      EQUIVALENCE (RANGE,RRANGE),(OUTTC,ROUTTC)
      EQUIVALENCE (SBALR1,RSBALR1)
      EQUIVALENCE (SBALR2,RSBALR2)
      EQUIVALENCE (SBALR3,RSBALR3)
      EQUIVALENCE (SBALR4,RSBALR4)

C     COMMON BLOCKS
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin61.f,v $
     . $',                                                             '
     .$Id: pin61.f,v 1.2 1998/10/14 13:46:18 page Exp $
     . $' /
C    ===================================================================
C

C
C     CHECK TRACE LEVEL
      CALL FPRBUG('PIN61   ',1,61,IBUG)


C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012

C                   NWRFS CARDS
C                   FREE FORMAT
C
C
C STAGE REVIEW
C CSTAO STGO CSTAP STGP RANGE OUTTC
C SBALR1 OUTTC SBALR2 OUTTC SBALR3 OUTTC SBALR4 OUTTC
C
C     CARD 1 - GENERAL USER SUPPLIED INFORMATION
C     CARD 2 - TIME SERIES DEFINITION
C	 FIELD 1  - INPUT TIME INTERVAL		      - 1 INTEGER
C        FIELD 2  - OBSERVED STAGE TIME SERIES ID     - 8 CHAR
C        FIELD 3  - OBSERVED STAGE DATA TYPE CODE     - 4 CHAR
C        FIELD 4  - DW OBS/FCST STAGE TIME SERIES ID  - 8 CHAR
C        FIELD 5  - DW OBS/FCST STAGE DATA TYPE CODE  - 4 CHAR
C	 FIELD 6  - OUTPUT TIME INTERVAL	      - 1 INTEGER
C        FIELD 7  - RANGE LIMIT TIME SERIES ID        - 8 CHAR
C        FIELD 8  - OUTPUT TIME SERIES DATA TYPE CODE - 4 CHAR
C     CARD 3 - TIME SERIES DEFINITION
C        FIELD 1  - RANGE1 AVE TIDE BALANCE TIME SERIES ID -8 CHAR
C        FIELD 2  - RANGE2 AVE TIDE BALANCE TIME SERIES ID -8 CHAR
C        FIELD 3  - RANGE3 AVE TIDE BALANCE TIME SERIES ID -8 CHAR
C        FIELD 4  - RANGE4 AVE TIDE BALANCE TIME SERIES ID -8 CHAR

C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012

C     READ IN CARDS IN FREE FORMAT
      IUSEP = 0
      IUSEC = 0
      IERROR = 0

      READ(IN,FMT='(A)',ERR=9981) CTITLE
      IF (IBUG.GE.1) WRITE(IODBUG,801) CTITLE
 801  FORMAT('PIN61: CTITLE: ',A72)

      READ(IN,*,ERR=9982) ITIME,CSTAO,STGO,CSTAP,STGP,OTIME,
     1                    RANGE,OUTTC
      IF (IBUG.GE.1) WRITE(IODBUG,805)
     +       ITIME,CSTAO,STGO,CSTAP,STGP,OTIME,RANGE,OUTTC
 805  FORMAT('PIN61: ITIME (HRS) : ',I3,/,
     +       '       CSTAO,STGO  : ',2A8,/,
     +       '       CSTAP,STGP  : ',2A8,/,
     +       '       OTIME (HRS) : ',I3,/,
     +       '       RANGE,OUTTC : ',2A8)

      READ(IN,*,ERR=9983) SBALR1,SBALR2,SBALR3,
     +       SBALR4
      IF (IBUG.GE.1) WRITE(IODBUG,806)
     +       SBALR1,OUTTC,SBALR2,OUTTC,SBALR3,OUTTC,SBALR4,OUTTC
 806  FORMAT('PIN61: SBALR1,OUTTC: ',2A8,/,
     +       '       SBALR2,OUTTC: ',2A8,/,
     +       '       SBALR3,OUTTC: ',2A8,/,
     +       '       SBALR4,OUTTC: ',2A8)


C23456789012345678901234567890123456789012345678901234567890123456789012

C     CHECK TS - CSTAO,STGO 
C                CSTAP,STGP 
C                RANGE1,OUTTC
C                RANGE2,OUTTC
C                RANGE3,OUTTC
C                RANGE4,OUTTC
C                RANGE5,OUTTC
C                SBALR1,OUTTC
C                SBALR2,OUTTC
C                SBALR3,OUTTC
C                SBALR4,OUTTC

      CALL CHEKTS (CSTAO,STGO,ITIME,1,'L   ',1,1,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      CALL CHEKTS (CSTAP,STGP,ITIME,1,'L   ',1,1,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      CALL CHEKTS (RANGE,OUTTC,OTIME,1,'L   ',0,1,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      CALL CHEKTS (SBALR1,OUTTC,OTIME,1,'L   ',0,1,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      CALL CHEKTS (SBALR2,OUTTC,OTIME,1,'L   ',0,1,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      CALL CHEKTS (SBALR3,OUTTC,OTIME,1,'L   ',0,1,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      CALL CHEKTS (SBALR4,OUTTC,OTIME,1,'L   ',0,1,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1


C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012

 
C     CHECK SPACE IN P AND C ARRAYS
C     NUMBER POINTS REQUIRED IN THE P ARRAY IS 40
C     NUMBER POINTS REQUIRED IN THE C ARRAY IS 0
C     IF ICARRY IS NONZERO

      IUSEP = 40 

      CALL CHECKP (IUSEP,LEFTP,IERFLG)
      IF (IERFLG.NE.0) IERROR = 1

      IF (IERROR.GE.1) GO TO 990

C     SAVE TIME SERIES IDENTIFIERS AND PARAMETERS TO P AND C ARRAYS

      IVERSN = 1
      P(1) = REAL(IVERSN)
      DO 500 I=1,18
 500  P(I+1) = RTITLE(I)

      P(20) = REAL(ITIME)
      P(21) = RCSTAO(1)
      P(22) = RCSTAO(2)
      P(23) = RSTGO

      P(24) = RCSTAP(1)
      P(25) = RCSTAP(2)
      P(26) = RSTGP

      P(27) = REAL(OTIME)
      P(28) = RRANGE(1)
      P(29) = RRANGE(2)
      P(30) = ROUTTC

      P(31) = RSBALR1(1)
      P(32) = RSBALR1(2)

      P(33) = RSBALR2(1)
      P(34) = RSBALR2(2)

      P(35) = RSBALR3(1)
      P(36) = RSBALR3(2)

      P(37) = RSBALR4(1)
      P(38) = RSBALR4(2)

      P(39) = -999.
      P(40) = -999.
      GO TO 990

 9981 IERROR = 1
      WRITE(IPR,9901)
 9901 FORMAT(//,10X,'**ERROR**  STAGE REVIEW INPUT ERROR',/,
     +          10X,'CARD 1 READ: USER SUPPLIED INFORMATION')
      CALL ERROR
      GO TO 990

 9982 IERROR = 1
      WRITE(IPR,9902)
 9902 FORMAT(//,10X,'**ERROR**  STAGE REVIEW INPUT ERROR',/,
     +          10X,'CARD 2 READ: TIME SERIES INFORMATION')
      CALL ERROR
      GO TO 990

 9983 IERROR = 1
      WRITE(IPR,9903)
 9903 FORMAT(//,10X,'**ERROR**  STAGE REVIEW INPUT ERROR',/,
     +          10X,'CARD 3 READ: TIME SERIES INFORMATION')
      CALL ERROR
      GO TO 990

 990  IF (IERROR.GE.1) THEN
         IUSEP = 0
      ENDIF

      IF (ITRACE.GE.1) WRITE(IODBUG,999)
 999  FORMAT('PIN61 () EXITED')

      RETURN
      END
