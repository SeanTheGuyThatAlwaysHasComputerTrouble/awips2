C MEMBER PIN39
C  (from old member FCPIN39)
C
C                             LAST UPDATE: 01/06/94.10:18:53 BY $WC21DT
C
C
C    INPUT ROUTINE FOR WGRFC TABLE LOOKUP OPERATION
C
C    DAVE REED   WGRFC 7/87
C
C    CALL TO SUBROUTINE ERROR MADE AFTER ERROR CONDITIONS FOUND IN
C    THIS SUBROUTINE.    JERRY M. NUNN    WGRFC     01/26/90
C
C
       SUBROUTINE PIN39(P,LEFTP,IUSEP)
C
C    THIS SUBROUTINE READS INPUT CARDS FOR THE WEST GULF RFC
C    TABLE LOOKUP OPERATION
C
C    ARGUMENT LIST
C    ARG    DIM    DESCRIPTION
C    P      VAR    PARAMETER ARRAY FOR THIS OPERATION
C    LEFTP   1     NUMBER OF SPACES AVAILABLE IN P ARRAY
C    IUSEP   1     NUMBER OF PLACES IN THE P ARRAY REQUIRED BY THIS OPER
C
C    PROCEDURE
C  1. DIMENSION AND COMMON
C  2. CHECK TRACE LEVEL
C  3. CHECK DEBUG VARIABLE
C  4. READ IN CARD OF NUMBER OF POINTS AND UNITS OF LOOKUP TABLE
C  5. CHECK ALL VALUES FOR PROPER RANGE
C  6. DETERMINE IF SUFFICIENT SOPACE IS AVAILABLE FOR THIS OPERATION
C  7. READ IN INPUT TIME SERIES IDENTIFICATION
C  8. READ IN OUTPUT TIME SERIES IDENTIFICATION
C  9. CHECK EACH TIME SERIES WITH CHEKTS
C  10. IF NO ERRORS, SET IUSEP AND LOAD VARIABLE ARRAY
C  11. READ POINTS OF LOOKUP TABLE
C  12. CHECK ALL LOOKUP VALUES FOR ERRORS
C  13 RETURN
C
C
C    CONTENTS OF P ARRAY
C   LOCATION    CONTENTS
C    1          VERSION NUMBER
C    2          IUSEP
C    3          NP - NUMBER OF TABLE LOOKUP POINTS
C    4          LOOKUP TABLE UNITS  =0 ENG, =1, MET
C    5-10       UNUSED
C    11,12      INPUT TIME SERIES ID
C    13         INPUT TIME SERIES TYPE
C    14         INPUT TIME SERIES TIME INTERVAL
C    15,16      OUTPUT TIME SERIES ID
C    17         OUTPUT TIME SERIES TYPE
C    18         OUTPUT TIME SERIES TIME INTERVAL
C    19,20      LOOKUP POINT 1, FIRST VALUE IS FOR TS INPUT, SECOND FOR
C    21,22         POINT NUMBER 2
C     :           THESE POINTS ARE REPEATED NP TIMES
C
C    SPACE REQUIRED = 18+NP*2
C
C
C    ERROR CHECKING
C
C  1. ALL TIME SERIES MUST BE DEFINED AND ARE CHECKED BY CHEKTS
C  2. INTERVAL OF INPUT AND OUTPUT TIME SERIES MUST BE THE SAME
C  3. SUFFICIENT SPACE MUST BE AVAILABLE
C  4. NUMBER OF LOOKUP POINTS MUST BE >1
C  5. UNITS VARIABLES MUST BE 0 OR 1
C
C
C
C    DIMENSION AND COMMON
C
C
      DIMENSION SNAME(2),P(1)
C
C    COMMON
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin39.f,v $
     . $',                                                             '
     .$Id: pin39.f,v 1.3 2002/10/10 13:46:24 xfan Exp $
     . $' /
C    ===================================================================
C
C
C
      DATA SNAME/4HPIN3,4H9   /
C
C   SET CURRENT VERSION    -  VERSION 1   JULY 1987
C
      IVER=1
C
C    CHECK TRACE LEVEL
C
      IF(ITRACE.GE.1)WRITE(IODBUG,900)
  900 FORMAT(1H0,19H** PIN39 ENTERED **)
C
C    CHECK FOR DEBUG OUTPUT
C
      CALL FPRBUG(SNAME,1,39,IBUG)
C
C
C    READ IN NUMBER OF POINTS,UNITS FLAG, AND INTERPOLATION OPTION
C
C
      READ(IN,905)NP,IUNIT
  905 FORMAT(I5,3(4X,I1))
C
      IF(IBUG.EQ.1)WRITE(IODBUG,906)NP,IUNIT
  906 FORMAT(1H0,19HNUMBER OF POINTS IS,I3,
     114H UNITS FLAG IS,I3)
C
C    CALCULATE SPACE REQUIRED
C
      IUSEP=18+NP*2
C
      CALL CHECKP(IUSEP,LEFTP,IERFLG)
C
      IER=0
      IF(IERFLG.NE.0)IER=IER+1
C
C    CHECK IF VALUES READ IN ARE PROPER
C
      IF(NP.GE.2)GOTO 110
      WRITE(IPR,907)NP
  907 FORMAT(1H0,10X,36HNUMBER OF POINTS IN TABLE LOOKUP IS ,I3,
     130H. THIS MUST BE GREATER THAN 1.)
C
      IER=IER+1
      CALL ERROR
C
  110 CONTINUE
      IF(IUNIT.EQ.0.OR.IUNIT.EQ.1)GOTO 120
      WRITE(IPR,908)IUNIT
  908 FORMAT(1H0,10X,25H** ERROR ** UNITS FLAG IS,I2,
     120H. IT MUST BE 0 OR 1.)
      IER=IER+1
      CALL ERROR
C
  120 CONTINUE
C
C    READ IN TIME SERIES INFORMATION
C
      READ(IN,901)(P(J),J=11,13),IDTI
  901 FORMAT(2A4,2X,A4,4X,I2)
C
      IF(IBUG.EQ.1)WRITE(IODBUG,902)(P(J),J=11,13),IDTI
  902 FORMAT(1H0,21HINPUT TIME SERIES ID=,2A4,6H TYPE=,A4,
     110H TIME INT=,I3)
C
C    CHECK TIME SERIES
C
      I1=0
      I2=1
      I3=1
      CALL CHEKTS(P(11),P(13),IDTI,I1,DCODE,I2,I3,IERFLG)
      IF(IERFLG.NE.0)IER=IER+1
C
C      GET DIMENSIONS AND CONVERSIONS FOR MET-ENG CONV
C
C
      CALL FDCODE(P(13),RIMET,DIMS,IMSGI,I2,I3,I4,I5)
      CALL FCONVT(RIMET,DIMS,RIENG,RMI,RBI,IER)
C
C
C
C    READ IN OPUTPUT TIME SERIES
C
  121 READ(IN,901)(P(J),J=15,17),IDTO
      IF(IBUG.EQ.1)WRITE(IODBUG,903)(P(J),J=15,17),IDTO
  903 FORMAT(1H0,22HOUTPUT TIME SERIES ID=,2A4,6H TYPE=,A4,
     110H TIME INT=,I3)
C
C
      I1=0
      I2=1
      CALL CHEKTS(P(15),P(17),IDTO,I1,DCODE,IMSGI,I2,IERFLG)
      IF(IERFLG.NE.0)IER=IER+1
C
C    GET DIMS AND MET-ENG CONVERSION IF NEEDED
C
      IF (IUNIT.EQ.1)GOTO 122
      CALL FDCODE(P(17),ROMET,DIMS,I1,I2,I3,I4,I5)
      CALL FCONVT(ROMET,DIMS,ROENG,RMO,RBO,IER)
C
C
C
C    CHECK IF IDTI=IDTO
C
  122 IF(IDTO.EQ.IDTI)GOTO 100
C
      WRITE(IPR,904)IDTI,IDTO
      IER=IER+1
      CALL ERROR
  904 FORMAT(1H0,10X,46H** ERROR ** INPUT TIME SERIES TIME INTERVAL IS,
     1I3,36H OUTPUT TIME SERIES TIME INTERVAL IS,I3,/,10X,
     225H THESE MUST BE THE SAME..)
C
C
C    LOAD P ARRAY IF NO ERRORS
C
  100 IF(IER.NE.0)GOTO 140
      P(1)=IVER+.1
      P(2)=IUSEP+.1
      P(3)=NP+.1
      P(4)=IUNIT+.1
      P(4)=IUNIT+.1
      DO 101 I=5,10
  101 P(I)=0.1
      P(14)=IDTI+.1
      P(18)=IDTO+.1
C
C      READ IN POINTS OF LOOKUP TABLE
C
  140 NEND=18+NP*2
      READ(IN,910)(P(J),J=19,NEND)
  910 FORMAT(6F12.4)
C
C    CHECK TO INSURE ALL INPUT TIME SERIES POINTS ARE IN
C    INCREASING ORDER
C
      DO 150 I=2,NP
      I2=17+I*2
      I1=I2-2
      IF(P(I2).GT.P(I1)) GOTO 150
      I1=I-1
C jgg corrected index for previous value (out of seq.)- MR 1674  7/02 
      I3=I2-2    
      WRITE(IPR,909)I1,P(I3),I,P(I2)
C jgg end of fix
  909 FORMAT(1H0,10X,17H**ERROR** LOOKUP ,
     133HFOR INPUT TIME SERIES ARE NOT IN ,
     217HINCREASING ORDER.,//,
     310X,5HPOINT,I3,1H=,F12.4,/,10X,5HPOINT,I3,1H=,F12.4)
      IER=IER+1
      CALL ERROR
  150 CONTINUE
C
C    CONVERT TO METRIC IF NEEDED
C
      IF(IUNIT.EQ.1)GOTO 141
      DO 142 I=1,NP
      I1=19+(I-1)*2
      P(I1)=P(I1)/RMI-RBI/RMI
      I1=I1+1
  142 P(I1)=P(I1)/RMO-RBO/RMO
  141 CONTINUE
C
C
      IF(IER.NE.0)IUSEP=-1
C
C   THATS ALL FOLKS - WRITE TRACE MSG IF NECESSARY
C
      IF(ITRACE.GE.1)WRITE(IODBUG,913)
  913 FORMAT(1H0,18H** PIN39 EXITED **)
C
      RETURN
      END
