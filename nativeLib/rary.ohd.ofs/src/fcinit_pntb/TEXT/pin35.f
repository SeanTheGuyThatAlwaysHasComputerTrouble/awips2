C MODULE PIN35
C-----------------------------------------------------------------------
C
      SUBROUTINE PIN35(PO,LEFTP,IUSEP,CO,LEFTC,IUSEC)
C
C#######################################################################
C
C  THIS SUBROUTINE READS AND CHECKS ALL CARD INPUT FOR SEGMENT
C  DEFINITION FOR THE API-HAR OPERATION.  IT THEN FILLS
C  THE PO AND CO ARRAYS.
C
C#######################################################################
C
C  CONTENTS OF THE PO ARRAY
C
C
C
C     WORD    NAME       DESCRIPTION                            UNITS
C  ________   _______    _______________________________________________
C      1      IVERS      VERSION NUMBER
C    2 - 3    RID        RUNOFF ZONE ID
C    4 - 8    RNAME      RUNOFF ZONE NAME
C      9      IRNUM      RUNOFF ZONE NUMBER
C     10      RLAT       LATITUDE OF RUNOFF ZONE CENTROID       DEG DEC
C     11      RLNG       LONGITUDE OF RUNOFF ZONE CENTROID      DEG DEC
C     12      RFCTR      RUNOFF ADJUSTMENT FACTOR
C     13      R24        24-HOUR API RECESSION FACTOR
C     14      PMAX       MAX LIMIT FOR NEW STORM RAIN/MELT      INCHES
C     15      ULIMW      UPPER AEI LIMIT (WINTER CURVE)         INCHES
C     16      BLIMW      BOTTOM AEI LIMIT (WINTER CURVE)        INCHES
C     17      ULIMS      UPPER AEI LIMIT (SUMMER CURVE)         INCHES
C     18      BLIMS      BOTTOM AEI LIMIT (SUMMER CURVE)        INCHES
C     19      KURV       API/AEI/FI CURVE NUMBER
C     20      IDELTA     COMPUTATIONAL TIME STEP                HOURS
C     21      NSW        NEW STORM WINDOW                       HOURS
C     22      NSPER      NUMBER OF PERIODS IN NSW
C     23      IUSEC      NUMBER OF WORDS NEEDED IN CO ARRAY
C     24      IOFAAA     I/O FLAG FOR API, AEI & FI TIME SERIES
C     25      ICOF       CARRYOVER INPUT FLAG
C   26 - 27   TSIDRM     TIME SERIES ID FOR RAIN/MELT
C     28      DTCRM      DATA TYPE CODE FOR RAIN/MELT
C   29 - 30   TSIDPE     TIME SERIES ID FOR POTENTIAL ET
C     31      DTCPE      DATA TYPE CODE FOR POTENTIAL ET
C   32 - 33   TSIDRO     TIME SERIES ID FOR RUNOFF
C     34      DTCRO      DATA TYPE CODE FOR RUNOFF
C   35 - 36   TSIDFI     TIME SERIES ID FOR FI
C     37      DTCFI      DATA TYPE CODE FOR FI
C   38 - 39   TSIDAP     TIME SERIES ID FOR API
C     40      DTCAP      DATA TYPE CODE FOR API
C   41 - 42   TSIDAE     TIME SERIES ID FOR AEI
C     43      DTCAE      DATA TYPE CODE FOR AEI
C   44 - 47              EMPTY
C
C
C#######################################################################
C
C  CONTENTS OF THE CO ARRAY
C
C
C
C     WORD    NAME       DESCRIPTION                            UNITS
C  ________   _______    _______________________________________________
C      1      TAPI       12Z API VALUE                          INCHES
C      2      TAEI       12Z AEI VALUE                          INCHES
C      3      TFI        12Z FI VALUE
C      4      SAPI       STORM API VALUE AT 12Z                 INCHES
C      5      SAEI       STORM AEI VALUE AT 12Z                 INCHES
C      6      SFI        STORM FI VALUE AT 12Z
C      7      SRAIM      STORM RAIN/MELT                        INCHES
C      8      SRO        STORM RUNOFF                           INCHES
C      9      DRAIM      24-HOUR RAIN/MELT                      INCHES
C     10      DRO        24-HOUR RUNOFF                         INCHES
C   11 - 34   RNSP       RAIN/MELT IN EACH PERIOD OF THE        INCHES
C                        NEW STORM WINDOW
C
C#######################################################################
C
      CHARACTER*80 CARD
      DIMENSION RID(2),RNAME(5),TSIDRM(2),TSIDAE(2),TSIDRO(2),
     1TSIDFI(2),TSIDAP(2),TSIDPE(2),PO(*),CO(*),SUBNAM(2),RNSP(24)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      COMMON /FCARY/ IFILLC,NCSTOR,ICDAY(20),ICHOUR(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin35.f,v $
     . $',                                                             '
     .$Id: pin35.f,v 1.2 2002/02/11 19:01:56 dws Exp $
     . $' /
C    ===================================================================
C
      DATA SUBNAM/4hPIN3,4h5   /,NOP/35/
      DATA DIML/4hL   /,DLES/4hDLES/,BLANK/4h    /
C
C  CALL DEBUG CHECK ROUTINE AND SET INITIAL VARIABLES
C
      CALL FPRBUG(SUBNAM,1,NOP,IFDEB)
      IVERS=1
      IUSEP=47
      ICHKDM=1
      IF(IFDEB)110,110,4100
C
C  READ 8 LETTER RUNOFF ZONE ID, 20 CHARACTER RUNOFF ZONE NAME,
C  RUNOFF ZONE NUMBER, AND LATITUDE AND LONGITUDE OF RUNOFF ZONE
C  CENTROID.
C
110   READ(IN,7110)RID,RNAME,IRNUM,RLAT,RLNG
      IF(IFDEB)115,115,4110
C
C  READ BASIN RUNOFF FACTOR, 24 HOUR API RECESSION COEFFICIENT,
C  NEW STORM RAIN/MELT LIMIT AND UPPER AND BOTTOM LIMITS
C  FOR AEI VALUES (WINTER AND SUMMER).
C
115   READ(IN,7115)RFCTR,R24,PMAX,ULIMW,BLIMW,ULIMS,BLIMS
      IF(IFDEB)120,120,4115
C
C  READ API/AEI/FI CURVE NUMBER, COMPUTATIONAL TIME STEP INTERVAL,
C  NEW STORM WINDOW, I/O FLAG FOR FI, API AND AEI TIME SERIES,
C  AND CARRYOVER INPUT FLAG.
C
120   READ(IN,7120)KURV,IDELTA,NSW,IOFAAA,ICOF
      IF(IFDEB)130,130,4120
C
C  READ IN TIME SERIES INFORMATION
C
130   READ(IN,7130)TSIDRM,DTCRM,TSIDPE,DTCPE,TSIDRO,DTCRO
      IF(IFDEB)132,132,4130
C
C  CHECK TO SEE IF THESE TIME SERIES ARE DEFINED FOR THIS OPERATION
C
C  TIME SERIES FOR THE API - HAR OPERATION CONTAIN NO
C  MISSING VALUES AND HAVE ONLY 1 VALUE PER TIME INTERVAL.
C
132   MISS=0
      NOVAL=1
      CALL CHEKTS(TSIDRM,DTCRM,IDELTA,ICHDKM,DIML,MISS,NOVAL,IERR)
      CALL CHEKTS(TSIDPE,DTCPE,24,ICHKDM,DIML,MISS,NOVAL,IERR)
      CALL CHEKTS(TSIDRO,DTCRO,IDELTA,ICHKDM,DIML,MISS,NOVAL,IERR)
C
C  READ IN INFORMATION FOR FI, API AND AEI TIME SERIES, IF REQUESTED.
C
      IF(IOFAAA)140,140,135
135   READ(IN,7130)TSIDFI,DTCFI,TSIDAP,DTCAP,TSIDAE,DTCAE
      IF(IFDEB)137,137,4135
137   CALL CHEKTS(TSIDFI,DTCFI,IDELTA,ICHKDM,DLES,MISS,NOVAL,IERR)
      CALL CHEKTS(TSIDAP,DTCAP,IDELTA,ICHKDM,DIML,MISS,NOVAL,IERR)
      CALL CHEKTS(TSIDAE,DTCAE,IDELTA,ICHKDM,DIML,MISS,NOVAL,IERR)
C
C  CHECK VALIDITY OF PARAMETRIC DATA JUST READ IN
C
140   IF((IRNUM.LT.0).OR.(IRNUM.GT.1000))GO TO 5140
150   IF((RLAT.LT.36.0).OR.(RLAT.GT.43.5))GO TO 5150
160   IF((RLNG.LT.73.5).OR.(RLNG.GT.83.5))GO TO 5160
170   IF((RFCTR.LT.0.0).OR.(RFCTR.GT.5.00))GO TO 5170
180   IF((R24.LT.0.75).OR.(R24.GE.1.00))GO TO 5180
190   IF((PMAX.LT.0.0).OR.(PMAX.GT.1.00))GO TO 5190
200   IF((ULIMW.GT.11.00).OR.(ULIMW.LT.9.50))GO TO 5200
202   IF((BLIMW.GT.1.90).OR.(BLIMW.LT.1.00))GO TO 5202
210   IF((ULIMS.GT.11.00).OR.(ULIMS.LT.9.50))GO TO 5210
212   IF((BLIMS.GT.2.00).OR.(BLIMS.LT.1.00))GO TO 5212
220   IF((KURV.LT.1).OR.(KURV.GT.21))GO TO 5220
230   IF((IDELTA.LT.1).OR.(IDELTA.GT.24))GO TO 5230
240   IF((NSW.LT.1).OR.(NSW.GT.24))GO TO 5240
C
C  CONSTRUCT ALL ADDITIONAL INFO NEEDED FOR THE API - HAR OPERATION
C  FROM THIS PARAMETRIC DATA.
C
C  FIRST, CHECK TO MAKE SURE THE NEW STORM WINDOW IS A POSITIVE,
C  NON-ZERO INTEGER MULTIPLE OF IDELTA.  IF NOT, PRINT A WARNING
C  MESSAGE.
C
300   RX1=FLOAT(NSW)/FLOAT(IDELTA)
      NSPER=NSW/IDELTA
      RX1=RX1*IDELTA
      RX2=NSPER*IDELTA
      IF(RX1-RX2)5310,310,5310
C
C  CHECK TO MAKE SURE NSPER IS GREATER THAN ZERO AND LESS THAN OR
C  EQUAL TO 24.
C
310   IF(NSPER)5320,5320,320
320   IF(24-NSPER)5330,330,330
330   IUSEC=NSPER+10
C
C  CHECK TO SEE IF CARRYOVER VALUES ARE BEING INPUT.  IF NOT,
C  USE DEFAULTS.
C
350   IF(ICOF)380,380,370
370   READ(IN,'(A)') CARD
      READ(CARD,7150,ERR=371)TAPI,TAEI,TFI,SAPI,SAEI,SFI,SRAIM,SRO,
     * DRAIM,DRO
      GO TO 372
371   CALL FRDERR (IPR,' ',CARD)
372   READ(IN,'(A)') CARD
      READ(CARD,7151,ERR=373)(RNSP(I),I=1,NSPER)
      GO TO 374
373   CALL FRDERR (IPR,' ',CARD)
374   IF(IFDEB)610,610,4380
380   TAPI=1.00
      TAEI=5.00
      TFI=2.60
      SAPI=1.00
      SAEI=5.00
      SFI=2.60
      SRAIM=0.0
      SRO=0.0
      DRAIM=0.0
      DRO=0.0
      DO 390 I=1,NSPER
390   RNSP(I)=0.0
      IF(IFDEB)610,610,4390
C
C  CHECK VALIDITY OF CARRYOVER DATA
C
610   IF((TAPI.LT.0.10).OR.(TAPI.GT.60.0))GO TO 5610
620   IF((TAEI.LT.1.00).OR.(TAEI.GT.11.00))GO TO 5620
630   IF((TFI.LT.0.0).OR.(TFI.GT.10.0))GO TO 5630
640   IF((SAPI.LT.0.10).OR.(SAPI.GT.60.0))GO TO 5640
650   IF((SAEI.LT.1.00).OR.(SAEI.GT.11.00))GO TO 5650
660   IF((SFI.LT.0.0).OR.(SFI.GT.10.0))GO TO 5660
665   IF((SRAIM.LT.0.0).OR.(SRAIM.GT.60.0))GO TO 5665
670   IF((SRO.LT.0.0).OR.(SRO.GT.60.0))GO TO 5670
690   RNS=0.0
      DO 695 I=1,NSPER
695   RNS=RNSP(I)+RNS
      IF((RNS.LT.0.0).OR.(RNS.GT.60.0))GO TO 5685
C
C
C  CHECK P ARRAY TO MAKE SURE SPACE IS AVAILABLE TO STORE PARAMETRIC
C DATA
C
710   CALL CHECKP(IUSEP,LEFTP,IERR)
      IF(IERR)730,730,720
720   CALL ERROR
      IUSEP=0
      GO TO 755
C
C  NOW STORE PARAMETRIC DATA IN PO ARRAY.
C
730   PO(1)=IVERS+0.01
      DO 740 I=1,2
740   PO(I+1)=RID(I)
      DO 750 I=1,5
750   PO(I+3)=RNAME(I)
      PO(9)=IRNUM+0.01
      PO(10)=RLAT
      PO(11)=RLNG
      PO(12)=RFCTR
      PO(13)=R24
      PO(14)=PMAX
      PO(15)=ULIMW
      PO(16)=BLIMW
      PO(17)=ULIMS
      PO(18)=BLIMS
      PO(19)=KURV+0.01
      PO(20)=IDELTA+0.01
      PO(21)=NSW
      PO(22)=NSPER
      PO(23)=IUSEC
      PO(24)=IOFAAA
      PO(25)=ICOF
      PO(26)=TSIDRM(1)
      PO(27)=TSIDRM(2)
      PO(28)=DTCRM
      PO(29)=TSIDPE(1)
      PO(30)=TSIDPE(2)
      PO(31)=DTCPE
      PO(32)=TSIDRO(1)
      PO(33)=TSIDRO(2)
      PO(34)=DTCRO
      IF(IOFAAA)751,751,753
751   DO 752 I=35,43
752   PO(I)=BLANK
      GO TO 754
753   PO(35)=TSIDFI(1)
      PO(36)=TSIDFI(2)
      PO(37)=DTCFI
      PO(38)=TSIDAP(1)
      PO(39)=TSIDAP(2)
      PO(40)=DTCAP
      PO(41)=TSIDAE(1)
      PO(42)=TSIDAE(2)
      PO(43)=DTCAE
754   PO(44)=0.01
      PO(45)=0.01
      PO(46)=0.01
      PO(47)=0.01
C
C  CHECK C ARRAY TO MAKE SURE SPACE IS AVAILABLE TO STORE
C  CARRYOVER DATA
C
755   CALL CHECKC(IUSEC,LEFTC,IERR)
      IF(IERR)770,770,760
760   CALL ERROR
      IUSEC=0
      GO TO 10000
770   CO(1)=TAPI
      CO(2)=TAEI
      CO(3)=TFI
      CO(4)=SAPI
      CO(5)=SAEI
      CO(6)=SFI
      CO(7)=SRAIM
      CO(8)=SRO
      CO(9)=DRAIM
      CO(10)=DRO
      DO 780 I=1,NSPER
780   CO(I+10)=RNSP(I)
      GO TO 10000
C
C#######################################################################
C
C  DEBUG WRITE STATEMENTS
C
C#######################################################################
C
4100  WRITE(IODBUG,8100)SUBNAM,IVERS
      GO TO 110
4110  WRITE(IODBUG,8110)RID,RNAME,IRNUM,RLAT,RLNG
      GO TO 115
4115  WRITE(IODBUG,8115)RFCTR,R24,PMAX,ULIMW,BLIMW,ULIMS,BLIMS
      GO TO 120
4120  WRITE(IODBUG,8120)KURV,IDELTA,NSW,IOFAAA,ICOF
      GO TO 130
4130  WRITE(IODBUG,8130)TSIDRM,DTCRM,TSIDPE,DTCPE,TSIDRO,DTCRO
      GO TO 132
4135  WRITE(IODBUG,8135)TSIDFI,DTCFI,TSIDAP,DTCAP,TSIDAE,DTCAE
      GO TO 137
4380  WRITE(IODBUG,8380)TAPI,TAEI,TFI,SAPI,SAEI,SFI,SRAIM,SRO,DRAIM,DRO
      WRITE(IODBUG,8381)(RNSP(I),I=1,NSPER)
      GO TO 610
4390  WRITE(IODBUG,8390)
      WRITE(IODBUG,8380)TAPI,TAEI,TFI,SAPI,SAEI,SFI,SRAIM,SRO,DRAIM,DRO
      WRITE(IODBUG,8381)(RNSP(I),I=1,NSPER)
      GO TO 610
C
C#######################################################################
C
C  ERROR AND WARNING WRITE STATEMENTS
C
C#######################################################################
C
5140  WRITE(IPR,9140)IRNUM
      CALL WARN
      GO TO 150
5150  WRITE(IPR,9150)RLAT
      CALL WARN
      GO TO 160
5160  WRITE(IPR,9160)RLNG
      CALL WARN
      GO TO 170
5170  WRITE(IPR,9170)RFCTR
      CALL ERROR
      GO TO 180
5180  WRITE(IPR,9180)R24
      CALL ERROR
      GO TO 190
5190  WRITE(IPR,9190)PMAX
      CALL ERROR
      GO TO 200
5200  WRITE(IPR,9200)ULIMW
      CALL ERROR
      GO TO 202
5202  WRITE(IPR,9202)BLIMW
      CALL ERROR
      GO TO 210
5210  WRITE(IPR,9210)ULIMS
      CALL ERROR
      GO TO 212
5212  WRITE(IPR,9212)BLIMS
      CALL ERROR
      GO TO 220
5220  WRITE(IPR,9220)KURV
      CALL ERROR
      GO TO 230
5230  WRITE(IPR,9230)IDELTA
      CALL ERROR
      GO TO 240
5240  WRITE(IPR,9240)NSW
      CALL ERROR
      GO TO 300
5310  WRITE(IPR,9310)NSW,IDELTA
      CALL ERROR
      GO TO 310
5320  WRITE(IPR,9320)NSPER
      CALL ERROR
      GO TO 330
5330  WRITE(IPR,9330)NSPER
      CALL ERROR
      GO TO 330
5610  WRITE(IPR,9610)TAPI
      CALL ERROR
      GO TO 620
5620  WRITE(IPR,9620)TAEI
      CALL ERROR
      GO TO 630
5630  WRITE(IPR,9630)TFI
      CALL ERROR
      GO TO 640
5640  WRITE(IPR,9640)SAPI
      CALL ERROR
      GO TO 650
5650  WRITE(IPR,9650)SAEI
      CALL ERROR
      GO TO 660
5660  WRITE(IPR,9660)SFI
      CALL ERROR
      GO TO 665
5665  WRITE(IPR,9665)SRAIM
      CALL ERROR
      GO TO 670
5670  WRITE(IPR,9670)SRO
      CALL ERROR
      GO TO 690
5685  WRITE(IPR,9685)RNS
      CALL ERROR
      GO TO 710
C
C#######################################################################
C
C  INPUT FORMAT STATEMENTS
C
C#######################################################################
C
7110  FORMAT(2A4,6X,5A4,5X,I4,6X,F5.2,5X,F5.2)
7115  FORMAT(3F5.3,4F5.2)
7120  FORMAT(5I5)
7130  FORMAT(2A4,3X,A4,4X,2A4,3X,A4,5X,2A4,3X,A4)
7150  FORMAT(10F5.2)
7151  FORMAT(12F5.2,/,12F5.2)
C
C#######################################################################
C
C  DEBUG FORMAT STATEMENTS
C
C#######################################################################
C
8100  FORMAT(/5X,2A4,' DEBUG OUTPUT.',5X,'VERSION: ',I4)
8110  FORMAT(/20X,'RID   = ',2A4,/20X,'RNAME = ',5A4,
     1      /20X,'IRNUM = ',I4,/20X,'RLAT  = ',F5.2,
     2      /20X,'RLNG  = ',F5.2)
8115  FORMAT(/20X,'RFCTR = ',F5.3,/20X,'R24   = ',F5.3,
     1      /20X,'PMAX  = ',F5.3,/20X,'ULIMW = ',F5.2,
     2      /20X,'BLIMW = ',F5.2,/20X,'ULIMS = ',F5.2,
     3      /20X,'BLIMS = ',F5.2)
8120  FORMAT(/20X,'KURV  = ',I4,/20X,'IDELTA= ',I4,
     1      /20X,'NSW   = ',I4,/20X,'IOFAAA= ',I4,
     2      /20X,'ICOF  = ',I4)
8130  FORMAT(/20X,'TSIDRM= ',2A4,10X,'DTCRM= ',A4,
     1      /20X,'TSIDPE= ',2A4,10X,'DTCPE= ',A4,
     2      /20X,'TSIDRO= ',2A4,10X,'DTCRO= ',A4)
8135  FORMAT(/20X,'TSIDFI= ',2A4,10X,'DTCFI= ',A4,
     1      /20X,'TSIDAP= ',2A4,10X,'DTCAP= ',A4,
     2      /20X,'TSIDAE= ',2A4,10X,'DTCAE= ',A4)
8380  FORMAT(/20X,'TAPI  = ',F5.2,/20X,'TAEI  = ',F5.2,
     1      /20X,'TFI   = ',F5.2,/20X,'SAPI  = ',F5.2,
     2      /20X,'SAEI  = ',F5.2,/20X,'SFI   = ',F5.2,
     3      /20X,'SRAIM = ',F5.2,/20X,'SRO   = ',F5.2,
     4      /20X,'DRAIM = ',F5.2,/20X,'DRO   = ',F5.2)
8381  FORMAT(20X,'RAIN/MELT FOR EACH PERIOD WITHIN THE NEW STORM ',
     1      'WINDOW (OLDEST PERIOD IS FIRST):',
     2      /20X,12(F5.2,3X),/20X,12(F5.2,3X))
8390  FORMAT(/20X,'DEFAULT VALUES WERE USED FOR THE ',
     1      'FOLLOWING CARRYOVER VARIABLES: ',/)
C
C#######################################################################
C
C  ERROR AND WARNING FORMAT STATEMENTS
C
C#######################################################################
C
9140  FORMAT(/10X,'** WARNING ** ILLEGAL RUNOFF ZONE NUMBER :  ',I4,
     1      /24X,'LIMITS ARE 0 THROUGH 1000.',
     2      /24X,'NOTE:  RUNOFF ZONE NUMBER IS ENTERED FOR POSSIBLE ',
     3      'EXTERNAL USE,',/24X,'BUT IS NOT NEEDED BY THE API - HAR ',
     4      'OPERATION ITSELF.')
9150  FORMAT(/10X,'** WARNING ** ILLEGAL RUNOFF ZONE LATITUDE :  ',F6.2,
     1      /24X,'LIMITS ARE 36.00 THROUGH 43.50 DEG DECIMAL.',
     2      /24X,'NOTE:  RUNOFF ZONE LATITUDE IS ENTERED FOR POSSIBLE ',
     3      'EXTERNAL USE, ',/24X,'BUT IS NOT NEEDED BY THE API - HAR ',
     4      'OPERATION ITSELF.')
9160  FORMAT(/10X,'** WARNING ** ILLEGAL RUNOFF ZONE LONGITUDE :  ',
     1      F6.2,/24X,'LIMITS ARE 73.50 THROUGH 83.50 DEG DECIMAL.',
     2      /24X,'NOTE:  RUNOFF ZONE LONGITUDE IS ENTERED FOR ',
     3      'POSSIBLE EXTERNAL USE,',/24X,'BUT IS NOT NEEDED BY THE ',
     4      'API - HAR OPERATION ITSELF.')
9170  FORMAT(/10X,'** ERROR **   ILLEGAL ZONE RUNOFF FACTOR :  ',F6.2,
     1      /24X,'LIMITS ARE 0 THROUGH 5.00.',
     2      /24X,'A VALUE OF 0.0 MEANS NO ADJUSTMENTS TO API - HAR ',
     3      'COMPUTED RUNOFF.')
9180  FORMAT(/10X,'** ERROR **   ILLEGAL 24-HOUR API RECESSION ',
     1      'FACTOR :  ',F6.2,/24X,'LIMITS ARE 0.75 THROUGH 0.99.')
9190  FORMAT(/10X,'** ERROR **   ILLEGAL NEW STORM RAIN/MELT LIMIT :  ',
     1      F6.2,/24X,'LIMITS ARE 0.0 THROUGH 1.00 INCHES.')
9200  FORMAT(/10X,'** ERROR **   ILLEGAL UPPER LIMIT FOR AEI ',
     1      '(WINTER CURVE) :  ',F6.2,/24X,'LIMITS ARE 9.50 THROUGH ',
     2      '11.00 INCHES.')
9202  FORMAT(/10X,'** ERROR **   ILLEGAL LOWER LIMIT FOR AEI ',
     1      '(WINTER CURVE) :  ',F6.2,/24X,'LIMITS ARE 1.00 THROUGH ',
     1      '1.90 INCHES.')
9210  FORMAT(/10X,'** ERROR **   ILLEGAL UPPER LIMIT FOR AEI ',
     1      '(SUMMER CURVE) :  ',F6.2,/24X,'LIMITS ARE 9.50 THROUGH ',
     2      '11.00 INCHES.')
9212  FORMAT(/10X,'** ERROR **   ILLEGAL LOWER LIMIT FOR AEI ',
     1      '(SUMMER CURVE) :  ',F6.2,/24X,'LIMITS ARE 1.00 THROUGH ',
     2      '2.00 INCHES.')
9220  FORMAT(/10X,'** ERROR **   ILLEGAL API/AEI/FI CURVE NUMBER :  ',
     1      I5,/24X,'LIMITS ARE 1 THROUGH 21.')
9230  FORMAT(/10X,'** ERROR **   ILLEGAL TIME STEP INTERVAL :  ',I5,
     1      /24X,'LEGAL VALUES ARE 1,2,3,4,6,8,12 OR 24 HOURS.')
9240  FORMAT(/10X,'** ERROR **   ILLEGAL NEW STORM WINDOW ',I5,
     1      /24X,'LEGAL VALUES ARE 1,2,3,4,6,8,12 OR 24 HOURS.')
9310  FORMAT(/10X,'** ERROR **   THE NUMBER OF HOURS USED TO DEFINE ',
     1      'THE NEW STORM WINDOW ',/24X,'IS NOT A POSITIVE, NON-ZERO ',
     2      'INTEGER MULTIPLE OF THE COMPUTATIONAL TIME STEP INTERVAL.',
     3      /24X,'THIS MEANS THAT THE TIME INTERVAL OVER WHICH THE ',
     4      'API - HAR OPERATION CHECKS FOR A STORM BREAK ',/24X,
     5      'MAY NOT BE WHAT IS ACTUALLY DESIRED.  SUGGESTED REMEDY:  ',
     6      'ADJUST THE NEW STORM WINDOW AND/OR THE COMPUTATIONAL ',
     7      'TIME STEP INTERVAL.',/24X,'THE RESPECTIVE VALUES OF THESE',
     8      ' VARIABLES JUST READ IN ARE ',I5,' AND ',I5)
9320  FORMAT(/10X,'** ERROR **   THE NUMBER OF PERIODS CALCULATED ',
     1      'FROM THE SPECIFIED NEW STORM WINDOW IS LESS THAN 1 :  ',
     2      F6.2,/24X,'LIMITS ARE 1 THROUGH 24.')
9330  FORMAT(/10X,'** ERROR **   THE NUMBER OF PERIODS CALCULATED ',
     1      'FROM THE SPECIFIED NEW STORM WINDOW IS GREATER THAN 24 :',
     2      '  ',F6.2,/24X,'LIMITS ARE 1 THROUGH 24.')
9610  FORMAT(/10X,'** ERROR **   ILLEGAL 12Z API VALUE :  ',F6.2,
     1      /24X,'LIMITS ARE 0.10 THROUGH 60.00 INCHES.')
9620  FORMAT(/10X,'** ERROR **   ILLEGAL 12Z AEI VALUE :  ',F6.2,
     1      /24X,'LIMITS ARE 1.00 THROUGH 11.00 INCHES.')
9630  FORMAT(/10X,'** ERROR **   ILLEGAL 12Z FI VALUE :  ',F6.2,
     1      /24X,'LIMITS ARE 0.00 THROUGH 10.00 INCHES.')
9640  FORMAT(/10X,'** ERROR **   ILLEGAL 12Z STORM API VALUE :  ',F6.2,
     1      /24X,'LIMITS ARE 0.10 THROUGH 60.00 INCHES.')
9650  FORMAT(/10X,'** ERROR **   ILLEGAL 12Z STORM AEI VALUE :  ',F6.2,
     1      /24X,'LIMITS ARE 1.00 THROUGH 11.00 INCHES.')
9660  FORMAT(/10X,'** ERROR **   ILLEGAL 12Z STORM FI VALUE :  ',F6.2,
     1      /24X,'LIMITS ARE 0.00 THROUGH 10.00.')
9665  FORMAT(/10X,'** ERROR **   ILLEGAL STORM RAIN/MELT VALUE ',
     1      'AT 12Z :  ',F6.2,/24X,'LIMITS ARE 0.00 THROUGH 60.00 ',
     2      'INCHES.')
9670  FORMAT(/10X,'** ERROR **   ILLEGAL STORM RUNOFF VALUE AT 12Z :  ',
     1      /24X,'LIMITS ARE 0.00 THROUGH 60.00 INCHES.')
9685  FORMAT(/10X,'** ERROR **   ILLEGAL RAIN/MELT TOTAL IN THE ',
     1      'NEW STORM WINDOW :  ',F6.2,/24X,
     2      'LIMITS ARE 0.00 THROUGH 60.00 INCHES.')
10000 RETURN
      END
