C MEMBER PIN12
C  (from old member FCPIN12)
C
      SUBROUTINE PIN12(P,LEFTP,IUSEP)
C
C     THIS IS THE INPUT SUBROUTINE FOR THE INSTANTANEOUS
C     DISCHARGE PLOT OPERATION. THIS OPERATION HAS THE OPTION
C     OF READING AND TABULATING A PRECIPITATION AND/OR RUNOFF
C     TIME SERIES. IT ALSO CAN TABULATE UP TO 2 INSTANTANEOUS
C     DISCHARGE TIME SERIES. THERE IS NO LIMIT TO THE NUMBER OF
C     INSTANTANEOUS DISCHARGE TIME SERIES THAT CAN BE PLOTTED.
C     THIS SUBROUTINE INPUTS ALL PARAMETERS FOR THE INSTANTANEOUS
C     DISCHARGE OPERATION AND PUTS THESE VALUES IN THE P ARRAY.
C     THERE IS NO CARRYOVER FOR THIS OPERATION.
C
C     THIS SUBROUTINE INITIALLY WRITTEN BY
C          DAVID REED--HRL   NOV 1979
C
C
      DIMENSION P(1),TITLE(5),TSID(2),TSTL(3),RCID(2)
C
C     COMMON BLOCKS
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
      INCLUDE 'common/fprog'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin12.f,v $
     . $',                                                             '
     .$Id: pin12.f,v 1.1 1995/09/17 18:48:04 dws Exp $
     . $' /
C    ===================================================================
C
      DATA DOT/1H./,BLANK/1H /,DIM/4HL3/T/,DIMPN/4HL   /,EXCLAM/1H!/
      DATA BLANK4/4H    /
      IF(MAINUM.EQ.1)DOT=EXCLAM
C
C     CHECK TRACE LEVEL--FOR THIS SUBROUTINE=1
C
      IF(ITRACE.GE.1)WRITE(IODBUG,900)
  900 FORMAT(1H0,16H** PIN12 ENTERED)
C
C     CHECK TO SEE IF DEBUG OUTPUT IS NEEDED
C
      IBUG=0
      IF(IDBALL.GT.0)IBUG=1
      IF(NDEBUG.EQ.0) GO TO 12
      DO 10 I=1,NDEBUG
      IF(IDEBUG(I).EQ.12) GO TO 11
   10 CONTINUE
      GO TO 12
   11 IBUG=1
C
C     READ IN DATA CARD #1-TITLE,NUMBER OF PLOTS,AND OPERATOR
C            PLOT OPTIONS
C
   12 CONTINUE
      READ(IN,901)TITLE,NPLOTS,IOPTAB,IOPLT,RCID
  901 FORMAT(5A4,3X,I2,4X,I1,3X,I2,2X,2A4)
C
C     CALCULATE SIZE OF P ARRAY
C
      IVER=1
      IF(IOPTAB.EQ.0)NTAB=0
      IF(IOPTAB.EQ.1)NTAB=1
      IF(IOPTAB.EQ.2)NTAB=1
      IF(IOPTAB.EQ.3)NTAB=2
      IF(IOPTAB.GE.0.AND.IOPTAB.LE.3) GO TO 200
      WRITE(IPR,920)
  920 FORMAT(1H0,10X,88H**ERROR** OPTION TO TABULATE THE PCPN AND RUNOFF
     1 TIME SERIES IS NOT IN THE PROPER RANGE.)
      CALL ERROR
      IUSEP=0
      GO TO 160
  200 IRC=0
      IERRC=2
      IF(RCID(1).NE.BLANK4.OR.RCID(2).NE.BLANK4)CALL CHEKRC(RCID,IERRC)
      IF(IERRC.NE.2)IRC=1
      NPREQ=11+NTAB*7+NPLOTS*8+IRC*5
      NOFILL=0
      IF(NPLOTS.GE.1)GO TO 202
      WRITE(IPR,921)
  921 FORMAT(1H0,10X,47H**ERROR** NUMBER OF TIME SERIES TO PLOT IS ZERO)
      CALL ERROR
      IUSEP=0
      GO TO 160
  202 IF(IOPLT.LE.NPLOTS) GO TO 201
      WRITE(IPR,922)
  922 FORMAT(1H0,10X,83H**ERROR** PLOT OPTION PARAMETER IS GREATER THAN
     1NUMBER OF TIME SERIES TO BE PLOTTED)
      CALL ERROR
  201 IF(NPREQ.LE.LEFTP) GO TO 100
      NOFILL=1
      IUSEP=0
      WRITE(IPR,902)
  902 FORMAT(1H0,10X,74H**ERROR** THIS OPERATION NEEDS MORE SPACE THAN I
     1S AVAILABLE IN THE P ARRAY)
      CALL ERROR
      GO TO 110
C
C     STORE VALUES IN P ARRAY IF THERE IS ENOUGH ROOM
C
  100 P(1)=IVER+0.01
      DO 120 I=1,5
  120 P(I+1)=TITLE(I)
      P(7)=NPLOTS+0.01
      P(8)=IOPTAB+0.01
      P(9)=IOPLT+0.01
      IDTPLT=24
      P(11)=0.01
      IF(IRC.EQ.1)P(11)=NPREQ-4+0.01
      IPTR=11
C
C     READ IN PCPN AND RUNOFF TIME SERIES IF NEEDED
C     IF BOTH ARE READ IN, READ IN PCPN TIME SERIES FIRST
C
  110 IF(IOPTAB.EQ.0) GO TO 130
      DO 140 I=1,NTAB
      READ(IN,903)TSID,TSTYPE,IDT,TSTL
  903 FORMAT(2X,2A4,1X,A4,3X,I2,8X,3A4)
C
C     CHECK TIME SERIES USING SUBROUTINE CHEKTS
C
      CALL CHEKTS(TSID,TSTYPE,IDT,1,DIMPN,1,1,IERFLG)
      IF(IBUG.EQ.1)WRITE(IODBUG,988)
  988 FORMAT(1H0,10X,'**BACK FROM CHEKTS**')
      IF(IDT.LT.IDTPLT)IDTPLT=IDT
      IF(NOFILL.EQ.1) GO TO 140
C
C     FILL P ARRAY
C
      P(IPTR+1)=TSID(1)
      P(IPTR+2)=TSID(2)
      P(IPTR+3)=TSTYPE
      P(IPTR+4)=IDT+0.01
      P(IPTR+5)=TSTL(1)
      P(IPTR+6)=TSTL(2)
      P(IPTR+7)=TSTL(3)
      IPTR=IPTR+7
  140 CONTINUE
C
C     READ IN INFORMATION FOR TIME SERIES THAT ARE TO BE PLOTTED
C     THE FIRST TWO TIME SERIES READ IN WILL BE PLOTTED
C
  130 DO 150 I=1,NPLOTS
      READ(IN,904)TSID,TSTYPE,IDT,TSTL,PSBL
  904 FORMAT(2X,2A4,1X,A4,3X,I2,8X,3A4,4X,A1)
      IF(IDT.LE.0)NOFILL=1
C
C     CHECK TIME SERIES USING SUBROUTINE CHEKTS
C
      CALL CHEKTS(TSID,TSTYPE,IDT,1,DIM,1,1,IERFLG)
      IF(IBUG.EQ.1)WRITE(IODBUG,988)
      IF(PSBL.EQ.DOT)GO TO 300
      IF(PSBL.EQ.BLANK)GO TO 300
      GO TO 301
  300 WRITE(IPR,940)DOT
  940 FORMAT(1H0,10X,45H**ERROR** INVALID PLOTTING SYMBOL OF ' ' OR ',
     1  A1,1H')
      CALL ERROR
  301 IF(IDT.LT.IDTPLT)IDTPLT=IDT
      IF(NOFILL.EQ.1) GO TO 150
C
C     FILL P ARRAY
C
      P(IPTR+1)=TSID(1)
      P(IPTR+2)=TSID(2)
      P(IPTR+3)=TSTYPE
      P(IPTR+4)=IDT+0.01
      P(IPTR+5)=TSTL(1)
      P(IPTR+6)=TSTL(2)
      P(IPTR+7)=TSTL(3)
      P(IPTR+8)=PSBL
      IPTR=IPTR+8
  150 CONTINUE
      IF(NOFILL.EQ.0)P(10)=IDTPLT+0.01
      IF(IRC.EQ.0)GO TO 155
      IF(NOFILL.EQ.1)GO TO 180
      P(IPTR+1)=RCID(1)
      P(IPTR+2)=RCID(2)
  180 READ(IN,950)SYMFS,SYMLL,SYMUL
  950 FORMAT(3(4X,A1))
      IF(SYMFS.EQ.DOT.OR.SYMLL.EQ.DOT.OR.SYMUL.EQ.DOT)GO TO 151
      IF(NOFILL.EQ.1)GO TO 155
      DO 154 I=1,3
  154 P(IPTR+2+I)=BLANK4
      P(IPTR+3)=SYMFS
      P(IPTR+4)=SYMLL
      P(IPTR+5)=SYMUL
      GO TO 155
  151 WRITE(IPR,951)DOT
  951 FORMAT(1H0,10X,'**ERROR** INVALID PLOTTING SYMBOL (',A1,
     1  ') ENTERED')
      CALL ERROR
  155 IUSEP=0
      IF(NOFILL.EQ.1) GO TO 160
C
C     DETERMINE PLOT TIME INTERVAL
C
      IPOINT=11
      IERFLG=0
      IF(NTAB.EQ.0) GO TO 323
      DO 305 I=1,NTAB
      IDT=P(IPOINT+4)
      IF(((IDT/IDTPLT)*IDTPLT).EQ.IDT) GO TO 311
      IERFLG=1
  311 IPOINT=IPOINT+7
  305 CONTINUE
  323 DO 310 I=1,NPLOTS
      IDT=P(IPOINT+4)
      IF(((IDT/IDTPLT)*IDTPLT).EQ.IDT) GO TO 302
      IERFLG=1
  302 IPOINT=IPOINT+8
  310 CONTINUE
      IF(IERFLG.EQ.0) GO TO 331
      WRITE(IPR,931)
  931 FORMAT(1H0,10X,84H**ERROR** ALL TIME INTERVALS MUST BE AN EVEN MUL
     1TIPLE OF THE SMALLEST TIME INTERVAL.)
      CALL ERROR
  331 IUSEP=NPREQ
      IF(IBUG.EQ.0) GOTO 160
      WRITE(IODBUG,905)NPREQ
  905 FORMAT(1H0,10X,39HCONTENTS OF P ARRAY--NUMBER OF VALUES =,I3)
      WRITE(IODBUG,906)(P(I),I=1,NPREQ)
  906 FORMAT(1H0,14(F8.3,1X))
C
C......................................................................
C
C     CONTENTS OF P ARRAY
C
C   POSITION    CONTENTS
C
C     1         VERSION NUMBER- INTEGER
C     2-6       GENERAL TITLE OR HEADING
C     7         NUMBER OF TIME SERIES TO BE PLOTTED
C     8         PLOT TABULATION INDICATOR
C                =0, PCPN AND RUNOFF TIME SERIES NOT TABULATED
C                =1, ONLY PCPN TIME SERIES TABULATED
C                =2, ONLY RUNOFF TIME SERIES TABULATED
C                =3, BOTH PCPN AND RUNOFF TIME SERIES TABULATED
C     9         PLOT INDICATOR
C                =0, ALL DISCHARGE TIME SERIES ARE PLOTTED EVERY
C                        TIME INTERVAL
C                = INTEGER VALUE - INTEGER VALUE INDICATES WHICH
C                        DISCHARGE TIME SERIES MUST HAVE A VALUE DURING
C                        ONE DAY SO THAT THE OTHER DISCHARGES ARE
C                        PLOTTED FOR THAT DAY
C     10         TIME INCREMENT OF PLOT
C     11         RATING CURVE INDICATOR
C                 =0, NO RATING CURVE INFORMATION STORED AND
C                     NO STAGE SCALE WILL BE COMPUTED
C               GT 0, RATING CURVE I.D. STORED IN LOCATION P(P(11))
C                     STAGE SCALE WILL BE COMPUTED FOR PLOT
C                     FLOOD STAGE, AND LOWER AND UPPER LIMIT OF
C                     RATING CURVE MAY BE PLOTTED
C
C          THE REMAINDER OF THE P ARRAY IS FILLED AS FOLLOWS:
C            (1) THE PCPN AND RUNOFF TIME SERIES INFORMATION
C                7 POSITIONS FOR EACH TIME SERIES
C                THE 7 POSITIONS ARE FILLED AS FOLLOWS:
C                       1-2 TIME SERIES I.D.
C                       3   TIME SERIES TYPE
C                       4   TIME SERIES TIME INTERVAL
C                       5-7 TIME SERIES NAME OR TITLE
C
C            (2) INSTANTANEOUS DISCHARGE TIME SERIES INFORMATION
C                8 POSITIONS FOR EACH FILLED AS FOLLOWS:
C                       1-2 TIME SERIES I.D.
C                       3   TIME SERIES TYPE
C                       4   TIME SERIES TIME INTERVAL
C                       5-7 TIME SERIES TITLE OR NAME
C                       8   PLOTTING SYMBOL FOR TIME SERIES
C
C            (3) RATING CURVE INFORMATION (IF P(11) GREATER THAN ZERO)
C                5 POSITIONS FILLED AS FOLLOWS:
C                       1-2 RATING CURVE I.D.
C                       3   PLOTTING SYMBOL FOR FLOOD STAGE
C                           (DO NOT PLOT IF BLANK)
C                       4   PLOTTING SYMBOL FOR RATING CURVE LOWER LIMIT
C                           (DO NOT PLOT IF BLANK)
C                       5   PLOTTING SYMBOL FOR RATING CURVE UPPER LIMIT
C                           (DO NOT PLOT IF BLANK)
C
C.......................................................................
  160 RETURN
      END
