C MODULE PIN18
C
C   THIS SUBROUTINE IS THE INPUT SUBROUTINE FOR  THE
C   PLOT-TS OPERATION.
C
C   THIS SUBROUTINE WAS INITIALLY PROGRAMMED BY
C   GERALD N. DAY - HRL  MAY 1980
C
C***********************************************************************
C
C   CONTENTS OF THE P ARRAY -
C
C    GENERAL INFO
C
C    POSITION      NAME      DESCRIPTION
C       1          IVER       VERSION NUMBER
C      2-6         TITLE      RUN INFO
C       7          NOPT       PLOT OPTION
C       8          NPLOTS     NUMBER OF PLOTS
C       9          NTTS       TOTAL NUMBER OF TIME SERIES
C      10          NPER       NUMBER OF PLOTTING PERIODS
C                               = 0 IF NOPT.NE.2
C      11          IDTP       PLOT TIME INTERVAL
C
C    PLOT INFO
C
C    POSITION      NAME      DESCRIPTION
C       1          TYPE       PLOT TYPE ('ARIT' OR 'LOG ')
C       2          NPTS       NUMBER OF COLUMNS
C       3          PMIN       MINIMUM PLOT ORDINATE
C       4          PMAX       MAXIMUM PLOT ORDINATE
C       5          NTS        NUMBER OF TIME SERIES
C       6          UNITSM     STANDARD METRIC UNITS
C       7          UNITSE     STANDARD ENGLISH UNITS
C       8          CFACT      MULTIPLICATION FACTOR
C       9          CONST      ADDITION CONSTANT
C         ENGLISH = METRIC * CFACT + CONST
C
C    TIME SERIES INFO
C
C    POSITION      NAME      DESCRIPTION
C      1-2         TSID       INTERNAL TS IDENTIFICATION
C       3          TSTYPE     TS DATA TYPE
C       4          IDT        TS TIME INTERVAL
C      5-7         TSTL       TS TITLE
C       8          SYMBOL     PLOT SYMBOL
C      9-10        VALNM      VALUE NAME FOR MULTI-VALUED TS
C       11         NVPDT      NUMBER OF VALUES PER TIME INTERVAL
C       12         LOCV       VALUE NUMBER
C
C    INFO FOR PLOT OPTION 1
C
C    POSITION      NAME      DESCRIPTION
C       1          NFRWY      FIRST AVAILABLE RECORD ON WY SCRATCH FILE
C
C    INFO FOR PLOT OPTION 2
C    POSITION      NAME      DESCRIPTION
C       1          IJDAP      START JULIAN DAY FOR PERIOD
C       2          LJDAP      END JULIAN DAY FOR PERIOD
C
C    INFO FOR PLOT OPTION 4
C
C    POSITION      NAME      DESCRIPTION
C       1          ITS        TS SEQUENCE NUMBER
C       2          CRIT       PLOTTING CRITERIA
C       3          KODE       CRITERIA CODE
C                               =0 MINIMUM CRITERIA
C                               =1 MAXIMUM CRITERIA
C
C   ORGANIZATION
C    A. GENERAL INFO
C      B. PLOT 1 INFO
C
C          PLOT NPLOTS INFO
C         C. TS 1 INFO
C
C             TS NTTS INFO
C            D. PLOT OPTION INFO
C
C***********************************************************************
      SUBROUTINE PIN18(PO,LEFTP,IUSEP)
C
      DIMENSION PO(1),TITLE(5),PTYPE(2),TSID(2)
      DIMENSION TSTL(3),VALNM(2)
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
      COMMON/FPROG/MAINUM,VERS,VDATE(2),PNAME(5),NDD
      COMMON/FWYDS/IRWY,NXWY,NTWY,IDEFWY
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin18.f,v $
     . $',                                                             '
     .$Id: pin18.f,v 1.2 1998/07/02 16:43:05 page Exp $
     . $' /
C    ===================================================================
C
      DATA PTYPE  / 4hARIT,4hLOG  /, BLANK / 4h     /, DOT / 4h.    /
      DATA BLANK4 / 4h     /, SYMI / 4hI    /
      IVER=1
      IF(ITRACE.GE.1) WRITE(IODBUG,900)
C   CHECK TO SEE IF DEBUG INFO SHOULD BE PRINTED.
      IBUG=0
      IF(IDBALL.GT.0) IBUG=1
      IF(NDEBUG.EQ.0) GO TO 12
      DO 10 I=1,NDEBUG
      IF(IDEBUG(I).EQ.18) GO TO 11
   10 CONTINUE
      GO TO 12
   11 IBUG=1
   12 CONTINUE
C
      NOFILL=0
C   READ GENERAL RUN INFO
      READ(IN,800) TITLE,NOPT,NPLOTS,NTTS,NPER
C   CHECK PLOT OPTION
      IF(NOPT.EQ.0) NOPT=3
      IF(NOPT.LT.1.OR.NOPT.GT.4) GO TO 15
      IF(MAINUM.EQ.1.AND.NOPT.LT.3) GO TO 15
      GO TO 20
   15 WRITE(IPR,905) NOPT
      CALL ERROR
      NOFILL=1
      GO TO 550
C   CALCULATE THE SPACE NEEDED IN THE P ARRAY, AND CHECK TO SEE
C   IF IT IS AVAILABLE
   20 NPREQ=11+9*NPLOTS+12*NTTS
      IF(NOPT.EQ.1) NPREQ=NPREQ+1
      IF(NOPT.EQ.2) NPREQ=NPREQ+NPER*2
      IF(NOPT.EQ.4) NPREQ=NPREQ+3
      CALL CHECKP(NPREQ,LEFTP,IERROR)
      IF(IERROR.EQ.0) GO TO 22
      NOFILL=1
      GO TO 30
   22 PO(1)=IVER+0.01
      DO 25 I=1,5
      PO(1+I)=TITLE(I)
   25 CONTINUE
      PO(7)=NOPT+.01
      PO(8)=NPLOTS+.01
      PO(9)=NTTS+.01
      PO(10)=NPER+.01
C   INITIALIZE COUNTERS AND CHECKS
   30 ICPTS=0
      NPTCHK=0
      NTSCHK=0
      NOTDAY=0
      IDTP=0
C   READ PLOT INFO
      DO 90 I=1,NPLOTS
      READ(IN,805) TYPE,NPTS,PMIN,PMAX,NTS
C   IF PLOT TYPE IS NOT 'LOG ' SET IT EQUAL TO 'ARIT'
      IF(TYPE.NE.PTYPE(2)) TYPE=PTYPE(1)
C   CHECK TO SEE IF THE NUMBER OF COLUMNS PER PLOT MUST
C   BE CALCULATED
      IF(ICPTS.EQ.1) GO TO 35
      IF(NPTS.GT.0.OR.I.GT.1) GO TO 40
      ICPTS=1
   35 NPTS=120/NPLOTS
   40 IF(NPTS.GE.20) GO TO 45
      WRITE(IPR,910) I
      CALL ERROR
      NOFILL=1
C
   45 IF(PMIN.GE.PMAX) GO TO 48
      IF(TYPE.EQ.PTYPE(1)) GO TO 50
      IF(PMIN.GT.0.0.AND.PMAX.GT.0.0) GO TO 50
   48 WRITE(IPR,915) I
      CALL ERROR
      NOFILL=1
C   SUM THE TOTAL NUMBER OF POINTS
   50 NPTCHK=NPTCHK+NPTS
C   SUM THE TOTAL NUMBER OF TIME SERIES
      NTSCHK=NTSCHK+NTS
      IF(NTSCHK.LE.NTTS) GO TO 52
      WRITE(IPR,935)
      CALL ERROR
      NOFILL=1
C
   52 ITSCHK=1
C   READ TIME SERIES INFO
      DO 85 J=1,NTS
      READ(IN,810) TSID,TSTYPE,IDT,TSTL,SYMBOL,VALNM
      IF(IDT.NE.24) NOTDAY=1
      IDUM=0
      LOCV=1
      IF(J.GT.1) GO TO 60
C   CHECK TO SEE IF THE FIRST TIME SERIES FOR THE PLOT EXISTS.
      CALL CHEKTS(TSID,TSTYPE,IDT,0,DIM,1,IDUM,IERROR)
      IF(IERROR.EQ.0) GO TO 55
C   IF THE FIRST T.S. WAS NOT FOUND, SET ITSCHK = 0
      ITSCHK=0
      NOFILL=1
      GO TO 70
C   DETERMINE THE STANDARD METRIC UNITS FOR THE PLOT
   55 CALL FDCODE(TSTYPE,UNITSM,DIM,MDAT,NVPDT,TSCALE,NPADD,IERROR)
      IF(NVPDT.EQ.1) GO TO 58
C   DETERMINE THE LOCATION OF THE PARTICULAR VALUE OF INTEREST FOR
C   MULTI-VALUED TIME SERIES.
C
      CALL FMVTSL(TSTYPE,VALNM,LOCV)
      IF(LOCV.GT.0) GO TO 58
      WRITE(IPR,938) TSTYPE,VALNM
      CALL ERROR
      NOFILL=1
   58 IF(NOFILL.EQ.1) GO TO 70
C   DETERMINE  THE STANDARD ENGLISH UNITS FOR THE PLOT
C   AND THE METRIC/ENGLISH CONVERSION FACTORS
      CALL FCONVT(UNITSM,DIM,UNITSE,CFACT,CONST,IER)
      IPO=(I-1)*9+11
      PO(IPO+1)=TYPE
      PO(IPO+2)=NPTS+.01
      PO(IPO+3)=PMIN
      PO(IPO+4)=PMAX
      PO(IPO+5)=NTS+.01
      PO(IPO+6)=UNITSM
      PO(IPO+7)=UNITSE
      PO(IPO+8)=CFACT
      PO(IPO+9)=CONST
      GO TO 70
C   CHECK TO SEE IF THE TIME SERIES EXISTS
   60 CALL CHEKTS(TSID,TSTYPE,IDT,ITSCHK,DIM,1,IDUM,IERROR)
      IF(IERROR.EQ.0) GO TO 65
      NOFILL=1
      GO TO 70
   65 IF(ITSCHK.EQ.0) GO TO 70
C   CHECK TO SEE IF THE TIME SERIES HAS THE PROPER UNITS
      CALL FDCODE(TSTYPE,TSUNIT,DIM,MDAT,NVPDT,TSCALE,NPADD,IERROR)
      IF(NVPDT.EQ.1) GO TO 68
      CALL FMVTSL(TSTYPE,VALNM,LOCV)
      IF(LOCV.GT.0) GO TO 68
      WRITE(IPR,938) TSTYPE,VALNM
      CALL ERROR
      NOFILL=1
   68 IF(TSUNIT.EQ.UNITSM) GO TO 70
      WRITE(IPR,920) I
      CALL ERROR
      NOFILL=1
C   CHECK IF THE TIME SERIES PLOT SYMBOL IS LEGAL
   70 IF(SYMBOL.NE.DOT.AND.SYMBOL.NE.SYMI.AND.SYMBOL.NE.BLANK) GO TO 72
      WRITE(IPR,925) J,I
      CALL ERROR
      NOFILL=1
C
   72 IF(NOFILL.EQ.1) GO TO 85
      IPO=11+9*NPLOTS+(NTSCHK-NTS+J-1)*12
      PO(IPO+1)=TSID(1)
      PO(IPO+2)=TSID(2)
      PO(IPO+3)=TSTYPE
      PO(IPO+4)=IDT+.01
      PO(IPO+5)=TSTL(1)
      PO(IPO+6)=TSTL(2)
      PO(IPO+7)=TSTL(3)
      PO(IPO+8)=SYMBOL
      PO(IPO+9)=VALNM(1)
      PO(IPO+10)=VALNM(2)
      PO(IPO+11)=NVPDT+.01
      PO(IPO+12)=LOCV+.01
      IF(IDTP.NE.0) GO TO 80
      IDTP=IDT
      GO TO 85
C   DETERMINE THE LARGEST COMMON MULTIPLE
   80 CALL FLCMPL(IDT,IDTP)
   85 CONTINUE
   90 CONTINUE
      IPO=11+9*NPLOTS+12*NTTS
C   CHECK THE TOTAL NUMBER OF POINTS USED
      IF(NPTCHK.LE.120) GO TO 100
      WRITE(IPR,930)
      CALL ERROR
      NOFILL=1
C   IF PLOT OPTION IS WATER YEAR PLOT, CHECK TO SEE IF
C   ALL THE TIME SERIES ARE DAILY
  100 IF(NOTDAY.EQ.1.AND.NOPT.EQ.1) GO TO 105
      GO TO 110
  105 WRITE(IPR,940)
      CALL ERROR
      NOFILL=1
C   CALCULATE THE SPACE NEEDED ON THE WATER YEAR SCRATCH
C   FILE, AND UPDATE THE NEXT AVAILABLE RECORD
  110 IF(NOPT.NE.1) GO TO 114
      CALL DEFWY
      NFRWY=NXWY
      NTRWY=NTTS
      NXWY=NFRWY+NTRWY
      IF((NXWY-1).LE.NTWY) GO TO 112
      NXWY=NFRWY
      WRITE(IPR,942)
      CALL ERROR
      NOFILL=1
  112 IF(NOFILL.EQ.1) GO TO 113
      PO(IPO+1)=NFRWY+.01
      IPO=IPO+1
  113 CONTINUE
      GO TO 200
C
  114 IF(NOPT.NE.2) GO TO 125
C   READ STARTING AND ENDING DATES OF THE PLOTTING PERIODS.
      DO 120 I=1,NPER
      READ(IN,815) IMOP,IDAP,IYRP,LMOP,LDAP,LYRP
      CALL FCTZC(100,0,TZONE)
      CALL JULDA(IJDAP,IJHR,IMOP,IDAP,IYRP,1,100,0,TZONE)
      CALL JULDA(LJDAP,LJHR,LMOP,LDAP,LYRP,1,100,0,TZONE)
      IF(IJDAP.LE.LJDAP) GO TO 115
      WRITE(IPR,945) I
      CALL ERROR
      NOFILL=1
C
  115 IF(NOFILL.EQ.1) GO TO 120
      PO(IPO+1)=IJDAP+.01
      PO(IPO+2)=LJDAP+.01
      IPO=IPO+2
  120 CONTINUE
      GO TO 200
C
  125 IF(NOPT.NE.4) GO TO 200
C   READ DEFINITION INFO FOR THE CRITERIA TIME SERIES
      READ(IN,820) TSID,TSTYPE,IDT,CRIT,KODE,VALNM
      IF(NOFILL.EQ.1) GO TO 200
      DO 140 I=1,NTTS
      ITPO=11+9*NPLOTS+12*(I-1)
C   CHECK TO SEE IF THE CRITERIA T.S. IS IN THE LIST OF
C   T.S.'S TO BE PLOTTED
      IDTTS=PO(ITPO+4)
      IF(TSID(1).NE.PO(ITPO+1))GO TO 140
      IF(TSID(2).NE.PO(ITPO+2)) GO TO 140
      IF(TSTYPE.NE.PO(ITPO+3)) GO TO 140
      IF(IDT.NE.IDTTS) GO TO 140
      IF(VALNM(1).NE.PO(ITPO+9)) GO TO 140
      IF(VALNM(2).NE.PO(ITPO+10)) GO TO 140
      ITS=I
      GO TO 160
  140 CONTINUE
C
C   TIME SERIES IS NOT IN THE LIST, MUST ADD
C
      NTSCHK=NTSCHK+1
      IF(NTSCHK.LE.NTTS) GO TO 145
      WRITE(IPR,935)
      CALL ERROR
      NOFILL=1
  145 IDUM=0
      CALL CHEKTS(TSID,TSTYPE,IDT,0,DIM,1,IDUM,IERROR)
      IF(IERROR.EQ.0) GO TO 150
      NOFILL=1
      GO TO 200
  150 CALL FDCODE(TSTYPE,TSUNIT,DIM,MDAT,NVPDT,TSCALE,NPADD,
     1 IERROR)
      IF(NVPDT.EQ.1) GO TO 155
      CALL FMVTSL(TSTYPE,VALNM,LOCV)
      IF(LOCV.GT.0) GO TO 155
      WRITE(IPR,938) TSTYPE,VALNM
      CALL ERROR
      NOFILL=1
  155 IF(NOFILL.EQ.1) GO TO 200
C
      IPO=11+9*NPLOTS+(NTSCHK-1)*12
      PO(IPO+1)=TSID(1)
      PO(IPO+2)=TSID(2)
      PO(IPO+3)=TSTYPE
      PO(IPO+4)=IDT+.01
      PO(IPO+5)=BLANK4
      PO(IPO+6)=BLANK4
      PO(IPO+7)=BLANK4
      PO(IPO+8)=BLANK4
      PO(IPO+9)=VALNM(1)
      PO(IPO+10)=VALNM(2)
      PO(IPO+11)=NVPDT+.01
      PO(IPO+12)=LOCV+.01
      IPO=IPO+12
      ITS=NTSCHK
C
  160 IF(NOFILL.EQ.1) GO TO 200
      PO(IPO+1)=ITS+.01
      PO(IPO+2)=CRIT
      PO(IPO+3)=KODE+.01
      IPO=IPO+3
C
C   CHECK IF THE TOTAL NUMBER OF TIME SERIES IS
C   EQUAL TO THE SUM OF THE NUMBER OF TIME
C   SERIES ON EACH PLOT PLUS ANY ADDITIONAL
C   CRITERIA TIME SERIES. AN ERROR MESSAGE HAS
C   ALREADY BEEN WRITTEN IF THE TOTAL NUMBER OF
C   TIME SERIES IS LESS THAN THE SUM OF THE
C   NUMBER OF TIME SERIES .
C
  200 IF(NTSCHK.EQ.NTTS) GO TO 205
      WRITE(IPR,935)
      CALL ERROR
      NOFILL=1
C
C   PLACE PLOTTING TIME INTERVAL IN THE P ARRAY
C
  205 IF(NOFILL.EQ.1) GO TO 550
      PO(11)=IDTP+.01
C   SET NUMBER OF PLACES USED IN THE P ARRAY
      IUSEP=IPO
C   CHECK IF DEBUG INFO IS REQUESTED
      IF(IBUG.NE.1) GO TO 600
      WRITE(IODBUG,955)
      WRITE(IODBUG,956) (PO(I),I=1,IPO)
      GO TO 600
  550 IUSEP=0
  600 IF(ITRACE.GE.1) WRITE(IODBUG,960)
      RETURN
  800 FORMAT(5X,5A4,4I5)
  805 FORMAT(1X,A4,I5,2F10.0,I5)
  810 FORMAT(2X,2A4,1X,A4,3X,I2,8X,3A4,4X,A1,2X,2A4)
  815 FORMAT(6I5)
  820 FORMAT(2X,2A4,1X,A4,3X,I2,F10.0,I5,2X,2A4)
  900 FORMAT(1H0,16H** PIN18 ENTERED)
  905 FORMAT(1H0,10X,9H**ERROR**,I4,27H IS NOT A VALID PLOT OPTION)
  910 FORMAT(1H0,10X,14H**ERROR** PLOT,I4,
     1 33H WAS NOT ALLOCATED ENOUGH COLUMNS)
  915 FORMAT(1H0,10X,14H**ERROR** PLOT,I4,
     1 47H HAS INVALID MINIMUM AND MAXIMUM PLOT ORDINATES)
  920 FORMAT(1H0,10X,38H**ERROR** ALL THE TIME SERIES FOR PLOT,
     1 I4,27H DO NOT HAVE THE SAME UNITS)
  925 FORMAT(1H0,10X,12H**ERROR** TS,I4,8H ON PLOT,I4,
     1 22H HAS AN ILLEGAL SYMBOL)
  930 FORMAT(1H0,10X,47H**ERROR** THE TOTAL NUMBER OF COLUMNS ALLOCATED,
     1 20H IS GREATER THAN 120)
  935 FORMAT(1H0,10X,44H**ERROR** THE TOTAL NUMBER OF TIME SERIES IS,
     1 10H INCORRECT)
  938 FORMAT(1H0,10X,10H**ERROR** ,A4,
     1 33H IS A MULTI-VALUED DATA TYPE AND ,2A4,
     2 26H IS NOT A VALID VALUE NAME)
  940 FORMAT(1H0,10X,39H**ERROR** ALL TIME SERIES ARE NOT DAILY)
  942 FORMAT(1H0,10X,30H**ERROR** NOT ENOUGH SPACE ON ,
     1 16HTHE SCRATCH FILE)
  945 FORMAT(1H0,10X,32H**ERROR** THE DATE(S) FOR PERIOD,
     1 I4,14H ARE NOT VALID)
  950 FORMAT(1H0,10X,42H**ERROR** THE CRITERIA TIME SERIES WAS NOT,
     1 47H FOUND IN THE LIST OF TIME SERIES TO BE PLOTTED)
  955 FORMAT(1H0,33H** PIN18 CONTENTS OF THE P ARRAY )
  956 FORMAT(1X,10G12.5)
  960 FORMAT(1H0,15H** PIN18 EXITED)
      END
