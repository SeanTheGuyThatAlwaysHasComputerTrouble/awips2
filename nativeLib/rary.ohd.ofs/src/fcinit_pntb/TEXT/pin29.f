C MODULE PIN29
C-----------------------------------------------------------------------
C
      SUBROUTINE PIN29 (PO,LEFTP,IUSEP,CO,LEFTC,IUSEC)
C
C  THIS SUBROUTINE READS AND CHECKS ALL CARD INPUT FOR SEGMENT
C  DEFINITION FOR THE API-MKC OPERATION.  IT THEN FILLS THE PO AND CO
C  ARRAYS.
C
C  THERE ARE 3 OPTIONS FOR SELECTING THE 6 CONSTANTS AND 2 API
C  RECESSION VALUES FOR THE API/AI RELATIONSHIP IN THIS ROUTINE.  THE
C  USER MAY SELECT A RELATIONSHIP FROM EITHER THE KANSAS CITY OR
C  MINNEAPOLIS RFC'S OR INPUT HIS OWN VALUES.
C
C  THE USER HAS THE OPTION OF CREATING OUTPUT TIME SERIES FOR CURRENT
C  API AND/OR AI VALUES.
C
C  ROUTINE INITIALLY WRITTEN BY - LARRY BLACK - MBRFC - 7/982
C***********************************************************************
C  PRINCIPAL VARIABLES...
C
C  FOR DEFINITION OF VARIABLES IN COMMON BLOCKS, SEE SECTION IX.3.3C
C  OF THE NWSRFS USER'S MANUAL.
C
C     AI,IAI         CURRENT AI VALUE
C     AIADJ,IAIADJ   INITIAL AI ADJUSTMENT FACTOR
C     AICO,IAICO     STORM AI VALUE
C     API,IAPI       CURRENT API VALUE
C     CO(1)          INPUT CARRYOVER DATA FROM C ARRAY
C     C1 - C6        6 API/AI RELATIONSHIP CONSTANTS
C     DIML           NWSRFS CODE FOR TIME SERIES VALUE DIMENSION OF
C                      LENGTH
C     DLES           NWSRFS CODE FOR TIME SERIES VALUE DIMENSION OF
C                      DIMENSIONLESS
C     DTCAP          DATA TYPE CODE OF API TIME SERIES
C     DTCAI          DATA TYPE CODE OF AI TIME SERIES
C     DTCRF          DATA TYPE CODE OF RAINFALL/MELT TIME SERIES
C     DTCRO          DATA TYPE CODE OF RUNOFF TIME SERIES
C     DTCWE          DATA TYPE CODE OF WATER EQUIVALENT TIME SERIES
C     IAIREL         API/AI RELATIONSHIP NUMBER
C     ICHKDM         SWITCH INDICATING WHETHER DIMENSION OF TIME SERIES
C                      SHOULD BE CHECKED, 0 = NO, 1 = YES
C     IDELT          DELTA-T OF TIME SERIES FOR RAINFALL/MELT AND RUNOFF
C     IERR           ERROR FLAG RETURNED FROM CALLED SUBROUTINES,
C                      0 = NO ERROR, 1 = ERROR
C     IFATAL         FATAL ERROR FLAG, 0 = NO FATAL ERRORS, 1 = ERRORS
C     IFDEB          DEBUG OUTPUT SWITCH, 0 = OFF, 1 = ON
C     IFUTWK         FUTURE WEEK NUMBER
C     INCOFL         READ/NO READ CARRYOVER FLAG, 0 = NO READ, 1 = READ
C     IOTSFL         OUTPUT TIME SERIES FLAG FOR CURRENT API AND/OR AI
C                      VALUES, 0 = NO OUTPUT, 1 = OUTPUT
C     IUSEC          NUMBER OF WORDS REQUIRED IN THE C ARRAY FOR
C                      THIS OPERATION
C     IUSEP          NUMBER OF WORDS REQUIRED IN THE P ARRAY FOR
C                      THIS OPERATION
C     IVERS          VERSION NUMBER
C     LEFTC          NUMBER OF WORDS AVAILABLE IN THE C ARRAY
C     LEFTP          NUMBER OF WORDS AVAILABLE IN THE P ARRAY
C     MISS           SWITCH INDICATING WHETHER TIME SERIES CAN CONTAIN
C                      MISSING VALUES, 0 = NO, 1 = YES
C     NEWSTM         STORM PERIOD COUNTER
C     NOP            NUMBER OF OPERATION ASSIGNED TO THIS OPERATION
C     NOVAL          NUMBER OF VALUES PER TIME INTERVAL THAT THE TIME
C                      SERIES CAN HAVE FOR THIS APPLICATION
C     NOZON          RUNOFF ZONE NUMBER
C     PO(1)          INPUT PARAMETRIC DATA FROM P ARRAY
C     RAINCO,IRANCO  STORM RAINFALL VALUE
C     REG1           FIRST API RECESSION CONSTANT
C     REG2           SECOND API RECESSION CONSTANT
C     RFCID          INPUT RFC ID CODE FOR API/AI RELATIONSHIP DESIRED
C     ROCO,IROCO     STORM RUNOFF VALUE
C     SUBNAM(2)      SUBROUTINE NAME
C     TSIDAP(2)      ID OF API TIME SERIES
C     TSIDAI(2)      ID OF AI TIME SERIES
C     TSIDRF(2)      ID OF RAINFALL/MELT TIME SERIES
C     TSIDRO(2)      ID OF RUNOFF TIME SERIES
C     TSIDWE(2)      ID OF WATER EQUIVALENT TIME SERIES
C     WE,IWE         CURRENT WATER EQUIVALENT VALUE
C     ZMKC           RFC ID CODE FOR MISSOURI BASIN RFC API/AI
C                      RELATIONSHIPS
C     ZMSP           RFC ID CODE FOR NORTH CENTRAL RFC API/AI
C                      RELATIONSHIPS
C     ZONAME(6)      RUNOFF ZONE NAME
C     ZUSE           USER ID CODE FOR USER SUPPLIED API/AI RELATIONSHIP
C
C***********************************************************************
C  CONTENTS OF THE PO ARRAY...
C
C       WORD   ITEM
C     -------  ---------------------------------------------------------
C        1     VERSION NUMBER
C      2 - 7   RUNOFF ZONE NAME
C        8     RUNOFF ZONE NUMBER
C        9     RFC ID CODE FOR API/AI RELATIONSHIP TO BE USED
C       10     API/AI RELATIONSHIP NUMBER
C       11     FUTURE WEEK NUMBER
C       12     AI ADJUSTMENT FACTOR
C       13     DELTA-T OF RAINFALL/MELT AND RUNOFF TIME SERIES
C     14 - 15  ID OF RAINFALL/MELT TIME SERIES
C       16     DATA TYPE CODE OF RAINFALL/MELT TIME SERIES
C     17 - 18  ID OF RUNOFF TIME SERIES
C       19     DATA TYPE CODE OF RUNOFF TIME SERIES
C     20 - 21  ID OF WATER EQUIVALENT TIME SERIES
C       22     DATA TYPE CODE OF WATER EQUIVALENT TIME SERIES
C     23 - 28  6 API/AI CONSTANTS
C     29 - 30  2 API RECESSION CONSTANTS
C       31     CARRYOVER CARD INPUT READ/NO READ FLAG, 0 = NO READ,
C                1 = READ
C     32 - 33  ID OF API TIME SERIES
C       34     DATA TYPE CODE OF API TIME SERIES
C     35 - 36  ID OF AI TIME SERIES
C       37     DATA TYPE CODE OF AI TIME SERIES
C     38 - 42  EMPTY SPACE (ZEROS)
C
C***********************************************************************
C  CONTENTS OF THE CO ARRAY...
C
C       WORD   ITEM
C     -------  ---------------------------------------------------------
C        1     STORM PERIOD COUNTER
C        2     STORM RAINFALL VALUE
C        3     STORM AI VALUE
C        4     STORM RUNOFF VALUE
C        5     CURRENT API VALUE
C        6     CURRENT AI VALUE
C        7     CURRENT WATER EQUIVALENT VALUE
C        8     24-HOUR RAINFALL/MELT ENDING 12Z
C
C***********************************************************************
C
      CHARACTER*80 CARD
C
      DIMENSION PO(*),CO(*)
      DIMENSION ZONAME(6),SUBNAM(2),TSIDRF(2),TSIDRO(2),TSIDWE(2)
      DIMENSION TSIDAP(2),TSIDAI(2)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/pin29.f,v $
     . $',                                                             '
     .$Id: pin29.f,v 1.2 2002/02/11 19:01:34 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SUBNAM/4hPIN2,4h9   /,NOP/29/,ZMKC/4h MKC/,ZMSP/4h MSP/,
     1     ZUSE/4hUSER/,EMPTY/4h    /,DIML/4hL   /,DLES/4hDLES/
C
C
C  CALL DEBUG CHECK ROUTINE AND SET INITIAL VARIABLES.
C
      CALL FPRBUG (SUBNAM,1,NOP,IFDEB)
      IVERS=1
      IFATAL=0
      IUSEP=42
      IUSEC=8
      ICHKDM=1
      MISS=0
      NOVAL=1
      DO 1001 I=1,2
      TSIDAP(I)=EMPTY
      TSIDAI(I)=EMPTY
 1001 CONTINUE
      DTCAP=EMPTY
      DTCAI=EMPTY
      IF(IFDEB.GT.0) GO TO 126
C
C  READ ZONE NAME AND NUMBER, RFC ID CODE, API/AI RELATIONSHIP NUMBER,
C  FUTURE WEEK NUMBER, INITIAL AI ADJUSTMENT FACTOR, CARRYOVER INPUT
C  FLAG, AND OUTPUT TIME SERIES FLAG.
C
  101 READ(IN,11) ZONAME,NOZON,RFCID,IAIREL,IFUTWK,AIADJ,INCOFL,IOTSFL
      IF(IFDEB.GT.0) GO TO 127
C
C  READ MANDATORY TIME SERIES INFORMATION.
C
  102 READ(IN,12) IDELT,TSIDRF,DTCRF,TSIDRO,DTCRO,TSIDWE,DTCWE
      IF(IFDEB.LE.0) GO TO 103
C
C  IF PRINTING TIME SERIES DEBUG OUTPUT, CHECK FOR DEFAULT DELTA-T.
C
      IF(IDELT) 128,128,129
C
C  CHECK TO INSURE MANDATORY TIME SERIES ARE AVAILABLE.
C
  103 CALL CHEKTS (TSIDRF,DTCRF,IDELT,ICHKDM,DIML,MISS,NOVAL,IERR)
      CALL CHEKTS (TSIDRO,DTCRO,IDELT,ICHKDM,DIML,MISS,NOVAL,IERR)
      CALL CHEKTS (TSIDWE,DTCWE,24,ICHKDM,DIML,MISS,NOVAL,IERR)
C
C  IF TIME SERIES FOR CURRENT API AND/OR AI VALUES ARE TO BE OUTPUT,
C  READ CARD WITH TIME SERIES INFORMATION AND CHECK TO SEE IF THOSE
C  TIME SERIES ARE AVAILABLE.
C
      IF(IOTSFL.LE.0) GO TO 107
      READ(IN,13) TSIDAP,DTCAP,TSIDAI,DTCAI
      IF(DTCAP.EQ.EMPTY) GO TO 105
      IF(IFDEB.GT.0) GO TO 130
  104 CALL CHEKTS (TSIDAP,DTCAP,24,ICHKDM,DIML,MISS,NOVAL,IERR)
  105 IF(DTCAI.EQ.EMPTY) GO TO 107
      IF(IFDEB.GT.0) GO TO 131
  106 CALL CHEKTS (TSIDAI,DTCAI,24,ICHKDM,DLES,MISS,NOVAL,IERR)
C
C  CHECK TO SEE IF INITIAL CARRYOVER VALUES ARE BEING INPUT FROM CARDS.
C
  107 IF(INCOFL.GT.0) GO TO 108
C
C  SET DEFAULT CARRYOVER VALUES.
C
      NEWSTM=4
      RAINCO=0.0
      AICO=0.0
      ROCO=0.0
      API=0.0
      AI=0.0
      WE=0.0
      IF(IFDEB) 109,109,132
C
C  READ CARRYOVER FROM CARDS.
C
  108 READ (IN,'(A)') CARD
      READ(CARD,14,ERR=98) NEWSTM,RAINCO,AICO,ROCO,API,AI,WE
      IF(IFDEB.GT.0) GO TO 133
      GO TO 109
98    CALL FRDERR (IPR,' ',CARD)
C
C  CONVERT CARRYOVER VALUES TO INTEGER WORDS.
C
  109 IRANCO=(RAINCO*100.0)+0.1
      IAICO=(AICO*10.0)+0.1
      IROCO=(ROCO*100.0)+0.1
      IAPI=(API*100.0)+0.1
      IAI=(AI*10.0)+0.1
      IWE=(WE*100.0)+0.1
      IAIADJ=(AIADJ+1000.01)*10.0
      IAIADJ=IAIADJ-10000
C
C  IF INPUT DELTA-T OF TIME SERIES IS OMITTED, DEFAULT IS 6 HOURS.
C
      IF(IDELT.LE.0) IDELT=6
C
C  CHECK VALIDITY OF INPUT DATA AND SET BRANCH VARIABLE KALL FOR
C  INPUT OF API/AI RELATIONSHIP CONSTANTS AND RECESSIONS.
C
      IF(NOZON.LT.101.OR.NOZON.GT.4092) GO TO 990
  110 IF(RFCID.NE.ZMKC) GO TO 111
      KALL=1
      GO TO 113
  111 IF(RFCID.NE.ZMSP) GO TO 112
      KALL=2
      GO TO 113
  112 IF(RFCID.NE.ZUSE) GO TO 991
      KALL=3
      IAIREL=0
      GO TO 114
  113 IF(IAIREL.LT.1.OR.IAIREL.GT.35) GO TO 992
  114 IF(IFUTWK.LT.1.OR.IFUTWK.GT.25) GO TO 993
  115 IADJ=IABS(IAIADJ)
      IF(IADJ.GT.50) GO TO 994
  116 IF(IDELT.NE.6) GO TO 995
C
C  IF DEFAULT CARRYOVERS USED, DO NOT CHECK VALIDITY.
C
  117 IF(INCOFL.LE.0) GO TO 118
      IF(NEWSTM.LT.0.OR.NEWSTM.GT.4) GO TO 996
      IF(IRANCO.LT.0.OR.IRANCO.GT.5000) GO TO 996
      IF(IAICO.LT.11.OR.IAICO.GT.99) GO TO 996
      IF(IROCO.LT.0.OR.IROCO.GT.5000) GO TO 996
      IF(IAPI.LT.0.OR.IAPI.GT.600) GO TO 996
      IF(IAI.LT.11.OR.IAI.GT.99) GO TO 996
      IF(IWE.LT.0.OR.IWE.GT.2000) GO TO 996
C
C  DEPENDING ON RFC ID CODE AND API/AI RELATIONSHIP NUMBER, CALL
C  ROUTINE FOR SUPPLYING 6 API/AI CONSTANTS AND 2 API RECESSION FACTORS.
C  IF USER SUPPLIED, READ CARD WITH VALUES.
C
  118 GO TO (119,120,121,123), KALL
  119 CALL MK6C29 (IAIREL,C1,C2,C3,C4,C5,C6,REG1,REG2,IERR)
      IF(IERR) 122,122,997
  120 CALL MS6C29 (IAIREL,C1,C2,C3,C4,C5,C6,REG1,REG2,IERR)
      IF(IERR) 122,122,997
  121 READ(IN,15) C1,C2,C3,C4,C5,C6,REG1,REG2
C
C  CHECK FOR ALLOWABLE VALUES OF 6 CONSTANTS AND API RECESSION VALUES
C  IF READ FROM CARDS.
C
      IF(C1.LT.0.0.OR.C1.GT.1.5) GO TO 998
      IF(C2.LT.-0.5.OR.C2.GT.1.0) GO TO 998
      IF(C3.LT.2.0.OR.C3.GT.6.0) GO TO 998
      IF(C4.LT.-1.5.OR.C4.GT.-0.5) GO TO 998
      IF(C5.LT.-2.5.OR.C5.GT.-0.5) GO TO 998
      IF(C6.LT.-0.1.OR.C6.GT.0.5) GO TO 998
      IF(REG1.LT.0.50.OR.REG1.GT.1.00) GO TO 998
      IF(REG2.LT.0.50.OR.REG2.GT.1.00) GO TO 998
  122 IF(IFDEB.GT.0) GO TO 134
C
C  CHECK P ARRAY TO MAKE SURE SPACE IS AVAILABLE FOR PO ARRAY.
C
  123 CALL CHECKP (IUSEP,LEFTP,IERR)
      IF(IERR.LE.0) GO TO 124
      IFATAL=1
      GO TO 125
C
C  STORE PARAMETRIC DATA IN PO ARRAY.
C
  124 PO(1)=IVERS+0.01
      DO 1002 I=1,6
      PO(I+1)=ZONAME(I)
 1002 CONTINUE
      PO(8)=NOZON+0.01
      PO(9)=RFCID
      PO(10)=IAIREL+0.01
      PO(11)=IFUTWK+0.01
      PO(12)=IAIADJ-0.01
      IF(IAIADJ.GE.0) PO(12)=IAIADJ+0.01
      PO(13)=IDELT+0.01
      PO(14)=TSIDRF(1)
      PO(15)=TSIDRF(2)
      PO(16)=DTCRF
      PO(17)=TSIDRO(1)
      PO(18)=TSIDRO(2)
      PO(19)=DTCRO
      PO(20)=TSIDWE(1)
      PO(21)=TSIDWE(2)
      PO(22)=DTCWE
      PO(23)=C1
      PO(24)=C2
      PO(25)=C3
      PO(26)=C4
      PO(27)=C5
      PO(28)=C6
      PO(29)=REG1
      PO(30)=REG2
      PO(31)=INCOFL+0.01
      PO(32)=TSIDAP(1)
      PO(33)=TSIDAP(2)
      PO(34)=DTCAP
      PO(35)=TSIDAI(1)
      PO(36)=TSIDAI(2)
      PO(37)=DTCAI
      DO 1003 I=38,42
      PO(I)=0
 1003 CONTINUE
C
C  CHECK C ARRAY TO MAKE SURE SPACE IS AVAILABLE FOR CO ARRAY.
C
  125 CALL CHECKC (IUSEC,LEFTC,IERR)
      IF(IERR.GT.0) GO TO 999
C
C  STORE CARRYOVER DATA IN CO ARRAY.
C
      CO(1)=NEWSTM+0.01
      CO(2)=IRANCO+0.01
      CO(3)=IAICO+0.01
      CO(4)=IROCO+0.01
      CO(5)=IAPI+0.01
      CO(6)=IAI+0.01
      CO(7)=IWE+0.01
      CO(8)=0
      IF(IFATAL.GT.0) GO TO 999
      RETURN
C
C  DEBUG OUTPUT STATEMENTS.
C
  126 WRITE(IODBUG,16) SUBNAM,IVERS
      GO TO 101
  127 WRITE(IODBUG,17) ZONAME,NOZON,RFCID,IAIREL,IFUTWK,AIADJ,INCOFL,
     1                 IOTSFL
      GO TO 102
  128 WRITE(IODBUG,18) TSIDRF,DTCRF,TSIDRO,DTCRO,TSIDWE,DTCWE
      GO TO 103
  129 WRITE(IODBUG,19) TSIDRF,DTCRF,IDELT,TSIDRO,DTCRO,IDELT,TSIDWE,
     1                 DTCWE
      GO TO 103
  130 WRITE(IODBUG,20) TSIDAP,DTCAP
      GO TO 104
  131 WRITE(IODBUG,21) TSIDAI,DTCAI
      GO TO 106
  132 WRITE(IODBUG,22)
      GO TO 109
  133 WRITE(IODBUG,23) NEWSTM,RAINCO,AICO,ROCO,API,AI,WE
      GO TO 109
  134 WRITE(IODBUG,24) C1,C2,C3,C4,C5,C6,REG1,REG2
      GO TO 123
C
C  ERROR STATEMENTS.
C
  990 WRITE(IPR,25) NOZON
      IFATAL=1
      GO TO 110
  991 WRITE(IPR,26) RFCID
      IFATAL=1
      KALL=4
      GO TO 113
  992 WRITE(IPR,27) IAIREL
      IFATAL=1
      KALL=4
      GO TO 114
  993 WRITE(IPR,28) IFUTWK
      IFATAL=1
      GO TO 115
  994 WRITE(IPR,29) AIADJ
      IFATAL=1
      GO TO 116
  995 WRITE(IPR,30) IDELT
      IFATAL=1
      GO TO 117
  996 WRITE(IPR,31) NEWSTM,RAINCO,AICO,ROCO,API,AI,WE
      IFATAL=1
      GO TO 118
  997 WRITE(IPR,32) IAIREL,RFCID
      IFATAL=1
      GO TO 123
  998 WRITE(IPR,33) C1,C2,C3,C4,C5,C6,REG1,REG2
      IFATAL=1
      GO TO 122
  999 IUSEP=0
      IUSEC=0
      CALL ERROR
      RETURN
C
C
   11 FORMAT(6A4,I6,1X,A4,2I5,F5.1,2I5)
   12 FORMAT(I5,3(2X,2A4,1X,A4))
   13 FORMAT(5X,2(2X,2A4,1X,A4))
   14 FORMAT(I5,F5.2,F5.1,2F5.2,F5.1,F5.2)
   15 FORMAT(6F7.3,2F7.2)
   16 FORMAT(/5X,2A4,' DEBUG OUTPUT.',5X,'VERSION:',I4)
   17 FORMAT(/12X,'ZONE NAME:',2X,6A4,
     1       3X,'ZONE NUMBER:',I8,5X,'RFC ID CODE:',4X,A4/
     2       51X,'API/AI REL NO:',I6,5X,'FUTURE WEEK NO:',I5/
     3       51X,'AI ADJ FACTOR:',F6.1,5X,'C/O INPUT FLAG:',I5/
     4       51X,'OUTPUT TS FLAG:',I5)
   18 FORMAT(/20X,'TIME SERIES USED BY THIS OPERATION...'/
     1       25X,'DELTA-T WAS ZERO OR BLANK.  DEFAULT VALUE OF 6 ',
     2       'HOURS SUBSTITUTED.'/
     3       25X,'CONTENTS',15X,'TS I.D.',5X,'TYPE',5X,'TIME INTERVAL'/
     4       25X,20('-'),2X,8('-'),5X,4('-'),5X,13('-')/
     5       25X,'RAINFALL/MELT',9X,2A4,5X,A4,8X,'6 HOURS'/
     6       25X,'RUNOFF',16X,2A4,5X,A4,8X,'6 HOURS'/
     7       25X,'WATER EQUIVALENT',6X,2A4,5X,A4,7X,'24 HOURS')
   19 FORMAT(/20X,'TIME SERIES USED BY THIS OPERATION...'/
     1       25X,'CONTENTS',15X,'TS I.D.',5X,'TYPE',5X,'TIME INTERVAL'/
     2       25X,20('-'),2X,8('-'),5X,4('-'),5X,13('-')/
     3       25X,'RAINFALL/MELT',9X,2A4,5X,A4,I9,' HOURS'/
     4       25X,'RUNOFF',16X,2A4,5X,A4,I9,' HOURS'/
     5       25X,'WATER EQUIVALENT',6X,2A4,5X,A4,7X,'24 HOURS')
   20 FORMAT(25X,'CURRENT API',11X,2A4,5X,A4,7X,'24 HOURS')
   21 FORMAT(25X,'CURRENT AI',12X,2A4,5X,A4,7X,'24 HOURS')
   22 FORMAT(20X,'INPUT CARRYOVER VALUES...'/
     1       25X,'DEFAULT VALUES WERE USED.')
   23 FORMAT(20X,'INPUT CARRYOVER VALUES...'/
     1       25X,'INITIAL 24-HOUR RAINFALL/MELT VALUE SET TO ZERO'/
     2       25X,'NEWSTM    RAINCO      AICO      ROCO       ',
     3       'API        AI        WE'/
     4       27X,I4,F10.2,F10.1,2F10.2,F10.1,F10.2)
   24 FORMAT(20X,'API/AI CONSTANTS AND RECESSION FACTORS...'/
     1       29X,'C1',8X,'C2',8X,'C3',8X,'C4',8X,'C5',8X,'C6',
     2       6X,'REG1',6X,'REG2'/
     3       21X,6F10.3,2F10.2)
   25 FORMAT(/10X,'**ERROR** ILLEGAL RUNOFF ZONE NUMBER:',I6/
     1       22X,'LIMITS ARE...  101 THRU 4092')
   26 FORMAT(/10X,'**ERROR** ILLEGAL RFC ID CODE:',2X,'''',A4,''''/
     1       22X,'CHOICE MUST BE '' MKC'', '' MSP'', OR ''USER'''/
     2       22X,'NO API/AI CONSTANTS OR RECESSION FACTORS CAN BE ',
     3       'LOCATED.')
   27 FORMAT(/10X,'**ERROR** ILLEGAL API/AI RELATIONSHIP NUMBER:',I6/
     1       22X,'LIMITS ARE...  1 THRU 35'/
     2       22X,'NO API/AI CONSTANTS OR RECESSION FACTORS CAN BE ',
     3       'LOCATED.')
   28 FORMAT(/10X,'**ERROR** ILLEGAL FUTURE WEEK NUMBER:',I6/
     1       22X,'LIMITS ARE...  1 THRU 25')
   29 FORMAT(/10X,'**ERROR** ILLEGAL INITIAL AI ADJ FACTOR:',F6.1/
     1       22X,'LIMITS ARE...  -5.0 THRU +5.0')
   30 FORMAT(/10X,'**ERROR** ILLEGAL TIME STEP (DELTA-T):',I6/
     1       22X,'VALUE MUST BE 6 HOURS.  IF LEFT BLANK, OR ZERO, ',
     2       'DEFAULT IS 6 HOURS.')
   31 FORMAT(/10X,'**ERROR** ILLEGAL INITIAL CARRYOVER VALUE.'/
     1       22X,'ONE OF THE FOLLOWING VALUES IS OUTSIDE LIMITS.'//
     2       22X,'ITEM',9X,'LIMITS',6X,'VALUE PUNCHED'/
     3       22X,6('-'),3X,14('-'),2X,13('-')/
     4       22X,'NEWSTM     0 THRU 4',8X,I6/
     5       22X,'RAINCO   0.0 THRU 50.00',4X,F6.2/
     6       22X,'AICO     1.1 THRU 9.9',6X,F6.1/
     7       22X,'ROCO     0.0 THRU 50.00',4X,F6.2/
     8       22X,'API      0.0 THRU 6.00',5X,F6.2/
     9       22X,'AI       1.1 THRU 9.9',6X,F6.1/
     1       22X,'WE       0.0 THRU 20.00',4X,F6.2)
   32 FORMAT(/10X,'**ERROR** ILLEGAL API/AI RELATIONSHIP NUMBER',I4,
     1       1X,'FOR RFC ID CODE ''',A4,''''/
     2       22X,'API/AI CONSTANTS DO NOT EXIST FOR THIS NUMBER.')
   33 FORMAT(/10X,'**ERROR** ILLEGAL USER SUPPLIED API/AI CONSTANTS ',
     1       'OR RECESSION VALUES.'/
     2       22X,'ONE OF THE FOLLOWING VALUES IS OUTSIDE LIMITS.'//
     3       22X,'ITEM',7X,'LIMITS',6X,'VALUE PUNCHED'/
     4       22X,4('-'),3X,14('-'),2X,13('-')/
     5       22X,'C1      0.0 THRU 1.5',5X,F7.3/
     6       22X,'C2     -0.5 THRU 1.0',5X,F7.3/
     7       22X,'C3      2.0 THRU 6.0',5X,F7.3/
     8       22X,'C4     -1.5 THRU -0.5',4X,F7.3/
     9       22X,'C5     -2.5 THRU -0.5',4X,F7.3/
     1       22X,'C6     -0.1 THRU 0.5',5X,F7.3/
     2       22X,'REG1   0.50 THRU 1.00',3X,F7.2/
     3       22X,'REG2   0.50 THRU 1.00',3X,F7.2)
      END
