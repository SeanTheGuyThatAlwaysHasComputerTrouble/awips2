C MEMBER TAB57
C-----------------------------------------------------------------------
C
      SUBROUTINE TAB57 (TO, LEFT, IUSET, NXT, LPO, PO, LCO, TS, MTS,
     1                  LWORK, IDT)
C-----------------------------------------------------------------------
C   THIS IS THE OPERATIONS TABLE ENTRY ROUTINE FOR THE
C   CONSUMPTIVE USE 'CONS_USE' OPERATION.
C
C   ROUTINE INITIALLY WRITTEN BY RUSS ERB - HRL  JULY 1997
C-----------------------------------------------------------------------
C   PASSED ARGUMENTS
C    NAME  I/O
C   ------ ---
C   TO      O  INT  ARRAY USED TO STORE OPERATIONS TABLE ENTRIES
C   LEFT    I  INT  INDICATING HOW MUCH SPACE IS LEFT IN T()
C   IUSET   O  INT  HOW MUCH SPACE IN THE T() IS USED BY THIS OPER
C   NXT     I  INT  STARTING LOCATION OF THE TO() FOR THIS OPERATION
C   LPO     I  INT  STARTING LOCATION OF THE PO() FOR THIS OPERATION
C   PO      I  REAL ARRAY TO STORE ALL PARAMETERS FOR THIS OPERATION
C   LCO     I  INT  STARTING LOCATION OF THE CO() FOR THIS OPERATION
C   TS      I  REAL ARRAY OF INFORMATION ON ALL TIME SERIES IN SEGMENT
C   MTS     I  INT  MAXIMUM DIMENSION OF THE TS()
C   LWORK   O  INT  HOW MUCH WORKING SPACE IS NEEDED FOR THIS OPERATION
C   IDT     O  INT  DELTA T, MINIMUM TIME INTERVAL THIS OPER CAN BE RUN
C
      INTEGER TO(*), LEFT, IUSET, NXT, LPO, LCO, MTS, LWORK, IDT
      REAL    PO(*), TS(MTS)
C
C   LOCAL VARIABLES
C    NAME
C   ------
C   IBUG    DEBUG SWITCH
C   INEED   NUMBER OF SPACES NEEDED IN THE T()
C   IDELT   TIME INTERVAL FOR ALL TIME SERIES
C   LD      LOCATION OF THE TIME SERIES DATA IN THE D()
C   LTS     LOCATION OF INFORMATION ABOUT THE TIME SERIES IN THE TS()
C
      INTEGER IBUG, INEED, IERR, IDELT, LD, LTS
      REAL    DIM
C.......................................................................
C   COMMON BLOCKS FOR DEBUG UNIT NUMBERS
      INCLUDE 'common/fdbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_pntb/RCS/tab57.f,v $
     . $',                                                             '
     .$Id: tab57.f,v 1.1 1997/12/31 17:52:36 page Exp $
     . $' /
C    ===================================================================
C
C.......................................................................
C   DEBUG INITIALIZATION WITH TRACE LEVEL = 1
      CALL FPRBUG ('TAB57   ', 1, 57, IBUG)
C.......................................................................
C   NUMBER OF SPACES I NEED IN T().
      INEED = 14
C
C   CHECK TO SEE IF SPACE IS AVAILABLE IN T().
      CALL CHECKT (INEED, LEFT, IERR)
      IF(IERR .NE. 0) THEN
         IUSET = 0
         LWORK = 0
         IDT = 0
         RETURN
      END IF
C.......................................................................
C   SPACE IS AVAILABLE SO MAKE ENTRIES INTO TO().
      TO(1) = 57
      TO(2) = NXT + INEED
      TO(3) = LPO
      TO(4) = LCO
C
C   TIME INTERVAL OF ALL TIME SERIES EXCEPT MEAN AREAL TEMPERATURE (6HR)
      IDELT = 24
C
C   CALL CKINPT AND GET THE LOCATION OF THE MEAN AREAL TEMPERATURE
C   TIME SERIES IN THE D() IF THE EVAPO-TRANSPIRATION ESTIMATION METHOD
C   IS SET TO 0.  IF NOT, THEN THE LOCATION IN THE D() IS SET TO ZERO.
      IF(PO(50) .EQ. 0) THEN
         CALL CKINPT (PO(20), PO(22), 6, LD, TS, MTS, IERR)
         TO(5) = LD
      ELSE
         TO(5) = 0
      ENDIF
C
C   CALL CKINPT AND GET THE LOCATION OF THE POTENTIAL EVAPORATION
C   TIME SERIES IN THE D() IF THE EVAPO-TRANSPIRATION ESTIMATION METHOD
C   IS SET TO 1.  IF NOT, THEN THE LOCATION IN THE D() IS SET TO ZERO.
      IF(PO(50) .EQ. 1) THEN
         CALL CKINPT (PO(23), PO(25), IDELT, LD, TS, MTS, IERR)
         TO(6) = LD
      ELSE
         TO(6) = 0
      ENDIF
C
C   CALL CKINPT AND GET THE LOCATION OF THE NATURAL FLOW
C   TIME SERIES IN THE D().
      CALL CKINPT (PO(26), PO(28), IDELT, LD, TS, MTS, IERR)
      TO(7) = LD
C
C   GET THE LOCATION OF THE ADJUSTED FLOW TIME SERIES IN THE D()
C   AND SET FLAG INDICATING THAT THIS TIME SERIES CONTAINS DATA VALUES.
      CALL FINDTS (PO(29), PO(31), IDELT, LD, LTS, DIM)
      TO(8) = LD
      IF(LTS .GT. 0) TS(LTS+8) = 1.01
C
C   GET THE LOCATION OF THE DIVERSION FLOW TIME SERIES IN THE D()
C   AND SET FLAG INDICATING THAT THIS TIME SERIES CONTAINS DATA VALUES.
      CALL FINDTS (PO(32), PO(34), IDELT, LD, LTS, DIM)
      TO(9) = LD
      IF(LTS .GT. 0) TS(LTS+8) = 1.01
C
C   GET THE LOCATION OF THE RETURN FLOW IN TIME SERIES IN THE D()
C   AND SET FLAG INDICATING THAT THIS TIME SERIES CONTAINS DATA VALUES.
      CALL FINDTS (PO(35), PO(37), IDELT, LD, LTS, DIM)
      TO(10) = LD
      IF(LTS .GT. 0) TS(LTS+8) = 1.01
C
C   GET THE LOCATION OF THE RETURN FLOW OUT TIME SERIES IN THE D()
C   AND SET FLAG INDICATING THAT THIS TIME SERIES CONTAINS DATA VALUES.
      CALL FINDTS (PO(38), PO(40), IDELT, LD, LTS, DIM)
      TO(11) = LD
      IF(LTS .GT. 0) TS(LTS+8) = 1.01
C
C   GET THE LOCATION OF THE OTHER LOSSES TIME SERIES IN THE D()
C   AND SET FLAG INDICATING THAT THIS TIME SERIES CONTAINS DATA VALUES.
      CALL FINDTS (PO(41), PO(43), IDELT, LD, LTS, DIM)
      TO(12) = LD
      IF(LTS .GT. 0) TS(LTS+8) = 1.01
C
C   GET THE LOCATION OF THE CROP DEMAND TIME SERIES IN THE D()
C   AND SET FLAG INDICATING THAT THIS TIME SERIES CONTAINS DATA VALUES.
      CALL FINDTS (PO(44), PO(46), IDELT, LD, LTS, DIM)
      TO(13) = LD
      IF(LTS .GT. 0) TS(LTS+8) = 1.01
C
C   GET LOCATION OF THE CROP EVAPO-TRANSPIRATION TIME SERIES IN THE D()
C   AND SET FLAG INDICATING THAT THIS TIME SERIES CONTAINS DATA VALUES.
      CALL FINDTS (PO(47), PO(49), IDELT, LD, LTS, DIM)
      TO(14) = LD
      IF(LTS .GT. 0) TS(LTS+8) = 1.01
C
C.......................................................................
C   SET HOW MUCH SPACE IN THE T() IS USED BY THIS OPERATION.
      IUSET = INEED
C.......................................................................
C   NO WORKING SPACE IS USED IN THIS OPERATION.
      LWORK = 0
C.......................................................................
C   SET COMPUTATIONAL TIME INTERVAL FOR THE OPERATION.
      IDT = IDELT
C.......................................................................
C  CHECK IF DEBUG OUTPUT IS TO BE PRINTED.
      IF(IBUG .GT. 0) THEN
         WRITE(IODBUG,1000) (TO(I),I=1,INEED)
 1000    FORMAT(1H0,'CONS_USE DEBUG - CONTENTS OF TO()=',7I6)
      END IF
C.......................................................................
C   THE TO ARRAY ENTRIES ARE AS FOLLOWS:
C   POS
C   ---
C    1      I.D. NUMBER OF THE OPERATION (57)
C    2      LOCATION OF NEXT OPERATION IN T ARRAY (NXT + INEED)
C    3      LOCATION OF PARAMETERS     IN P ARRAY (LPO)
C    4      LOCATION OF CARRYOVER      IN C ARRAY (LCO)
C    5      LOCATION OF MEAN AREAL TEMPERATURE   TS IN D ARRAY
C    6      LOCATION OF POTENTIAL EVAPORATION    TS IN D ARRAY
C    7      LOCATION OF NATURAL FLOW             TS IN D ARRAY
C    8      LOCATION OF ADJUSTED FLOW            TS IN D ARRAY
C    9      LOCATION OF DIVERSION FLOW           TS IN D ARRAY
C   10      LOCATION OF RETURN FLOW IN           TS IN D ARRAY
C   11      LOCATION OF RETURN FLOW OUT          TS IN D ARRAY
C   12      LOCATION OF OTHER LOSSES             TS IN D ARRAY
C   13      LOCATION OF CROP DEMAND              TS IN D ARRAY
C   14      LOCATION OF CROP EVAPO-TRANSPIRATION TS IN D ARRAY

C.......................................................................
      RETURN
      END
