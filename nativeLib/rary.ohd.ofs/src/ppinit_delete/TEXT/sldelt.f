C MODULE SLDELT
C-----------------------------------------------------------------------
C
C  MAIN ROUTINE FOR DELETING PARAMETERS.
C
      SUBROUTINE SLDELT (LARRAY,ARRAY,NFLD,IOAUTO,ISTAT)
C
      CHARACTER*4 PTYPE
      CHARACTER*8 STAID,GOESID,TYPMSG
      PARAMETER (MGROUP=12)
      CHARACTER*8 GROUP(MGROUP)
     *   /'AREA    ','BASIN   ','STATION ','STA     ',
     *    'PPD     ','PPP     ','PRD     ','SASM    ',
     *    'GOES    ','GBOX    ','RFRO   ' ,'       ' /
      CHARACTER*20 CHAR/' '/,CHK/' '/ 
      LOGICAL CFGRP,PFGRP
C
      DIMENSION ARRAY(LARRAY)
C
      INCLUDE 'uiox'
      INCLUDE 'ufreex'
      INCLUDE 'scommon/sudbgx'
      INCLUDE 'scommon/suoptx'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppinit_delete/RCS/sldelt.f,v $
     . $',                                                             '
     .$Id: sldelt.f,v 1.6 2002/02/11 21:02:40 dws Exp $
     . $' /
C    ===================================================================
C      
C
C
      IF (ISTRCE.GT.0) THEN
         WRITE (IOSDBG,260)
         CALL SULINE (IOSDBG,1)
         ENDIF
C
C  SET DEBUG LEVEL
      LDEBUG=ISBUG('DELT')
C
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,*)
     *      ' LARRAY=',LARRAY,
     *      ' NFLD=',NFLD,
     *      ' '
         CALL SULINE (IOSDBG,1)
         ENDIF
C
      ISTAT=0
C
      LCHAR=LEN(CHAR)/4
      LCHK=LEN(CHK)/4
      LGROUP=0
      ILPFND=0
      IRPFND=0
      CFGRP=.FALSE.
      INDERR=0
      NUMERR=0
      IRUNCK=0
      ISTRT=1
C
C  PRINT CARD
      CALL SUPCRD
C
C  PRINT HEADER
      WRITE (LP,270)
      CALL SULINE (LP,2)
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK IF DELETE COMMAND CURRENTLY ALLOWED
      CALL SUSPCK ('DEL ','@DELETE ',2,'COMMAND ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 10
C
C  CHECK IF RUNCHECK OPTION SET
      IF (IRUNCK.EQ.0.AND.IOPRCK.EQ.0) GO TO 10
         WRITE (LP,280)
         CALL SULINE (LP,2)
         IRUNCK=1
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK FIELDS FOR DELETE OPTIONS
10    PFGRP=CFGRP
      NUMOPT=0
      CALL UFIELD (NFLD,ISTRT,LENGTH,ITYPE,NREPET,INTEGR,REAL,
     *   LCHAR,CHAR,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,IERR)
      IF (NFLD.EQ.-1) GO TO 240
      IF (LDEBUG.GT.0) THEN
         CALL UPRFLD (NFLD,ISTRT,LENGTH,ITYPE,NREPET,INTEGR,REAL,
     *      LCHAR,CHAR,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,IERR)
         ENDIF
      IF (IERR.EQ.1) THEN
         IF (LDEBUG.GT.0) THEN
            WRITE (IOSDBG,330) NFLD
            CALL SULINE (IOSDBG,1)
            ENDIF
         GO TO 10
         ENDIF
C
C  CHECK FOR COMMAND
      IF (LATSGN.EQ.1) GO TO 70
C
C  CHECK FOR PAIRED PARENTHESIS
      NPCFLD=0
      CALL SUPFND (ILPFND,IRPFND,NFLD,NPCFLD)
      IF (LLPAR.GT.0) ILPFND=1
      IF (LRPAR.GT.0) IRPFND=1
C
C  CHECK FOR PARENTHESIS IN FIELD
      IF (LLPAR.GT.0) CALL UFPACK (LCHK,CHK,ISTRT,1,LLPAR-1,IERR)
      IF (LLPAR.EQ.0) CALL UFPACK (LCHK,CHK,ISTRT,1,LENGTH,IERR)
C
C  CHECK FOR GROUP
      DO 50 IGROUP=1,MGROUP
         NWORDS=2
         CALL SUCOMP (NWORDS,CHK,GROUP(IGROUP),IRETRN)
         IF (IRETRN.NE.1) GO TO 50
            CFGRP=.TRUE.
            IF (PFGRP.AND.CFGRP) THEN
               WRITE (LP,300) CHK(1:LENSTR(CHK)),GROUP(LGROUP)
               CALL SUWRNS (LP,2,NUMWRN)
               ENDIF
            LGROUP=IGROUP
            IF (LDEBUG.GT.0) THEN
               WRITE (IOSDBG,340) LGROUP,GROUP(LGROUP)
               CALL SULINE (IOSDBG,1)
               ENDIF
            IF (LGROUP.EQ.2.OR.(LGROUP.GE.5.AND.LGROUP.LE.9)) GO TO 70
            IF (NFLD.EQ.1) CALL SUPCRD
            GO TO 10
50       CONTINUE
C
C  CHECK FOR KEYWORD
      CALL SUIDCK ('DELT',CHK,NFLD,0,IKEYWD,IRETRN)
      IF (IRETRN.EQ.0) GO TO 60
         IF (LDEBUG.GT.0) THEN
            WRITE (IOSDBG,350) CHK
            CALL SULINE (IOSDBG,1)
            ENDIF
         GO TO 90
C
C  INVALID OPTION
60    IF (NFLD.EQ.1) CALL SUPCRD
      CFGRP=.FALSE.
      IF (INDERR.EQ.1) GO TO 10
         WRITE (LP,310) CHK
         CALL SUERRS (LP,2,NUMERR)
         GO TO 10
C
C  CHECK IF ERROR ON PREVIOUS OPTION
70    IF (INDERR.EQ.0) GO TO 80
         IF (LATSGN.EQ.0.AND.NFLD.EQ.1) CALL SUPCRD
         INDERR=0
         NSCARD=NRDCRD-NCSKIP
         IF (NSCARD.GT.0) THEN
            WRITE (LP,290) NSCARD
            CALL SULINE (LP,2)
            ENDIF
C
C  CHECK IF COMMAND
80    IF (LATSGN.EQ.1) GO TO 240
C
      IF (NFLD.EQ.1) CALL SUPCRD
      ILPFND=0
      IRPFND=0
C
C  CHECK WHICH PARAMETER TYPE IS TO BE DELETED
90    IF (LGROUP.GT.0) GO TO 100
         WRITE (LP,360) CHK(1:LENSTR(CHK))
         CALL SUERRS (LP,2,NUMERR)
         GO TO 10
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
100   GO TO (120,130,140,140,150,160,170,180,190,200,210,110),LGROUP
110      WRITE (LP,320) GROUP(LGROUP)
         CALL SULINE (LP,2)
         GO TO 240
C
C  DELETE AREA
120   CALL SUSPCK ('DELA',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
      CALL SLAREA (LARRAY,ARRAY,NFLD,IOAUTO,IERR)
      GO TO 220
C
C  DELETE BASIN
130   CALL SUSPCK ('DELB',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
      CALL SLBASN (LARRAY,ARRAY,NFLD,IERR)
      GO TO 220
C
C  DELETE STATION
140   CALL SUSPCK ('DELS',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
      CALL SLSTA (LARRAY,ARRAY,NFLD,IRUNCK,IERR)
      GO TO 220
C
C  DELETE FROM PREPROCESSOR DATA BASE
150   CALL SUSPCK ('DELPPD',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
      IDTYPE=0
      CALL SLPPD (NFLD,STAID,IDTYPE,IERR)
      GO TO 220
C
C  DELETE FROM PARAMETRIC DATA BASE
160   CALL SUSPCK ('DELPPP',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
      CALL SLPPP (NFLD,STAID,TYPE,NUMERR,NUMWRN,LARRAY,ARRAY,ISTAT)
      GO TO 220
C
C  DELETE FROM PROCESSED DATA BASE
170   CALL SUSPCK ('DELPRD',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
      CALL SLPRD (NFLD,STAID,TYPE,IERR)
      GO TO 220
C
C  DELETE FROM SASM CONTROL FILE
180   CALL SUSPCK ('DELSASM',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
      TYPMSG=' '
      CALL SLSASM (NFLD,STAID,TYPMSG,IERR)
      GO TO 220
C
C  DELETE FROM GOES CONTROL FILE
190   CALL SUSPCK ('DELGOES',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
      TYPMSG=' '
      PTYPE=' '
      CALL SLGOES (NFLD,STAID,PTYPE,GOESID,TYPMSG,IERR)
      GO TO 220
C
C  DELETE GRID BOX PARAMETERS
200   CALL SUSPCK ('DELGBOX',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
CCC   CALL SLGBOX (LARRAY,ARRAY,NFLD,IERR)
      WRITE (LP,370) GROUP(LGROUP)
      CALL SUERRS (LP,2,NUMERR)
      GO TO 220
C
C  DELETE RAINFALL-RUNOFF PARAMETERS
210   CALL SUSPCK ('DELRFRO',GROUP(LGROUP),2,'OPTION  ',IRUNCK,INDERR,
     *   NCSKIP,IRETRN)
      IF (IRETRN.EQ.1) GO TO 230
CCC   CALL SLRFRO (LARRAY,ARRAY,NFLD,IERR)
      WRITE (LP,370) GROUP(LGROUP)
      CALL SUERRS (LP,2,NUMERR)
      GO TO 220
C
220   NUMOPT=NUMOPT+1
C
C  CHECK IF END OF INPUT FOUND
      IF (NFLD.EQ.-1) GO TO 240
C
C  SET INDICATOR TO RE-READ FIELD READ BY DELETE ROUTINE
      ISTRT=-1
C
C  LAST FIELD READ WAS NOT A GROUP
230   CFGRP=.FALSE.
      LGROUP=0
      GO TO 10
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK NUMBER OF KEYWORDS PROCESSED
240   IF (NUMOPT.EQ.0) THEN
         IF (LGROUP.GT.0) THEN
            WRITE (LP,380) GROUP(LGROUP)
            CALL SULINE (LP,2)
            ENDIF
         ENDIF
C
      IF (ISTRCE.GT.0) THEN
         WRITE (IOSDBG,390)
         CALL SULINE (IOSDBG,1)
         ENDIF
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
260   FORMAT (' *** ENTER SLDELT')
270   FORMAT ('0*--> DELETE PARAMETERS')
280   FORMAT ('0*** NOTE - RUNCHECK OPTION HAS BEEN SPECIFIED. INPUT ',
     *   'WILL BE CHECKED FOR ERRORS.')
290   FORMAT ('0*** NOTE - ',I4,' CARD IMAGES NOT PROCESSED BECAUSE ',
     *   'INVALID OPTION FOUND.')
300   FORMAT ('0*** WARNING - THE GROUP ',A,' WAS FOUND WHEN ',
     *   'ANOTHER GROUP (',A,') WAS JUST SPECIFIED.')
310   FORMAT ('0*** ERROR - INVALID DELETE OPTION : ',A)
320   FORMAT ('0*** ERROR - ERROR PROCESSING OPTION : ',A)
330   FORMAT (' BLANK STRING FOUND IN FIELD ',I2)
340   FORMAT (' LGROUP=',I2,3X,'GROUP(LGROUP)=',A)
350   FORMAT (' CHK=',A)
360   FORMAT ('0*** ERROR - THE KEYWORD ',A,' WAS FOUND BUT ',
     *   'NO GROUP NAME WAS SPECIFIED.')
370   FORMAT ('0*** ERROR - ROUTINE TO DELETE ',A,' ',
     *   'PARAMETERS IS AVAILABLE.')
380   FORMAT ('0*** NOTE - NO DELETE OPTIONS FOUND FOR GROUP ',A,'.')
390   FORMAT (' *** EXIT SLDELT')
C
      END
