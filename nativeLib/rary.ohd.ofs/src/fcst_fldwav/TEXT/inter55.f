      SUBROUTINE INTER55(PO,CO,Z,ST1,LTST1,T1,LTT1,POLH,LTPOLH,ITWT,
     . LTITWT,QLJ,LTQLJ,QL,LTQL,NCML,NQCM,NQL,C,NB,NJUN,QD,QU,YD,YU,
     . DDX,D,ATF,QLV,QLSUM,KRCH,KSUPC,MIXF,KFTR,FKEC,TFT,KLPI,X,XLOS,
     . QLOS,ALOS,KLOS,QDI,SLFI,MRU,NJUM,MRV,QJ,YJ,IS1,ISN,L1,L2,L3,L4,
     . K1,K2,K7,K8,K9,K10,K15,K16,K19,K20,K21)
C
C      THIS SUBROUTINE COMPUTES COEFFICIENTS USED IN NEWTON-RALPHSON
C           ITERATIVE SOLUTION OF UNSTEADY FLOW EQUATIONS
C
      COMMON/METR55/METRIC
      COMMON/M155/NU,JN,JJ,KIT,G,DT,TT,TIMF,F1
      COMMON/M655/KTIME,DTHYD,J1
      COMMON/LEV55/NLEV,DHLV,NPOND,DTHLV,IDTHLV
      COMMON/VS55/MUD,IWF,SHR,VIS,UW,PB,SIMUD
      COMMON/SS55/ NCS,A,B,DB,R,DR,AT,BT,P,DP,ZH
      COMMON/M3255/IOBS,KTERM,KPL,JNK,TEH
      COMMON/M3455/ KXP,ICD,ITMAX,KWARM
      COMMON/FLP55/KFLP
      COMMON/PRES55/KPRES
      COMMON/PRMS55/DPRM,WPRM
      COMMON/IONUM/IN,IPR,IPU
        COMMON/NETWK55/NET

      INCLUDE 'common/fdbug'
      INCLUDE 'common/ofs55'
C
      DIMENSION PO(*),CO(*),Z(*),ST1(*),LTST1(*),T1(*),LTT1(*),POLH(*)
      DIMENSION LTPOLH(*),ITWT(*),LTITWT(*),QLJ(*),LTQLJ(*),QL(*)
      DIMENSION C(K15),D(4,K15),NB(K1),QD(K2,K1),NQCM(K1),NQL(K1)
      DIMENSION YD(K2,K1),YU(K2,K1),ATF(K1),DDX(K2,K1),FKEC(K2,K1)
      DIMENSION NJUN(K1),QLV(K2,K1),QLSUM(K2,K1),QU(K2,K1),QDI(K2,K1)
      DIMENSION KRCH(K2,K1),MIXF(K1),KFTR(K1),TFT(4,1),KLPI(K1),LTQL(*)
      DIMENSION X(K2,K1),XLOS(2,K1),QLOS(K1),ALOS(K1),SLFI(K2,K1)
      DIMENSION MRU(K1),NJUM(K1),MRV(K1),QJ(K1),YJ(K1)
      DIMENSION KLOS(K1)
      CHARACTER*8 SNAME
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_fldwav/RCS/inter55.f,v $
     . $',                                                             '
     .$Id: inter55.f,v 1.4 2004/02/02 21:53:28 jgofus Exp $
     . $' /
C    ===================================================================
C

      DATA SNAME/ 'INTER55 ' /
C
      CALL FPRBUG(SNAME,1,55,IBUG)

      J=JJ
      N=NB(J)
      F1C=F1
      QK=0.0
      DQK=0.0
      BET=1.06
      DBE=0.0
      ISN1=ISN-1
      II=1
      IF(KSUPC.EQ.1) ISN1=IS1
      DO 900 I=IS1,ISN1
      IP=I+1
      IF(KSUPC.EQ.1) GO TO 5
      IF(I.GT.IS1) GO TO 2
      II=2
      GO TO 5
    2 II=II2+1
    5 II2=II+1
C  IF LEVEL POOL ROUTING USED
C  LEAVE SPACE FOR LEVEL POOL ROUTING MATRIX COEFFIEIENTS
C  COEFFICIENTS TO BE ESTABLISHED IN DAM SUBROUTINE
      IF(KRCH(I,J).EQ.4) GO TO 90
 10   DO 80 K=1,4
      GO TO(20,25,30,35),K
 20   Y=YD(I,J)
      KTMSL=1
      IS=I
      GO TO 40
 25   Y=YU(I,J)
      KTMSL=2
      IS=I
      GO TO 40
 30   Y=YD(IP,J)
      KTMSL=1
      IS=IP
      GO TO 40
 35   Y=YU(IP,J)
      KTMSL=2
      IS=IP
 40   CALL SECT55(PO(LCPR),PO(LOAS),PO(LOBS),PO(LOHS),PO(LOASS),
     . PO(LOBSS),J,IS,Y,PO(LCHCAV),PO(LCIFCV),K1,K2,K9)
      IF(KFLP.GE.1) CALL CONV55(PO(LOQKC),PO(LOHKC),PO(LCBEV),PO(LCNKC),
     1 J,IS,Y,QK,DQK,BET,DBE,0,K1,K2)
C
      GO TO(55,60,65,70),K
 55   ATDL=AT
      ADL=A
      BDL=B
      QKDL=QK
      B1=BET
      PDL=WPRM
      GO TO 75
 60   AUL=A
      BUL=B
      DBUL=DB
      ATUL=AT
      BTUL=BT
      QKUL=QK
      DQKUL=DQK
      B2=BET
      DB2=DBE
      PUL=WPRM
      DPUL=DPRM
      GO TO 75
 65   ATDR=AT
      ADR=A
      BDR=B
      QKDR=QK
      B3=BET
      PDR=WPRM
      GO TO 75
 70   AUR=A
      BUR=B
      BTUR=BT
      ATUR=AT
      DBUR=DB
      QKUR=QK
      DQKUR=DQK
      B4=BET
      DB4=DBE
      PUR=WPRM
      DPUR=DPRM
 75   CONTINUE
 80   CONTINUE
C------------------   LPI COEFFICIENT IS CLPI   --------------------
      CLPI=1.00
      IF (KLPI(J).EQ.0) GO TO 82
      IF (KRCH(I,J).NE.6) GOTO 82
      FRL=(QD(I,J)/ADL)/(G*ADL/BDL)**0.5
      FRR=(QD(IP,J)/ADR)/(G*ADR/BDR)**0.5
      FRA=0.5*(FRL+FRR)
      CLPI=1.0-FRA**KLPI(J)
      IF(FRA.GE.1.0) CLPI=0.0
C---------------------    Internal Boundaries   ------------------------
82    DX=DDX(I,J)
      KRA=IABS(KRCH(I,J))
      IF(KRA.LT.10) GO TO 350
      IFL=0
      QQ=QU(I,J)
      IF (KRA.GE.10.AND.KRA.LE.30) THEN
      ITRS=0
      CALL DAM55(PO,CO,Z,ST1,LTST1,T1,LTT1,POLH,LTPOLH,ITWT,LTITWT,D,C,
     . QU,QD,YU,YD,PO(LOHDD),PO(LOHSPD),PO(LOHGTD),PO(LOCSD),PO(LOCGD),
     1 PO(LOZBCH),PO(LOCDOD),PO(LCNFLD),Z(LZBBP),Z(LZYBP),Z(LZSQS1),
     2 Z(LZSQS2),Z(LZSQO),Z(LZQBCH),Z(LZQOTP),Z(LZQOTR),KRCH,PO(LOQGH),
     3 PO(LOCLL),PO(LOSPL),PO(LORHI),PO(LORQI),PO(LOTIQH),QDI,QN,DQNYU,
     4 DT,IFL,ITRS,II,II2,L1,L2,L3,L4,ITQ,I,J,K1,K2,K7,K8,K9,K15,K16,
     5 K19,K20,K21)
      
      IF (KRA.EQ.28 .AND. QN.EQ.-0.30) GOTO 350

      IF(ISN.EQ.2) THEN
        D(L4,1)=0.
        YU(2,J)=YD(2,J)
        C(4)=0.
        D(3,4)=0.0
        D(4,4)=1.0
      ENDIF
      END IF
      IF(IFL.NE.0) GO TO 350
      IF(KRA.GE.10.AND.KRA.LE.30) GO TO 90
      IF(KRA.EQ.41) CALL CRTFLO55(I,J,II,II2,L1,L2,L3,L4,AUL,BUL,DBUL,
     1 D,C,QU,K1,K2,K15)
      IF(KRA.EQ.41) GO TO 90
      IF(KRA.NE.35) GO TO 90
      YUL=YU(I,J)
      YUR=YU(IP,J)
      YDR=YD(IP,J)
      CALL BRIDGE55(PO,Z,NCML,PO(LONQCM),I,J,II,II2,L1,L2,L3,L4,YUL,YUR,
     1 YDR,DDX,PO(LOEBE1),PO(LOEBE2),PO(LOBRGW),PO(LOCDBR),Z(LZD),
     2 Z(LZC),Z(LZQU),PO(LCNFLD),Z(LZBBP),Z(LZYBP),Z(LZSQW),
     3 Z(LZSQS1),Z(LZSQO),Z(LZQBCH),Z(LZQOTP),Z(LZQOTR),
     3 PO(LOHS),QTS,TT,QQ,PO(LOZBCH),PO(LCTOPN),PO(LOEBW2),
     4 PO(LOEBW1),K1,K2,K7,K8,K9,K15,K16)
      GO TO 90
C-----------------------------------------------------------------------            
  350 CONTINUE
      CMU=0.01
      CMD=0.01
      DCMU=0.0
      YAU = 0.5*(YU(I,J)+YU(IP,J))
      YAD = 0.5*(YD(I,J)+YD(IP,J))
      IRCH=I
      IRCM=IRCH
      LL=IRCH
      LLP=LL+1
      IF(LLP.GE.N) LLP=N
      IF(NQCM(J).LT.0) GO TO 276
      YQU = 0.5*(YU(LL,J)+YU(LLP,J))
      YQD = 0.5*(YD(LL,J)+YD(LLP,J))
      GO TO 277
  276 YQU = 0.5*(QU(LL,J)+QU(LLP,J))
      YQD = 0.5*(QD(LL,J)+QD(LLP,J))
  277 CALL FRICT55(NCML,PO(LOCM),PO(LOYQCM),J,IRCM,YQU,CMU,DCMU,
     . K1,K7,K8)
      CALL FRICT55(NCML,PO(LOCM),PO(LOYQCM),J,IRCM,YQD,CMD,DCMD,
     . K1,K7,K8)
      CALL SINC55(YAD,J,I,IP,NCS,PO(LOHS),PO(LOSNC),SC,DSC,PO(LOSNM),SM,
     * DSM,K1,K2,K9)
      S1C=SC
      S2C=SC
      S1M=SM
      S2M=SM
      CALL SINC55(YAU,J,I,IP,NCS,PO(LOHS),PO(LOSNC),SC,DSC,PO(LOSNM),SM,
     * DSM,K1,K2,K9)
      S3C=SC
      DS3C=DSC
      S4C=SC
      DS4C=DSC
      S3M=SM
      DS3M=DSM
      S4M=SM
      DS4M=DSM
      IF(MIXF(J).GE.2.AND.KSUPC.EQ.1) F1=1.00
      IF(MIXF(J).GE.2.AND.KSUPC.EQ.1) F1C=1.00
      PNL=DCMU/2/CMU
      PNR=DCMU/2/CMU
      QUL=QU(I,J)
      QUR=QU(IP,J)
      QDL=QD(I,J)
      QDR=QD(IP,J)
      FE=FKEC(I,J)
      FXT=0.5*DX/DT  
      FXTFT=FXT
      IF (KFTR(J).EQ.1) FXTFT=0.5*DX/(DTHYD*3600.)
      QAU=0.5*(QUL+QUR)
      QAD=0.5*(QDL+QDR)
      IF(QAD.GT.0.0) GO TO 84
      IF(FE.GT.0.001) FE=-(2.*FE+0.1)
      IF(FE.LT.-0.001) FE=-(FE+0.1)/2.
   84 AQKU=0.5*(QKUL+QKUR)
      AQKD=0.5*(QKDL+QKDR)
      BAU=0.5*(BUL+BUR)
      BAD=0.5*(BDL+BDR)
      AAU=0.5*(AUL+AUR)
      AAD=0.5*(ADL+ADR)
      IF(KFLP.GE.1) GO TO 1186
      IF(KPRES.EQ.0) GO TO 1184
      PAU=0.5*(PUL+PUR)
      PAD=0.5*(PDL+PDR)
      RAU=(AAU/PAU)**(2./3.)
      RAD=(AAD/PAD)**(2./3.)
      SFU=CMU*CMU*ABS(QAU)*QAU/(AAU*AAU*RAU*RAU*2.208)
      SFD=CMD*CMD*ABS(QAD)*QAD/(AAD*AAD*RAD*RAD*2.208)
      PSFHL=2.*SFU*(PNL-5.*BUL/6./AAU+DPUL/3./PAU)
      PSFHR=2.*SFU*(PNR-5.*BUR/6./AAU+DPUR/3./PAU)
      GO TO 85
 1184 RAU=(AAU/BAU)**(2./3.)
      RAD=(AAD/BAD)**(2./3.)
      SFU=CMU*CMU*ABS(QAU)*QAU/(AAU*AAU*RAU*RAU*2.208)
      SFD=CMD*CMD*ABS(QAD)*QAD/(AAD*AAD*RAD*RAD*2.208)
      PSFHL=2.*SFU*(PNL-5.*BUL/6./AAU+DBUL/3./BAU)
      PSFHR=2.*SFU*(PNR-5.*BUR/6./AAU+DBUR/3./BAU)
      GO TO 85
 1186 SFU=ABS(QAU)*QAU/(AQKU*AQKU)
      SFD=ABS(QAD)*QAD/(AQKD*AQKD)
      PSFHL=-SFU/AQKU*DQKUL
      PSFHR=-SFU/AQKU*DQKUR
   85 PSFQL=SFU/QAU
      PSFQR=SFU/QAU
C-------------------   SI FOR MUD FLOW  AND S=SF+SI  -----------------
      IF(MUD.NE.1) GO TO 287
      P1=1.0/(PB+0.15)
      P2=(SHR/VIS)**PB
      PA=((PB+1.0)*(PB+2.0)/(0.74+0.656*PB)/P2)**P1
      DAU=AAU/BAU
      DAD=AAD/BAD
      SIU=(1.0+PA*(ABS(QAU)/AAU/DAU)**P1)*SHR/UW/DAU
      SID=(1.0+PA*(ABS(QAD)/AAD/DAD)**P1)*SHR/UW/DAD
      SFU=SFU+SIU
      SFD=SFD+SID
      SIMUD=SID
      PSIQL=(ABS(QAU)/AAU/DAU)**(P1-1.0)*(PA*P1*SHR/UW/DAU/DAU/AAU)
      PSIQR=PSIQL
      PSIHL=-(1.0+PA*(2.0*P1+1.0)*(ABS(QAU)/AAU/DAU)**P1)*SHR/UW/DAU/DAU
      PSIHR=PSIHL
      PSFHL=PSFHL+PSIHL
      PSFHR=PSFHR+PSIHR
      PSFQL=PSFQL+PSIQL
      PSFQR=PSFQR+PSIQR
C GRANULAR SLIDING MODELING FOR DEBRIS MUDFLOW (MUD>2)
C FRICTION ANGLE (UW) IS USED (AMUD=tan UW)
  287 IF (I.GE.MUD) GOTO 288
      AMUD=TAN(6.283*UW/360.0)
      SLOP=(1.0-(SLFI(I,J))**2)**0.5
      AI=I
      AM=MUD
      RMUD=(AI+1.0)/AM
      IF (MUD.LE.2) RMUD=1.0
      SIU=AMUD*SLOP*RMUD*1.02
      SID=AMUD*SLOP*RMUD*1.02
      SFU=SFU+SIU
      SFD=SFD+SID     
C-------------------  END OF MUD FLOW  ---------------------------------
  288 VUL=QUL/AUL
      VUR=QUR/AUR
      VDL=QDL/ADL
      VDR=QDR/ADR
      HLU=(VUR**2-VUL**2)/64.4*FE
      HLD=(VDR**2-VDL**2)/64.4*FE
      HDU=YU(IP,J)-YU(I,J)+HLU
      HDD=YD(IP,J)-YD(I,J)+HLD
      PSEHL=FE/32.2*VUL*VUL*BUL/AUL
      PSEHR=-FE/32.2*VUR*VUR*BUR/AUR
      PSEQL=-FE/32.2*VUL/AUL
      PSEQR=FE/32.2*VUR/AUR
      QSEEP=0.
      QLL=0.0
      QLU=0.
      QLD=0.
      PQLH=0.
      PQLQ=0.
      QVU=0.
      QVD=0.
      PQVHL=0.
      PQVHR=0.
      PQVQL=0.
      PQVQR=0.
      IF(ABS(QLL).GT.0.0001) THEN
        OM=QAD/AAD*QSEEP/2.*DX
        QVU=QVU+OM
        QVD=QVD+OM
      END IF
C..........................................................................
C  TRIBUTARY FLOW .... QLJNU,QLJND < 0 ==> BULK LATERAL OUTLFOW (cfs)
C                      QLJNU,QLJND > 0 ==> BULK LATERAL INLFOW (cfs)
      IF(JN.LE.1 .OR. J.GT.JN) GO TO 260
      CALL QLJN55(PO,T1,LTT1,QLJ,LTQLJ,NB,NJUN,Z(LZQJ),QD,MRV,DDX,
     . NQL,PO(LOLQ1),PO(LCLQN),YU,YD,QDI,J,I,QLJU,QLJD,ATRBU,ATRBD,
     . K1,K2,K9,K10)
      QLJNU=QLJU*DX
      QLJND=QLJD*DX
      QLU=QLU+QLJNU
      ATFJ=ATF(J)
      IF(QLJNU.LE.-0.1) THEN
       QVU=QVU+QLJNU*QAU/AAU
      ELSE
       QVU=QVU+ATFJ*QLJNU*QLJNU/ATRBU
      ENDIF
      QLD=QLD+QLJND
      ATFJ=ATF(J)
      IF(QLJND.LE.-0.1) THEN
       QVD=QVD+QLJND*QAD/AAD
      ELSE
       QVD=QVD+ATFJ*QLJND*QLJND/ATRBD
      ENDIF
C..........................................................................
C  LATERAL FLOWS FROM NETWORK-RIVERS  (QJ=U/S  YJ=D/S)
  260 IF(NET.LE.0) GOTO 265
      QLNETU=0.0
      QLNETD=0.0
      DO 263 JNET=JN+1,JN+NET
      QL1=0.0
      QL2=0.0
      JIN=MRV(JNET)
      IIN=NJUN(JNET)
      JOUT=MRU(JNET)
      IOUT=NJUM(JNET)
      NBIN=NB(JNET)
      IF(J.EQ.JIN.AND.I.EQ.IIN) THEN
        QL1=QJ(JNET)
        QL2=QD(NBIN,JNET)
      ENDIF
      IF(J.EQ.JOUT.AND.I.EQ.IOUT)THEN
        QL1=-YJ(JNET)
        QL2=-QD(1,JNET)
      ENDIF
      QLNETU=QLNETU+QL1
      QLNETD=QLNETD+QL2
  263 CONTINUE
      QLU=QLU+QLNETU
      QLD=QLD+QLNETD
C..........................................................................
C  LEVEE FAILURE INDUCED LATERAL FLOW
  265 IF (NLEV.EQ.0) GOTO 268
      QLEVU=QLV(I,J)
      QLEVD=QLEVU
      QLU=QLU+QLEVU
      IF(QLEVU.LE.-0.1) THEN
        QVU=QVU+QLEVU*QAU/AAU
        PQVHL=0.5*QVU*BUL/AAU
        PQVHR=0.5*QVU*BUR/AAU
        PQVQL=-0.5*QVU/QAU
        PQVQR=PQVQL
      END IF
      QLD=QLD+QLEVD
      IF(QLEVD.LE.-0.1) QVD=QVD+QLEVD*QAD/AAD
  268 CONTINUE
C..........................................................................
C LATERAL INFLOWS
      IF(NQL(J).LE.0) GO TO 88
      CALL QLTS55(T1,LTT1,QL,LTQL,CO(LXQLI),I,NQL,PO(LOLQ1),PO(LCLQN),
     . QLTSU,QLTSD,K1,K10)

      QLU=QLU+QLTSU*DX
      IF(QLTSU.LE.-0.1) QVU=QVU+QLTSU*QAU/AAU
      QLD=QLD+QLTSD*DX
      IF(QLTSD.LE.-0.1) QVD=QVD+QLTSD*QAD/AAD
   88 CONTINUE
C-------------------   FLOW LOSS OR GAIN   -------------------------
      IF(KLOS(J).EQ.0) GO TO 882
      IF (ABS(QLOS(J)).LE.0.001) GOTO 882
      QBASE=QDI(1,J)
      AN=ALOS(J)
      XL=(X(I,J)-XLOS(1,J))/(XLOS(2,J)-XLOS(1,J))
      IF (XL.LE.0.01 .OR. XL.GT.1.0) GOTO 882
      BTAX=AN*0.01*QLOS(J)*XL**(AN-1.0)/
     $     (1.0+0.01*QLOS(J)*XL**AN)/(XLOS(2,J)-XLOS(1,J))/5279.0
      QLDL=(QD(I,J)-QBASE)*BTAX
      QLDR=(QD(IP,J)-QBASE)*BTAX
      IF (QD(I,J).LT.QBASE) QLDL=0.0
      IF (QD(IP,J).LT.QBASE) QLDR=0.0
      QLD=0.5*(QLDL+QLDR)*DX
      QLU=QLD
C-------------------  END OF LOSS TAKEN  -------------------------
882   CONTINUE
      QVU=-1.0*QVU
      QVD=-1.0*QVD
      QLSUM(I,J)=QLU
      C(II)=-(F1C*(QUR-QUL)+(1.-F1C)*(QDR-QDL)+FXT*(S3C*ATUL+S4C*
     1 ATUR-S1C*ATDL-S2C*ATDR)-(F1C*QLU+(1.-F1C)*QLD))
      IF(KSUPC.EQ.0) THEN
        D(L1,II)=FXT*(BTUL*S3C+ATUL*DS3C)-F1C*PQLH
        TEMP1=FXTFT*(BTUL*S3C+ATUL*DS3C)-F1C*PQLH
        D(L2,II)=-F1C-F1C*PQLQ
      END IF
      D(L3,II)=FXT*(BTUR*S4C+ATUR*DS4C)-F1C*PQLH
      TEMP2=FXTFT*(BTUR*S4C+ATUR*DS4C)-F1C*PQLH
      D(L4,II)=F1C-F1C*PQLQ
      IF(KRCH(I,J).NE.1) GO TO 89
      C(II2)=-(F1*(G*AAU*(HDU+DX*SFU)+QVU)+(1.-F1)*(G*AAD*(HDD+DX*SFD)+
     1QVD))
      IF(KSUPC.EQ.0) THEN
        D(L1,II2)=F1*(G*AAU*(-1.+DX*PSFHL+PSEHL)+
     &  0.5*G*BUL*(HDU+DX*SFU))+PQVHL
        D(L2,II2)=F1*(G*AAU*DX*PSFQL+G*AAU*PSEQL)+PQVQL
      END IF
      D(L3,II2)=F1*(G*AAU*(1.+DX*PSFHR+PSEHR)+0.5*G*BUR*(HDU+DX*SFU))+
     1 PQVHR
      D(L4,II2)=F1*(G*AAU*DX*PSFQR+G*AAU*PSEQR)+PQVQR
      GO TO 880
   89 CONTINUE
C  MOMENTUM
      C(II2)=-(CLPI*FXT*(S3M*QUL+S4M*QUR-S1M*QDL-S2M*QDR)+
     * F1*(CLPI*B4*QUR*ABS(QUR)/AUR-CLPI*B2*QUL*ABS(QUL)/AUL+
     *   G*AAU*(HDU+DX*SFU))+
     * (1.-F1)*(CLPI*B3*QDR*ABS(QDR)/ADR-CLPI*B1*QDL*ABS(QDL)/ADL+
     * G*AAD*(HDD+DX*SFD))+F1*QVU+(1.-F1)*QVD)
      IF(KSUPC.EQ.0) THEN
        D(L1,II2)=F1*(CLPI*B2*QUL*ABS(QUL)*BUL/AUL/AUL+
     *             CLPI*DB2*QUL*ABS(QUL)/AUL+
     *  G*AAU*(-1.+DX*PSFHL+PSEHL)+0.5*G*BUL*(HDU+DX*SFU))+
     *  PQVHL+FXT*QUL*DS3M
        D(L2,II2)=CLPI*FXT*S3M+F1*(-2*CLPI*B2*ABS(QUL)/AUL+
     *  G*AAU*DX*PSFQL+G*AAU*PSEQL)+PQVQL

        TEMP3=F1*(B2*QUL*ABS(QUL)*BUL/AUL/AUL+DB2*QUL*ABS(QUL)/AUL+
     *  G*AAU*(-1.+DX*PSFHL+PSEHL)+0.5*G*BUL*(HDU+DX*SFU))+
     *  PQVHL+FXTFT*QUL*DS3M
        TEMP4=FXTFT*S3M+F1*(-2*B2*ABS(QUL)/AUL+
     *  G*AAU*DX*PSFQL+G*AAU*PSEQL)+PQVQL

      END IF
      D(L3,II2)=F1*(-B4*CLPI*QUR*ABS(QUR)*BUR/AUR/AUR+
     *               CLPI*DB4*QUR*ABS(QUR)/AUR+
     * G*AAU*(1.+DX*PSFHR+PSEHR)+0.5*G*BUR*(HDU+DX*SFU))+
     * PQVHR+FXT*QUR*DS4M
      D(L4,II2)=CLPI*FXT*S4M+F1*(CLPI*2*B4*ABS(QUR)/AUR+
     * G*AAU*DX*PSFQR+G*AAU*PSEQR)+PQVQR

      TEMP5=F1*(-B4*QUR*ABS(QUR)*BUR/AUR/AUR+DB4*QUR*ABS(QUR)/AUR+
     * G*AAU*(1.+DX*PSFHR+PSEHR)+0.5*G*BUR*(HDU+DX*SFU))+
     * PQVHR+FXTFT*QUR*DS4M
      TEMP6=FXTFT*S4M+F1*(2*B4*ABS(QUR)/AUR+
     * G*AAU*DX*PSFQR+G*AAU*PSEQR)+PQVQR
  880 CONTINUE
   90 CONTINUE
C-------------   STORE THE MATRIX FOR KALMAN FILTER  -------------------
      IF (KFTR(JJ).EQ.0) GOTO 99
      NSTART=1
      IF (JJ.EQ.1) GOTO 960
      DO 920 K=2,JJ
920   NSTART=NSTART+2*NB(K-1)*KFTR(K-1)
960   DO 980 K=1,4
      TFT(K,II+NSTART-1)=D(K,II)
980   TFT(K,II2+NSTART-1)=D(K,II2)
      IF (KRA.GE.10) GOTO 99
      TFT(L1,II+NSTART-1)=TEMP1
      TFT(L3,II+NSTART-1)=TEMP2
      TFT(L1,II2+NSTART-1)=TEMP3
      TFT(L2,II2+NSTART-1)=TEMP4
      TFT(L3,II2+NSTART-1)=TEMP5
      TFT(L4,II2+NSTART-1)=TEMP6
99    CONTINUE
C-----------------------------------------------------------------------
      IF(JNK.LT.110) GO TO 900
      IF(KRA.GE.10.AND.KRA.NE.28) GO TO 890
      IF (KRA.EQ.28 .AND. QN.NE.-0.30) GOTO 890
      PADL=ADL
      PADR=ADR
      PAAD=AAD
      PAUL=AUL
      PAUR=AUR
      PAAU=AAU
      PATUL=ATUL
      PATUR=ATUR
      PATDL=ATDL
      PATDR=ATDR
      PHDD=HDD
      PHDU=HDU
      PQUR=QUR
      PQUL=QUL
      PQDR=QDR
      PQDL=QDL
      PQLU=QLU
      PQLD=QLD
      PQVD=QVD
      PQVU=QVU
      PDX=DX
      IF(METRIC.EQ.1) THEN
        PADL=ADL/10.765
        PADR=ADR/10.765
        PAAD=AAD/10.765
        PAUL=AUL/10.765
        PAUR=AUR/10.765
        PAAU=AAU/10.765
        PATUL=ATUL/10.765
        PATUR=ATUR/10.765
        PATDL=ATDL/10.765
        PATDR=ATDR/10.765
        PHDD=HDD/3.281
        PHDU=HDU/3.281
        PQUR=QUR/35.32
        PQUL=QUL/35.32
        PQDR=QDR/35.32
        PQDL=QDL/35.32
        PQLU=QLU/35.32
        PQLD=QLD/35.32
        PQVD=QVD/35.32
        PQVU=QVU/35.32
        PDX=DX/3.281
      ENDIF
      IF(JNK.GT.110) WRITE(IPR,885) F1C,PQUR,PQUL,PQDR,PQDL,FXT,
     1 S3C,PATUL,S4C,PATUR,S1C,PATDL,S2C,PATDR,PDX,PQLU,PQLD,BAU,
     2 BAD,CMU,CMD
  885 FORMAT(/13X,3HF1C,13X,3HQUR,13X,3HQUL,13X,3HQDR,13X,3HQDL,13X,
     . 3HFXT,13X,3HS3C,12X,4HATUL/8E16.8/13X,3HS4C,12X,4HATUR,13X,3HS1C,
     . 12X,4HATDL,13X,3HS2C,12X,4HATDR,14X,2HDX/7E16.8/13X,3HQLU,
     . 13X,3HQLD,13X,3HBAU,13X,3HBAD,13X,3HCMU,13X,3HCMD/8E16.8)
      WRITE(IPR,886) I,II,C(II),D(L1,II),D(L2,II),D(L3,II),D(L4,II)
  886 FORMAT(5X,'C-EQ: I,II,C,D= ',2I5,5F15.5)
      IF(JNK.GT.110) WRITE(IPR,887) FXT,S3M,PQUL,S4M,PQUR,S1M,PQDL,S2M,
     1 PQDR,F1,B4,PAUR,B2,PAUL,PAAU,PHDU,PDX,SFU,B3,PADR,B1,PADL,
     2 PAAD,PHDD,SFD,PQVU,PQVD,CLPI
  887 FORMAT(/13X,3HFXT,13X,3HS3M,13X,3HQUL,13X,3HS4M,13X,3HQUR,13X,
     . 3HS1M,13X,3HQDL,13X,3HS2M/8E16.8/13X,3HQDR,14X,2HF1,14X,2HB4,
     . 13X,3HAUR,13X,2HB2,13X,3HAUL,13X,3HAAU,13X,3HHDU/8E16.8/14X,
     . 2HDX,13X,3HSFU,14X,2HB3,13X,3HADR,14X,2HB1,13X,3HADL,13X,3HAAD,
     . 13X/7E16.8/13X,3HHDD,13X,3HSFD,13X,3HQVU,13X,3HQVD,12X,4HCLPI/
     . 8E16.8)
      WRITE(IPR,888) I,II2,C(II2),D(L1,II2),D(L2,II2),D(L3,II2),
     1  D(L4,II2)
  888 FORMAT(5X,'M-EQ: I,II,C,D= ',2I5,5F15.5)
  890 IF(ISN.EQ.2) WRITE(IPR,888) 1,4,C(4),D(L1,4),D(L2,4),D(L3,4),
     1   D(L4,4)
  900 CONTINUE
      RETURN
      END
