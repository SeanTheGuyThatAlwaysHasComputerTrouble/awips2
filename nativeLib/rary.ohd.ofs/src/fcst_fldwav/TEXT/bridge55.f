      SUBROUTINE BRIDGE55(PO,Z,NCML,NQCM,I,J,II,II2,L1,L2,L3,L4,YUL,
     1 YUR,YDR,DDX,EMBEL1,EMBEL2,BRGW,CDBRG,D,C,QU,NFAILD,BBP,YBP,SQW,
     2 SQS1,SQO,QBRCH,QOVTP,QOTHR,HS,QTL,TT,QQ,ZBCH,TOPN,EMBW2,EMBW1,
     3 K1,K2,K7,K8,K9,K15,K16)
C
C  THIS SUBROUTINE PERFORM BRIDGE FLOW COMPUTATION
C  YUL,YUR= NEW WATER DEPTH AT U/S AND D/S AT TT
C  YDL,YDR= OLD WATER DEPTH AT U/S AND D/S AT TT-DT
C  QN= FLOW THROUGH BRIDGE
C  QNWL= FLOW OVER LOWER ROAD EMBANKMENT
C  QNWU= FLOW OVER UPPER ROAD EMBANKMENT
C  QW= BREACH FLOW
C  CDBRG= DISCHARGE COEF. THROUGH BRIDGE
C  EMBEL1= CREST ELEV. OF LOWER ROAD EMBANKMENT OF BRIDGE
C  EMBEL2= CREST ELEV. OF LEVEL UPPER ROAD EMBANKMENT OF BRIDGE
C  EMBW1= CREST LENGTH OF LOWER ROAD EMBANKMENT OF BRIDGE
C  EMBW2= CREST LENGTH OF LEVEL UPPER ROAD EMBANKMENT OF BRIDGE
C  BRGW= WIDTH OF UPPER AND LOWER BRGANKMENT // TO FLOW
C
      COMMON/SS55/NCS,A,B,DB,R,DR,AT,BT,P,DP,ZH
      COMMON/M3255/IOBS,KTERM,KPL,JNK,TEH
      COMMON/FLP55/KFLP
      COMMON/IT55/ITER
      COMMON/IONUM/IN,IPR,IPU

      INCLUDE 'common/fdbug'
      INCLUDE 'common/ofs55'

      DIMENSION PO(*),Z(*),NQCM(K1),DDX(K2,K1)
      DIMENSION EMBEL1(K16,K1),EMBEL2(K16,K1)
      DIMENSION BRGW(K16,K1),CDBRG(K16,K1),D(4,K15),C(K15),QU(K2,K1)
      DIMENSION NFAILD(K16,K1),BBP(K16,K1),YBP(K16,K1)
      DIMENSION SQW(2,K16,K1),SQS1(2,K16,K1),SQO(2,K16,K1)
      DIMENSION QBRCH(K16,K1),QOVTP(K16,K1),QOTHR(K16,K1)
      DIMENSION HS(K9,K2,1),ZBCH(K16,K1)
      DIMENSION TOPN(K16,K1),EMBW2(K16,K1),EMBW1(K16,K1)
      CHARACTER*8 SNAME
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_fldwav/RCS/bridge55.f,v $
     . $',                                                             '
     .$Id: bridge55.f,v 1.2 2000/12/19 15:45:44 dws Exp $
     . $' /
C    ===================================================================
C

      DATA SNAME/ 'BRIDGE55' /
C
      CALL FPRBUG(SNAME,1,55,IBUG)

      CALL IDDB55(I,J,PO(LONLAD),PO(LOLAD),KL,K1,K16)
      SGN=1.0
      Y=YUL
      YY=YUR
      IF(YUL.GE.YUR) GO TO 20
      Y=YUR
      YY=YUL
      SGN=-1.0
CCC   20 QOVT=0.
   20 QNWU=0.
      QNWL=0.
      QN=0.
      QW=0.
      QTL=QQ
      BO=0.
      YB=EMBEL2(KL,J)
      DXX=DDX(I,J)
      DQNYU=0.
      DQNQU=0.
      DQNWUU=0.
      DQNWLU=0.
      DQWU=0.
      DQNYD=0.
      DQNQD=0.
      DQNWUD=0.
      DQNWLD=0.
      DQWD=0.
      IR=I+1
      CALL SECT55(PO(LCPR),PO(LOAS),PO(LOBS),HS,PO(LOASS),PO(LOBSS),
     * J,I,Y,PO(LCHCAV),PO(LCIFCV),K1,K2,K9)
      DHLN=0.
      ALN=1.
      ULEN=EMBW2(KL,J)
      IF(ULEN.GT.0.01) ALN=ULEN*(Y-EMBEL2(KL,J))
      IF(ULEN.LE.0.01) CALL CRESTL55(PO(LOHCRL),PO(LOCRL),ALN,ULEN,DHLN,
     * KL,J,Y,K1,K16)

      RCW=BRGW(KL,J)
      CB=CDBRG(KL,J)
      HCL=EMBEL1(KL,J)
      HCU=EMBEL2(KL,J)
      IF(ABS(Y-YY).LT.0.001) GO TO 37
      TBR=Y-TOPN(KL,J)
      IF(TBR.LT.0.0) GO TO 29
      RRT=TBR/(Y-HS(1,I,J))
      CKT=1.0
      IF(RRT.GT.0.09) CKT=1.0-(RRT-0.09)
      IF(CKT.GT.1.0) CKT=1.0
      IF(CKT.LT.0.8) CKT=0.8
      CB=CB*CKT
   29 CALL SECBR55(PO(LOBRHS),PO(LOBRBS),AD,BD,DBD,KL,J,YY,TOPN,K1,K16)
      VH=QQ*QQ/64.4
      BR1=Y-YY+VH/A/A
      BRAK=ABS(BR1)
      QN=8.02*CB*AD*SQRT(BRAK)
      IF(KFLP.EQ.0) GO TO 131
      CALL CONV55(PO(LOQKC),PO(LOHKC),PO(LCBEV),PO(LCNKC),J,IR,Y,QK,DQK,
     2 BET,DBE,0,K1,K2)
      FKB=QK
      GO TO 132
  131 YQ=YDR
      IF(NQCM(J).LT.0) YQ=QN
      CALL FRICT55(NCML,PO(LOCM),PO(LOYQCM),J,I,YDR,CMN,DC,K1,K7,K8)
      IF(CMN.GT.0.20) CMN=0.03
      IF(CMN.LT.0.01) CMN=0.03
      FKB=1.49/CMN*AD*(AD/BD)**(2./3.)
  132 HF=DXX*(QN/FKB)**2.
      BR1=Y-YY+VH/A/A-HF
      BRAK=ABS(BR1)
      QN=8.02*CB*AD*SQRT(BRAK)
CC      WRITE(88, 8888) KL,TT,Y,YY,(Y-YY),(VH/A/A),HF,BR1,QN
CC 8888 FORMAT(2X,3HKL=,I3,2X,3HTT=,F10.4,2X,2HY=F8.3,2X,3HYY=,F7.3,2X,
CC     . 3HDH=,F7.4,2X,3HVH=,F7.4,2X,3HHF=,F7.4,2X,4HSUM=,F7.4,2X,
CC     . 3HQN=,F10.0)
      DQNYU=4.01*CB*AD*(1.-2.*B/A**3*VH)/SQRT(BRAK)
      DQNYD=8.02*CB*SQRT(BRAK)*(BD-0.5*AD/BRAK)
      DQNQU=8.02*CB*AD*QQ/(SQRT(BRAK)*64.4*A*A)
      IF(HCU.LT.0.001) HCU=100000.
      IF(HCL.LT.0.001) HCL=100000.
C      IF(Y.LE.HCU.AND.Y.LE.HCL) GO TO 37
      HRU=(YY-HCU)/(Y-HCU)
      HRL=(YY-HCL)/(Y-HCL)
      HRRU=(Y-HCU)/RCW
      HRRL=(Y-HCL)/RCW
      CALL BRECH55(PO,KL,I,J,TT,QU,Z(LZYU),ZBCH,PO(LOYMIN),
     1 PO(LOBBD),PO(LOTFH),EMBEL2,PO(LCNFLD),Z(LZTFDB),
     2 Z(LZTFDO),QW,DQWU,DQWD,SQW,YB,BO,QT,PO(LOBEXP),
     3 Z(LZIORF),EMBEL1,BRGW,PO(LOCPIP),PO(LOHFDD),K1,K2,K9,K16)

      IF(Y.LE.HCU) GO TO 135
      CF=3.00
      DCF=0.0
      IF(HRU.GT.0.76.AND.HRU.LE.0.96) COF=133.*(HRU-0.78)+10.0
      IF(HRU.GT.0.96) COF=400.*(HRU-0.96)+34.0
      IF(HRRU.GT.0.15) CF=3.06+0.04/0.15*(HRRU-0.15)
      IF(HRRU.GT.0.0.AND.HRRU.LE.0.15) CF=3.02*(Y-HCU)**0.015
      IF(HRU.GT.0.76.AND.HRU.LE.0.96) DCOF=-133.*HRU/(Y-HCU)
      IF(HRU.GT.0.96) DCOF=-400.*HRU/(Y-HCU)
      IF(HRU.GT.0.76.AND.HRU.LE.0.96) DCOFD=133./(Y-HCU)
      IF(HRU.GT.0.96) DCOFD=400./(Y-HCU)
      IF(HRRU.GT.0.15) DCF=0.04/0.15/RCW
      IF(HRRU.GT.0.0.AND.HRRU.LE.0.15) DCF=0.015*3.02*(Y-HCU)**(-.985)
      SUB=1.
      DSUB=0.
      DSUBD=0.
      IF(HRU.LE.0.76) GO TO 30
      DUMY=1.0-COF*(HRU-0.76)**3
C      IF(DUMY.GT.SQO(2,KL,J)) GO TO 30
      SUB=DUMY
      DSUB=3.*COF*(HRU-0.76)**2*HRU/(Y-HCU)-(HRU-0.76)**3*DCOF
      DSUBD=-3.*COF*(HRU-0.76)**2/(Y-HCU)-(HRU-0.76)**3*DCOFD
      SQO(1,KL,J)=SUB
   30 SQYH=SQRT(Y-HCU)
C  SUBTRACT BREACH SECTION FROM UPPER EMBANKMENT FLOW SECTION
      IF(NFAILD(KL,J).LE.0) GO TO 31
      BCHW=BO+2.0*ZBCH(KL,J)*(HCU-YB)
      ULEN=ULEN-BCHW
      IF(ULEN.LE.0.) ULEN=0.
      ALN=ALN-BCHW*(Y-HCU)
      IF(ALN.LE.0.) ALN=0.
   31 QNWU=CF*ALN*SQYH*SUB
      DQNWUU=QNWU/SUB*DSUB+QNWU/CF*DCF+0.5*QNWU/(Y-HCU)+
     & QNWU*ULEN/ALN
      DQNWUD=QNWU/SUB*DSUBD
  135 IF(Y.LE.HCL) GO TO 37
      CF=3.
      DCF=0.
      IF(HRL.GT.0.76.AND.HRL.LE.0.96) COF=133.*(HRL-0.78)+10.0
      IF(HRL.GT.0.96) COF=400.*(HRL-0.96)+34.0
      IF(HRRL.GT.0.15) CF=3.06+0.04/0.15*(HRRL-0.15)
      IF(HRRL.GT.0.0.AND.HRRL.LE.0.15) CF=3.02*(Y-HCL)**0.015
      IF(HRL.GT.0.76.AND.HRL.LE.0.96) DCOF=-133.*HRL/(Y-HCL)
      IF(HRL.GT.0.96) DCOF=-400.*HRL/(Y-HCL)
      IF(HRL.GT.0.76.AND.HRL.LE.0.96) DCOFD=133./(Y-HCL)
      IF(HRL.GT.0.96) DCOFD=400./(Y-HCL)
      IF(HRL.GT.0.15) DCF=0.04/0.15/RCW
      IF(HRRL.GT.0.0.AND.HRRL.LE.0.15) DCF=0.015*3.02*(Y-HCL)**(-.985)
      SUB=1.
      SUB=SQS1(2,KL,J)
      DSUB=0.
      DSUBD=0.
      DUMY=1.0-COF*(HRL-0.76)**3
C      IF(DUMY.GT.SQS1(2,KL,J)) GO TO 32
      SUB=DUMY
      DSUB=3.*COF*(HRL-0.76)**2*HRL/(Y-HCL)-(HRL-0.76)**3*DCOF
      DSUBD=-3.*COF*(HRL-0.76)**2/(Y-HCL)-(HRL-0.76)**3*DCOFD
      SQS1(1,KL,J)=SUB
   32 QNWL=CF* EMBW1(KL,J)*(Y-HCL)**1.5*SUB
      DQNWLU=QNWL/SUB*DSUB+QNWL/CF*DCF+1.5*QNWL/(Y-HCL)
      DQNWLD=QNWL/SUB*DSUBD
   37 CONTINUE
cc      WRITE(88, 8888) KL,TT,Y,YY,(Y-YY),(VH/A/A),HF,BR1,QN,QNWU,QNWL
cc 8888 FORMAT(2X,3HKL=,I3,2X,3HTT=,F10.4,2X,2HY=F8.3,2X,3HYY=,F7.3,2X,
cc     . 3HDH=,F7.4,2X,3HVH=,F7.4,2X,3HHF=,F7.4,2X,4HSUM=,F7.4,2X,
cc     . 3HQN=,F10.0,2X,'QNWU=',F10.2,2X,'QNWL=',F10.2)
      QTL=(QN+QNWU+QNWL+QW)*SGN
      PQL=(DQNYU+DQNWUU+DQNWLU+DQWU)*SGN
      PQR=(DQNYD+DQNWUD+DQNWLD+DQWD)*SGN
      C(II)=-(QU(I,J)-QTL)
      D(L1,II)=-PQL
      D(L2,II)=1.-DQNQU
      D(L3,II)=-PQR
      D(L4,II)=-DQNQD
      C(II2)=-(QU(I,J)-QU(IR,J))
      D(L1,II2)=0.
      D(L2,II2)=1.
      D(L3,II2)=0.
      D(L4,II2)=-1.
      BBP(KL,J)=BO
      YBP(KL,J)=YB
      QBRCH(KL,J)=SGN*QW
      QOVTP(KL,J)=SGN*(QNWU+QNWL)
      QOTHR(KL,J)=SGN*QN
      IF(JNK.LT.100) GO TO 60
      WRITE(IPR,888)
     &I,II,C(II),D(L1,II),D(L2,II),D(L3,II),D(L4,II)
      WRITE(IPR,888)
     &I,II2,C(II2),D(L1,II2),D(L2,II2),D(L3,II2),D(L4,II2)
  888 FORMAT(5X,'BRIDGE: I,II,C,D= ',2I5,5F15.5)
   60 RETURN
      END
