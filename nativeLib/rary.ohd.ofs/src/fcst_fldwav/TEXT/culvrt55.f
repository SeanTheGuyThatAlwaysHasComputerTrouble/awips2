      SUBROUTINE CULVRT55(I,KL,J,KRR,QU,RHI,QHT,HGTD,HSPD,CGD,Y,YY,QN,
     1  DQNYU,DQNYD,K1,K2,K16)

      COMMON/M3055/EPSY,EPSQ,EPSQJ,THETA,XFACT
      INCLUDE 'common/fdbug'

      DIMENSION QU(K2,K1),RHI(112,K16,K1),QHT(8,8,K16,K1),HGTD(K16,K1)
      DIMENSION HSPD(K16,K1),CGD(K16,K1)

      CHARACTER*8 SNAME
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_fldwav/RCS/culvrt55.f,v $
     . $',                                                             '
     .$Id: culvrt55.f,v 1.2 2004/02/02 21:53:04 jgofus Exp $
     . $' /
C    ===================================================================
C

      DATA SNAME/'CULVRT55'/
C
      CALL FPRBUG(SNAME,1,55,IBUG)

C        THIS SUBROUTINE COMPUTES FLOW THRU CULVERTS WITH/WITHOUT
C    * TAILWATER EFFECTS.  THIS SUBROUTINE ALSO COMPUTES FLOW
C    * THRU TIDE GATES WITH/WITHOUT TAILWATER EFFECTS.  FLOW THRU
C    * CULVERTS MAY REVERSE DIRECTION IF TAILWATER IS HIGHER THAN
C    * HEADWATER.  FLOW THRU TIDE GATE WILL STOP IF TAILWATER IS
C    * EQUAL OR GREATER THAN THE HEADWATER.
C        Q IS A FUNCTION OF HEADWATER FOR 8 DIFFERENT TAILWATER VALUES.
C    * TABLE OF DISCHARGES(Q) USED. HGTD IS INVERT ELEVATION OF CULVERT
C    * ENTRY. HSPD IS CONSTANT DIFFERENCE BETWEEN TAILWATER VALUES.
C    * CGD IS THE FALL IN THE INVERT OF THE CULVERT FROM THE HEADWATER
C    * SIDE TO THE TAILWATER SIDE.
      QN=0.
      DQNYU=0.
      DQNYD=0.
      ADY=ABS(Y-YY)
      KRA=IABS(KRR)
      IF(KRA.EQ.28.AND.YY.GE.Y) QU(I,J)=0.
      IF(KRA.EQ.28.AND.YY.GE.Y) GO TO 50
      IF(ADY.LE.EPSY) QU(I,J)=0.
      IF(ADY.LE.EPSY) GO TO 50
      FALL=0.
      MQ=1
      IF(YY.LT.Y) GO TO 5
      MQ=-1
      YS=Y
      Y=YY
      YY=YS
      FALL=CGD(KL,J)
    5 HIV=HGTD(KL,J)-FALL
C     FIND NUMBER OF UPPER AND LOWER YD CURVE
      DYD=HSPD(KL,J)
      DO 10 K=2,8
      YD=HIV+(K-1)*DYD
      IF(YY.LT.YD) GO TO 12
   10 CONTINUE
   12 KYDU=K
      KYDL=K-1
      YDL=HIV+(K-2)*DYD
C     FIND UPPER AND LOWER VALUES ON LOWER YD CURVE
      DYDL=RHI(KYDL,KL,J)
      DO 15 K=2,8
      YL=HIV+(K-1)*DYDL
      IF(Y.LT.YL) GO TO 17
   15 CONTINUE
   17 KYULU=K
      KYULL=K-1
      YULL=HIV+(K-2)*DYDL
C     FIND FLOW FROM LOWER YD CURVE
      DQL=(QHT(KYULU,KYDL,KL,J)-QHT(KYULL,KYDL,KL,J))/DYDL
      QL=QHT(KYULL,KYDL,KL,J)+DQL*(Y-YULL)
C     FIND UPPER AND LOWER VALUES ON UPPER YD CURVE
      DYDU=RHI(KYDU,KL,J)
      DO 20 K=2,8
      YL=HIV+(K-1)*DYDU
      IF(Y.LT.YL) GO TO 22
   20 CONTINUE
   22 KYUUU=K
      KYUUL=K-1
      YUUL=HIV+(K-2)*DYDU
C     FIND FLOW FROM UPPER YD CURVE
      DQU=(QHT(KYUUU,KYDU,KL,J)-QHT(KYUUL,KYDU,KL,J))/DYDU
      QUU=QHT(KYUUL,KYDU,KL,J)+DQU*(Y-YUUL)
      QN=QL+(QUU-QL)/DYD*(YY-YDL)
      DQNYU=DQL+(DQU-DQL)/DYD*(YY-YDL)
      DQNYD=(QUU-QL)/DYD
      QN=MQ*QN
      DQNYU=MQ*DQNYU
      DQNYD=MQ*DQNYD
   50 CONTINUE
      RETURN
      END
