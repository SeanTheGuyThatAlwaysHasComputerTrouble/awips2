C MEMBER COX19
C  (from old member FCCOX19)
C
      SUBROUTINE COX19(PSOLD,CSOLD,PSNEW,CSNEW)
C.......................................
C     THIS IS THE CARRYOVER TRANSFER SUBROUTINE FOR THE 'SNOW-17 '
C        OPERATION.
C.......................................
C     SUBROUTINE INITIALLY WRITTEN BY...
C        ERIC ANDERSON - HRL   MAY 1980

CVK      MODIFIED 4/00 BY V. KOREN: TWO NEW STATES ADDED
C
CEA      MODIFIED 11/05 BY E. ANDERSON TO ADD TAPREV AND TO CORRECT
CEA        ERRORS FROM VERSIONS 2 AND 3 OF THE OPERATION
C.......................................
      REAL LIQW
      DIMENSION PSOLD(*),CSOLD(*),PSNEW(*),CSNEW(*)
      DIMENSION SNAME(2),ADC(11)
C
C     COMMON BLOCKS
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_cox/RCS/cox19.f,v $
     . $',                                                             '
     .$Id: cox19.f,v 1.5 2006/10/06 11:56:46 hsu Exp $
     . $' /
C    ===================================================================
C
C     DATA STATEMENT
      DATA SNAME/4HCOX1,4H9   /
C.......................................
C     TRACE LEVEL=1, DEBUG SWITCH=IBUG
      CALL FPRBUG(SNAME,1,19,IBUG)
C.......................................
C     CONTROL VARIABLES
      IVERO=PSOLD(1)
      ITPXO=PSOLD(10)
      ITPXN=PSNEW(10)
      NLOLD=5/ITPXO+2
      NLNEW=5/ITPXN+2
      LPM=PSNEW(25)
C.......................................
C     CHECK FOR DEBUG OUTPUT
      IF(IBUG.EQ.0) GO TO 100
C     PRINT OLD AND NEW PS AND CS ARRAYS.
      NPSO=PSOLD(16)
      NPSN=PSNEW(16)
CEA  DETERMINE NUMBER OF CARRYOVER VALUES IN OLD CS ARRAY (I.E. VERSION
CEA    STORED IN OPERATIONAL PROGRAM FILES)
      NCSO=10+NLOLD
      IF (IVERO.GT.1) NCSO=12+NLOLD
      IF (IVERO.GT.3) NCSO=13+NLOLD
CEA  NEW CS ARRAY ALWAYS HAS THE NUMBER OF VALUES FOR THE LATEST VERSION
      NCSN=13+NLNEW
      WRITE(IODBUG,900)
  900 FORMAT(1H0,48HSNOW-17 COX DEBUG--OLD AND NEW PS AND CS ARRAYS.)
      WRITE (IODBUG,901) (PSOLD(I),I=1,NPSO)
cav  901 FORMAT(1H0,15F8.3)
  901 FORMAT(1H0,16F8.3)
      WRITE(IODBUG,901) (CSOLD(I),I=1,NCSO)
      WRITE(IODBUG,901) (PSNEW(I),I=1,NPSN)
      WRITE(IODBUG,901) (CSNEW(I),I=1,NCSN)
C.......................................
C     BEGIN CARRYOVER TRANSFER.
  100 WE=CSOLD(1)
      CSNEW(1)=WE
      CSNEW(2)=CSOLD(2)
      LIQW=CSOLD(3)
      PLWHC=PSNEW(LPM+11)
      WLOS=0.0
      IF(LIQW.LE.PLWHC*WE) GO TO 101
      WLOS=LIQW-PLWHC*WE
      LIQW=PLWHC*WE
  101 CSNEW(3)=LIQW
      CSNEW(4)=CSOLD(4)
      ACCMAX=CSOLD(5)
      CSNEW(5)=ACCMAX
      SB=CSOLD(6)
      SB=SB-WLOS
      CSNEW(6)=SB
      AEADJ=CSOLD(10)
      SBAESC=CSOLD(7)
      SI=PSNEW(LPM+6)
      TWE=WE+LIQW
      AI=ACCMAX
      IF(AI.GT.SI) AI=SI
      IF (AEADJ.GT.0.0) AI=AEADJ
      IF(TWE.GE.AI) GO TO 105
      IF(SB.GT.TWE)SB=TWE
      LADC=PSNEW(26)
      ADC(1)=0.05
      DO 102 I=2,10
      J=LADC+I-2
  102 ADC(I)=PSNEW(J)
      ADC(11)=1.0
      R=(SB/AI)*10.0+1.0
      N=R
      FN=N
      R=R-FN
      SBAESC=ADC(N)+(ADC(N+1)-ADC(N))*R
      IF(SBAESC.GT.1.0) SBAESC=1.0
      IF(SBAESC.LT.0.05) SBAESC=0.05
  105 CSNEW(7)=SBAESC
      SBWS=CSOLD(8)
      SBWS=SBWS-WLOS
      CSNEW(8)=SBWS
      CSNEW(9)=CSOLD(9)
      CSNEW(10)=AEADJ
C*******************************************************************
CEA  FOLLOWING CODE WAS REPLACED - PROBLEMS WERE:
CEA    1. SNOW DEPTH AND TEMP VALUES WERE ONLY TRANSFERRED
CEA         WHEN PRECIPITATION TIME INTERVAL WAS CHANGED -
CEA         SHOULD HAVE BEEN ALWAYS TRANSFERRED.
CEA    2. VALUES NOT ASSIGNED TO NEW STATES IF NOT IN EXISTING FILES
CEA    3. OBSERVED SNOW DEPTH NEVER WRITTEN TO CARRYOVER
CEA         FILES, THUS CANNOT BE TRANSFERRED - NOT THE WAY
CEA         TO HANDLE TIME SERIES ANYWAYS.
C
CVK  TWO NEW STATES ADDED
CEA   IF(NLOLD.NE.NLNEW)THEN
CEA      CSNEW(11+NLNEW)=CSOLD(11+NLOLD)
CEA      CSNEW(12+NLNEW)=CSOLD(12+NLOLD)
cav  observed snow depth
CEA      CSNEW(13+NLNEW)=CSOLD(13+NLOLD)
CEA   ENDIF
C*******************************************************************
CEA  NEW CODE TO PERFORM CARRYOVER TRANSFER
      IF (IVERO.GT.1) GO TO 108
CEA  OLD CARRYOVER FROM VERSION 1 -- ESIMATE SNOW DEPTH AND SNOW
CEA   TEMPERATURE CARRYOVER BASED ON EXISTING VALUES
CEA   USE LOGIC IN CKCO19 SUBROUTINE
      CSNEW(11+NLNEW)=(0.1*CSNEW(1))/0.2
      CSNEW(12+NLNEW)=CSNEW(4)
      CSNEW(13+NLNEW)=-99.
      GO TO 109
CEA  ONLY IF THE VERSION NUMBER OF THE OLD CARRYOVER READ FROM
CEA    THE OFS FILES IS GREATER THAN 1 DOES SNOW DEPTH AND SNOW
CEA    TEMPERATURE CARRYOVER VALUES EXIST - IF THEY DO, THEY SHOULD
CEA    BE USED IN PLACE OF THE VALUES READ BY THE PIN19 ROUTINE
  108 CSNEW(11+NLNEW)=CSOLD(11+NLOLD)
      CSNEW(12+NLNEW)=CSOLD(12+NLOLD)
      IF (IVERO.GT.3) GO TO 107
      CSNEW(13+NLNEW)=-99.
      GO TO 109
CEA  IF VERSION NUMBER OF OLD CARRYOVER IS GREATER THAN 3, THEN
CEA    TAPREV VALUE IS STORED IN THE OFS FILES AND SHOULD BE
CEA    TRANSFERRED.
  107 CSNEW(13+NLNEW)=CSOLD(13+NLOLD)
C*******************************************************************
C     LAGGED EXCESS WATER.
  109 IF(ITPXN.NE.ITPXO) GO TO 110
      DO 111 N=1,NLNEW
  111 CSNEW(10+N)=CSOLD(10+N)
      GO TO 190
  110 NHR=NLOLD*ITPXO
      FN=ITPXO
      FHR=1.0/FN
      DO 113 N=1,NLNEW
  113 CSNEW(10+N)=0.0
      DO 112 N=1,NHR
      NO=(N-1)/ITPXO+1
      NN=(N-1)/ITPXN+1
      IF(NN.GT.NLNEW) GO TO 190
      CSNEW(10+NN)=CSNEW(10+NN)+FHR*CSOLD(10+NO)
  112 CONTINUE
C     END OF CARRYOVER TRANSFER
C.......................................
C     CHECK FOR DEBUG OUTPUT
  190 IF(IBUG.EQ.0) GO TO 199
      WRITE(IODBUG,901) (CSNEW(I),I=1,NCSN)
C.......................................
  199 CONTINUE
      RETURN
      END
