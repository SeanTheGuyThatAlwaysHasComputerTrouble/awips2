C MEMBER COX41
C  (from old member FCCOX41)
C
      SUBROUTINE COX41(POLD,COLD,PONEW,CONEW)
C
C#######################################################################
C
C  THIS SUBROUTINE TRANSFERS OLD CARRYOVER VALUES FROM THE COLD ARRAY
C  INTO THE NEW CARRYOVER ARRAY CONEW WHEN A SEGMENT IS REDEFINED.
C  BEFORE THIS TRANSFER TAKES PLACE, HOWEVER, A CHECK IS MADE TO SEE
C  IF EITHER THE COMPUTATIONAL TIME STEP INTERVAL AND/OR THE NEW STORM
C  WINDOW HAS CHANGED.  IF SO, ADJUSTMENTS ARE MADE TO THE RNSP ARRAY.
C
C#######################################################################
C
C  FOR DEFINITION OF VARIABLES STORED IN THE CO AND PO ARRAYS,
C  SEE THE PIN41 ROUTINE.
C
C#######################################################################
C
      DIMENSION POLD(1),COLD(1),PONEW(1),CONEW(1),SUBNAM(2)
      DIMENSION RNSP(24)
      COMMON /FDBUG/ IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON /IONUM/ IN,IPR,IPU
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_cox/RCS/cox41.f,v $
     . $',                                                             '
     .$Id: cox41.f,v 1.1 1995/09/17 18:47:24 dws Exp $
     . $' /
C    ===================================================================
C
      DATA SUBNAM/4hCOX4,4h1   /,NOP/41/
C
C  CALL DEBUG CHECK ROUTINE.
C
      CALL FPRBUG(SUBNAM,1,NOP,IFDEB)
C
C  EXTRACT NEEDED VARIABLES FROM OLD AND NEW PARAMETER ARRAYS.
C
      IDELTO=POLD(26)
      IVERS=PONEW(1)
      NSWO=POLD(27)
      NSPERO=POLD(28)
      IDELTN=PONEW(26)
      NSWN=PONEW(27)
      NSPERN=PONEW(28)
      IF(IFDEB)50,50,4050
C
C  READ IN VARIABLES FROM OLD CARRYOVER FILE.
C
50    TAPI=COLD(1)
      TAEI=COLD(2)
      TAI=COLD(3)
      SAPI=COLD(4)
      SAEI=COLD(5)
      SAI=COLD(6)
      SRAIM=COLD(7)
      SRO=COLD(8)
      DRAIM=COLD(9)
      DRO=COLD(10)
      DO 100 I=1,NSPERO
100   RNSP(I)=COLD(10+I)
C
C  CHECK TO SEE IF THE COMPUTATIONAL TIME STEP INTERVAL AND THE
C  NEW STORM WINDOW HAVE BEEN CHANGED.  IF NOT, STORE THE
C  OLD CARRYOVER VALUES IN THE NEW CARRYOVER ARRAY.  OTHERWISE,
C  MAKE ADJUSTMENTS TO THE RNSP ARRAY.
C
      IDELTD=IDELTN-IDELTO
      IDELW=NSWN-NSWO
      IF((IDELTD.EQ.0).AND.(IDELW.EQ.0))GO TO 900
C
C  IF THE COMPUTATIONAL TIME STEP INTERVAL HAS CHANGED, SET ALL
C  VALUES IN THE RNSP ARRAY TO ZERO AND PRINT A WARNING MESSAGE
C  TO THIS EFFECT.
C
300   IF(IDELTD)500,310,500
C
C  IF THE NSW HAS INCREASED, FILL THE NEEDED EXTRA SPACES IN
C  THE RNSP ARRAY TO ZERO.
C
310   IF(IDELW)410,900,320
320   I1=NSPERO+1
      DO 330 I=I1,NSPERN
330   RNSP(I)=0.0
      GO TO 5330
C
C  IF THE NSW HAS DECREASED, DOWNSHIFT PRECIPITATION IN THE RNSP
C  ARRAY BY THE APPROPRIATE NUMBER OF POSITIONS.  TO AVOID POTENTIAL
C  ERROR OR CONFUSION, SET ALL UNNECESSARY POSITIONS IN THE RNSP ARRAY
C  TO ZERO.
C
410   ISHIFT=NSPERO-NSPERN
      DO 420 I=1,NSPERN
        RNSP(I)=RNSP(I+ISHIFT)
420   CONTINUE
      IBEG=NSPERN+1
      DO 430 I=IBEG,24
        RNSP(I)=0.0
430   CONTINUE
      GO TO 5430
C
C  IF THERE ARE ANY CHANGES IN THE COMPUTATIONAL TIME STEP INTERVAL,
C  REGARDLESS OF ANY CHANGES WHICH MAY HAVE OCCURRED IN THE
C  NEW STORM WINDOW, SET ALL VALUES IN THE RNSP ARRAY TO ZERO.
C  THEN PRINT OUT A WARNING MESSAGE TO THIS EFFECT.
C
500   DO 530 I=1,NSPERN
530   RNSP(I)=0.0
      GO TO 5530
C
C  NOW PLACE VARIABLES INTO NEW CARRYOVER ARRAY.
C
900   CONEW(1)=TAPI
      CONEW(2)=TAEI
      CONEW(3)=TAI
      CONEW(4)=SAPI
      CONEW(5)=SAEI
      CONEW(6)=SAI
      CONEW(7)=SRAIM
      CONEW(8)=SRO
      CONEW(9)=DRAIM
      CONEW(10)=DRO
      DO 930 I=1,NSPERN
930   CONEW(10+I)=RNSP(I)
      IF(IFDEB)1000,1000,4200
1000  RETURN
C
C#######################################################################
C
C  DEBUG WRITE STATEMENTS.
C
C#######################################################################
C
4050  WRITE(IODBUG,8050)SUBNAM,IVERS
      WRITE(IODBUG,8060)
      WRITE(IODBUG,8151)(POLD(I),I=2,11)
      WRITE(IODBUG,8152)(POLD(I),I=12,17)
      WRITE(IODBUG,8153)(POLD(I),I=18,22)
      WRITE(IODBUG,8154)(POLD(I),I=23,30)
      WRITE(IODBUG,8160)
      WRITE(IODBUG,8151)(PONEW(I),I=2,11)
      WRITE(IODBUG,8152)(PONEW(I),I=12,17)
      WRITE(IODBUG,8153)(PONEW(I),I=18,22)
      WRITE(IODBUG,8154)(PONEW(I),I=23,30)
      WRITE(IODBUG,8170)
      WRITE(IODBUG,8180)(COLD(I),I=1,10)
      WRITE(IODBUG,8190)(COLD(10+I),I=1,NSPERO)
      GO TO 50
4200  WRITE(IODBUG,8200)
      WRITE(IODBUG,8180)(CONEW(I),I=1,10)
      WRITE(IODBUG,8190)(CONEW(10+I),I=1,NSPERN)
      GO TO 1000
C
C#######################################################################
C
C  WARNING WRITE STATEMENTS.
C
C#######################################################################
C
5330  WRITE(IPR,9330)
      CALL WARN
      GO TO 900
5430  WRITE(IPR,9430)
      CALL WARN
      GO TO 900
5530  WRITE(IPR,9530)
      CALL WARN
      GO TO 900
C
C#######################################################################
C
C  DEBUG FORMAT STATEMENTS.
C
C#######################################################################
C
8050  FORMAT(/1X,2A4,' DEBUG OUTPUT.',5X,'VERSION : ',I4)
8060  FORMAT(/2X,'CONTENTS OF THE POLD ARRAY :')
8151  FORMAT(/8X,'RID',14X,'RNAME',10X,'IRNUM',5X,'RLAT',5X,'RLNG',
     1      /5X,2A4,4X,5A4,4X,F4.0,4X,F5.2,4X,F5.2)
8152  FORMAT(/4X,'AEIWET',5X,'BWET',5X,'CWET',3X,'AEIDRY',5X,'BDRY',
     1      5X,'CDRY',/5X,F5.2,5(4X,F5.2))
8153  FORMAT(/7X,'PA',7X,'PB',7X,'PC',7X,'PD',7X,'PE',
     1      /5X,F5.2,4(4X,F5.2))
8154  FORMAT(/4X,'FIXPEV',5X,'PMAX',6X,'R24',4X,'IDELTA',4X,'NSW',
     1      3X,'NSPER',3X,'IUSEC',2X,'IOFAAA',/5X,F5.2,4X,F5.2,
     2      4X,F5.2,5(4X,F4.0))
8160  FORMAT(/2X,'CONTENTS OF THE PONEW ARRAY')
8170  FORMAT(/2X,'CONTENTS OF THE COLD ARRAY')
8180  FORMAT(/6X,'TAPI',5X,'TAEI',5X,'TAI',6X,'SAPI',5X,'SAEI',
     1      5X,'SAI',5X,'SRAIM',5X,'SRO',5X,'DRAIM',5X,'DRO',/5X,
     2      F5.2,9(4X,F5.2))
8190  FORMAT(/6X,'RAIN/MELT FOR EACH PERIOD WITHIN THE NEW STORM ',
     1      'WINDOW (OLDEST PERIOD IS FIRST) :',/8X,12(F5.2,3X),
     2      /8X,12(F5.2,3X))
8200  FORMAT(/2X,'CONTENTS OF THE CONEW ARRAY')
C
C#######################################################################
C
C  WARNING FORMAT STATEMENTS.
C
C#######################################################################
C
9330  FORMAT(/10X,'** WARNING ** THE NUMBER OF HOURS IN THE NEW STORM ',
     1      'WINDOW',/24X,'HAS BEEN INCREASED DURING SEGMENT ',
     2      'REDEFINITION, BUT THE COMPUTATIONAL TIME STEP INTERVAL',
     3      /24X,'HAS REMAINED CONSTANT.  THE NUMBER OF RAIN/MELT ',
     4      'VALUES WITHIN THE NEW STORM WINDOW',/24X,
     5      'OF THE NEW CARRYOVER ARRAY HAS BEEN EXPANDED WITH ZEROS ',
     6      'TO ALLOW FOR THIS INCREASED WINDOW SIZE.',/24X,
     7      'THE API OPERATION SHOULD NOW BE EXECUTED STARTING WITH ',
     8      'THE OLDEST AVAILABLE DAY OF CARRYOVER',/24X,'TO AVOID ',
     9      'INCONSISTENCIES IN RUNOFF CALCULATIONS.')
9430  FORMAT(/10X,'** WARNING ** THE NUMBER OF HOURS IN THE NEW STORM ',
     1      'WINDOW',/24X,'HAS BEEN DECREASED DURING SEGMENT ',
     2      'REDEFINITION, BUT THE COMPUTATIONAL TIME STEP INTERVAL ',
     3      /24X,'HAS REMAINED CONSTANT.  THE NUMBER OF RAIN/MELT ',
     4      'VALUES WITHIN THE NEW STORM WINDOW OF THE',/24X,'NEW ',
     5      'CARRYOVER ARRAY HAS BEEN TRUNCATED TO COMPENSATE FOR ',
     6      'THIS DECREASED WINDOW SIZE.',/24X,'NO ERRORS SHOULD ',
     7      'OCCUR IN RUNOFF COMPUTATIONS AS A RESULT OF THIS ACTION.')
9530  FORMAT(/10X,'** WARNING ** THE COMPUTATIONAL TIME STEP INTERVAL ',
     1      'HAS BEEN CHANGED',/24X,'DURING SEGMENT REDEFINITION.  ',
     2      'RAIN/MELT VALUES WITHIN THE NEW STORM WINDOW STORED IN ',
     3      /24X,'THE NEW CARRYOVER ARRAY HAVE BEEN SET TO ZERO TO ',
     4      'COMPENSATE FOR THESE CHANGES.',/24X,'THE API OPERATION ',
     5      'MUST NOW BE EXECUTED STARTING WITH THE OLDEST DAY OF ',
     6      'AVAILABLE',/24X,'CARRYOVER TO AVOID ERRORS IN RUNOFF ',
     7      'CALCULATIONS.')
      END
