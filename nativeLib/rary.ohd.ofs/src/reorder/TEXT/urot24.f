C MODULE UROT24
C-----------------------------------------------------------------------

C
      SUBROUTINE UROT24 (LARRAY,ARRAY,LPNTRS,IPNTRS,LAOT24,IAOT24,
     *   ISTAT)
C
C    THIS ROUTINE REORDERS THE TEMP PARAMETRIC RECORDS BASED
C    ON THE ALPHABETICALLY ORDERED LIST OF TEMPERATURE STATIONS STORED
C    IN THE PREPROCESSOR PARAMETRIC DATA BASE PARAMETER TYPES OT24.
C
C    IF THE OT24 RECORD DOES NOT EXIST THE RECORDS WILL BE COPIED IN
C    THE PRESENT ORDER TO THE NEW FILES.
C
C    ARGUMENT LIST:
C
C       NAME     TYPE   I/O  DIM     DESCRIPTION
C       ------   ----   ---  ------  -----------
C       LARRAY    I*4    I     1     LENGTH OF ARRAY
C       ARRAY     I*4   I/O  LARRAY  TEMP PARAMETER ARRAY
C       LPNTRS    I*4    I     1     LENGTH OF ARRAY IPNTRS
C       IPNTRS    I*2   I/O  LPNTRS  TAVR/TM24 POINTERS
C       LAOT24    I*4    I     1     LENGTH OF ARRAY IAOT24
C       IAOT24    I*2   I/O  LAOT24  OT24 PARAMETER ARRAY
C       ISTAT     I*4    I     1     STATUS CODE:
C                                      0=NORMAL RETURN
C                                      OTHER=ERROR
C
      CHARACTER*4 XDISP,PTYPE
      CHARACTER*6 CALLEE
      CHARACTER*8 PARMID,STAID
      INTEGER*2 IPNTRS(LPNTRS)
      INTEGER*2 IAOT24(LAOT24)
      INTEGER*2 MSNG
C
      DIMENSION ARRAY(LARRAY)
      DIMENSION IDATES(1)
C
      INCLUDE 'uiox'
      INCLUDE 'udebug'
      INCLUDE 'udatas'
      INCLUDE 'ucommon/uordrx'
      INCLUDE 'urcommon/urcdta'
      INCLUDE 'urcommon/urppdt'
      INCLUDE 'pppcommon/ppdtdr'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/reorder/RCS/urot24.f,v $
     . $',                                                             '
     .$Id: urot24.f,v 1.2 2001/06/13 14:09:35 dws Exp $
     . $' /
C    ===================================================================
C
C
      IF (IPPTR.GT.0) THEN
         CALL SULINE (LP,1)
         WRITE (IOGDB,*) 'ENTER UROT24'
         ENDIF
C
      ISTAT=0
C
      IPTR=0
      IFLAG=0
C
      CALL SULINE (LP,2)
      WRITE (LP,330)
C
      PTYPE='TEMP'
C
C  FIND DATA TYPE IN OLD DIRECTORY
      XDISP='OLD'
      IAMORD=0
      IDXOLD=IPCKDT(PTYPE)
      IF (IDXOLD.GT.0) GO TO 10
         CALL SUERRS (LP,2,-1)
         WRITE (LP,340) PTYPE,XDISP
         GO TO 310
C
C  CHECK NUMBER OF RECORDS DEFINED
10    IF (IPDTDR(5,IDXOLD).GT.0) GO TO 20
         CALL SULINE (LP,2)
         WRITE (LP,350)
         GO TO 310
C
C  FIND DATA TYPE IN NEW DIRECTORY
20    XDISP='NEW'
      IAMORD=1
      IDXNEW=IPCKDT(PTYPE)
      IF (IDXNEW.GT.0) GO TO 30
         CALL SUERRS (LP,2,-1)
         WRITE (LP,340) PTYPE,XDISP
         GO TO 310
C
C  GET TAVR POINTERS
30    PTYPE='TAVR'
      LDATA=1
      LENDAT=1
      CALL RPDFIL (PTYPE,IRDATE,LPFIL,LDFIL,ISTAT)
      IF (ISTAT.GT.0) GO TO 40
         IF (LPFIL.LE.LPNTRS) GO TO 40
            WRITE (LP,380) PTYPE,LPNTRS,LPFIL
            CALL SUERRS (LP,2,-1)
            IERR=1
            GO TO 270
40    CALLEE='RPDDLY'
      IRETRN=2
      CALL RPDDLY (PTYPE,IRDATE,IRETRN,LPNTRS,IPNTRS,LPF,LDATA,IDATA,
     *   LDFILL,NUMSTA,MSNG,LENDAT,IDATES,ISTAT)
      IF (ISTAT.EQ.0) GO TO 50
      GO TO (190,50,190,50,60,60,50,250),ISTAT
C
C  WRITE POINTERS
50    CALL URWPTR (PTYPE,IPNTRS,ISTAT)
      IF (ISTAT.NE.0) GO TO 270
C
C  GET PARAMETER RECORDS FOR TYPE OT24
60    PARMID=' '
      PTYPE='OT24'
      IAMORD=0
      CALL RPPREC (PARMID,PTYPE,IPTR,LAOT24,IAOT24,NFILL,IPTRNX,ISTAT)
      IF (ISTAT.EQ.0) GO TO 110
         IF (ISTAT.EQ.2.OR.ISTAT.EQ.4) GO TO 70
         CALL SRPPST (PARMID,PTYPE,IPTR,LAOT24,NFILL,IPTRNX,ISTAT)
         CALL SUERRS (LP,2,-1)
         GO TO 270
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  OT24 RECORDS NOT FOUND - COPY IN CURRENT ORDER
70    CALL SULINE (LP,2)
      WRITE (LP,360)
      ISTAT=0
C
C  COPY RECORDS TO THE NEW FILES
80    PTYPE='TEMP'
      CALL URCPYR (PTYPE,LARRAY,ARRAY,
     *   1,DMARR1,1,DMARR2,1,DMARR3,1,DMARR4,ISTAT)
      IF (IWURFL.NE.0.OR.ISTAT.NE.0) GO TO 270
C
C  READ DAILY DATA RECORD FOR TM24 TO GET POINTER ARRAY FOR 24 HR DATA
      PTYPE='TM24'
      CALL RPDFIL (PTYPE,IRDATE,LPFIL,LDFIL,ISTAT)
      IF (ISTAT.GT.0) GO TO 90
         IF (LPFIL.LE.LPNTRS) GO TO 90
            WRITE (LP,380) PTYPE,LPNTRS,LPFIL
            CALL SUERRS (LP,2,-1)
            IERR=1
            GO TO 270
90    CALLEE='RPDDLY'
      IRETRN=2
      CALL RPDDLY (PTYPE,IRDATE,IRETRN,LPNTRS,IPNTRS,LPF,LDATA,IDATA,
     *   LFDILL,NUMSTA,MSNG,LENDAT,IDATES,ISTAT)
      IF (ISTAT.EQ.0) GO TO 100
      GO TO (190,100,190,100,210,230,100,250),ISTAT
C
C  WRITE POINTERS TO NEW FILES
100   CALL URWPTR ('TM24',IPNTRS,ISTAT)
      IF (ISTAT.NE.0) GO TO 270
      GO TO 300
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  READ DAILY DATA RECORD FOR TM24 TO GET POINTER ARRAY FOR 24 HR DATA
110   CALLEE='RPDDLY'
      PTYPE='TM24'
      IDATE=0
      IRETRN=2
      CALL RPDDLY (PTYPE,IDATE,IRETRN,LPNTRS,IPNTRS,LPF,LDATA,IDATA,
     *   LDFILL,NUMSTA,MSNG,LENDAT,IDATES,ISTAT)
      IF (ISTAT.EQ.0) GO TO 120
      GO TO (190,120,190,120,210,230,120,250),ISTAT
C
C  CHECK NUMBER OF STATIONS DEFINED
120   CALL UMEMOV (IAOT24(9),REAL,1)
      NUMSTA=REAL
      IF (NUMSTA.GT.0) GO TO 130
         CALL SULINE (LP,2)
         WRITE (LP,370)
         GO TO 80
C
C  SET TEMP PARAMETER RECORD NUMBER
130   DO 140 I=1,LPFIL,5
         IPNTRS(I)=-IPNTRS(I)
140      CONTINUE
C
      IF (IPPDB.GT.0) THEN
         CALL SULINE (IOGDB,1)
         WRITE (IOGDB,*) 'NUMSTA=',NUMSTA
         ENDIF
C
C  REORDER TEMP PARAMETER RECORDS FOR STATIONS WITH 24 HOUR DATA
      IPOS=11
      PTYPE='TEMP'
      DO 170 I=1,NUMSTA
C     GET TEMP PARAMETER RECORD NUMBER FROM POINTER ARRAY USING
C     SUBSCRIPT FROM OT24 RECORD
         IT24PT=IAOT24(IPOS)
         IRECO=IPNTRS(IT24PT)
         IRECO=IABS(IRECO)
         IF (IRECO.LE.0) GO TO 170
         IFLAG=1
         IAMORD=0
         STAID=' '
         CALL RPPREC (STAID,PTYPE,IRECO,LARRAY,ARRAY,NUMFIL,IPTRNX,
     *      ISTAT)
         IF (ISTAT.EQ.0) GO TO 150
            CALL SRPPST (STAID,PTYPE,IRECO,LARRAY,NUMFIL,IPTRNX,ISTAT)
            CALL SUERRS (LP,2,-1)
            GO TO 270
C     WRITE PARAMETER RECORD TO THE NEW FILES
150      IAMORD=1
         IRECN=0
         CALL WPPREC (STAID,PTYPE,NUMFIL,ARRAY,IRECN,ISTAT)
         IF (ISTAT.NE.0) GO TO 270
C     UPDATE RECORD NUMBER IN THE GENL PARAMETER RECORD
         IF (STAID.EQ.'DDMC1') THEN
            IFOUND=1
            ENDIF
         IAMORD=0
         CALL URUDGL (STAID,PTYPE,IRECN,LARRAY,ARRAY,ISTAT)
         IF (ISTAT.NE.0) THEN
            IF (ISTAT.EQ.1) GO TO 270
            GO TO 160
            ENDIF
         IPNTRS(IT24PT)=IRECN
160      IPOS=IPOS+1
170      CONTINUE
C
C  WRITE POINTERS TO NEW FILES
      CALL URWPTR ('TM24',IPNTRS,ISTAT)
      IF (ISTAT.NE.0) GO TO 270
      GO TO 290
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
190   WRITE (LP,200) LPNTRS,PTYPE
      CALL SUERRS (LP,2,-1)
200   FORMAT ('0*** ERROR - IN UROT24 - SIZE OF POINTER ARRAY (',I6,
     *   ') TOO SMALL FOR DAILY DATA TYPE ',A,'.')
      GO TO 270
C
210   WRITE (LP,220) PTYPE
      CALL SUERRS (LP,2,-1)
220   FORMAT ('0*** ERROR - IN UROT24 - INVALID DATA TYPE ',A)
      GO TO 270
C
230   WRITE (LP,240) PTYPE
      CALL SUERRS (LP,2,-1)
240   FORMAT ('0*** NOTE - NO STATIONS DEFINED FOR DATA TYPE ',A,
     *   'IN THE PREPROCESSOR DATA BASE.')
      GO TO 310
C
250   WRITE (LP,260) CALLEE,ISTAT,PTYPE
      CALL SUERRS (LP,2,-1)
260   FORMAT ('0*** ERROR - IN UROT24 - STATUS CODE RETURNED FROM ',
     *   'ROUTINE ',A,' IS ',I2,' FOR DATA TYPE ',A,'.')
C
C  SET ERROR FLAG
270   IWURFL=1
      CALL SULINE (LP,2)
      WRITE (LP,280)
280   FORMAT ('0*** NOTE - ERRORS HAVE OCCURRED IN THE REORDER OF ',
     *   'TEMP PARAMETER RECORDS.')
      GO TO 310
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
290   CALL SULINE (LP,2)
      WRITE (LP,420) JPDTDR(5,IDXNEW)
      GO TO 310
C
300   CALL SULINE (LP,2)
      WRITE (LP,410) JPDTDR(5,IDXNEW)
C
310   IF (IPPTR.GT.0) THEN
         CALL SULINE (LP,1)
         WRITE (IOGDB,*) 'EXIT UROT24'
         ENDIF
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
330   FORMAT ('0*** NOTE - BEGIN TO REORDER  << TEMP PARAMETER >>  ',
     *   'RECORDS.')
340   FORMAT ('0*** ERROR - IN UROT24 - TYPE ',A,' NOT FOUND IN ',
     *   A,' PREPROCESSOR PARAMETRIC DATA BASE DIRECTORY.')
350   FORMAT ('0*** NOTE - NO TEMP PARAMETERS ARE DEFINED IN ',
     *   'PREPROCESSOR PARAMETRIC DATA BASE.')
360   FORMAT ('0*** NOTE - PARAMETER TYPE OT24 NOT FOUND. TEMP ',
     *   'PARAMETER RECORDS WILL BE COPIED AND NOT REORDERED.')
370   FORMAT ('0*** NOTE - NO STATIONS DEFINED IN OT24 RECORD. TEMP ',
     *   'RECORDS WILL COPIED AND NOT REORDERED.')
380   FORMAT ('0*** ERROR - IN UROT24 - ARRAY TO HOLD POINTERS FOR ',
     *   'DATA TYPE ',A,' TOO SMALL. ',I5,' AVAILABLE. ',
     *   I5,' NEEDED.')
410   FORMAT ('0*** NOTE - ',A,' TEMP PARAMETER RECORDS HAVE BEEN ',
     *   'SUCCESSFULLY COPIED.')
420   FORMAT ('0*** NOTE - ',I4,1X,'TEMP PARAMETER RECORDS HAVE BEEN ',
     *   'SUCCESSFULLY REORDERED.')
C
      END
