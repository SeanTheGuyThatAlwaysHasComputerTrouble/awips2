C MODULE URFC1
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 12/15/93.09:47:33 BY $WC21DT
C
C DESC CREATE NEW CARRYOVER GROUP DEFINITION FILE
C
      SUBROUTINE URFC1
C
C  CREATE NEW FILE FCCOGDEF
C
C  NOTE THAT ICRDA2 IS SET TO NOW, I.E. THE
C  CREATION DATE OF THE FCCOGDEF FILE IS SET TO THE TIME THE FILE IS
C  FILLED.  HOWEVER, THE TIME OF DEFINITION OF EACH CARRYOVER GROUP
C  IS LEFT AS IT WAS ON THE OLD FILE.
C
C  ALSO NOTE THAT THE NUMBER OF WORDS USED IN EACH SLOT
C  IS NOT KNOWN UNTIL LATER IN FCCOPY WHEN THE CARRYOVER IS COPIED
C  FROM THE OLD FILES TO THE NEW FILES.  THE OLD FILES MAY HAVE
C  'HOLES' FOR SEGMENTS WHICH HAVE BEEN SET TO OBSOLETE.
C
C  THE OLD CARRYOVER GROUP DEFINITION FILE WILL HAVE NCG CARRYOVER
C  GROUPS NONE OF WHICH ARE SET TO 'OBSOLETE' (WHEN CARRYOVER GROUPS
C  ARE DELETED, THEY ARE ACTUALLY DELETED).  HOWEVER, THE NCG
C  CARRYOVER GROUPS WILL NOT NECESSARILY BE IN ORDER 1-NCG ON THE
C  OLD FILE.  IN THE NEW FILE THEY ARE SET IN ORDER 1-NCG ON THE FILE.
C
C  ROUTINE ORIGINALLY WRITTEN BY -
C    ED JOHNSON -- HRL -- 12 DEC 1979
C.......................................................................
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fccgd'
      INCLUDE 'common/fccgd1'
      INCLUDE 'common/fccgd2'
      INCLUDE 'common/fcunit'
      INCLUDE 'urcommon/urunts'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fctime'
      INCLUDE 'common/where'
C
      DIMENSION SUBNAM(2)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/reorder/RCS/urfc1.f,v $
     . $',                                                             '
     .$Id: urfc1.f,v 1.1 1995/09/17 19:17:26 dws Exp $
     . $' /
C    ===================================================================
C
      DATA SUBNAM/4HCOPY,4HA   /
C
C  TRACE LEVEL = 1
C
      IF (ITRACE.GE.1) WRITE (IODBUG,900)
 900  FORMAT(' *** ENTER URFC1')
      IBUG=IFBUG(4HFC1 )
C
      IOPNUM=0
      OPNAME(1)=SUBNAM(1)
      OPNAME(2)=SUBNAM(2)
C
C  NOTE -- THE VALUES IN /FCCGD/ AND /FCCGD2/ HAVE BEEN DEFINED
C          FROM THE FILES BY SUBROUTINE COPY0
C
      DO 10 I=1,5
      ICRDA2(I)=NOW(I)
 10   CONTINUE
C
      CALL SULINE (IPR,2)
      WRITE (IPR,901)
 901  FORMAT('0*** NOTE - BEGIN TO COPY CARRYOVER GROUP DEFINITION ',
     *   'FILE.')
C
      NCG2=NCG
      IF (NCG.EQ.0)GO TO 1000
C
      DO 100 ICG=1,NCG
      CALL UREADT (KFCGD,ICOREC(ICG),CGIDC,ISTAT)
      CGIDS2(1,ICG)=CGIDC(1)
      CGIDS2(2,ICG)=CGIDC(2)
      ICORE2(ICG)=ICG+1
      IF (IBUG.GT.0) CALL SULINE (IODBUG,1)
      IF (IBUG.GT.0) WRITE (IODBUG,902)CGIDC
 902  FORMAT(' CARRYOVER GROUP ',2A4,' SUCCESSFULLY COPIED')
      CALL UWRITT (LFCGD,ICORE2(ICG),CGIDC,ISTAT)
 100  CONTINUE
C
 1000 CALL UWRITT (LFCGD,1,NSLOT2,ISTAT)
C
      CALL SULINE (IPR,2)
      WRITE (IPR,903)NCG2
 903  FORMAT('0*** NOTE - ',I3,' CARRYOVER GROUP DEFINITIONS ',
     *   'SUCCESSFULLY COPIED.')
C
      IF (ITRACE.GE.1) WRITE (IODBUG,800)
 800  FORMAT(' *** EXIT URFC1')
C
      RETURN
C
      END
