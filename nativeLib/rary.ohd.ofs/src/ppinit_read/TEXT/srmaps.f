C MEMBER SRMAPS
C-----------------------------------------------------------------------
C
C  ROUTINE TO READ AREA MAPS PARAMETER RECORD.
C
      SUBROUTINE SRMAPS (IVMAPS,XMAPID,CENTRD,RADINF,IPMAP,NTECH,
     *   IBASN,MSTWT,NSTWT,MPCPN,NPCPN,MBOX,NBOX,ITECHS,IPD6,IPD24,
     *   MDRBOX,CORMDR,UNUSED,LARRAY,ARRAY,IPTR,IPRERR,IPTRNX,ISTAT)
C
      DIMENSION ARRAY(LARRAY)
      DIMENSION XMAPID(1),CENTRD(1)
      DIMENSION ITECHS(1),IPD6(1),IPD24(1),MDRBOX(1),CORMDR(1)
      DIMENSION UNUSED(1)
C
      INCLUDE 'uio'
      INCLUDE 'scommon/sudbgx'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppinit_read/RCS/srmaps.f,v $
     . $',                                                             '
     .$Id: srmaps.f,v 1.2 1997/04/06 12:25:21 page Exp $
     . $' /
C    ===================================================================
C
C
C
      IF (ISTRCE.GT.0) THEN
         WRITE (IOSDBG,140)
         CALL SULINE (IOSDBG,1)
         ENDIF
C
C  SET DEBUG LEVEL
      LDEBUG=ISBUG('MAPS')
C
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,*) 'LARRAY=',LARRAY
         CALL SULINE (IOSDBG,1)
         ENDIF
C
      ISTAT=0
C
C  READ PARAMETER RECORD
      CALL SUDOPN (1,'PPP ',IERR)
      CALL RPPREC (XMAPID,'MAPS',IPTR,LARRAY,ARRAY,NFILL,IPTRNX,
     *   IERR)
      IF (IERR.NE.0) THEN
         ISTAT=IERR
         IF (IPRERR.EQ.1) THEN
            CALL SRPPST (XMAPID,'MAPS',IPTR,LARRAY,NFILL,IPTRNX,IERR)
            CALL SUERRS (LP,0,-1)
            WRITE (LP,220)
            CALL SULINE (LP,2)
            ENDIF
         GO TO 130
         ENDIF
C
C  SET PARAMETER ARRAY VERSION NUMBER
      IVMAPS=ARRAY(1)
C
C  SET MAPS AREA IDENTIFIER
      XMAPID(1)=ARRAY(2)
      XMAPID(2)=ARRAY(3)
C
      NPOS=3
C
C  SET CENTROID (NWSRFS/HRAP COORDINATES)
      DO 20 I=1,2
         NPOS=NPOS+1
         CENTRD(I)=ARRAY(NPOS)
20       CONTINUE
C
C  SET RADIUS OF INFLUENCE (NWSRFS/HRAP COORDINATES)
      NPOS=NPOS+1
      RADINF=ARRAY(NPOS)
C
C  SET POINTER TO MAP AREA PARAMETER RECORD
      NPOS=NPOS+1
      IPMAP=ARRAY(NPOS)
C
C  SET NUMBER OF WORDS RESERVED FOR NON-UNIVERSAL HCL TECNIQUES
      NPOS=NPOS+1
      NTECH=ARRAY(NPOS)
C
C  THE NEXT POSITION IS UNUSED
      NPOS=NPOS+1
      UNUSED(1)=ARRAY(NPOS)
C
C  SET INDICATOR WHETHER BASIN BOUNDARY IS DEFINED IN MAP
C  PARAMETER RECORD
      NPOS=NPOS+1
      IBASN=ARRAY(NPOS)
C
C  SET NUMBER OF STATIONS USED FOR TIME DISTRIBUTION
      NPOS=NPOS+1
      NSTWT=ARRAY(NPOS)
C
C  SET NUMBER OF STATIONS USED TO COMPUTE MAP
      NPOS=NPOS+1
      NPCPN=ARRAY(NPOS)
C
C  SET NUMBER OF MDR BOXES FOR THIS AREA
      NPOS=NPOS+1
      NBOX=ARRAY(NPOS)
C
C  SET VALUES OF NON-UNIVERSAL TECHNIQUES
      DO 30 I=1,NTECH
         NPOS=NPOS+1
         ITECHS(I)=ARRAY(NPOS)
30       CONTINUE
C
C  SET ARRAY LOCATION OF POINTERS FOR 6 HR STATIONS USED FOR TIMING
      IF (NSTWT.LE.MSTWT) GO TO 40
         WRITE (LP,170) NSTWT,MSTWT
         CALL SUERRS (LP,2,-1)
         ISTAT=1
         GO TO 120
40    DO 50 I=1,NSTWT
         NPOS=NPOS+1
         IPD6(I)=ARRAY(NPOS)
50       CONTINUE
C
C  SET ARRAY LOCATION OF POINTERS FOR 24 HR STATIONS USED TO COMPUTE MAP
      IF (NPCPN.LE.MPCPN) GO TO 60
         WRITE (LP,190) NPCPN,MPCPN
         CALL SUERRS (LP,2,-1)
         ISTAT=1
         GO TO 120
60    DO 70 I=1,NPCPN
         NPOS=NPOS+1
         IPD24(I)=ARRAY(NPOS)
70       CONTINUE
C
      IF (IVMAPS.GT.1.AND.NBOX.EQ.0) GO TO 95
C
C  SET MDR BOXES ASSIGNED TO THIS AREA
      IF (NBOX.LE.MBOX) GO TO 80
         WRITE (LP,200) NBOX,MBOX
         CALL SUERRS (LP,2,-1)
         ISTAT=1
         GO TO 120
80    DO 90 I=1,NBOX
         NPOS=NPOS+1
         MDRBOX(I)=ARRAY(NPOS)
90       CONTINUE
C
95    IF (IBASN.EQ.1) GO TO 110
C
C  SET LATITUDE/LONGITUDE PAIRS DEFINING SUBSETS OF MDR GRID
      DO 100 I=1,8
         NPOS=NPOS+1
         CORMDR(I)=ARRAY(NPOS)
100      CONTINUE
C
110   IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,*)
     *      ' NPOS=',NPOS,
     *      ' NFILL=',NFILL,
     *      ' IPTRNX=',IPTRNX,
     *      ' IVMAPS=',IVMAPS,
     *      ' '
         CALL SULINE (IOSDBG,1)
         CALL SUPDMP ('MAPS','REAL',0,NPOS,ARRAY,ARRAY)
         ENDIF
C
120   IF (LDEBUG.GT.0) THEN
         IF (ISTAT.EQ.0) THEN
            WRITE (IOSDBG,210)
            CALL SULINE (IOSDBG,1)
            ENDIF
         IF (ISTAT.GT.0) THEN
            WRITE (IOSDBG,220)
            CALL SULINE (IOSDBG,1)
            ENDIF
          ENDIF
C
130   IF (ISTRCE.GT.0) THEN
         WRITE (IOSDBG,230)
         CALL SULINE (IOSDBG,1)
         ENDIF
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
140   FORMAT (' *** ENTER SRMAPS')
170   FORMAT ('0*** ERROR - IN SRMAPS - NUMBER OF TIMING STATIONS TO ',
     *   'BE PROCESSED (',I3,' EXCEEDS MAXIMUM ARRAY DIMENSIONS (',
     *   I3,').')
190   FORMAT ('0*** ERROR - IN SRMAPS - NUMBER OF WEIGHTING STATIONS ',
     *   'TO BE PROCESSED (',I3,') EXCEEDS MAXIMUM ',
     *   'ARRAY DIMENSIONS (',I3,').')
200   FORMAT ('0*** ERROR - IN SRMAPS - NUMBER OF MDR BOXES ',
     *   'TO BE PROCESSED (',I3,') EXCEEDS MAXIMUM ',
     *   'ARRAY DIMENSIONS (',I3,').')
210   FORMAT ('0*** NOTE - MAPS PARAMETERS SUCCESSFULLY ',
     *     'READ.')
220   FORMAT ('0*** NOTE - MAPS PARAMETERS NOT SUCCESSFULLY ',
     *     'READ.')
230   FORMAT (' *** EXIT SRMAPS')
C
      END
