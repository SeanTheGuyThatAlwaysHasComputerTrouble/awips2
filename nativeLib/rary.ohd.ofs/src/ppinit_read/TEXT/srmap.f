C MEMBER SRMAP
C-----------------------------------------------------------------------
C
C DESC READ AREA MAP PARAMETER RECORD
C
      SUBROUTINE SRMAP (IVMAP,XMAPID,ITIME,ITMWT,POWER,MSTWT,NSTWT,
     *   ISTWT,MPCPN,NPCPN,NSETS,DESCRP,BASNID,FMAPID,CENTRD,
     *   TSTAID,TSTAWT,CTSTA,CSTAID,CSTAWT,CCSTA,
     *   UNUSED,LARRAY,ARRAY,IPTR,IPRERR,IPTRNX,ISTAT)
C
      DIMENSION ARRAY(LARRAY)
      DIMENSION XMAPID(1),DESCRP(1),BASNID(1),FMAPID(1),CENTRD(1)
      DIMENSION TSTAID(2,1),TSTAWT(1),CTSTA(2,1),CSTAID(2,1)
      DIMENSION CSTAWT(1),CCSTA(2,1),UNUSED(1)
C
      INCLUDE 'uio'
      INCLUDE 'scommon/sudbgx'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppinit_read/RCS/srmap.f,v $
     . $',                                                             '
     .$Id: srmap.f,v 1.1 1995/09/17 19:14:51 dws Exp $
     . $' /
C    ===================================================================
C
C
C
      IF (ISTRCE.GT.0) WRITE (IOSDBG,180)
      IF (ISTRCE.GT.0) CALL SULINE (IOSDBG,1)
C
C  SET DEBUG LEVEL
      LDEBUG=ISBUG(4HMAP )
C
      IF (LDEBUG.GT.0) WRITE (IOSDBG,190) LARRAY
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
C
      ISTAT=0
C
C  READ PARAMETER RECORD
      CALL SUDOPN (1,4HPPP ,IERR)
      CALL RPPREC (XMAPID,4HMAP ,IPTR,LARRAY,ARRAY,NFILL,IPTRNX,
     *     IERR)
      IF (IERR.EQ.0) GO TO 10
      ISTAT=IERR
      IF (ISTAT.EQ.6) GO TO 170
      IF (IPRERR.EQ.0) GO TO 170
         CALL SRPPST (XMAPID,4HMAP ,IPTR,LARRAY,NFILL,IPTRNX,IERR)
         WRITE (LP,250)
         CALL SUERRS (LP,2,-1)
         GO TO 170
C
C  SET PARAMETER ARRAY VERSION NUMBER
10    IVMAP=ARRAY(1)
C
C  SET MAP AREA IDENTIFIER
      XMAPID(1)=ARRAY(2)
      XMAPID(2)=ARRAY(3)
C
      NPOS=3
C
C  SET DESCRIPTIVE INFORMATION
      DO 20 I=1,5
         NPOS=NPOS+1
20       DESCRP(I)=ARRAY(NPOS)
C
C  SET TIME INTERVAL (HOURS)
      NPOS=NPOS+1
      ITIME=ARRAY(NPOS)
C
C  SET BASIN BOUNDARY IDENTIFIER
      DO 30 I=1,2
         NPOS=NPOS+1
30       BASNID(I)=ARRAY(NPOS)
C
C  SET TYPE OF TIMING WEIGHTS
      NPOS=NPOS+1
      ITMWT=ARRAY(NPOS)
C
C  SET EXPONENT IN 1/D**POWER
      NPOS=NPOS+1
      POWER=ARRAY(NPOS)
C
C  SET NUMBER OF STATIONS USED FOR TIME DISTRIBUTION
      NPOS=NPOS+1
      NSTWT=ARRAY(NPOS)
C
C  SET TYPE OF STATION WEIGHTS
      NPOS=NPOS+1
      ISTWT=ARRAY(NPOS)
C
C  SET IDENTIFIER OF FUTURE MAP AREA ASSIGNED TO AREA
      DO 40 I=1,2
         NPOS=NPOS+1
40       FMAPID(I)=ARRAY(NPOS)
C
C  SET CENTROID (NWSRFS/HRAP COORDINATES)
      DO 50 I=1,2
         NPOS=NPOS+1
50       CENTRD(I)=ARRAY(NPOS)
C
C  THE NEXT TWO POSITIONS ARE UNUSED
      DO 60 I=1,2
         NPOS=NPOS+1
60       UNUSED(I)=ARRAY(NPOS)
C
C  SET NUMBER OF STATIONS USED TO COMPUTE MAP
      NPOS=NPOS+1
      NPCPN=ARRAY(NPOS)
C
C  SET NUMBER OF SETS OF STATION WEIGHTS
      NPOS=NPOS+1
      NSETS=ARRAY(NPOS)
C
C  SET STATION IDENTIFIERS OF TIMING STATIONS
      IF (NSTWT.LE.MSTWT) GO TO 70
         WRITE (LP,210) NSTWT,MSTWT
         CALL SUERRS (LP,2,-1)
         ISTAT=1
         GO TO 160
70    DO 80 J=1,NSTWT
         DO 80 I=1,2
            NPOS=NPOS+1
80          TSTAID(I,J)=ARRAY(NPOS)
C
C  SET TIMING WEIGHTS
      DO 90 I=1,NSTWT
         NPOS=NPOS+1
90       TSTAWT(I)=ARRAY(NPOS)
C
      IF (ITMWT.EQ.1) GO TO 110
C
C  SET NWSRFS/HRAP COORDINATES OF TIMING STATIONS
      DO 100 J=1,NSTWT
         DO 100 I=1,2
            NPOS=NPOS+1
100         CTSTA(I,J)=ARRAY(NPOS)
C
C  SET STATION IDENTIFIERS OF STATIONS USED TO COMPUTE MAP
110   NUMVAL=NPCPN*NSETS
      IF (NUMVAL.LE.MPCPN*2) GO TO 120
         WRITE (LP,230) NPCPN,NSETS,MPCPN
         CALL SUERRS (LP,2,-1)
         ISTAT=1
         GO TO 160
120   DO 130 J=1,NPCPN
         DO 130 I=1,2
            NPOS=NPOS+1
130         CSTAID(I,J)=ARRAY(NPOS)
C
C  SET 24-HR MAP STATION WEIGHTS
      IPOS=0
      DO 140 I=1,NSETS
         DO 140 J=1,NPCPN
            IPOS=IPOS+1
            NPOS=NPOS+1
140         CSTAWT(IPOS)=ARRAY(NPOS)
C
C  SET NWSRFS/HRAP COORDINATES OF STATIONS USED TO COMPUTE MAP
      DO 150 J=1,NPCPN
         DO 150 I=1,2
            NPOS=NPOS+1
150         CCSTA(I,J)=ARRAY(NPOS)
C
      IF (LDEBUG.EQ.0) GO TO 160
         WRITE (IOSDBG,200) NPOS,NFILL,IPTRNX,IVMAP
         CALL SULINE (IOSDBG,1)
         CALL SUPDMP (4HMAP ,4HREAL,0,NPOS,ARRAY,ARRAY)
C
160   IF (LDEBUG.GT.0.AND.ISTAT.EQ.0) WRITE (IOSDBG,240)
      IF (LDEBUG.GT.0.AND.ISTAT.EQ.0) CALL SULINE (IOSDBG,1)
      IF (LDEBUG.GT.0.AND.ISTAT.GT.0) WRITE (IOSDBG,250)
      IF (LDEBUG.GT.0.AND.ISTAT.GT.0) CALL SULINE (IOSDBG,1)
C
170   IF (ISTRCE.GT.0) WRITE (IOSDBG,260) ISTAT
      IF (ISTRCE.GT.0) CALL SULINE (IOSDBG,1)
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
180   FORMAT (' *** ENTER SRMAP')
190   FORMAT (' LARRAY=',I4)
200   FORMAT (' NPOS=',I3,3X,'NFILL=',I3,3X,'IPTRNX=',I3,3X,
     *   'IVMAP=',I3)
210   FORMAT ('0*** ERROR - IN SRMAP - NUMBER OF TIMING STATIONS TO ',
     *   'BE PROCESSED (',I3,') EXCEEDS MAXIMUM ARRAY DIMENSIONS (',
     *   I3,').')
230   FORMAT ('0*** ERROR - IN SRMAP - NUMBER OF WEIGHTING STATIONS ',
     *   'TO BE PROCESSED (',I3,') FOR ',I2,' SEASONS EXCEEDS MAXIMUM ',
     *   'ARRAY DIMENSIONS (',I3,').')
240   FORMAT ('0*** NOTE - MAP  PARAMETERS SUCCESSFULLY ',
     *     'READ.')
250   FORMAT ('0*** NOTE - MAP  PARAMETERS NOT SUCCESSFULLY ',
     *     'READ.')
260   FORMAT (' *** EXIT SRMAP : STATUS CODE=',I2)
C
      END
