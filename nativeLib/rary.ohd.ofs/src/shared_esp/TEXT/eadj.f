C MEMBER EADJ
C  (from old member EEADJ)
C
      SUBROUTINE EADJ(P,T,C,TSESP)
C
C   THIS SUBROUTINE MAKES NECESSARY CHANGES TO ADJUST-Q
C   OPERATION ARRAYS. IF THE ADJUST OPERATION USES INSTANTANEOUS
C   DATA WHICH HAVE BEEN REPLACED WITH MEAN DAILY DATA, CHANGES
C   ARE MADE TO THE P,T, AND C ARRAYS.
C
C   THIS SUBROUTINE WAS WRITTEN BY GERALD N DAY.
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/flarys'
      INCLUDE 'common/ionum'
      INCLUDE 'common/where'
      INCLUDE 'common/fprog'
      INCLUDE 'common/espseg'
C
      INTEGER T
C
      DIMENSION P(1),T(1),C(1),TSIDI(2),TSIDR(2),OP(2),SBNAME(2),
     1 OLDOPN(2),TSESP(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/shared_esp/RCS/eadj.f,v $
     . $',                                                             '
     .$Id: eadj.f,v 1.1 1995/09/17 19:18:21 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA REPL/4HREPL/,SBNAME/4HEADJ,4H    /,DEBUG/4HEADJ/
C
      IOLDOP=IOPNUM
      IOPNUM=0
      DO 10 I=1,2
      OLDOPN(I)=OPNAME(I)
   10 OPNAME(I)=SBNAME(I)
C
      IF(ITRACE.GE.1) WRITE(IODBUG,900)
  900 FORMAT(1H0,15H** EADJ ENTERED)
C
      IBUG=IFBUG(DEBUG)
C
C
C   SEARCH T ARRAY FOR ADJUST OPERATION.
C
      LOCT=0
   50 IF(T(LOCT+1).EQ.-1) GO TO 999
      NEXTT=T(LOCT+2)
      IF(T(LOCT+1).NE.14) GO TO 300
      IF(IBUG.EQ.1) WRITE(IODBUG,910)
  910 FORMAT(1H0,10X,22HADJUST OPERATION FOUND)
      LOCP=T(LOCT+3)-1
      LOCC=T(LOCT+4)-1
C
      IOINST=P(LOCP+7)
      IOMD=P(LOCP+8)
C
      IF(IOINST.EQ.0) GO TO 300
      IF(IOMD.EQ.0) GO TO 200
C
C   MEAN DAILY AND INSTANTANEOUS OBSERVED DATA ARE USED -
C   CHECK TO MAKE SURE INSTANTANEOUS DATA HAVE NOT BEEN REPLACED.
C
      IF(IBUG.EQ.1) WRITE(IODBUG,920)
  920 FORMAT(1H0,10X,39HBOTH QIN AND QME ARE USED IN ADJUSTING.)
C
      TSIDI(1)=P(LOCP+29)
      TSIDI(2)=P(LOCP+30)
      DTYPEI=P(LOCP+31)
      IDTI=P(LOCP+32)
C
      LOCTS=0
  100 NEXTTS=TSESP(LOCTS+2)
      ITYPE=TSESP(LOCTS+1)
      IF(ITYPE.EQ.0) GO TO 400
C
      IF(TSESP(LOCTS+3).NE.TSIDI(1)) GO TO 150
      IF(TSESP(LOCTS+4).NE.TSIDI(2)) GO TO 150
      IF(TSESP(LOCTS+5).NE.DTYPEI) GO TO 150
      ITIME=TSESP(LOCTS+6)
      IF(ITIME.NE.IDTI) GO TO 150
C
C   FOUND TIME SERIES
C
      IF(TSESP(LOCTS+10).NE.REPL) GO TO 300
C
C   INSTANTANEOUS DATA WERE REPLACED .
C
      OP(1)=P(LOCP-5)
      OP(2)=P(LOCP-4)
      WRITE(IPR,600) OP
  600 FORMAT(1H0,10X,31H**WARNING** OPERATION ADJUST-Q ,2A4,
     1 2X,45HUSES BOTH INST. AND MEAN DAILY OBSERVED DATA.,
     2 43H THE INST. TIME SERIES CAN NOT BE REPLACED.,
     3 42H THE REPLACED TIME SERIES WILL BE IGNORED.)
      CALL WARN
      P(LOCP+7)=0.01
      T(LOCT+5)=0
      T(LOCT+10)=0
      GO TO 300
C
C   INCREMENT TO NEXT TIME SERIES
C
  150 IF(NEXTTS.GT.LTSESP) GO TO 400
      LOCTS=NEXTTS-1
      GO TO 100
C
C   INSTANTANEOUS OBSERVED DATA ARE USED FOR ADJUSTING
C
  200 CONTINUE
      IF(IBUG.EQ.1) WRITE(IODBUG,930)
  930 FORMAT(1H0,10X,26HQIN IS USED FOR ADJUSTING.)
      TSIDI(1)=P(LOCP+25)
      TSIDI(2)=P(LOCP+26)
      DTYPEI=P(LOCP+27)
      IDTI=P(LOCP+28)
C
C   SEARCH TSESP FOR TIME SERIES
C
      LOCTS=0
  210 NEXTTS=TSESP(LOCTS+2)
      ITYPE=TSESP(LOCTS+1)
      IF(ITYPE.EQ.0) GO TO 400
      IF(TSESP(LOCTS+3).NE.TSIDI(1)) GO TO 250
      IF(TSESP(LOCTS+4).NE.TSIDI(2)) GO TO 250
      IF(TSESP(LOCTS+5).NE.DTYPEI) GO TO 250
      ITIME=TSESP(LOCTS+6)
      IF(ITIME.NE.IDTI) GO TO 250
      IF(TSESP(LOCTS+10).NE.REPL) GO TO 300
C
C   INSTANTANEOUS DATA HAVE BEEN REPLACED.
C
      NV=TSESP(LOCTS+12)
      NADD=TSESP(LOCTS+13+NV)
      TSIDR(1)=TSESP(LOCTS+16+NV+NADD)
      TSIDR(2)=TSESP(LOCTS+17+NV+NADD)
      DTYPER=TSESP(LOCTS+18+NV+NADD)
C
      P(LOCP+7)=0.01
      P(LOCP+8)=1.01
      P(LOCP+25)=.025
      P(LOCP+26)=TSIDR(1)
      P(LOCP+27)=TSIDR(2)
      P(LOCP+28)=DTYPER
C
      T(LOCT+6)=T(LOCT+5)
      T(LOCT+5)=0
      T(LOCT+11)=NDD
      IF(IBUG.EQ.1) WRITE(IODBUG,940)
  940 FORMAT(1H0,10X,31HCHANGES HAVE BEEN MADE FOR THE ,
     1 20HREPLACE TIME SERIES.)
      GO TO 300
C
  250 IF(NEXTTS.GT.LTSESP) GO TO 400
      LOCTS=NEXTTS-1
      GO TO 210
C
C   INCREMENT THE T ARRAY
C
  300 IF(NEXTT.GT.LT) GO TO 999
      LOCT=NEXTT-1
      GO TO 50
C
  400 OP(1)=P(LOCP-5)
      OP(2)=P(LOCP-4)
      WRITE(IPR,610) TSIDI,DTYPEI,IDTI,OP
  610 FORMAT(1H0,10X,21H**ERROR** TIME SERIES,5X,2A4,2X,A4,2X,I2,
     1 28H USED IN OPERATION ADJUST-Q ,2A4,14H WAS NOT FOUND)
      CALL KILLFN(8HESP     )
C
  999 CONTINUE
      IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
      RETURN
      END
