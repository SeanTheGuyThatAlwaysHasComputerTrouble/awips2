C MEMBER ECONVT
C  (from old member EECONVT)
C
      SUBROUTINE ECONVT(UNITSM,UNITSD,CFACT,CONST,IER)
C
C ......................................................................
C
C             ESP UNITS CONVERSION SUBROUTINE
C
C
C THIS ROUTINE DECIDES WHAT UNITS ARE DESIRED (METRIC,ENGLISH, OR
C WATER SUPPLY) BY CHECKING SWITCHES IN COMMON BLOCKS FENGMT AND
C EOUTPT.
C THEN CONVERSION FACTORS ARE GIVEN TO CONVERT INPUT UNITS
C (USUALLY IN STANDARD METRIC) TO DESIRED UNITS.
C
C
C IF UNITS ARE CMS OR CMSD THEN USE FOLLOWING CHART, OTHERWISE
C JUST CONVERT FROM STD. METRIC USING SUBROUTINE FCONVT:
C
C               CMS                         CMSD
C               ---                         ----
C         ENGLISH   METRIC          ENGLISH       METRIC
C NORMAL   CFS       CMS            CFSD         CMSD
C WATER    MGD       CMS            MG            1000 M**3
C SUPPLY
C
C ** SPECIAL NOTE ** IF INPUT UNITS ARE TM3 THEN THIS INDICATES THAT
C THE OUTPUT VARIABLE IS CUMULATIVE (NVAR=4) AND THE ACCM VALUES ARE
C REALLY IN CMSD BUT THE OUTPUT UNITS ARE TO BE AC-FT(ENGLISH) OR
C 1000 M**3(METRIC) FOR NORMAL ONLY. FOR WATER SUPPLY USE THE SAME
C PROCEDURE AS INDICATED FOR CMSD.
C
C
C ORIGINALLY BY    ED VANBLARGAN - HRL - JUNE,1981
C
C ......................................................................
C
C VARIABLES:
C UNITSM - (INPUT VAR) INPUT UNITS, USUALLY STANDARD METRIC
C UNITSD - (OUTPUT VAR) DESIRED OUTPUT UNITS
C CFACT  - (   "   "  ) MULTIPLICATION CONVERSION FACTOR
C CONST  - (   "   "  ) ADDITION CONSTANT
C METRIC - (CB/FENGMT/) METRIC SWITCH  0=ENGLISH, 1=METRIC
C IWS    - (CB/EOUTPT/) WATER SUPPLY UNITS SWITCH  0=NORMAL UNITS,
C                                                  1=WATER SUPPLY UNITS
C
C *** NOTE *** EQUATION FOR GENERAL CONVERSION IS
C     UNITSD = UNITSM * CFACT + CONST
C
C
C UNITS USED ARE
C CFS -CFS
C MGD - MILLION GALLONS/DAY
C TM3 - 1000'S METERS CUBED
C ACFT- ACRE FEET
C MG  - MILLION GALLONS
C
C
      DIMENSION SBNAME(2),OLDOPN(2)
C
      INCLUDE 'common/where'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/eoutpt'
      INCLUDE 'common/fengmt'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/shared_esp/RCS/econvt.f,v $
     . $',                                                             '
     .$Id: econvt.f,v 1.1 1995/09/17 19:18:28 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA NAME,SBNAME,DIM/4HEXEC,4HECON,4HVT  ,4HL   /
      DATA CMS,CFS,CMSD,XMGD,TM3,ACFT,XMG,CFSD
     * /4HCMS ,4HCFS ,4HCMSD,4HMGD ,4HTM3 ,4HACFT,4HMG  ,4HCFSD/
C
C SET ERROR TRACES IN CB /WHERE/
C
      IOLDOP=IOPNUM
      IOPNUM=0
      DO 10 I=1,2
      OLDOPN(I)=OPNAME(I)
10    OPNAME(I)=SBNAME(I)
C
C GET TRACE LEVEL AND DEBUG
C
      IF (ITRACE.GE.1) WRITE(IODBUG,100)
100   FORMAT(1H0,14HECONVT ENTERED)
      IBUG=IFBUG(NAME)
C
C INITIALIZE VARIABLES
C
      IER=0
      UNITSD=UNITSM
      CFACT=1.000
      CONST=0.00
C
C BASIC CONVERSION FACTORS USED ARE:
C 1 FT = 0.3048 METER(M)
C 1 CU. FT = 7.481 GAL
C 1 CU. M  = 264.2 GAL
C 1 AC-FT = 1233.5 CU. M
C
C
      IF (UNITSM.NE.CMS) GO TO 200
C
C UNITS OF CMS...IF METRIC DESIRED JUST RETURN, FOR ENGLISH CONVERT
C AND CHECK IF WATER SUPPLY.
C
      IF (METRIC.EQ.1) GO TO 999
      IF (IWS.EQ.1) GO TO 140
C CMS-CFS
      UNITSD=CFS
      CFACT=(1.0/0.3048)**3.0
      GO TO 999
C CMS-MGD
140   UNITSD=XMGD
      CFACT= 264.2 * 3600. * 24.  / 1000000.
      GO TO 999
C
C
200   IF (UNITSM.NE.TM3) GO TO 300
C
C UNITS OF TM3 - VALUES ARE ACTUALLY IN CMSD. FOR WATER SUPPLY JUST
C MOVE ONTO WATER SUPPLY PORTION OF CMSD CODE. OTHERWISE FOR NORMAL
C CONVERT CMSD TO TM3(METRIC) OR ACFT(ENGLISH).
C
      IF (IWS.EQ.1) GO TO 320
      IF (METRIC.EQ.0) GO TO 210
C CMSD-TM3
      CFACT = 24. * 3600. / 1000.
      GO TO 999
C CMSD-ACFT
210   UNITSD=ACFT
      CFACT = 24. * 3600. / 1233.5
      GO TO 999
C
C
C
300   IF (UNITSM.NE.CMSD) GO TO 350
C
C UNITS OF CMSD - FOR NORMAL METRIC DESIRED JUST RETURN.
C FOR ENGLISH OR WATER SUPPLY (METRIC OR ENGLISH) MUST CONVERT.
C
      UNITSD=CMSD
      IF(IWS.EQ.1) GO TO 320
      IF(METRIC.EQ.1) GO TO 999
C CMSD-CFSD
      UNITSD=CFSD
      CFACT=(1.0/0.3048)**3.0
      GO TO 999
C
320   IF (METRIC.EQ.0) GO TO 330
C CMSD-TM3
      UNITSD=TM3
      CFACT = 24. * 3600. / 1000.
      GO TO 999
C CMSD-MG
330   UNITSD=XMG
      CFACT = 24. * 3600. * 264.2 / 1000000.
      GO TO 999
C
C AT THIS POINT ONLY STANDARD UNITS ARE ALLOWED. CHECK IWS TO BE
C SURE OF THIS.
C
350   IF (IWS.EQ.0) GO TO 400
      WRITE(IPR,310) UNITSM
310   FORMAT(1H0,10X,44H**WARNING** WATER SUPPLY UNITS NOT AVAILABLE,
     * 30H FOR STANDARD METRIC UNITS OF ,A4)
      CALL WARN
C
C IF METRIC DESIRED THEN SIMPLY RETURN, OTHERWISE CALL FCONVT
C TO CONVERT STD METRIC TO STD ENGLISH UNITS.
C
400   IF (METRIC.EQ.1) GO TO 999
      CALL FCONVT(UNITSM,DIM,UNITSD,CFACT,CONST,IER)
C
C...........DEBUG TIME................................
C
999   IF (IBUG.EQ.0) GO TO 1000
C
      WRITE(IODBUG,910) UNITSM,UNITSD,CFACT,CONST,METRIC,IWS
910   FORMAT(1H0,7HUNITSM=,A4,8H UNITSD=,A4,7H CFACT=,F10.3,
     * 7H CONST=,F10.3,8H METRIC=,I2,5H IWS=,I2)
C
C ......................................................................
C
C
C RESET ERROR TRACES IN CB /WHERE/
C
1000  IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      RETURN
      END
