C MEMBER EVAC07
C  (from old member EEVAC07)
C
      SUBROUTINE EVAC07(IDACC,IHACC,LDACC,LHACC,D,A,NDAYS,NHRS,TSID,
     * TYPE,IDT,NPDT,ITSCAL,IVALUE,CO,KODE,VALUE,DAVG,MDAVG)
C
C ......................................................................
C
C      ACCUMULATOR 07 (NUMBER OF DAYS TO A VALUE) SUBROUTINE
C
C THIS IS THE ACCUMULATOR SUBROUTINE FOR FINDING THE NUMBER OF DAYS
C UNTIL THE TIME SERIES GETS ABOVE (OR FALLS BELOW) THE SPECIFIED
C CRITERIA VALUE.
C ......................................................................
C
C     SUBROUTINE INITIALLY WRITTEN BY
C       ED VANBLARGAN - HRL - SEPT, 1981
C
C ......................................................................
C
C
C VARIABLES USED ARE:
C
C IDACC = 1ST JULIAN DAY TO BE ACCUMULATED
C IHACC = 1ST HOUR TO BE ACCM.
C LDACC = LAST JULIAN DAY TO BE ACCM.
C LHACC = LAST HOUR TO BE ACCM.
C     D = ARRAY CONTAINING TIME SERIES DATA
C     A = ACCUMULATOR ARRAY. A(1) IS NUMBER OF DAYS TO CRITERIA VALUE.
C NDAYS = NUMBER OF DAYS ALREADY ACCM.
C  NHRS = NUMBER OF HOURS ALREADY ACCM.
C  TSID = TIME SERIES I.D.
C  TYPE = DATA TYPE
C   IDT = TIME INTERVAL OF TIME SERIES
C    CO = CARRYOVER (VALID ONLY IF NDAYS OR NHRS IS GT 0)
C         ** SEE NOTE UNDER DAVG **
C  KODE = IS 1 TO CHECK WHEN TIME SERIES(TS) GETS'ABOVE' VALUE.
C         IS 2 TO CHECK WHEN TS GETS 'BELOW' VALUE.
C         ALSO IF NEGATIVE MUST CONVERT TO MEAN DAILY VALUES FIRST.
C VALUE = THIS IS THE CRITERIA VALUE TS GETS CHECKED AGAINST.
C ITSCAL= TIME SCALE OF TIME SERIES
C NUMLOC= NUMBER OF D VALUES PER 24 HR INTERVAL
C LOCIN = 1ST (OR CURRENT) LOCATION IN D ARRAY
C LASTIN= LAST LOCATION WE WANT IN D ARRAY
C KDAYS = COUNTER OF THE NUMBER OF DAYS WE GO THRU
C  KHRS = COUNTER OF THE HOUR FOR EACH D VALUE EACH DAY.
C  DAVG = WORK SPACE ARRAY. CANNOT USE FOR CARRYOVER SINCE THIS ARRAY
C         MAY GET WIPED OUT.
C MDAVG = MAXIMUM DAVG ARRAY LENGTH
C .........................................................
C
      DIMENSION SBNAME(2),OLDOPN(2),D(1),A(1),TSID(2),ISUBN(2)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/where'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fctime'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/shared_esp/RCS/evac07.f,v $
     . $',                                                             '
     .$Id: evac07.f,v 1.1 1995/09/17 19:19:24 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA NAME,SBNAME/4HEACC,4HEVAC,4H07  /
      DATA INTL/4HINTL/
      DATA MEAN,INST/4HMEAN,4HINST/
C
C
C PUT ERROR TRACES FOR THIS ROUTINE IN COMMON BLK/WHERE
C
      IOLDOP=IOPNUM
      IOPNUM=0
      DO 10 I=1,2
        OLDOPN(I)=OPNAME(I)
        OPNAME(I)=SBNAME(I)
10    CONTINUE
C
C GET TRACE LEVEL AND DEBUG CODE.
C
      IF (ITRACE.GE.1) WRITE (IODBUG,115)
115   FORMAT(1H0,14HEVAC07 ENTERED)
      IBUG=IFBUG(NAME)
C
C IF NDAYS AND NHRS=0 THEN SET A(1)=-999 AND MOVE ON TO LOOPS.
C
C IF EITHER NHRS OR NDAYS NE 0 THEN A(1) MAY HAVE A VALID ACCM NUMBER.
C IF THIS NUMBER IS NOT MISSING THEN JUST RETURN CAUSE CRITERIA HAS
C BEEN MET! ...OTHERWISE CONTINUE ON.
C
C THEN CHECK IF A(1) IS MISSING. IF SO, RETURN
C IF A(1) GE 9999 THEN CONTINUE ON CAUSE CRITERIA WASNT MET YET.
C
      IF (NDAYS.NE.0 .OR. NHRS.NE.0) GO TO 120
      A(1)=9999.01
      SUM=0.0
      GO TO 150
C
120   IF (A(1).LT.9998.5) GO TO 900
      IF (IFMSNG(A(1)).NE.0) GO TO 999
      SUM=CO
C
C SET VARIABLES
C
150   LOCIN = (IDACC-IDADAT)*24*NPDT/IDT + (IHACC-1)*NPDT/IDT +IVALUE
      LASTIN= (LDACC-IDADAT)*24*NPDT/IDT + (LHACC-1)*NPDT/IDT +IVALUE
      KDAYS=NDAYS
      KHRS=IHACC-IDT
      NUMLOC=24/IDT
      CO=0.0
C
C
C ......................................................................
C
C TIME FOR DEBUG
C
      IF (IBUG.EQ.0) GO TO 600
C
      WRITE(IODBUG,210) A(1),NDAYS,NHRS,KODE,VALUE
210   FORMAT(1H0,10X,11H** INPUT ** // 7X,26HA(1) NDAYS NHRS KODE VALUE
     * / 1X,F10.2,2X,3I4,F10.2)
C
      WRITE(IODBUG,220) IDACC,IHACC,LDACC,LHACC,TSID,TYPE,
     *IDT,LOCIN,LASTIN,CO
220   FORMAT(// 1H0,23HIDACC IHACC LDACC LHACC,6X,4HTSID,1X,
     *21HTYPE IDT LOCIN LASTIN / 1H0,
     *I5,3I6,2X,2A4,1X,A4,I4,I6,I7 //
     *1H0,11HINPUT CO = ,F10.2 // 1X,16HD ARRAY FOLLOWS:)
C
      WRITE(IODBUG,230) (D(I),I=LOCIN,LASTIN,NPDT)
230   FORMAT(/ 1X,12F10.2)
C
C
C ......................................................................
C
C
C IF KODE = NEGATIVE THEN MUST CONVERT TO MEAN DAILY VALUES
C BEFORE DETERMINING A(1). OTHERWISE CONTINUE ON.
C
600   IF (KODE.LT.0) GO TO 2000
C
C ******   POSITIVE KODE  *****
C
C 2 LOOPS FOLLOW. THEY ARE BASICALLY THE SAME EXCEPT 1ST LOOP IS
C FOR KODE=1 AND 2ND IS FOR KODE=2.
C
C IN EITHER LOOP GO THRU D ARRAY FROM LOCIN TO LASTIN. IF ANY D VALUE
C MEETS CRITERIA VALUE THEN:
C  1. A(1)=NUMBER OF DAYS(KDAYS) ACCUMULATED.
C  2. GO TO END
C
C IF ANY D VALUE IS MISSING THEN:
C  1.A(1)= -999 AND CO=-999.
C
C KHRS GETS INCREMENTED BY IDT ON EACH PASS THRU LOOP. AT EACH 24
C HOUR MARK KDAYS GETS INCREMENTED BY 1 AND KHRS GETS RESET TO 0.
C
      IKOD=IABS(KODE)
      GO TO (700,1550),IKOD
      WRITE(IPR,2015)
      CALL ERROR
      GO TO 999
C
C LOOP FOR KODE=1
C
700   DO 1500 I=LOCIN,LASTIN,NPDT
        IF (IFMSNG(D(I)).NE.0) GO TO 1999
        KHRS=KHRS+IDT
        IF (KHRS.LT.24) GO TO 1100
        KDAYS=KDAYS+1
        KHRS=0
C
1100    IF (D(I).LE.VALUE) GO TO 1500
        A(1)=KDAYS + KHRS/24.
        GO TO 900
C
1500  CONTINUE
      GO TO 900
C
C LOOP FOR KODE=2
C
1550  DO 1900 I=LOCIN,LASTIN,NPDT
        IF (IFMSNG(D(I)).NE.0) GO TO 1999
        KHRS=KHRS+IDT
        IF (KHRS.LT.24) GO TO 1700
        KDAYS=KDAYS+1
        KHRS=0
C
1700    IF (D(I).GE.VALUE) GO TO 1900
        A(1)=KDAYS + KHRS/24.
        GO TO 900
C
1900  CONTINUE
      GO TO 900
C
C
C
C *****  NEGATIVE KODE  *****
C
C
C CHECK FOR ONE OF THREE POSSIBLE CASES:
C   1. MEAN (OR ACCM) TIME SCALE, 24 HR. TIME INTERVAL.
C   2. MEAN (OR ACCM), OTHER THAN 24 HR. IDT
C   3. INST, ANY IDT
C FOR CASE 1 SIMPLY GO BACK UP TO NORMAL (POSITIVE KODE) LOOPS) SINCE
C VALUES ARE ALREADY MEAN DAILY. OTHERWISE CONTINUE ON.
C
C FOLLOWING LOOPS FOR CONVERTING TO MEAN DAILY VALUES ARE SAME
C LOGIC AS EVAC01.
C
2000  IF (ITSCAL.EQ.INST) GO TO 4000
      IF (IDT.NE.24) GO TO 3000
C
      IKOD=IABS(KODE)
      GO TO (700,1550),IKOD
      WRITE(IPR,2015)
2015  FORMAT(1H0,10X,41H**ERROR** IMPOSSIBLE LOCATION AFTER GOTO.)
      CALL ERROR
      GO TO 999
C
C *****   MEAN OR ACCM - NON 24 HR IDT
C
C LOOP THRU D ARRAY FROM LOCIN TO LASTIN AND COMPUTE 24 HR MEAN OR ACCM
C BY ADDING EACH D VALUE TO SUM UNTIL THE 24 HR INTERVAL IS COMPLETE.
C
C KHRS GETS INCREMENTED BY IDT ON EACH LOOP AND THE INTERVAL IS
C COMPLETE WHEN KHRS=24.
C
C ONCE A 24 HR VALUE (SUM) IS IS COMPUTED THEN:
C     1. KDAYS GETS INCREMENTED BY 1
C     2. CHECK SUM AGAINST CRITERIA...IF MET A(1)=KDAYS AND RETURN
C     3. OTHERWISE RESET SUM AND KHR=0 AND CONTINUE
C
C CO=SUM AT END
C
3000  DO 3900 I=LOCIN,LASTIN,NPDT
        IF (IFMSNG(D(I)).NE.0) GO TO 1999
        SUM=SUM+D(I)
        KHRS=KHRS+IDT
        IF (KHRS.LT.24) GO TO 3900
        IF (ITSCAL.EQ.MEAN) SUM=SUM/NUMLOC
        KDAYS=KDAYS+1
C
        IF(IABS(KODE).EQ.2) GO TO 3200
        IF(SUM.LE.VALUE) GO TO 3500
        A(1)=KDAYS+0.01
        GO TO 900
3200    IF(SUM.GE.VALUE) GO TO 3500
        A(1)=KDAYS+0.01
        GO TO 900
C
3500    SUM=0.0
        KHRS=0
3900  CONTINUE
      CO=SUM
      GO TO 900
C
C ****   INST - ANY IDT
C
C LOOP THRU D ARRAY FROM LOCIN TO LASTIN AND COMPUTE 24 HR MEAN
C DO THIS BY ADDING EACH D VALUE TO SUM.  NOTE: FOR INST, THE D VALUES
C AT THE 24TH HR (BOTH PREVIOUS AND CURRENT) ARE DIVIDED BY 2 AND ADDED
C INTO SUM
C
C KHRS GETS INCREMENTED BY IDT ON EACH LOOP AND THE INTERVAL IS
C COMPLETE WHEN KHRS=24.
C
C ONCE A 24 HR VALUE (SUM) IS IS COMPUTED THEN:
C     1. KDAYS GETS INCREMENTED BY 1
C     2. CHECK IF SUM MEETS CRITERIA...IF SO A(1)=KDAYS AND RETURN.
C     3. OTHERWISE RESET KHRS=0 AND SUM=D(CURRENT) / 2.
C     4. ALSO CHECK D/2 FOR POSSIBLE UNDERFLOWS.
C
C CO=SUM AT END
C
4000  DO 4900 I=LOCIN,LASTIN,NPDT
        IF (IFMSNG(D(I)).NE.0) GO TO 1999
        KHRS=KHRS+IDT
        IF (KHRS.GE.24) GO TO 4200
        SUM=SUM+D(I)
        GO TO 4900
C
4200    PARTD=D(I)/2
        IF (ABS(PARTD).LT.1.E-5) PARTD=0.0
        SUM=(SUM+PARTD)/NUMLOC
        KDAYS=KDAYS+1
C
        IF(IABS(KODE).EQ.2) GO TO 4300
        IF(SUM.LE.VALUE) GO TO 4500
        A(1)=KDAYS+0.01
        GO TO 900
4300    IF(SUM.GE.VALUE) GO TO 4500
        A(1)=KDAYS+0.01
        GO TO 900
C
4500    SUM=PARTD
        KHRS=0
4900  CONTINUE
      CO=SUM
      GO TO 900
C
C MISSING DATA ENCOUNTERED
C
1999  A(1)=-999.0
      CALL MDYH1(IDACC,IHACC,MO,MDAY,MYR,MHR,100,0,INTL)
      MYR=MOD(MYR,100)
      WRITE(IPR,899) TSID,TYPE,IDT,MO,MYR
899   FORMAT(1H0,45H**NOTE** EVAC07 ENCOUNTERED MISSING DATA FOR:,
     * 6H TSID=,2A4,6H TYPE=,A4,5H IDT=,I2,3X,14HINITIAL MONTH=,
     * I2,1H/,I2)
C
C
C ................DEBUG TIME............................................
C
900   IF (IBUG.EQ.0) GO TO 999
C
      WRITE (IODBUG,910) A(1),CO,KDAYS,KHRS
910   FORMAT(// 11X,12H** OUTPUT ** / 1H0,7HA(1) = ,F10.2
     */ 1H0,9HEND CO = ,F10.2,7H KDAYS=,I5,6H KHRS=,I5)
C
C ......................................................................
C
C
C PUT ERROR TRACES BACK IN
C
999   IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      RETURN
      END
