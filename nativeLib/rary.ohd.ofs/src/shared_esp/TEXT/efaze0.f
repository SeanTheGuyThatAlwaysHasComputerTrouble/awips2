C MEMBER EFAZE0
C  (from old member EEFAZE0)
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 10/19/94.07:53:18 BY $WC20SV
C
C @PROCESS LVL(77)
C
      SUBROUTINE EFAZE0
C
C
C   THIS ROUTINE SETS INITIAL VALUES IN COMMON BLOCKS
C
C   THIS ROUTINE WAS WRITTEN GERALD N. DAY .
C
C  MODIFIED TO READ THE MINIMUM OF MRC AND MRCF RATING CURVES
C   INTO COMMON /FRCPTR/ - BY GEORGE F. SMITH - HRL - 2/22/88
C
C  MODIFIED TO IGNORE THE MRC VALUE WHEN READING THE FIRST RECORD
C  IN THE FCRCPTR FILE BECAUSE THIS VALUE IS SET IN MEMBER FCBLOCK.
C     BY DAVID TRAN -HRL- 2/23/94
C
      INCLUDE 'common/espfle'
      INCLUDE 'common/eunit'
      INCLUDE 'common/esprec'
      INCLUDE 'common/eperm'
      INCLUDE 'common/etsdat'
      INCLUDE 'common/fccgd'
      INCLUDE 'common/fccgd1'
      INCLUDE 'common/fcfgs'
      INCLUDE 'common/fcsegp'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fprog'
      INCLUDE 'common/fsglst'
      INCLUDE 'common/fcunit'
      INCLUDE 'common/where'
      INCLUDE 'common/frcptr'
C
      DIMENSION PNAMEX(5),SBNAME(2),OLDOPN(2)
      DIMENSION IBUF(3)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/shared_esp/RCS/efaze0.f,v $
     . $',                                                             '
     .$Id: efaze0.f,v 1.1 1995/09/17 19:18:36 dws Exp $
     . $' /
C    ===================================================================
C
C
C   DATA STATEMENTS USED TO SET VDATE AND PNAME IN CB FPROG
C
      DATA SVDAT1/4H????/,SVDAT2/4H????/
      DATA PNAMEX/4HESP ,4HEXEC,4HUTIO,4HN PR,4HOG  /
C
C   DATA STATEMENTS TO SET PROG NAME
C
      DATA SBNAME/4HEFAZ,4HE0  /
C
      DATA BLANK/4H    /
C
      IOLDOP=IOPNUM
      IOPNUM=0
      DO 10 I=1,2
      OLDOPN(I)=OPNAME(I)
10    OPNAME(I)=SBNAME(I)
C
      IF(ITRACE.GE.1) WRITE (IODBUG,20)
20    FORMAT(1H ,17H** EFAZE0 ENTERED)
C
C   COPY 1ST RECORD OF FILE FCCOGDEF INTO CB FCCGD -
C   STORES GENERAL CO GROUP INFORMATION INCLUDING
C   POINTERS TO INFO FOR INDIVIDUAL CO GROUPS.
C
      CALL UREADT (KFCGD,1,NSLOTS,IERR)
C
C   FILL CO GROUP ID IN CB FCCGD1 WITH BLANKS.
C
      CGIDC(1)=BLANK
      CGIDC(2)=BLANK
C
C   COPY 1ST RECORD OF FILE FCFGSTAT INTO CB FCFGS -
C   STORES INFO CONCERNING STATUS OF FC GROUPS AND
C   POINTERS TO LIST OF SEGMENTS.
C
      CALL UREADT (KFFGST,1,FGID,IERR)
      NFGREC=IDUMYG
C
C   COPY 1ST TWO RECORDS OF FILE FCSEGPTR INTO CB FCSEGP -
C   STORES RECORD INFO ABOUT FILES FCPARAM AND FCSEGSTS.
C
      CALL UREADT (KFSGPT,1,NS,IERR)
      CALL UREADT (KFSGPT,2,NRP,IERR)
C
C   COPY UP TO 1ST 500 RECORDS OF FILE FCFGLIST INTO CB FSGLST -
C   STORES LIST OF SEGMENTS FOR FC GROUP.
C
      NLIST=NS
      IF(MLIST.LT.NLIST) WRITE (IPR,30) NLIST,MLIST
30    FORMAT(1H0,' *** WARNING *** THE NUMBER OF SEGMENTS TO BE READ ',
     .1H(,I4,1H),' EXCEEDS THE MAXIMUM NUMBER THAT CAN BE PROCESSES ',
     .1H(,I3,2H).)
      IF(MLIST.LT.NLIST) NLIST=MLIST
      DO 40 I=1,NLIST
      J=I+2
      CALL UREADT (KFSGPT,J,IDZ(1,I),IERR)
40    CONTINUE
C
C   READ RATING CURVE POINTER FILE
C
      CALL UREADT (KFRCPT,1,IBUF,IERR)
      NRC=IBUF(1)
      MRCF=IBUF(3)
C
C  CAN ONLY READ MINIMUM OF MRC OR MRCF RATING CURVES FROM FILE
C
C     MRC IS THE MAXIMUM NUMBER OF RATING CURVES THAT CAN FIT
C       IN COMMON BLOCK /FRCPTR/
C     MRCF IS THE MAXIMUM NUMBER OF RATING CURVES THAT CAN BE
C       HELD IN FCRCPTR FILE
C
      IRCXX=MIN0(MRC,MRCF)
      IRCRD=IRCXX+1
C
      DO 50 J=2,IRCRD
      I = J-1
      CALL UREADT (KFRCPT,J,RCZZ(1,I),IERR)
50    CONTINUE
C
C   COPY 1ST RECORD OF ESP FILE INTO CB ESPFLE -
C   CONTAINS GENERAL FILE SIZE INFO.
C
      CALL UREADT (KEPARM,1,ESPDAT,IERR)
      MXREC=ESPDAT(1)
      NXREC=ESPDAT(2)
      LRECL=ESPDAT(3)
C
C   SET VALUES IN CB PROG
C
      MAINUM=2
      VERS=3.000
      VDATE(1)=SVDAT1
      VDATE(2)=SVDAT2
      DO 60 I=1,5
60    PNAME(I)=PNAMEX(I)
      NDD=31
C
      IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      RETURN
C
      END
