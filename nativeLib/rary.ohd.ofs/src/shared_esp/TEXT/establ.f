C MEMBER ESTABL
C  (from old member EESTABL)
C
      SUBROUTINE ESTABL(VARNAM,NVAR,UNIT,VAL9,VAL5,VAL1,NWIN,OAVG,BAVG,
     * OB50,BA50,IHYRD,IBHYRD)
C
C ....................................................................
C
C            SUMMARY TABLE SUBROUTINE AT END OF RUN
C
C THIS WILL PRINT OUT A SUMMARY TABLE FOR THE RUN FOR SELECT OUTPUT
C VARIABLES.  SUMMARY INFORMATION INCLUDES OUTPUT VARIABLE ID, FORECAST
C PERIOD(MONTHS), FORECAST VALUES AT THE .01,.50, AND .90 PROBABILITY
C LEVELS FOR THE CONDITIONAL SIMULATED TIME SERIES, AND THE .50 & MEAN
C VALUES FOR THE OBSERVED TIME SERIES AND BASE PERIOD OBSERVED.
C
C ORIGINALLY BY  ED VANBLARGAN - HRL - NOV, 1981
C ......................................................................
C
C VARIABLES:
C VARNAM- OUTPUT VARIABLE ID
C  NVAR - NUMBER ASSIGNED TO THE OUTPUT VARIABLE(1=MXMD,2=MNMD,ETC)
C OVTYP - ARRAY CONTAINING 4 CHARACTER OUTPUT VARIABLE TYPES
C  UNIT - UNITS OF ID
C  VAL9 - FORECASTED VALUE AT .90 EXCEEDANCE PROBABILITY LEVEL
C  VAL5 -    "         "   "  .50      "         "         "
C  VAL1 -    "         "   "  .10      "         "         "
C  OAVG - MEAN VALUE FOR OBSERVED TS
C  OB50 - .50   "   "      "       "
C  BAVG - MEAN  "    "  BASE PERIOD OBSERVED(IF ANY)
C  BA50 - .50   "    "   "     "        "     "   "
C  IHYR - INITIAL YEAR FOR OBSERVED TIME SERIES
C IHYRD - INITIAL YEAR (CORRECTED FOR IF 1ST YEAR WAS EXCLUDED)
C  LHYR - LAST     "    "    "       "     "
C IBHYR - INITIAL  "    "  BASE PERIOD OBSERVED
C IBHYRD- INITIAL YEAR (CORRECTED FOR IF 1ST YEAR WAS EXCLUDED)
C LBHYR - LAST     "    "   "     "      "
C NCALLS- NUMBER OF TIMES THIS ROUTINE WAS CALLED
C  IWJD - ARRAY CONTAINING INITIAL JULIAN DAYS OF WINDOW
C  LWJD -   "       "      LAST      "     "   "    "
C   IWM - INITIAL MONTH OF WINDOW
C   IWD -   "     DAY   "    "
C   IWY -   "     YEAR  "    "
C   LWM - LAST    MONTH "    "
C   LWD -   "     DAY   "    "
C   LWY -   "     YEAR  "    "
C  JBPS - BASE PERIOD SWITCH
C
C
      DIMENSION VARNAM(2),SBNAME(2),OLDOPN(2),MNTH(12),OVTYP(8),
     * XMISS(2)
      CHARACTER*8  FMTA,FMTB
      CHARACTER*32 FMTF
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/where'
      INCLUDE 'common/eoutpt'
      INCLUDE 'common/etime'
      INCLUDE 'common/eunit'
      INCLUDE 'common/eswtch'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/shared_esp/RCS/establ.f,v $
     . $',                                                             '
     .$Id: establ.f,v 1.2 2000/12/19 16:05:17 jgofus Exp $
     . $' /
C    ===================================================================
C
C
      DATA SBNAME/4HESTA,4HBL  /
      DATA INTL/4HINTL/
      DATA OVTYP/4HMXMD,4HMNMD,4HMD  ,4HSUM ,4HMXIN,4HMNIN,
     * 4HNDTO,4HNDIS/
      DATA MNTH/3HJAN,3HFEB,3HMAR,3HAPR,3HMAY,3HJUN,3HJUL,
     * 3HAUG,3HSEP,3HOCT,3HNOV,3HDEC/
      DATA FMTF/'(1H+,T82        T102        1X) '/
      DATA FMTA/',2F10.1,'/
      DATA FMTB/',9X,2A4,'/
      DATA XMISS/4HMISS,4HING /
C
C
C PUT IN ERROR TRACES FOR THIS ROUTINE
C
      IOLDOP=IOPNUM
      IOPNUM=0
      DO 10 I=1,2
        OLDOPN(I)=OPNAME(I)
        OPNAME(I)=SBNAME(I)
10    CONTINUE
C
C GET TRACE LEVEL
C
      IF (ITRACE.GE.1) WRITE(IODBUG,90)
90    FORMAT(1H0,14HESTABL ENTERED)
C
C WRITE HEADERS IF THIS IS FIRST CALL TO THIS ROUTINE
C
      NCALL=NCALL+1
      IF (NCALL.GT.1) GO TO 300
C
      WRITE(KESTBL,200) IHYRD,LHYR
200   FORMAT(/// 25X,32H*** END OF RUN SUMMARY TABLE *** / 40X,3HFOR
     * / 31X,23HSELECT OUTPUT VARIABLES
     * // 1X,51HNOTE: OBSERVED AND BASE PERIOD VALUES SHOW THE MEAN,
     * 38H AND 0.50 EXCEEDANCE PROBABILITY LEVEL
     * / 7X,37HYEARS COVERED BY OBSERVED VALUE    = ,I4,1H-,I4)
C
      IF (JBPS.NE.0) WRITE(KESTBL,210) IBHYRD,LBHYR
210   FORMAT(7X,37HYEARS COVERED BY BASE PERIOD VALUE = ,I4,1H-,I4)
C
      WRITE(KESTBL,230)
230   FORMAT(/ 43X,36HCONDITIONAL SIMULATED TIME SERIES AT
     * / 10X,15HOUTPUT VARIABLE,19X,
     * 31H3 EXCEEDANCE PROBABILITY LEVELS,7X,
     * 20HOBSERVED TIME SERIES ,5X,12HBASE  PERIOD
     * / 11X,13(1H-),19X,37(1H-),3X,18(1H-),5X,15(1H-)
     * / 1X,7HSEGMENT,3X,12HTYPE    I.D.,3X,15HFORECAST PERIOD,
     * 2X,5HUNITS,5X,4H0.90,7X,4H0.50,7X,4H0.10,7X,
     * 4HMEAN,3X,8H.5 VALUE,5X,4HMEAN,3X,8H.5 VALUE
     * / 1X,8(1H*),2X,13(1H*),2X,15(1H*),2X,37(1H*),3X,18(1H*),
     * 5X,15(1H*))
C
C CONVERT JULIAN DAYS TO MO,DAY,YR
C
300   CALL MDYH1(IWJD(NWIN),IWH(NWIN),IWM,IWD,IWY,IWHR,100,0,INTL)
      CALL MDYH1(LWJD(NWIN),LWH(NWIN),LWM,LWD,LWY,LWHR,100,0,INTL)
C
C CONVERT 4 DIGIT YEAR TO 2 DIGITS
C
      IWY=MOD(IWY,100)
      LWY=MOD(LWY,100)
C
C WRITE OUT VALUES.  NEED DIFFERENT FORMAYS FOR MISSING OBSERVED AND
C BASE PERIOD VALUES.
C
      WRITE(KESTBL,500) ISEG,OVTYP(NVAR),VARNAM,IWD,MNTH(IWM),IWY,LWD,
     * MNTH(LWM),LWY,UNIT,VAL9,VAL5,VAL1
500   FORMAT(/ 1X,2A4,2X,A4,1X,2A4,2X,I2,A3,I2,1H-,I2,A3,I2,2X,A4,
     * 3(1X,F10.1))
C
      XOAVG=OAVG
      XOB50=OB50
      XBAVG=BAVG
      XBA50=BA50

      FMTF( 9:16) = FMTA
      FMTF(21:28) = FMTA
      IF (IFMSNG(OAVG).EQ.0) GO TO 600
      XOAVG=XMISS(1)
      XOB50=XMISS(2)
      FMTF( 9:16)=FMTB
C
600   IF(IFMSNG(BAVG).EQ.0 .AND. JBPS.GE.1) GO TO 700
      XBAVG=XMISS(1)
      XBA50=XMISS(2)
      FMTF(21:28)=FMTB
C
700   WRITE(KESTBL,FMTF) XOAVG,XOB50,XBAVG,XBA50
C
      IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      RETURN
      END
