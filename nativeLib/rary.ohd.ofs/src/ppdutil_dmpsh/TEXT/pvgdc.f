C MODULE PVGDC
C-----------------------------------------------------------------------
C
      SUBROUTINE PVGDC (UWC,UWT,JULDB,INTHRB,JULDE,JULHB,JULHE,ICOND)
C
C   IN: UWC ........ UNIT NUMBER TO OUTPUT PROMPTS TO USER - INT
C   IN: UWT ........ UNIT NUMBER FOR OUTPUT TO TERMINAL DEVICE - INT
C  OUT: JULDB ...... BEGINNING ORDINAL DAY-OF-CENTURY (STRTS 1900) - INT
C  OUT: JULDE ...... ENDING ORDINAL DAY-OF-CENTURY (STARTS 1900) - INT
C  OUT: JULHB ...... BEGINNING ORDINAL HR-OF-CENTURY (STARTS 1900) - INT
C  OUT: JULHE ...... ENDING ORDINAL HOUR-OF-CENTURY (STARTS 1900) - INT
C  I/O: ICOND ...... PGM COND: IF NOT 0 SKIP RTN, SET TO 1 FOR ERR - INT
C   IN: NFIELD ..... (FROM COMMON) NUM OF FIELDS IN COMMAND LINE - INT
C   IN: IFSTRT(40) . (FROM COMMON) FIRST CHAR IN COMMND LINE FIELD - INT
C   IN: IFSTOP(40) . (FROM COMMON) LAST CHAR IN COMMAND LINE FIELD - INT
C   IN: IBUF(80) ... (FROM COMMON) PACKED CHAR COMMAND LINE - INT
C   IN: ICD ........ (FROM COMMON) UNIT NUM FOR INPUT (IF USED) - INT
C   IN:                NOTE, ICD IS USED IN ROUTINE RPCARD
C   IN: LP ........ (FROM COMMON) UNIT NUM FOR OUTPT ERR MESSAGES - INT
C
C  CMT: NOTE, IF UWC.NE.UWT THEN NO INTERACTIVE INPUT IS USED.
C  CMT: NOTE, FOR INPUT FIELDS 2 AND OPTIONALLY 3 ARE USED (NFIELD=2
C  CMT:       AND 3).  IF JUST TWO IS USED, THE ENDING DATE IS SET EQUAL
C  CMT:       TO THE BEGINNING DATE.
C  CMT: NOTE, IF NFIELD IS ENTERED AS ZERO, ROUTINE PROMPTS FOR NEW
C  CMT:       COMMAND LINE TO HOLD BEGINNING AND ENDING DATES.
C
      INCLUDE 'uiox'
      INCLUDE 'ufreei'
      INCLUDE 'hclcommon/hdflts'
C
      INTEGER    UWC,UWT
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppdutil_dmpsh/RCS/pvgdc.f,v $
     . $',                                                             '
     .$Id: pvgdc.f,v 1.4 2002/02/11 20:51:17 dws Exp $
     . $' /
C    ===================================================================
C
C
      CALL PVSUBB ('PVGDC   ',ICOND)
C
      ICOND=0
C
C  CHECK NUMBER OF FIELDS
      IF (NFIELD.GE.2) GO TO 20
         IF (UWC.NE.UWT) GO TO 80
         WRITE (UWC,10)
10       FORMAT (' ENTER THE BEGINNING AND ENDING DATES:')
         CALL RPCARD (IBUF,IERR)
         CALL WPCARD (IBUF)
         IF (IERR.NE.0) GO TO 80
         CALL UFREE (1,72)
         N=1
         GO TO 30
C
20    N=2
C
30    IF (NFIELD.LT.N) THEN
         WRITE (LP,40) N,NFIELD
40    FORMAT ('0**ERROR** IN PVGDC - AT LEAST ',I2,' FIELDS EXPECTED ',
     *   'BUT ONLY ',I2,' WERE FOUND.')
         ICOND=1
         GO TO 80
         ENDIF
C
C  GET BEGINNING DATE
      CALL HDATEC (IFSTRT(N),IFSTOP(N),0,0,1,JULDB,INTHRB,JULHB,IERR)
      IF (IERR.NE.0) THEN
         WRITE (LP,50) IERR,'BEGINNING'
50    FORMAT ('0**ERROR** IN PVGDC - STATUS CODE ',I2,' ENCOUNTERED ',
     *   'CALLING ROUTINE HDATEC FOR ',A,'DATE.')
         ICOND=1
         GO TO 80
         ENDIF
C
      N=N+1
      IF (NFIELD.LT.N) THEN
         JULDE=JULDB
         JULHE=JULHB
         GO TO 60
         ENDIF
C
C  GET ENDING DATE
      CALL HDATEC (IFSTRT(N),IFSTOP(N),0,0,1,JULDE,INTHRE,JULHE,IERR)
      IF (IERR.NE.0)  THEN
         WRITE (LP,50) IERR,'ENDING'
         ICOND=1
         GO TO 80
         ENDIF
C
60    CALL MDYH2 (JULDB,INTHRB,IMO1,IDA1,IYR1,IHR1,ITZ,IDSAV,TIME(3))
      CALL MDYH2 (JULDE,INTHRE,IMO2,IDA2,IYR2,IHR2,ITZ,IDSAV,TIME(3))
      WRITE (LP,70) IMO1,IDA1,IYR1,IHR1,TIME(3),
     *   IMO2,IDA2,IYR2,IHR2,TIME(3)
70    FORMAT ('0**NOTE** DATA FOR THE PERIOD ',
     *   I2.2,'/',I2.2,'/',I4.4,'-',I2.2,A4,' THROUGH ',
     *   I2.2,'/',I2.2,'/',I4.4,'-',I2.2,A4,' WILL BE PROCESSED.')
C
80    CALL PVSUBE ('PVGDC   ',ICOND)
C
      RETURN
C
      END
