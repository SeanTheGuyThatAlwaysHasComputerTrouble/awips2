C MEMBER XTIMCK
C  (from old member PPXTIMCK)
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 01/13/95.14:56:49 BY $WC20SV
C
C @PROCESS LVL(77)
C
C
      SUBROUTINE XTIMCK(LINPTZ,IERR)
C..........................................
C     SUBROUTINE CHECKS TIMING VARIABLES AND MAKES SURE DATA
C     ARE AVAILABLE FOR THE RUN PERIOD FOR THE MAP FUNCTION.
C..........................................
C     WRITTEN BY -- ERIC ANDERSON, HRL--FEBRUARY 1983
C..........................................
      DIMENSION PPDB(2),PDDTE(2),SNAME(2),WORK(16),PDFIL(2)
C
C     COMMON BLOCKS
      COMMON/FCTIME/IZDA,IJHRUN,LZDA,LJHRUN,LZDOBS,LJHOBS,NOW(5),
     1  LOCAL,NOUTZ,NOUTDS,NLSTZ,IDA,IHR,LDA,LHR,IZHHDA
      COMMON/IONUM/IN,IPR,IPU
      COMMON/XMDR/MDRSTA,ICTMDR,MDRST6,ICMDR,NCMDR,IRMDR,NRMDR,
     1  NMDR,MDR,MDRNUV
      COMMON/XDISPL/MDRPRT,MDRCOM,IPRT24,IPLT24,IPRT6,IPRSSR,
     1 IPRSRW,MAPPRT,METRIC,LASTDY,IPRDAY,IPLTMP
      COMMON/WHERE/IA(2),IND,SUB(2)
      COMMON/PUDBUG/IOPDBG,IPTRCE,NDBUG,PDBUG(20),IPALL
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_map/RCS/xtimck.f,v $
     . $',                                                             '
     .$Id: xtimck.f,v 1.1 1995/09/17 19:00:18 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA PDDTE/4HRPDD,4HTE  /
      DATA PP24,PPVR/4HPP24,4HPPVR/
      DATA TMDR/4HMDR6/
      DATA SNAME/4HXTIM,4HCK  /
      DATA XTCK,XPCB/4HXTCK,4HXPCB/
      DATA PDFIL/4HRPDF,4HIL  /
C..........................................
C     CHECK TRACE LEVEL
      IF(IPTRCE.GE.1) WRITE(IOPDBG,900)
  900 FORMAT(1H0,17H** XTIMCK ENTERED)
C..........................................
C     FILL WHERE COMMON BLOCK
      SUB(1)=SNAME(1)
      SUB(2)=SNAME(2)
C.......................................
C     CHECK IF DEBUG ON.
      IBUG=0
      IF (IPBUG(XTCK).EQ.1) IBUG=1
      JBUG=0
      IF (IPBUG(XPCB).EQ.1) JBUG=1
      IF ((IBUG.EQ.1).OR.(JBUG.EQ.1)) CALL XDMPCB(0,0,0,0,0,1,0)
C.......................................
C     SET FATAL ERROR INDICATOR AND INITIAL VALUES
      IERR=0
      NWORK=16
C..........................................
C     CHECK IF ENDING TIME IS PRIOR TO STARTING TIME.
      IF(IJHRUN.LT.LJHRUN) GO TO 105
      JD=(IJHRUN-1)/24+1
      JH=IJHRUN-(JD-1)*24
      CALL MDYH2(JD,JH,MO,ID,IY,IH,I,J,LINPTZ)
      JD=(LJHRUN-1)/24+1
      JH=LJHRUN-(JD-1)*24
      CALL MDYH2(JD,JH,KMO,KID,KYR,KIH,I,J,LINPTZ)
      WRITE(IPR,912) MO,ID,IY,IH,LINPTZ,KMO,KID,KYR,KIH,LINPTZ
  912 FORMAT(1H0,29H**FATAL ERROR** STARTING TIME,I3,1H/,I2,1H/,I4,
     1 1H-,I2,A4,1X,24HIS AFTER THE ENDING TIME,I3,1H/,I2,1H/,I4,
     2 1H-,I2,A4)
      CALL KILLFN(8HMAP     )
      IERR=1
      GO TO 99
C..........................................
C     CHECK IF INITIAL JULIAN HOUR IS A MULTIPLE OF 24.
  105 IF((IJHRUN/24)*24.EQ.IJHRUN) GO TO 110
      JD=(IJHRUN-1)/24+1
      JH=IJHRUN-(JD-1)*24
      CALL MDYH2(JD,JH,MO,ID,IY,IH,I,J,LINPTZ)
      WRITE(IPR,903) MO,ID,IY,IH,LINPTZ
  903 FORMAT(1HO,10X,90H**WARNING** THE STARTING TIME FOR MAP DOES NOT C
     1OINCIDE WITH THE START OF A HYDROLOGIC DAY,/16X,
     2 23HTHE STARTING TIME INPUT,I3,1H/,I2,1H/,I4,1H-,I2,A4,1X,
     3 41HWILL BE MOVED TO THE BEGINNING OF THE DAY)
      CALL WARN
      IJHRUN=(JD-1)*24
C..........................................
C     CHECK IF THE LAST JULIAN HOUR IS A MULTIPLE OF SIX.
  110 IF((LJHRUN/6)*6.EQ.LJHRUN) GO TO 115
      JD=(LJHRUN-1)/24+1
      JH=LJHRUN-(JD-1)*24
      CALL MDYH2(JD,JH,MO,ID,IY,IH,I,J,LINPTZ)
      WRITE(IPR,904) MO,ID,IY,IH,LINPTZ
  904 FORMAT(1H0,10X,84H**WARNING** THE ENDING HOUR FOR MAP IS NOT A MUL
     1TIPLE OF 6 WITHIN THE HYDROLOGIC DAY,/16X,14HTHE TIME INPUT,
     2 I3,1H/,I2,1H/,I4,1H-,I2,A4,1X,
     3 42HWILL BE MOVED TO THE NEXT 6 HOUR INCREMENT)
      CALL WARN
      JH=((JH+5)/6)*6
      LJHRUN=(JD-1)*24+JH
  115 LJHOBS=LJHRUN
C..........................................
C     COMPUTE INITIAL JULIAN Z TIME DAY
      JH=IJHRUN+24+IZHHDA
      IZDA=(JH+24)/24
C
C     COMPUTE LAST JULIAN Z TIME DAY.
      JH=((LJHRUN+23)/24)*24
      JH=JH+IZHHDA
      LZDA=(JH+24)/24
      LZDOBS=LZDA
C..........................................
C     CHECK IF 24 AND 6 HOUR PRECIPITATION DATA ARE AVAILABLE
C     FOR THE ENTIRE RUN PERIOD.
      TYPE=PP24
      CALL RPDDTE(TYPE,JFPP24,JLPP24,ISTAT)
      IF(ISTAT.EQ.0) GO TO 120
  118 PPDB(1)=PDDTE(1)
      PPDB(2)=PDDTE(2)
  119 WRITE(IPR,905)PPDB,ISTAT,TYPE
  905 FORMAT(1H0,69H**FATAL ERROR** MAP CANNOT CONTINUE DUE TO ERROR FRO
     1M PPDB,SUBROUTINE,1X,2A4,2X,12HSTATUS CODE=,I2,2X,5HTYPE=,A4)
      CALL KILLFN(8HMAP     )
      IERR=1
      GO TO 99
  120 TYPE=PPVR
      CALL RPDDTE(TYPE,JFPPVR,JLPPVR,ISTAT)
      IF(ISTAT.EQ.0) GO TO 121
      GO TO 118
  121 IF (IBUG.EQ.1) WRITE(IOPDBG,902) JFPP24,JLPP24,JFPPVR,JLPPVR
  902 FORMAT(1H0,20HXTCK DEBUG(XTIMCK)--,4I10)
      IF((IZDA.GT.JLPP24).OR.(LZDA.LT.JFPP24)) GO TO 122
      IF((IZDA.GT.JLPPVR).OR.(LZDA.LT.JFPPVR)) GO TO 122
      GO TO 125
C
C     NO DATA FOR THE RUN PERIOD.
  122 JF=JFPP24
      IF(JFPPVR.GT.JF) JF=JFPPVR
      JL=JLPP24
      CALL MDYH2(JF,0,MO,ID,IY,IH,I,J,LINPTZ)
      CALL MDYH2(JL,0,KMO,KID,KYR,KIH,I,J,LINPTZ)
      WRITE(IPR,906) MO,ID,IY,IH,LINPTZ,KMO,KID,KYR,KIH,LINPTZ
  906 FORMAT(1H0,101H**FATAL ERROR** THERE ARE NO OBSERVED PRECIPITATION
     1 DATA IN THE PPDB FOR THE REQUESTED MAP RUN PERIOD,/6X,
     2 49HOBSERVED DATA EXIST FOR HYDROLOGIC DAYS ENDING ON,I3,1H/,I2,
     3 1H/,I4,1H-,I2,A4,1X,4HTHRU,I3,1H/,I2,1H/,I4,1H-,I2,A4)
      CALL KILLFN(8HMAP     )
      IERR=1
      GO TO 99
  125 IF((IZDA.LT.JFPP24).OR.(IZDA.LT.JFPPVR)) GO TO 126
  128 IF(LZDA.GT.JLPP24) GO TO 127
      GO TO 130
C
C     RUN PERIOD STARTS BEFORE DATA PERIOD
  126 JF=JFPP24
      IF(JFPPVR.GT.JF) JF=JFPPVR
      K=JF-IZDA
      WRITE(IPR,907)K
  907 FORMAT(1H0,10X,37H**WARNING** MAP IS REQUESTED TO START,1X,
     1 I3,1X,42HDAYS BEFORE DATA ARE AVAILABLE ON THE PPDB,/16X,
     262HTHE START DATE IS ADJUSTED TO THE BEGINNING OF THE DATA PERIOD)
      CALL WARN
      IZDA=JF
      IJHRUN=IJHRUN+24*K
      GO TO 128
C
C     RUN ENDS AFTER DATA PERIOD
  127 JL=JLPP24
      K=LZDA-JL
      WRITE(IPR,908)K
  908 FORMAT(1H0,10X,35H**WARNING** MAP IS REQUESTED TO END,1X,I3,
     1 1X,41HDAYS AFTER DATA ARE AVAILABLE ON THE PPDB,/16X,
     2 54HTHE END DATE IS ADJUSTED TO THE END OF THE DATA PERIOD)
      CALL WARN
      LZDA=JL
      LZDOBS=LZDA
      JD=(LJHRUN-1)/24+1
      LJHRUN=JD*24-24*K
      LJHOBS=LJHRUN
C..........................................
C     CHECK THAT RUN DOES NOT EXCEED 10 DAYS
  130 LRUN=LZDA-IZDA+1
      IF(LRUN.LE.10) GO TO 131
      K=LRUN-10
      WRITE(IPR,909)K
  909 FORMAT(1H0,10X,77H**WARNING** THE LENGTH OF THE MAP RUN EXCEEDS 10
     1 DAYS.  THE RUN IS SHORTEN BY,1X,I3,1X,5HDAYS.)
      CALL WARN
      LZDA=LZDA-K
      LZDOBS=LZDA
      JD=(LJHRUN-1)/24+1
      LJHRUN=JD*24-24*K
      LJHOBS=LJHRUN
C..........................................
C     COMPUTE IDA,IHR,LDA AND LHR.
  131 IDA=IJHRUN/24+1
      IHR=6
      LDA=(LJHRUN-1)/24+1
      LHR=LJHRUN-(LDA-1)*24
      IF ((IBUG.EQ.1).OR.(JBUG.EQ.1)) CALL XDMPCB(0,0,0,0,0,1,0)
C..........................................
C     CHECK IF MDR DATA NEEDED FOR THIS RUN
      MDR=0
C
C     CHECK NON-UNIVERSAL TECHNIQUES
      MDRNUV=0
      MDRONY=0
      MDRDST=0
      CALL HCNUNV (8HMDRONLY ,0,NWORK,WORK,IHNUNV)
      IF(IHNUNV.LE.0) GO TO 132
      MDR=1
      MDRONY=1
  132 CALL HCNUNV (8HMDRDIST ,0,NWORK,WORK,IHNUNV)
      IF(IHNUNV.LE.0) GO TO 133
      MDR=1
      MDRDST=2
  133 MDRNUV=MDRONY+MDRDST
C
C     CHECK UNIVERSAL TECHNIQUES
      IF(MDRSTA.GT.0) MDR=1
      IF(MDRST6.GT.0) MDR=1
      IF(MDRPRT.GT.0) MDR=1
      IF(MDRCOM.GT.0) MDR=1
C
C     CHECK IF MDR DATA AVAILABLE
      IF(MDR.EQ.0) GO TO 99
      TYPE=TMDR
      CALL RPDDTE(TYPE,JF,JL,ISTAT)
      IF(ISTAT.EQ.0) GO TO 135
      IF(ISTAT.EQ.2) GO TO 136
      IF(ISTAT.EQ.3) GO TO 134
      GO TO 118
  135 IF (IBUG.EQ.1) WRITE(IOPDBG,902) JF,JL
      IF ((IBUG.EQ.1).OR.(JBUG.EQ.1)) CALL XDMPCB(0,1,0,0,0,0,0)
C
C     CHECK MDR DATES
      IF((JF.LE.IZDA).AND.(JL.GE.LZDA)) GO TO 138
C
C     MDR NOT AVAILABLE FOR ENTIRE PERIOD.
      CALL MDYH2(JF,1,MO,ID,IY,IH,I,J,LINPTZ)
      CALL MDYH2(JL,1,KMO,KID,KYR,KIH,I,J,LINPTZ)
      WRITE(IPR,910)MO,ID,IY,LINPTZ,KMO,KID,KYR,LINPTZ
  910 FORMAT(1H0,10X,96H**WARNING** MDR DATA ARE NOT AVAILABLE FOR THE E
     1NTIRE RUN PERIOD--ALL MDR OPTIONS ARE TURNED OFF,/16X,
     2 49HMDR DATA AVAILABLE FOR HYDROLOGIC DAYS ENDING ON ,I3,1H/,
     3 I2,1H/,I4,1X,A4,1X,4HTHRU,I3,1H/,I2,1H/,I4,1X,A4)
      CALL WARN
      GO TO 137
C
C     CHECK IF ANY MDR DATA HAVE BEEN POSTED.
 138  CALL RPDFIL(TYPE,IZDA,JF,JL,ISTAT)
      IF(ISTAT.EQ.0) GO TO 139
      PPDB(1)=PDFIL(1)
      PPDB(2)=PDFIL(2)
      GO TO 119
 139  IF(JL.GT.0) GO TO 99
  134 WRITE(IPR,913)
  913 FORMAT(1H0,10X,94H**WARNING** MDR RELATED OPTIONS CANNOT BE USED S
     1INCE NO MDR DATA HAVE BEEN WRITTEN TO THE PPDB,/16X,
     230HALL MDR OPTIONS ARE TURNED OFF)
      CALL WARN
      GO TO 137
C
C     MDR DATA NOT DEFINED
  136 WRITE(IPR,911)
  911 FORMAT(1H0,10X,89H**WARNING** MDR RELATED OPTIONS CANNOT BE USED S
     1INCE MDR DATA ARE NOT DEFINED ON THE PPDB,/16X,
     2 30HALL MDR OPTIONS ARE TURNED OFF)
      CALL WARN
C
C     TURN ALL MDR OPTIONS OFF
  137 MDR=0
      MDRNUV=0
      MDRSTA=0
      MDRST6=0
      MDRPRT=0
      MDRCOM=0
C     END OF TIME CHECKS
C..........................................
   99 IF(IPTRCE.GE.1) WRITE(IOPDBG,901)
  901 FORMAT(1H0,14H** EXIT XTIMCK)
C
      RETURN
C
      END
