C MEMBER XPPEST
C  (from old member PPXPPEST)
C
      SUBROUTINE XPPEST(PP24,PT24,PPVR,PTVR,CHAR,STBN,OP24,OPVR,WORK,
     1IWORK,LWORK,IERR)
C.......................................
C     THIS SUBROUTINE IS THE CONTROL SUBROUTINE FOR THE PORTION
C     OF THE MAP FUNCTION THAT CHECKS ALL OBSERVED DATA AND
C     ESTIMATES ALL MISSING DATA FOR A DAY.
C.......................................
C     WRITTEN BY -- ERIC ANDERSON, HRL -- FEBRUARY 1983
C.......................................
      INTEGER*2 PP24(1),PT24(1),PPVR(1),PTVR(1),CHAR(1)
      INTEGER*2 MSNG24,MSNG6,MSGMDR,MSNGSR,IPPMAX,ISUM
      INTEGER*2 OP24(1),OPVR(1)
      DIMENSION STBN(1),WORK(1),SNAME(2),STA(2)
C
C     COMMON BLOCKS
      COMMON/IONUM/IN,IPR,IPU
      COMMON/PUDBUG/IOPDBG,IPTRCE,NDBUG,PDBUG(20),IPALL
      COMMON/WHERE/ID(2),IND,SUB(2)
      COMMON/XOPT/IXTYPE,XNAME(2),IESTPX,ICONVC,CNVDIS,CVDISW,
     1  NOEST6,IUSESR,PPUSER(2),IMOSUM,IMOWTR,IWTEST
      COMMON/XOPT2/ICKTIM,ICKMAX,PPMAX,NPLT24,IXFUT(6)
      COMMON/XMDR/MDRSTA,ICTMDR,MDRST6,ICMDR,NCMDR,IRMDR,NRMDR,
     1 NMDR,MDR,MDRNUV
      COMMON/XTIME/KZDA,KDA,KHR,LSTMO,KMO,KID,KYR,KIH,TZCODE,ISW,
     1  IUTMP,NSSR,IDAY
      COMMON/XSIZE/NMAP,NDUPL,NSLT24,NSLOT6,LDATA6,LMDR,LPPSR,
     1  MSNG24,MSNG6,MSGMDR,MSNGSR,NRECTP,MXTEMP,SMALL
      COMMON/XCPU/ICKCPU,ICPU,KCPU
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_map/RCS/xppest.f,v $
     . $',                                                             '
     .$Id: xppest.f,v 1.1 1995/09/17 19:00:07 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA SNAME,XEST/4HXPPE,4HST  ,4HXEST/
      DATA APPVR,APP24,AMDR,PPSR/4HPPVR,4HPP24,4HMDR6,4HPPSR/
      DATA XMDR/4HXMDR/
      DATA XRPP,XSSR,XPCK,XPOS/4HXRPP,4HXSSR,4HXPCK,4HXPOS/
      DATA PCPN,BLANK/4HPCPN,4H    /
C.......................................
C     CHECK TRACE LEVEL
      IF(IPTRCE.GE.1) WRITE(IOPDBG,900)
  900 FORMAT(1H0,17H** XPPEST ENTERED)
C.......................................
C     FILL WHERE COMMON BLOCK
      IND=-1
      SUB(1)=SNAME(1)
      SUB(2)=SNAME(2)
C.......................................
C     CHECK IF DEBUG IS ON
      IBUG=0
      IF(IPBUG(XEST).EQ.1) IBUG=1
      JBUG=0
      IF(IPBUG(XMDR).EQ.1) JBUG=1
      KBUG=0
      IF (IPBUG(XRPP).EQ.1) KBUG=1
      LBUG=0
      IF (IPBUG(XSSR).EQ.1) LBUG=1
      MBUG=0
      IF (IPBUG(XPCK).EQ.1) MBUG=1
      NBUG=0
      IF (IPBUG(XPOS).EQ.1) NBUG=1
C.......................................
C     INITIAL VALUES
      IERR=0
      LBUF=1
      LMDR=0
      LSSR=0
      NW=1
      LW=LWORK
      IF (ICKMAX.NE.0) IPPMAX=PPMAX*100.0+0.1
      IPMX24=0
      IPMX6=0
C.......................................
C     READ SIX-HOUR PRECIPITATION DATA FOR THE CURRENT DAY
      TYPE=APPVR
      LPNTRS=NSLOT6*4
      CALL RPDDLY(TYPE,KZDA,IDAY,LPNTRS,PTVR,LPF6,LDATA6,
     1 PPVR,LDF6,NUM,MSNG6,LBUF,BUF,ISTAT)
      IF(ISTAT.EQ.0) GO TO 105
      CALL PSTRDD(KZDA,ISTAT,LPNTRS,LDATA6,TYPE)
  104 WRITE(IPR,901)
  901 FORMAT(1H0,40H**FATAL ERROR** THE ABOVE ERROR IS FATAL)
      CALL KILLFN(8HMAP     )
      IERR=1
      GO TO 99
  105 IF(KBUG.EQ.1) CALL PDMPDY(TYPE,KZDA,MSNG6,PTVR,LPF6,PPVR,LDF6)
C.......................................
C     READ 24 HOUR PRECIPITATION DATA FOR THE CURRENT DAY.
      TYPE=APP24
      LPNTRS=NSLT24*5
      CALL RPDDLY(TYPE,KZDA,IDAY,LPNTRS,PT24,LPF24,NSLT24,PP24,
     1 LDF24,NUM,MSNG24,LBUF,BUF,ISTAT)
      IF(ISTAT.EQ.0) GO TO 119
      CALL PSTRDD(KZDA,ISTAT,LPNTRS,NSLT24,TYPE)
      GO TO 104
  119 IF(KBUG.EQ.1) CALL PDMPDY(TYPE,KZDA,MSNG24,PT24,LPF24,PP24,LDF24)
      LHRCDE=(KHR/3)
      IF (KHR.EQ.24) LHRCDE=0
      DO 118 I=1,NSLT24
      IF (PP24(I).EQ.MSNG24) GO TO 117
      ICODE=PP24(I)-((PP24(I)/10)*10)
      ICODE=IABS(ICODE)
      IF (ICODE.EQ.8) GO TO 117
      IF (ICKTIM.EQ.0) GO TO 116
      IF (ICODE.NE.LHRCDE) GO TO 117
  116 PP24(I)=(PP24(I)/10)+3000
      IF ((ICKMAX.EQ.0).OR.(PPMAX.EQ.0.0)) GO TO 118
C     CHECK IF DAILY PRECIP EXCEEDS MAX SPECIFIED
      IF (PP24(I).LE.IPPMAX) GO TO 118
      J=(I-1)*5+1
      L=PT24(J)
      L=IABS(L)
      IF (L.EQ.0) GO TO 118
      IF (IPMX24.EQ.1) GO TO 106
      IPMX24=1
      WRITE(IPR,906) PPMAX
  906 FORMAT(1H1,10X,52H**WARNING** THE FOLLOWING DAILY PRECIP TOTALS EX
     1CEED,1X,F5.2,1X,38HINCHES--THE SPECIFIED MAX FOR THE RUN.,
     2/16X,36HTHESE VALUES WILL BE SET TO MISSING.)
  106 RPP24=PP24(I)*0.01
      STA(1)=BLANK
      STA(2)=BLANK
      L=-L
      CALL RPPREC(STA,PCPN,L,LWORK,WORK(1),NFILL,J,ISTAT)
      IF (ISTAT.EQ.0) GO TO 107
      CALL PSTRDC(ISTAT,PCPN,STA,L,LWORK,NFILL)
      GO TO 104
  107 WRITE(IPR,907) STA,(WORK(J),J=5,9),RPP24
  907 FORMAT(1H ,20X,2A4,2X,1H(,5A4,1H),3X,F5.2,1X,6HINCHES)
  117 PP24(I)=-9999
  118 CONTINUE
      IF (IPMX24.EQ.1) CALL WARN
      MSNG24=-9999
      IF(KBUG.EQ.1) CALL PDMPDY(TYPE,KZDA,MSNG24,PT24,LPF24,PP24,LDF24)
C.......................................
C     CHECK IF PPVR TOTALS EXCEED THE MAX SPECIFIED
      IF ((ICKMAX.EQ.0).OR.(PPMAX.EQ.0.0)) GO TO 114
      DO 113 I=1,NSLOT6
      L=(I-1)*4+1
      IF (PTVR(L).EQ.0) GO TO 113
      L24=PTVR(L+1)
      ISUM=0
      I1=PTVR(L+3)
      I2=PTVR(L+2)
      I2=I1+(KHR/I2)-1
      DO 112 J=I1,I2
      IF (PPVR(J).EQ.MSNG6) GO TO 112
      ISUM=ISUM+PPVR(J)
112   CONTINUE
      IF (ISUM.LE.IPPMAX) GO TO 113
      L=PT24(L24)
      L=IABS(L)
      IF (IPMX6.EQ.1) GO TO 111
      IPMX6=1
      IF (IPMX24.EQ.0) WRITE (IPR,909)
909   FORMAT (1H1)
      WRITE(IPR,908) PPMAX
  908 FORMAT(1H0,10X,60H**WARNING** THE FOLLOWING LESS THAN 24-HR PRECIP
     1 SUMS EXCEED,1X,F5.2,1X,38HINCHES--THE SPECIFIED MAX FOR THE RUN.,
     2/16X,36HTHESE VALUES WILL BE SET TO MISSING.)
  111 RPP24=ISUM*0.01
      STA(1)=BLANK
      STA(2)=BLANK
      L=-L
      CALL RPPREC(STA,PCPN,L,LWORK,WORK(1),NFILL,J,ISTAT)
      IF (ISTAT.EQ.0) GO TO 103
      CALL PSTRDC(ISTAT,PCPN,STA,L,LWORK,NFILL)
      GO TO 104
  103 WRITE(IPR,907) STA,(WORK(J),J=5,9),RPP24
      DO 102 J=I1,I2
  102 PPVR(J)=MSNG6
  113 CONTINUE
      IF (IPMX6.EQ.1) CALL WARN
C.......................................
C     CHECK IF MDR IS TO BE USED--IF SO, READ MDR AND CONVERT
C     MDR TO PRECIPITATION.
  114 IF(MDR.EQ.0) GO TO 120
C
C     READ MDR FOR THE CURRENT DAY
      TYPE=AMDR
      I=0
      L=LW*2
      J=0
      CALL RPDDLY(TYPE,KZDA,I,J,WORK(J),LPFMDR,L,WORK(1),LDFMDR,
     1 NUM,MSGMDR,LBUF,BUF,ISTAT)
      IF(ISTAT.EQ.0) GO TO 110
      CALL PSTRDD(KZDA,ISTAT,J,L,TYPE)
      GO TO 104
  110 IF (JBUG.EQ.1) CALL PDMPDY(TYPE,KZDA,MSGMDR,WORK(J),LPFMDR,
     1  WORK(1),LDFMDR)
      LMDR=IWORK
C
C     CHECK IF MDR SPACE IS FULL
      LDMDR=NMDR*4
      IF(LDFMDR.EQ.LDMDR) GO TO 115
C
C     REMOVE EXTRA SPACE IN MDR DATA ARRAY
      CALL XMDRPK(WORK(1),LDFMDR,NMDR)
      IF (JBUG.EQ.1) CALL PDMPDY(TYPE,KZDA,MSGMDR,WORK(J),LPFMDR,
     1  WORK(1),LDFMDR)
  115 NW=NW+LDMDR/2
      LW=LWORK-NW+1
C
C    CONVERT MDR TO PRECIPITATION AMOUNTS AND DISPLAY IF REQUESTED.
      IF (ICKCPU.EQ.1) CALL XCKCPU(8HBEFORE  ,8HXMDRCV  )
      CALL XMDRCV(PPVR,PTVR,STBN,WORK(1),PT24,WORK(NW),WORK(NW),LW)
      IF (ICKCPU.EQ.1) CALL XCKCPU(8HAFTER   ,8HXMDRCV  )
      IND=-1
      SUB(1)=SNAME(1)
      SUB(2)=SNAME(2)
      IF (JBUG.EQ.1) CALL PDMPDY(TYPE,KZDA,MSGMDR,WORK(J),LPFMDR,
     1  WORK(1),LDFMDR)
C.......................................
C     IF STRANGER REPORTS USED TO ESTIMATE MISSING POINT DATA, READ
C        STRANGER REPORTS FOR THE CURRENT DAY.
  120 IF (IUSESR.EQ.0) GO TO 125
      TYPE=PPSR
      I=0
      J=0
      L=LW*2
      CALL RPDDLY(TYPE,KZDA,I,J,WORK(J),LPFSSR,L,WORK(NW),LDFSSR,
     1 NSSR,MSNGSR,LBUF,BUF,ISTAT)
      IF ((ISTAT.EQ.6).OR.(NSSR.EQ.0)) GO TO 121
      IF(ISTAT.EQ.0) GO TO 122
      CALL PSTRDD(KZDA,ISTAT,J,L,TYPE)
      WRITE(IPR,902) KMO,KID,KYR,KIH,TZCODE
  902 FORMAT(1H0,10X,58H**WARNING** NO STRANGER REPORTS USED FOR THE DAY
     1 ENDING ON, I3,1H/,I2,1H/,I4,1H-,I2,A4,1X,
     2 23HDUE TO PRECEEDING ERROR)
      CALL WARN
  121 NSSR=0
      LPPSR=0
      GO TO 125
  122 IF(LBUG.EQ.1)CALL PDMPDY(TYPE,KZDA,MSNGSR,WORK(J),LPFSSR,
     1 WORK(NW),LDFSSR)
      LSSR=NW
      LPPSR=IWORK+NW-1
      NW=NW+((NSSR*3)-1)/2+1
      LW=LWORK-NW+1
C     DECODE STRANGER REPORTS.
      CALL XDPPSR(NSSR,WORK(LSSR),MSNGSR,LHRCDE,KHR)
      IF (LBUG.EQ.1) CALL PDMPDY(TYPE,KZDA,MSNGSR,WORK(J),LPFSSR,
     1 WORK(LSSR),LDFSSR)
C.......................................
C     CHECK PRECIPITATION DATA -- APPLY CORRECTIONS AND SET TO ZERO
C          WHEN INDICATED.
  125 IF (ICKCPU.EQ.1) CALL XCKCPU(8HBEFORE  ,8HXPPCHK  )
      CALL XPPCHK(PP24,PT24,PPVR,PTVR,WORK(1),CHAR,WORK(NW),LW,IERR)
      IF (ICKCPU.EQ.1) CALL XCKCPU(8HAFTER   ,8HXPPCHK  )
      IF(MBUG.EQ.0) GO TO 130
      CALL PDMPDY(APP24,KZDA,MSNG24,PT24,LPF24,PP24,LDF24)
      CALL PDMPDY(APPVR,KZDA,MSNG6,PTVR,LPF6,PPVR,LDF6)
  130 IF (IERR.EQ.1) GO TO 99
C.......................................
C     ESTIMATE MISSING FOR STATIONS WITH ONLY 24 HOUR DATA.
C         PRINT OBSERVED AND ESTIMATED IF REQUESTED.
      IF (ICKCPU.EQ.1) CALL XCKCPU(8HBEFORE  ,8HXEST24  )
      CALL XEST24(PP24,PT24,WORK(1),WORK(LSSR),CHAR,OP24,WORK(NW),
     1  LW,IERR)
      IF (ICKCPU.EQ.1) CALL XCKCPU(8HAFTER   ,8HXEST24  )
      IF(IBUG.EQ.0) GO TO 131
      CALL PDMPDY(APP24,KZDA,MSNG24,PT24,LPF24,PP24,LDF24)
  131 IF(IERR.EQ.1) GO TO 99
C.......................................
C     ESTIMATE MISSING DATA FOR STATIONS WITH BOTH 6 AND 24 HOUR REPORTS
C          PRINT OBSERVED AND ESTIMATED IF REQUESTED.
      IF (ICKCPU.EQ.1) CALL XCKCPU(8HBEFORE  ,8HXEST6   )
      CALL XEST6(PP24,PT24,PPVR,PTVR,WORK(1),WORK(LSSR),CHAR,OPVR,
     1  WORK(NW),LW,IERR)
      IF (ICKCPU.EQ.1) CALL XCKCPU(8HAFTER   ,8HXEST6   )
      IF(IBUG.EQ.0) GO TO 132
      CALL PDMPDY(APP24,KZDA,MSNG24,PT24,LPF24,PP24,LDF24)
      CALL PDMPDY(APPVR,KZDA,MSNG6,PTVR,LPF6,PPVR,LDF6)
  132 IF(IERR.EQ.1) GO TO 99
C.......................................
C     WRITE ESTIMATED 24-HOUR VALUES TO PPDB IF REQUESTED.
      IF(IWTEST.EQ.0) GO TO 135
      LW2=LW*2
      CALL WPD24P(KZDA,NSLT24,PP24,LW2,WORK(NW),ISTAT)
      IF(ISTAT.EQ.0) GO TO 134
      IF(ISTAT.EQ.1) GO TO 133
      WRITE(IPR,904)
 904  FORMAT(1H0,10X,79H**WARNING** INSUFFICIENT WORK SPACE TO WRITE EST
     1IMATED 24-HOUR DATA TO THE PPDB,/16X,
     229HTECHNIQUE WTEST24 TURNED OFF.)
      CALL WARN
      IWTEST=0
      GO TO 134
 133  WRITE(IPR,905)
 905  FORMAT(1H0,10X,93H**WARNING** ATTEMPT WAS MADE TO REPLACE AN OBSER
     1VED DAILY VALUE ON THE PPDB WITH AN ESTIMATE.)
      CALL WARN
 134  IF(IBUG.EQ.0) GO TO 135
      CALL PDMPDY(APP24,KZDA,MSNG24,PT24,LPF24,PP24,LDF24)
C.......................................
C     SET ALL ESTIMATES STORED AS NEGATIVES TO POSITIVE NOW THAT
C          ALL ESTIMATES COMPLETE.
  135 IF (ICKCPU.EQ.1) CALL XCKCPU(8HBEFORE  ,8HXSTPOS  )
      CALL XSTPOS(PP24,PT24,PPVR,PTVR,KHR)
      IF (ICKCPU.EQ.1) CALL XCKCPU(8HAFTER   ,8HXSTPOS  )
      IF(NBUG.EQ.0) GO TO 99
      CALL PDMPDY(APP24,KZDA,MSNG24,PT24,LPF24,PP24,LDF24)
      CALL PDMPDY(APPVR,KZDA,MSNG6,PTVR,LPF6,PPVR,LDF6)
C     ALL ESTIMATES COMPLETE FOR THE DAY.
C.......................................
   99 IF(IPTRCE.GE.1) WRITE(IOPDBG,903)
  903 FORMAT(1H0,14H** EXIT XPPEST)
      RETURN
      END
