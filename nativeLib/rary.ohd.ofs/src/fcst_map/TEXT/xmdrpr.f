C MODULE XMDRPR
C***********************************************************************
C    XMDRPR: PRINTS COMPARISON TABLE OF MDR DERIVED PRECIP AND OBSERVED
C            PRECIP
C
C SUBROUTINE ORIGINALLY BY ED VANBLARGAN - HRL - 4/83
C.......................................................................
C
C THIS ROUTINE PRINTS THE MDR DERIVED PRECIP AND OBSERVED PRECIP FOR
C THE 6-HR STATIONS. STATION MUST HAVE AT LEAST 1 NON-ZERO VALUE TO
C BE PRINTED. THE PRINTOUT INCLUDES THE STATION NAME AND NUMBER
C WITH THE VALUES FOR EACH 6-HR PERIOD PLUS A SUM TOTAL.
C A VARIABLE FORMAT IS USED SINCE MISSING VALUES ARE POSSIBLE.
C THE HEADING WAS WRIITTEN BY CALLING PROGRAM AND THE PRECIP VALUES
C ARE PASSED IN. STATION PARAMETERS ARE OBTAINED BY READING THE PPDB.
C
C ARGUMENT LIST
C
C NAME  I/O TYPE DIM DESC
C ----  --- ---- --- ------------------------------------------
C PPVR   I   I*2 VAR 6-HR PRECIP ARRAY (SEE MAIN MDR SUBROUTINE)
C PTVR   I   I*2 VAR 6-HR POINTER ARRAY (SEE MAIN MDR SUBROUTINE)
C MDR6   I   I*2 VAR 6-HR MDR PRECIP ARRAY (SEE MAIN MDR SUBROUTINE)
C PT24   I   I*2 VAR 24 HR POINTER ARRAY (SEE MAIN MDR SUBROUTINE)
C PGENL  I   R*4 LPG WORK SPACE ARRAY FOR STATION GENL PARAMETERS
C LPG    I    I    1 LENGTH OF PGENL
C
C INTERNAL VARIABLES
C ------------------
C MISSO  - MISSING FLAG FOR OBSERVED PRECIP (1=MISSING 0=NON-MISS)
C MISSM  -   "      "    "    MDR    DATA             "
C NOZER  - OBSERVED PRECIP IS ZERO FLAG (0=YES 1=NO)
C NMZER  -  MDR     VALUE   "  "    "       "
C POBS   - ARRAY CONTAINING PRECIP VALUES FOR EACH 6-HR PERIOD
C PMDR   -   "       "       MDR VALUES    "   "    "    "
C SUMOBS - SUM OF PRECIP VALUES IN POBS
C SUMMDR -  "  "  MDR VALUES    IN PMDR
C LXXXX  - POINTERS FOR ARRAYS PTVR,PT24,MDR6,PPVR (WHERE XXXX CAN BE
C          PTVR,PT24,OCMDR,PP RESPECTIVELY)
C***********************************************************************
      SUBROUTINE XMDRPR(PPVR,PTVR,MDR6,PT24,PGENL,LPG)
C
      CHARACTER*8   FMT(15),FORMS(5)
      INTEGER*2 PPVR,PTVR,PT24,MDR6
      DIMENSION PPVR(*),PTVR(*),MDR6(*),POBS(4),PMDR(4),PT24(*),
     $ PGENL(*),STNID(2)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/pudbug'
      INCLUDE 'common/xmdr'
      INCLUDE 'common/xtime'
      INCLUDE 'common/xsize'
      INCLUDE 'common/xopt'
      INCLUDE 'common/xmday'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_map/RCS/xmdrpr.f,v $
     . $',                                                             '
     .$Id: xmdrpr.f,v 1.2 2000/12/19 16:02:30 jgofus Exp $
     . $' /
C    ===================================================================
C
C
      DATA BLANK,AGEN/4H    ,4HGENL/,AMSG,AOBS/4HMSNG,4HOBS./,
     $ AMDR/4HMDR /,SNAME/4HXMDR/,
     $ FMT(1),FMT(2),FMT(3)/8H(1H0,5A4,8H,1X,2A4,,8H 2X,A4 ,/,
     $     FORMS/8H,1X,F5.2,8H ,2X,A4 ,8H /32X,A4,8H   )    ,
     $ 8H        /
C
C INITIALIZE VARIABLES
C
      IBUG=IPBUG(SNAME)
      IF (IPTRCE.GE.1) WRITE(IOPDBG,805)
      NFMT=15
      NPER=KHR/6
      LPTVR=-3
      NSTN=0
      IFOUND=0
C
C ** LOOP THRU THE STATIONS
C
      DO 790 N=1,NSLOT6
C CHECK IF STATION DEFINED AND SET UP POINTERS
      LPTVR=LPTVR+4
      IF (PTVR(LPTVR).EQ.0) GO TO 790
      LPT24=PTVR(LPTVR+1)
      LPP  =PTVR(LPTVR+3)
C CHECK TIME INTERVAL (IDT), IF .GT. 6 DO NOT USE
C ALSO, SET NUMBER OF OBS VALUES PER 6 HR PERIOD (NUMVAL)
      IDT=PTVR(LPTVR+2)
      IF (IDT.GT.6) GO TO 790
      NUMVAL=6/IDT
C GET MDRBOX AND CHECK IF VALID
      MDRBOX=PT24(LPT24+4)
      IF (MDRBOX.EQ.0) GO TO 790
      CALL XMDRAL(MDRBOX,LOCMDR,ISTAT)
      IF (ISTAT.NE.0) GO TO 790
C
C NOW DO NESTLED LOOP THRU EACH PERIOD TO CHECK IF STATION HAS PRECIP
C OR IS ZERO OR MISSING. DO THE FOLLOWING FOR EACH PERIOD:
C  A. CHECK FOR MISSING(BOTH OBS AND MDR BUT DO SEPARATELY),
C  IF VALUE IS MISSING THEN...
C  B. -SET MISSING FLAG
C     -SET PRECIP VALUE TO MISSING
C     -ADJUST VARIABLE FORMAT
C     -GO TO NEXT PERIOD
C IF VALUE NOT MISSING THEN...
C C. -CONVERT VALUE TO PRECIP
C    -CHECK IF VALUE IS NONZERO AND IF SO SET NONZERO FLAG
C    -ADD PRECIP VALUE INTO SUM, IF AND ONLY IF MISSING FLAG NOT ON
C
      MISSO=0
      MISSM=0
      NOZER=0
      NMZER=0
      SUMOBS=0
      SUMMDR=0
C
C **NESTLED LOOP THRU EACH PERIOD
      DO 770 IPER=1,NPER
      POBS(IPER)=0.0
      LOC=LPP+(IPER-1)*NUMVAL-1
C GO THRU LOOP TO SUM OBS INTO 6 HR VALUES.
C IF ANY OBS MISSING THEN ENTIRE PERIOD SET TO MISSING
      DO 500 NV=1,NUMVAL
      IF (PPVR(LOC+NV).EQ.MSNG6) GO TO 710
      POBS(IPER)=POBS(IPER)+PPVR(LOC+NV)/100.
500   CONTINUE
C NON-MSG OBS
      FMT(4+IPER)=FORMS(1)
      IF (POBS(IPER).GE.0.001) NOZER=1
      IF (MISSO.EQ.0) SUMOBS=SUMOBS+POBS(IPER)
      GO TO 720
C MISSING OBS
710   MISSO=MISSO+1
      POBS(IPER)=AMSG
      FMT(4+IPER)=FORMS(2)
C MDR.
720   LOC=LOCMDR+NMDR*(IPER-1)
      IF (MDR6(LOC).NE.MSNG6) GO TO 730
C MISSING MDR
      MISSM=MISSM+1
      PMDR(IPER)=AMSG
      FMT(6+NPER+IPER)=FORMS(2)
      GO TO 770
C NON-MSG MDR
730   PMDR(IPER)=MDR6(LOC)/100.
      FMT(6+NPER+IPER)=FORMS(1)
      IF (PMDR(IPER).GE.0.001) NMZER=1
      IF (MISSM.EQ.0) SUMMDR=SUMMDR+PMDR(IPER)
770   CONTINUE
C **END NESTLED LOOP
C
      FMT(4)=FORMS(1)
      IF (MISSO.EQ.0) GO TO 775
      SUMOBS=AMSG
      FMT(4)=FORMS(2)
775   FMT(5+NPER)=FORMS(3)
      FMT(6+NPER)=FORMS(1)
      IF (MISSM.EQ.0) GO TO 780
      SUMMDR=AMSG
      FMT(6+NPER)=FORMS(2)
780   FMT(7+NPER*2)=FORMS(4)
      LASTF=7+NPER*2+1
      IF (LASTF.GT.NFMT) GO TO 785
      DO 783 L=LASTF,NFMT
783   FMT(L)=FORMS(5)
C
C NOW HAVE ALL PERIODS FOR THIS STATION,
C SO, CHECK IF A NON-ZERO VALUE WAS FOUND OR ALL MDR OR PRECIP
C ARE NOT MISSING.
C IF NOT THEN...SKIP TO NEXT STATION
C IF SO, THEN...CALL ROUTINE TO FILL STATION GENL PARAMETERS AND
C               WRITE OUT THE INFO FOR THIS STATION
C
785   IF (NOZER.EQ.0 .AND.NMZER.EQ.0) GO TO 790
      IF (MISSM.EQ.NPER .OR. MISSO.EQ.NPER) GO TO 790
      STNID(1)=BLANK
      STNID(2)=BLANK
      L=PTVR(LPTVR)
      L=-L
      CALL RPPREC(STNID,AGEN,L,LPG,PGENL,NPUSE,IDUM,ISTAT)
      IF (ISTAT.NE.0) CALL PSTRDC(ISTAT,AGEN,STNID,L,
     $ LPG,NPUSE)
      IF (IFOUND.EQ.0)
     $ WRITE(IPR,875) PPUSER,MMO,MDAY,MYEAR,MHR,TZCODE
      WRITE(IPR,FMT) (PGENL(I),I=5,9),PGENL(2),PGENL(3),AOBS,SUMOBS,
     $ (POBS(I),I=1,NPER),AMDR,SUMMDR,(PMDR(I),I=1,NPER)
      IFOUND=1
      NSTN=NSTN+1
C
790   CONTINUE
C **END LOOP THRU STATIONS
C
C CHECK IF ANY STATIONS WERE FOUND
C
      IF (NSTN.EQ.0) WRITE(IPR,820)
C DONE
      RETURN
C
C................FORMATS................................................
C
805   FORMAT(1H0,21H *** ENTER XMDRPR ***)
C
820   FORMAT(///// 1X,43H** NO 6-HR STATIONS WITH PRECIPITATION WERE,
     $ 26H AVAILABLE FOR TABULATION.)
875   FORMAT(1H1,3X,7HUSER : ,2A4
     $ / 1X,52HTABULATION OF 6-HR MDR DERIVED AND OBSERVED RAINFALL,
     $ 41H (IN INCHES) FOR STATIONS WITH 6 HR DATA.
     $ / 1X,44H(EXCLUDES STATIONS WITH ALL ZERO VALUES, ALL,
     $ 38H MISSING OBSERVED, OR ALL MISSING MDR)
     $ // 1X,11HENDING TIME,3X,I2,1H/,I2,1H/,I4,3H HR,I3,1X,A4
     $ // 35X,30H RAINFALL FOR EACH 6-HR PERIOD / 50X,6HPERIOD
     $ / 1X,12HSTATION NAME,9X,4H  ID,13X,3HSUM,4X,3H1  ,3X,3H2  ,
     $ 3X,3H3  ,3X,1H4
     $ / 1X,20(1H.),1X,8(1H.),7X,5(1H.),4(1X,5(1H.)))
C
C.......................................................................
C
C
      END
