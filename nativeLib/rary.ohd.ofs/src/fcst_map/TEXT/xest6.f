C MEMBER XEST6
C  (from old member PPXEST6)
C
      SUBROUTINE XEST6(PP24,PT24,PPVR,PTVR,MDR6,PPSR,CHAR,OPVR,PARM,
     1  LWORK,IERR)
C.......................................
C     THIS SUBROUTINE PERFORMS A NUMBER OF FUNCTIONS FOR STATIONS
C     WITH LESS THAN 24 HOURS PRECIPITATION REPORTS.
C     FUNCTIONS INCLUDED ARE....
C          1. COMPUTES NORMALIZED VALUES FROM OBSERVED SIX-HOUR
C              REPORTS.
C          2. ESTIMATES MISSING 24 HOUR AMOUNTS.
C          3. ESTIMATES MISSING NORMALIZED VALUES IF REQUESTED.
C          4. DISPLAYS DATA IF REQUESTED.
C.......................................
C     WRITTEN BY -- ERIC ANDERSON, HRL -- FEBRUARY 1983
C.......................................
      INTEGER*2 PP24(1),PT24(1),PPVR(1),PTVR(1),MDR6(1),PPSR(1),CHAR(1)
      INTEGER*2 MSNG24,MSNG6,MSGMDR,MSNGSR,MDR24,MDRSIX(4),VAL,NORM(4)
      INTEGER*2 OPVR(1),NORM6(4)
       DIMENSION PARM(1),STA(2)
C
C     COMMON BLOCKS
      COMMON/PUDBUG/IOPDBG,IPTRCE,NDBUG,PDBUG(20),IPALL
      COMMON/XSIZE/NMAP,NDUPL,NSLT24,NSLOT6,LDATA6,LMDR,LPPSR,
     1 MSNG24,MSNG6,MSGMDR,MSNGSR,NRECTP,MXTEMP,SMALL
      COMMON/XDISPL/MDRPRT,MDRCOM,IPRT24,IPLT24,IPRT6,IPRSSR,
     1 IPRSRW,MAPPRT,METRIC,LASTDY,IPRDAY,IPLTMP
      COMMON/IONUM/IN,IPR,IPU
      COMMON/XTIME/KZDA,KDA,KHR,LSTMO,KMO,KID,KYR,KIH,TZCODE,ISW,
     1  IUTMP,NSSR,IDAY
      COMMON/XMDR/MDRSTA,ICTMDR,MDRST6,ICMDR,NCMDR,IRMDR,NRMDR,NMDR,
     1  MDR,MDRNUV
      COMMON/XOPT/IXTYPE,XNAME(2),IESTPX,ICONVC,CNVDIS,CVDISW,NOEST6,
     1IUSESR,PPUSER(2),IMOSUM,IMOWTR,IWTEST
      COMMON/XALPHA/IOP24,NOP24,LOP24,IOPVR,NOPVR,LOPVR
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_map/RCS/xest6.f,v $
     . $',                                                             '
     .$Id: xest6.f,v 1.1 1995/09/17 18:59:35 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA XEST,PCPN,BLANK/4HXEST,4HPCPN,4H    /
      DATA APPVR/4HPPVR/
C.......................................
C     CHECK TRACE LEVEL
      IF(IPTRCE.GE.1) WRITE(IOPDBG,900)
  900 FORMAT(1H0,16H** XEST6 ENTERED)
C.......................................
C     CHECK IF DEBUG ON.
      IBUG=0
      IF(IPBUG(XEST).EQ.1) IBUG=1
      JBUG=0
      IF(IPBUG(PCPN).EQ.1) JBUG=1
C.......................................
C.......................................
C     SET INITIAL VALUES
      ITITLE=1
      IERR=0
      NPRT=0
      N6=KHR/6
      PSMALL=SMALL*N6/4.0
      NUM=NOPVR
      IF (IOPVR.EQ.0) NUM=NSLOT6
      LPNTRS=NSLOT6*4
      ISAVE=IPRDAY
      IF (IPRDAY.EQ.0) GO TO 101
      IPRDAY=IPRT6
  101 IND=6
C.......................................
C.......................................
C     LOOP THROUGH 6 HOUR STATIONS LOOKING FOR THOSE WITH
C       NON-MISSING 6 HOUR DATA.
      DO 100 N=1,NUM
      I=OPVR(N)
      IF (IOPVR.EQ.0) I=(N-1)*4+1
      IF(PTVR(I).EQ.0) GO TO 100
      L=PTVR(I+3)-1
      I24=PTVR(I+1)
      IF (PTVR(I).GT.0) GO TO 104
C
C     SET ALL SIX HOUR VALUES TO MISSING FOR STATIONS WITH
C     SOME MISSING 6 HOUR DATA AND 24-HOUR TOTAL MISSING OR
C     LESS THAN PSMALL.
      L24=I24/5+1
      IF(PP24(L24).GE.PSMALL) GO TO 106
      DO 102 K=1,N6
 102  PPVR(L+K)=MSNG6
      GO TO 100
C
C     CHECK IF PARTIAL 6-HOUR REPORTS CONSTITUTE ALL
C     PRECIPITATION OR IF ONLY ONE MISSING REPORT.
 106  IS6=0
      NMSG=0
      MSUM=0
      DO 107 K=1,N6
      IF(PPVR(L+K).EQ.MSNG6) GO TO 108
      IS6=IS6+PPVR(L+K)
      GO TO 107
 108  NMSG=NMSG+1
      MSUM=MSUM+NMSG*K
 107  CONTINUE
      IF(IS6.GE.(PP24(L24))) GO TO 103
      IF(MSUM.GT.4) GO TO 100
C
C     ONLY ONE REPORT MISSING--FILL IN.
      PPVR(L+MSUM)=PP24(L24)-IS6
      PTVR(I)=-PTVR(I)
      GO TO 104
C     6-HOUR REPORTS CONTAIN ALL PRECIP./SET MISSING TO ZERO.
 103  DO 109 K=1,N6
      IF(PPVR(L+K).EQ.MSNG6) PPVR(L+K)=0
 109  CONTINUE
      PTVR(I)=-PTVR(I)
C
C     SIX-HOUR AVAILABLE--SEE IF NORMALIZED CAN BE COMPUTED.
  104 CALL XNORM(PPVR(L+1),PSMALL,N6,NORM,ISTAT)
      IF(ISTAT.EQ.0) GO TO 110
      DO 105 K=1,N6
  105 PPVR(L+K)=MSNG6
      PTVR(I)=-PTVR(I)
      GO TO 100
  110 DO 111 K=1,N6
  111 PPVR(L+K)=NORM(K)
C
C     RESET COLOCATED ESTIMATATION FLAG.
      INM=0
      IF(PT24(I24+1).LT.10000) GO TO 113
      IEST=3
      PT24(I24+1)=PT24(I24+1)-10000
      GO TO 115
C
C     RESET SET ZERO FLAGS
  113 IEST=0
      IF(PT24(I24+3).GT.-10000) GO TO 114
      PT24(I24+3)=PT24(I24+3)+10000
      INM=-1
      GO TO 115
  114 IF(PT24(I24+4).GE.0) GO TO 115
      PT24(I24+4)=-PT24(I24+4)
      INM=-2
  115 IF ((IPRDAY.EQ.0).AND.(IPLT24.EQ.0)) GO TO 100
C
C     READ PCPN PARAMETERS AND PRINT OR PLOT RESULTS.
      TYPE=PCPN
      LP=PT24(I24)
      LP=IABS(LP)
      STA(1)=BLANK
      STA(2)=BLANK
      LP=-LP
      CALL RPPREC(STA,TYPE,LP,LWORK,PARM(1),NFILL,K,ISTAT)
      IF(ISTAT.EQ.0) GO TO 120
      GO TO 190
  120 IF(JBUG.EQ.1) CALL PDUMPA(NFILL,PARM(1),TYPE,STA,1)
      L24=I24/5+1
      IF (IPRDAY.EQ.1) CALL XPRT6(STA,PARM(5),PARM(22),PP24(L24),IEST,
     1  PT24(I24+3),PPVR(L+1),INM,ITITLE,NPRT,N6)
      IF (IPLT24.EQ.0) GO TO 100
      IPC=PT24(I24+3)
      CALL XPLT24(PP24(L24),IND,PARM(10),PARM(11),IEST,IPC,ISW,STA)
  100 CONTINUE
C.......................................
C.......................................
      IF (IBUG.EQ.1) CALL PDMPDY(APPVR,KZDA,MSNG6,PTVR,LPNTRS,PPVR,
     1 LDATA6)
C.......................................
C.......................................
C     LOOP THROUGH 6 HOUR STATIONS LOOKING FOR THOSE WITH MISSING DATA.
      DO 140 N=1,NUM
      I=OPVR(N)
      IF (IOPVR.EQ.0) I=(N-1)*4+1
      IF(PTVR(I).GE.0) GO TO 140
      L=PTVR(I+3)-1
      I24=PTVR(I+1)
      L24=I24/5+1
      ICM=0
      IRDP=0
      IEST6=0
C
C     CHECK IF 24 HOUR TOTAL IS MISSING.
      IF(PP24(L24).EQ.MSNG24) GO TO 150
C.......................................
C     DAILY TOTAL NOT MISSING--SET PRINT FLAGS AND RESET ZERO FLAGS.
      VAL=PP24(L24)
      IEST=0
      IF(PT24(I24+1).LT.10000) GO TO 144
      PT24(I24+1)=PT24(I24+1)-10000
      IEST=3
      GO TO 146
  144 IF(PT24(I24+3).GT.-10000) GO TO 145
      PT24(I24+3)=PT24(I24+3)+10000
      IEST=-1
      GO TO 170
  145 IF(PT24(I24+4).GE.0) GO TO 146
      PT24(I24+4)=-PT24(I24+4)
      IEST=2
      GO TO 170
C
C     CHECK IF SOME NON-MISSING SIX HOUR VALUES EXIST.
  146 DO 147 K=1,N6
      IF(PPVR(L+K).EQ.MSNG6) GO TO 147
      IEST6=1
 147  CONTINUE
      IF (IEST6.EQ.0) GO TO 170
C     NORMALIZE EXISTING VALUES.
      SUM=VAL*0.01
      IS6=0
      DO 148 K=1,N6
      IF(PPVR(L+K).EQ.MSNG6) GO TO 149
      PP6=PPVR(L+K)*0.01
      NORM6(K)=(PP6/SUM)*100.0+0.5
      IS6=IS6+NORM6(K)
      GO TO 148
 149  NORM6(K)=MSNG6
 148  CONTINUE
      GO TO 170
C.......................................
C     DAILY TOTAL MISSING--ESTIMATE
C       CHECK FIRST IF MDR CAN BE USED.
  150 IF((MDR.EQ.0).OR.(MDRSTA.LT.2)) GO TO 160
      MDRBOX=PT24(I24+4)
      IF(MDRBOX.EQ.0) GO TO 160
      CALL XPPMDR(MDRBOX,MDR6,KHR,MDR24,MDRSIX,MSNG,ISTAT)
      ICM=-1
      IF(ISTAT.EQ.1) GO TO 160
      IF (MSNG.EQ.1) GO TO 160
      ICM=1
      IF(MDRSTA.GT.2) GO TO 155
      J=MDR24
      IF(J.GT.ICTMDR) GO TO 160
  155 VAL=MDR24
      IEST=2
      GO TO 165
C
C     READ PCPN PARAMETERS
  160 TYPE=PCPN
      LP=PT24(I24)
      LP=IABS(LP)
      STA(1)=BLANK
      STA(2)=BLANK
      LP=-LP
      CALL RPPREC(STA,TYPE,LP,LWORK,PARM(1),NFILL,J,ISTAT)
      IF(ISTAT.EQ.0) GO TO 161
      GO TO 190
  161 IF(JBUG.EQ.1) CALL PDUMPA(NFILL,PARM(1),TYPE,STA,1)
      INET=PARM(20)
      IF (INET.EQ.0) GO TO 140
      IF(IRDP.EQ.1) GO TO 176
      IRDP=1
      IWTGT=PARM(21)
      IF (IWTGT.EQ.0) GO TO 140
C
C    ESTIMATE USING STATION DATA
      NWT=PARM(19)
      LCHAR=PT24(I24+1)
      IF(NWT.GT.0) GO TO 162
C
C     USE ONE OVER DISTANCE SQUARED WEIGHTS.
      CALL XESTD2(PP24,PT24,CHAR,PARM(24),PARM(44),PPSR,NSSR,
     1  PARM(12),PARM(13),LCHAR,VAL)
      IEST=1
      GO TO 165
C
C     USE SIGNIFICANCE WEIGHTS
  162 CALL XSGFWT(PP24,PT24,CHAR,NWT,PARM(44),PARM(54),LCHAR,VAL)
      IEST=1
C
C     STORE DAILY ESTIMATE AS A NEGATIVE
  165 IF(VAL.GT.0) GO TO 166
      PP24(L24)=-9998
      GO TO 170
  166 PP24(L24)=-VAL
C.......................................
C     ESTIMATE NORMALIZED VALUE--CHECK MDR FIRST
  170 IF((MDR.EQ.0).OR.(MDRST6.EQ.0)) GO TO 174
      IF(ICM) 174,171,172
  171 MDRBOX=PT24(I24+4)
      IF(MDRBOX.EQ.0) GO TO 174
      CALL XPPMDR(MDRBOX,MDR6,KHR,MDR24,MDRSIX,MSNG,ISTAT)
      IF(ISTAT.EQ.1) GO TO 174
      IF (MSNG.EQ.1) GO TO 174
  172 CALL XNORM(MDRSIX,PSMALL,N6,NORM,ISTAT)
      IF(ISTAT.EQ.1) GO TO 174
      INM=2
      IF (IEST6.EQ.1) GO TO 177
      GO TO 185
C
C     CHECK IF SHOULD ESTIMATE FROM SURROUNDING STATIONS.
  174 IF(NOEST6.EQ.0) GO TO 175
      IF (IEST6.EQ.1) GO TO 175
C
C     DO NOT ESTIMATE, LEAVE AS MISSING
      INM=-4
      GO TO 185
C
C     TRY TO ESTIMATE NORMALIZED VALUE FROM SURROUNDING STATIONS
  175 IF(IRDP.EQ.1) GO TO 176
      IRDP=1
      GO TO 160
  176 CALL XNRMD2(PPVR,PTVR,MSNG6,PARM(64),PARM(76),N6,NORM,INM)
      IF(IEST6.EQ.0) GO TO 185
C
C     COMBINE NORMALIZED VALUE BASED ON PARTIAL STATION DATA
C     WITH ESTIMATED NORMALIZED VALUE.
  177 CALL XCMBNM(NORM6,IS6,NORM,N6,MSNG6)
      INM=-3
C.......................................
C     CHECK IF VALUES ARE TO BE PRINTED OR PLOTTED.
  185 IPLOT=0
      IF ((IPLT24.EQ.0).OR.(VAL.EQ.0)) GO TO 182
      IF ((IPLT24.EQ.1).AND.(IEST.NE.0)) GO TO 182
      IPLOT=1
  182 IF ((IPRDAY.EQ.0).AND.(IPLOT.EQ.0)) GO TO 180
C
C     READ PCPN PARAMETERS IF NOT READ PREVIOUSLY.
      IF(IRDP.EQ.1) GO TO 187
      TYPE=PCPN
      LP=PT24(I24)
      LP=IABS(LP)
      STA(1)=BLANK
      STA(2)=BLANK
      LP=-LP
      CALL RPPREC(STA,TYPE,LP,LWORK,PARM(1),NFILL,K,ISTAT)
      IF(ISTAT.EQ.0) GO TO 186
      GO TO 190
  186 IF(JBUG.EQ.1) CALL PDUMPA(NFILL,PARM(1),TYPE,STA,1)
  187 IF (IPRDAY.EQ.1) CALL XPRT6(STA,PARM(5),PARM(22),VAL,IEST,
     1  PT24(I24+3),NORM,INM,ITITLE,NPRT,N6)
      IF (IPLOT.EQ.0) GO TO 180
      IPC=PT24(I24+3)
      CALL XPLT24(VAL,IND,PARM(10),PARM(11),IEST,IPC,ISW,STA)
C
C     STORE ESTIMATED NORMALIZED VALUES AS VALUE MINUS 1100.
  180 PTVR(I)=-PTVR(I)
      IF (INM.EQ.-4) GO TO 140
      DO 181 K=1,N6
  181 PPVR(L+K)=NORM(K)-1100
  140 CONTINUE
C.......................................
C.......................................
C     FLUSH PRINT ARRAY IF NOT EMPTY
      IF(NPRT.EQ.0) GO TO 99
      ITITLE=-1
      CALL XPRT6(STA,PARM(5),PARM(22),VAL,IEST,K,NORM,INM,ITITLE,
     1  NPRT,N6)
      GO TO 99
C.......................................
C     FATAL ERROR READING PARAMETER RECORD.
  190 CALL PSTRDC(ISTAT,TYPE,STA,LP,LWORK,NFILL)
      WRITE(IPR,901)
  901 FORMAT(1H0,40H**FATAL ERROR** THE ABOVE ERROR IS FATAL)
      CALL KILLFN(8HMAP     )
      IERR=1
C.......................................
   99 IPRDAY=ISAVE
      IF(IPTRCE.GE.1) WRITE(IOPDBG,902)
  902 FORMAT(1H0,13H** EXIT XEST6)
      RETURN
      END
