C MEMBER XMDRCV
C  (from old member PPXMDRCV)
C
C DESC XMDRCV=CONVERT MDR VALUES TO PRECIP AND PLOT
C
      SUBROUTINE XMDRCV(PPVR,PTVR,STBDY,MDR6,PT24,WORK,IWORK,LW)
C.......................................................................
C
C   XMDRCV : MAP ROUTINE TO CONVERT 6 HOUR MDR TO PRECIPITATION
C
C THIS ROUTINE IS PART OF THE MEAN AREAL PRECIP(MAP) MODEL AND
C WILL DO THE FOLLOWING:
C   1. CONVERTS 6-HR SUMMED MDR DATA INTO PRECIP FOR EACH 6-HR PERIOD
C   2. PLOTS RAW MDR AND MDR PRECIP (SEPARATELY) ON THE STATE
C      BOUNDARY OUTLINES
C   3. PRINTS COMPARISON TABLE OF GAGE/RADAR VALUES FOR EACH
C      OF THE 3 MDR PROBABILITY LEVELS
C   4. PRINTS COMPARISON TABLE OF MDR PRECIP AND OBSERVED PRECIP
C      FOR THE NWS 1ST ORDER STATIONS
C
C NOTE: NUMBER 2-4 ABOVE ARE OPTIONS
C.......................................................................
C
C SUBROUTINE ORIGINALLY BY ED VANBLARGAN - HRL - APRIL,1983
C PROGRAM IS BASED ON TULSA RFC MDR PLOT PROGRAM BY RANDY TETZLOFF
C MDR-PRECIP CONVERSION USED AND OUTPUT ARE BOTH PATTERNED AFTER
C TULSA PROGRAM. MOST OF EFFORT HERE WAS ADAPTING MDR INTO
C NWSRFS FRAMEWORK
C.......................................................................
C
C GENERAL STEP-BY-STEP PROCEDURE
C I.  FOR EACH 6 HR PERIOD DO THE FOLLOWING:
C      A. PRINT RAW MDR
C      B. CONVERT MDR TO PRECIP
C      C. PRINT GAGE:RADAR COMPARISON RESULTS
C      D. PLOT THE CONVERTED MDR PRECIP ESTIMATES.
C     IN MORE DETAIL...
C      A. IF MDRPRT OPTION IS ON THEN PLOT RAW MDR ON THE STATE
C         BOUNDARIES. DO THIS BY:
C          1. PUT STATE BOUNDARIES IN WORK (ALREADY IN A3)
C          2. CONVERT MDR VALUES TO A3 AND PUT IN WORK (IN REVERSE
C             ORDER HOWEVER SINCE MDR STARTS AT SOUTH END).
C          3. PRINT WORK
C       B. CONVERT MDR TO PRECIP. DO THIS BY:
C           1. FIND ALL 6-HR STATIONS THAT REPORTED PRECIP(GT 0)
C           2. LOCATE MDR VALUE AND BOX NUMBER FOR THESE STATIONS
C           3. DETERMINE MDR PRECIP FROM MDR VALUE FOR THE 3 DIFFERENT
C              PROBABILTY LEVELS(30,50,70).
C           4. COMPUTE GAGE/RADAR RATIO FOR EACH OF ABOVE AND GET
C              AVERAGE G/R RATIO FOR EACH OF THE 3 PROBABILITY LEVELS.
C           5. DETERMINE WHICH PROB. LEVEL HAS A G/R AVERAGE CLOSEST
C              TO 1.0 AND USE THAT PROB. LEVEL TO CONVERT ALL THE
C              MDR VALUES TO PRECIP.
C       C. IF MDRPRT OPTION INDICATES IT, THEN PRINT G/R RATIO TABLE.
C       D. IF MDRPRT OPTION IS ON THEN PLOT MDR PRECIP ON TOP
C          OF THE STATE BOUNDARY BACKGROUND.
C REVISION NOTE: WHEN CHECKING MDRPRT OPTION ALSO CHECK
C                IPRPRT OPTION FOR DISPLAYING ONLY CERTAIN DAYS.
C
C II. ONCE ALL MDR IS CONVERTED TO PRECIP FOR ALL 6-HR PERIODS,
C     THEN PRINT COMPARISON TABLE OF MDR VS. OBSERVED PRECIP FOR THE
C     NWS 1ST ORDER STATIONS (IF MDRCOM OPTION ON).
C     USE ONLY STATIONS THAT HAVE AT LEAST 1 NON-ZERO MDR OR OBSERVED
C     PRECIP VALUE IN ANY 6-HR PERIOD.
C
C NOTES ON MDR DATA:
C  1. (-) VALUE IS ESTIMATED. THIS ROUTINE WILL CONVERT TO POSITIVE
C     AND USE AS THOUGH MEASURED(EXCEPT FOR PLOT OF RAW DATA).
C  2. MDR MAY BE MISSING IN WHICH CASE IT IS NOT USED AND THE MISSING
C     CODE FOR PRECIP IS RETURNED
C.......................................................................
C
C ARGUMENT LIST
C
C NAME  I/O TYPE DIM DESC
C ----  --- ---- --- ------------------------------------------
C PPVR   I   I*2 VAR DATA ARRAY WITH LESS THAN 24 HR PRECIP.
C                    VALUES ARE IN .01", I.E. PRECIP=PPVR(I)/100.
C                    NUMBER OF VALUES FOR EACH STATION=24/IDT
C                    VALUES FOR EACH PERIOD ARE SEQUENTIAL BY PERIOD,
C                    I.E., PPVR(1-4) ARE 4 6-HR VALUES FOR STN. 1
C                          PPVR(5-8)  "     "    "      "   "   2.
C PTVR   I   I*2 VAR POINTER ARRAY FOR PPVR. STARTING AT 1ST LOCATION
C                    THERE ARE 4 PIECES OF INFO FOR EACH STATION:
C                    LOC 1=POINTER TO STATION GENERAL PARAMS. ON PPPDB
C                        2=ARRAY LOCATION IN PT24 OF THIS STATION
C                        3=TIME INTERVAL OF DATA(IDT)
C                        4=LOCATION IN PPVR WHERE DATA STARTS FOR THIS
C                          STATION
C STBDY  I   R*4 VAR STATE BOUNDARY POINTS ARRAY(STARTS WITH 8TH SLOT)
C                      1-N=BOUNDARY POINTS (IN A3 FORMAT)
C                          START AT NORTH MOST ROW AND GO ACROSS ROWS
C                          FROM WEST (N=NUM. OF COLS*NUM. OF ROWS)
C MDR6  I/O  I*2 VAR DATA ARRAY CONTAINING 6-HR SUMMED MDR VALUES(IN)
C                    AND MDR PRECIP (OUT). VALUES STAR AT SOUTH MOST ROW
C                    AND GO ACROSS ROWS FROM WEST.
C                    NOTE:ALL VALUES FOR PERIOD 1 COME FIRST, THEN
C                    COMES PERIOD 2, ETC .
C PT24   I   I*2 VAR POINTER ARRAY FOR 24 HR PRECIP WITH 5 LOCATIONS
C                    FOR EACH STATION. ONLY LOC OF INTEREST IS EVERY 5TH
C                    WHICH = MDR BOX NUMBER (FOR NAT'L GRID)
C WORK   I   R*4 VAR WORK SPACE ARRAY
C IWORK  I   I*2 VAR INTEGER WORK SPACE ARRAY (SAME SPACE AS WORK)
C LW     I    I    1 LENGTH OF WORK
C
C NOTE: VARIABLES PPVR,PTVR,MDR6,PT24 ARE FROM RPDDLY
C       STBDY IS IN PPDB WRITE-UP
C
C SOME COMMON BLOCK VARIABLES:
C MDRPRT    PRINT OPTION FOR MDR DISPLAYS. =0, NO DISPLAYS
C IPRPRT    PRINT OPTION FOR EACH GIVEN DAY. =0, NO DISPLAY THIS DAY
C
C
C INTERNAL VARIABLES
C ------------------
C NPER   - NUMBER OF 6 HR PERIODS TO ANALYZE
C MDRVOK - FLAG IF MDR VALUES FOR A PERIOD ARE
C            =0,  ALL ZERO
C            =-9, ALL MISSING
C            =1, OTHERWISE
C NPRUSE - NUMBER OF PERIODS TO USE IN FINAL TABLE
C           (I.E., DO NOT USE A PERIOD WITH ALL MISSING MDR)
C
C.......................................................................
C
C
      INTEGER*2 PPVR,PTVR,MDR6,PT24,IWORK
      DIMENSION SUBNAM(2),OLDSUB(2),STBDY(1),WORK(1),IWORK(1),
     $          PPVR(1),PTVR(1),MDR6(1),PT24(1),FORMAT(8)
C
      INCLUDE 'common/where'
      INCLUDE 'common/ionum'
      INCLUDE 'common/pudbug'
      INCLUDE 'common/xmdr'
      INCLUDE 'common/xsize'
      INCLUDE 'common/xopt'
      INCLUDE 'common/xdispl'
      INCLUDE 'common/xtime'
      INCLUDE 'common/xmday'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_map/RCS/xmdrcv.f,v $
     . $',                                                             '
     .$Id: xmdrcv.f,v 1.1 1995/09/17 18:59:48 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SUBNAM/4HXMDR,4HCV  /,
     $ FORMAT/4H  NO,4HTE  ,4H ZER,4HO   ,
     $        4HWARN,4HING ,4HMISS,4HING /
C
C SET /WHERE/ INFO AND DEBUG CODES
C
      DO 100 I=1,2
      OLDSUB(I)=OPNAME(I)
100   OPNAME(I)=SUBNAM(I)
      IOLDOP=IOPNUM
      IOPNUM=-1
C
      IBUG=IPBUG(SUBNAM(1))
      IF (IPTRCE.GE.1) WRITE(IOPDBG,805)
C
C INITIALIZE VARIABLES
C
      NPER=KHR/6
      NPRUSE=0
      MISSM=MSGMDR
C
C..........DEBUG TIME...................................................
C
      IF (IBUG.EQ.0) GO TO 140
      WRITE(IOPDBG,890) NSLOT6
      DO 110 N=1,NSLOT6
      LPT24=PTVR((N-1)*4+2) - 1
      LPP  =PTVR((N-1)*4+4) - 1
110   WRITE(IOPDBG,891) (PPVR(LPP+I),I=1,4),
     $ (PTVR((N-1)*4+I),I=1,4),(PT24(LPT24+I),I=1,5)
      WRITE(IOPDBG,893) NRMDR,NCMDR,IRMDR,ICMDR,NMDR
      DO 135 IPER=1,NPER
      DO 130 N=1,NRMDR
130   WRITE(IOPDBG,894) (MDR6((N-1)*NCMDR+(IPER-1)*NMDR+I),
     $ I=1,NCMDR)
135   CONTINUE
      WRITE(IOPDBG,895) MDRPRT,MDRCOM,MSGMDR,MSNG6,KHR
C.......................................................................
C
C LOOP THRU THE NUMBER OF 6-HR PERIODS REQUIRED
C
140   NKHR=KHR-NPER*6
      DO 700 IPER=1,NPER
      LOCMDR=(IPER-1)*NMDR+1
C LOOP THRU ALL MDR VALUES FOR THE PERIOD
C TO SEE IF ALL ARE MISSING OR ALL ZERO.
      MDRVOK=-9
      DO 145 LM=1,NMDR
      MDRVAL=MDR6(LM+LOCMDR-1)
      IF (MDRVAL.NE.MISSM .AND. MDRVAL.NE.0) GO TO 147
      IF (MDRVAL.EQ.0) MDRVOK=0
145   CONTINUE
      GO TO 148
147   MDRVOK=1
C CALCULATE DATE AS MO,DAY,YEAR
148   NKHR=NKHR+6
      CALL MDYH2(KDA,NKHR,MMO,MDAY,MYEAR,MHR,IDUM1,IDUM,TZCODE)
C CHECK MDRVOK TO SEE IF MDR IS ALL ZEROS OR ALL MISSING
C PRINT NOTE FOR ZEROS (ONLY IF IPRDAY IS ON, =1) AND
C WARNING FOR MISSING
      IF (MDRVOK.EQ.1) GO TO 180
      LM=0
      IF (MDRVOK.EQ.-9) LM=4
      IF (MDRVOK.EQ.-9 .OR.IPRDAY.NE.0) WRITE(IPR,806) (FORMAT(N+LM),
     $N=1,4),IPER,MMO,MDAY,MYEAR,MHR,TZCODE,PPUSER
      IF (MDRVOK.EQ.0) GO TO 700
      CALL WARN
      DO 170 LM=1,NMDR
170   MDR6(LM+LOCMDR-1)=MSNG6
      GO TO 700
180   IF (IPRDAY.EQ.0 .OR. MDRPRT.LE.0) GO TO 200
C
C ........STEP I.A.- PLOT RAW MDR VALUES SINCE MDRPRT OPTION=1
C
C SIMPLY PRINT HEADING AND CALL ROUTINE XMDRPL TO PLOT VALUES
C FIRST CHECK IF WORK HAS ROOM...USES NCMDR+(NCMDR+1)
C IF NO ROOM, GIVE ERROR AND CHANGE MDRPRT SO PLOT OPTION IS OFF.
C
      NEEDLW=NCMDR*2 + 1
      IF (LW.GT.NEEDLW) GO TO 190
      WRITE(IPR,807) NEEDLW,LW
      CALL ERROR
      MDRPRT=-2
      GO TO 200
190   WRITE(IPR,810) PPUSER,IPER,MMO,MDAY,MYEAR,MHR,TZCODE
      CALL XMDRPL(STBDY,MDR6(LOCMDR),MSGMDR,WORK(1),WORK(NCMDR+1))
C
C ...STEP I.B AND I.C
C CONVERT MDR TO PRECIP AND PRINT G/R RATIOS
C
200   CALL XMDRRF(IPER,PPVR,PTVR,MDR6(LOCMDR),PT24,WORK,LW,NTABLE)
      NPRUSE=NPRUSE+1
      IF (IPRDAY.EQ.0 .OR. MDRPRT.LE.0) GO TO 700
C
C STEP I.D - PLOT THE MDR PRECIP
C FIRST CHECK IF WORK HAS ROOM...NEED (NMDR+1)/2+NCMDR+NCMDR+1
C THEN, NEED MDR6 IN TENTHS INSTEAD OF HUNDRETHS FOR PLOT. SO, TRANSFER
C MDR6 INTO IWORK ROUNDED TO NEAREST TENTH.
C IF NO ROOM, GIVE ERROR AND CHANGE MDRPRT SO PLOT OPTION IS OFF.
C
      LOCW=(NMDR+1)/2+1
      NEEDLW=LOCW+2*NCMDR+1
      IF (LW.GT.NEEDLW) GO TO 250
      WRITE(IPR,807) NEEDLW,LW
      CALL ERROR
      MDRPRT=-2
      GO TO 700
C WORK HAS ROOM
250   WRITE(IPR,820) PPUSER,NTABLE,IPER,MMO,MDAY,MYEAR,MHR,TZCODE
C TRANSFER MDR6 TO THE FIRST NMDR SLOTS OF IWORK
      DO 300 N=1,NMDR
      MVAL=MDR6(N+LOCMDR-1)
      IWORK(N)=MVAL/10 + MOD(MVAL,10)/5
300   IF (MDR6(N+LOCMDR-1).EQ.MSNG6) IWORK(N)=MVAL
C CALL PLOT
      CALL XMDRPL(STBDY,IWORK,MSNG6,WORK(LOCW),WORK(LOCW+NCMDR+1))
700   CONTINUE
      MDRPRT=IABS(MDRPRT)
C ** END LOOP THRU PERIODS
C
C...........STEP II. PRINT COMPARISON TABLE OF MDR DERIVED PRECIP
C                    VS. OBSERVED FOR 1ST ORDER(6 HR) STATIONS
C WRITE HEADER AND CALL SUBROUTINE XMDRPR TO MAKE COMPARISON
C DO ONLY IF MDRCOM OPTION IS ON
C
      IF (IPRDAY.EQ.0 .OR. NPRUSE.EQ.0) GO TO 920
      IF (MDRCOM.NE.0) CALL XMDRPR(PPVR,PTVR,MDR6,
     $ PT24,WORK,LW)
C
C............DEBUG TIME.................................................
C
920   IF (IBUG.EQ.0) GO TO 999
      WRITE(IPR,893) NRMDR,NCMDR,IRMDR,ICMDR,NMDR
      DO 935 IPER=1,NPER
      DO 930 N=1,NRMDR
930   WRITE(IOPDBG,894) (MDR6((N-1)*NCMDR+(IPER-1)*NMDR+I),
     $ I=1,NCMDR)
935   CONTINUE
C.......................................................................
C
C RESET CB /WHERE/
C
999   IOPNUM=IOLDOP
      DO 998 I=1,2
998   OPNAME(I)=OLDSUB(I)
      RETURN
C
C...................FORMATS.............................................
C
805   FORMAT(1H0,21H *** ENTER XMDRCV ***)
806   FORMAT(///// 1X,2H**,2A4,22H** ALL MDR VALUES ARE ,2A4,
     $ 15HFOR 6-HR PERIOD,I2,25H;ENDING ON HYDROLOGIC DAY,
     $ I3,1H/,I2,1H/,I4,2X,2HHR,I3,1X,A4,7H. USER=,2A4)
C
807   FORMAT(1H0,10X,46H**ERROR** NOT ENOUGH SPACE IN WORK ARRAY. NEED,
     $ I5,16H SPACES BUT ONLY,I5,6HEXIST.)
C
810   FORMAT(1H1,1X,7HUSER : ,2A4,3X,
     $ 33HSUMMED HOURLY MDR VALUES
     $ / 1X,10HFOR PERIOD,I3,35H OF THE HYDROLOGIC DAY WHICH IS THE,
     $ 24H 6 HOUR PERIOD ENDING AT,
     $ I3,1H/,I2,1H/,I4,2X,2HHR,I3,1X,A4
     $ / 1X,47HNOTE (-) SIGN INDICATES AN ESTIMATED MDR VALUE.)
C
820   FORMAT(1H1,1X,7HUSER : ,2A4,3X,
     1 I2,2H% ,37HPROBABLE RAINFALL (TENTHS OF INCHES) ,
     2   29HFROM SUMMED HOURLY MDR VALUES
     $ / 1X,10HFOR PERIOD,I3,27H... 6 HOUR PERIOD ENDING AT,
     $ I3,1H/,I2,1H/,I4,2X,2HHR,I3,1X,A4)
C
890   FORMAT(1H0,7HNSLOT6=,I5
     $ / 1X,10HPPVR ARRAY,20X,10HPTVR ARRAY,20X,10HPT24 ARRAY)
891   FORMAT(1X,4I6,6X,4I6,6X,5I6)
893   FORMAT(1H0,29HNRMDR,NCMDR,IRMDR,ICMDR,NMDR=,5I10
     $ / 1X,10HMDR ARRAY:)
894   FORMAT(1X,20I6)
895   FORMAT(1H0,31HMDRPRT,MDRCOM,MSGMDR,MSNG6,KHR=,5I10)
C.......................................................................
C
      END
