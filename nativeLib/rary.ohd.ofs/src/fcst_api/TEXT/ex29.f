C MEMBER EX29
C  (from old member FCEX29)
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 04/12/95.09:41:58 BY $WC20SV
C
C @PROCESS LVL(77)
C
      SUBROUTINE EX29 (PO,CO,PX,RO,WE,CURAPI,CURAI)
C
C***********************************************************************
C
C  THIS ROUTINE EXECUTES THE API-MKC OPERATION.
C
C***********************************************************************
C  ROUTINE INITIALLY WRITTEN BY
C        LARRY BLACK - MBRFC     JULY 1982
C
C    MODIFIED CARRYOVER SAVE FOR FFG OPERATION
C         TIM SWEENEY, HRL - NOV 1993  -  VERSION 2
C***********************************************************************
C  PRINCIPAL VARIABLES...
C
C  FOR DEFINITION OF VARIABLES IN COMMON BLOCKS, SEE SECTION IX.3.3C
C  OF THE NWSRFS USER'S MANUAL.
C
C     AI,IAI         CURRENT AI VALUE
C     API,IAPI       CURRENT API VALUE
C     CARY(8)        TEMPORARY ARRAY TO STORE CARRYOVER VALUES IN FOR
C                      TRANSFER TO ROUTINE FCWTCO
C     CO(1)          INPUT CARRYOVER DATA FROM C ARRAY
C     CURAI(1)       OUTPUT CURRENT API VALUE TIME SERIES DATA
C     CURAPI(1)      OUTPUT CURRENT AI VALUE TIME SERIES DATA
C     C1 - C6        6 API/AI RELATIONSHIP CONSTANTS
C     D              TERM IN THE API/AI EQUATION
C     DESC           DESCRIPTION, ZONE NAME
C     EN             TERM IN THE API/AI EQUATION
C     FX             TERM IN THE API/AI EQUATION
C     IAIADJ         AI ADJUSTMENT FACTOR
C     IAICO,FI       STORM AI VALUE
C     IDAY           NUMBER OF DAYS SINCE JAN 1 OF CURRENT YEAR
C     IDELT          DELTA-T OF TIME SERIES FOR RAINFALL/MELT AND RUNOFF
C     IFDEB          DEBUG OUTPUT SWITCH, 0 = OFF, 1 = ON
C     IFFG           FLASH FLOOD GUIDANCE SWITCH
C                       0 = NO FFG, 1 = FFG ONLY, 2 = BOTH FFG & FCST
C     INTDA          INTERNAL DAY OF CURRENT DATE BEING PROCESSED.
C     IPER           PERIOD COUNTER FOR 6-HOURLY TIME STEPS IN
C                      EACH COMPUTATIONAL DAY, PERIOD 1 ENDING 18Z
C     IRANCO,PCPN    STORM RAINFALL VALUE
C     IREG1          FIRST API RECESSION CONSTANT
C     IREG2          SECOND API RECESSION CONSTANT
C     IRO(4)         TEMPORARY ARRAY FOR STORING ONE DAY'S RUNOFF VALUES
C     IROCO,PREVRO   STORM RUNOFF VALUE
C     IVERS          VERSION NUMBER
C     IWE            CURRENT WATER EQUIVALENT VALUE
C     KCQN           ARRAY ELEMENT IN API CORRECTION ARRAY
C     KHR            SPECIFIED HOUR FOR TODAY'S DATE, RANGE 1 - 24
C     KDAFFG         COMPUTATIONAL DAY FOR FFG OUTPUT
C     KHRFFG         COMPUTATIONAL HOUR FOR FFG OUTPUT
C     KMO,KDA,KYR    TODAY'S DATE (MONTH, DAY, AND YEAR, 2 DIGITS EACH)
C     LAST           NUMBER OF DAYS RUNOFF IS TO BE CALCULATED
C     LIK            LOCATION OF ELEMENT IN CURAPI AND CURAI ARRAYS
C                      FOR STORING CURRENT API AND/OR AI VALUES WHEN
C                      OUTPUT OF TIME SERIES IS REQUESTED
C     LOC            LOCATION OF CURRENT DAY'S FIRST ELEMENT IN THE
C                      INPUT TIME SERIES DATA FOR RAINFALL/MELT AND
C                      WATER EQUIVALENT
C     MODA(12)       NUMBER OF DAYS IN EACH MONTH
C     NEWSTM         STORM PERIOD COUNTER
C     NLSTZ          NUMBER DESIGNATING SPECIFIED TIME ZONE
C     NMO,NDA,NYR    TOMORROW'S DATE (MONTH, DAY, AND YEAR, 2 DIGITS
C                      EACH)
C     NOC            COUNTER FOR NUMBER OF DAYS TO SAVE CARRYOVER
C     NOP            NUMBER OF OPERATION ASSIGNED TO THIS OPERATION
C     NOUTDS         DAYLIGHT SAVINGS TIME SWITCH, 0 = STD TIME,
C                      1 = DAYLIGHT TIME
C     NPER           NUMBER OF PERIODS PER DAY
C     PO(1)          INPUT PARAMETRIC DATA FROM P ARRAY
C     PX(1)          INPUT RAINFALL/MELT TIME SERIES DATA
C     RAIN(4)        TEMPORARY ARRAY FOR 4 PERIODS OF RAINFALL/MELT
C                      FOR CURRENT DAY
C     RO(1)          OUTPUT RUNOFF TIME SERIES DATA
C     STRO           TOTAL STORM RUNOFF
C     SUBNAM(2)      ROUTINE NAME
C     TOT24,ITOT24   24-HOUR RAINFALL/MELT ENDING 12Z
C     TZ             FOUR-CHARACTER TIME ZONE CODE
C     WE(1)          INPUT WATER EQUIVALENT TIME SERIES DATA
C     WEEK           WEEK NUMBER FOR CURRENT DAY
C     XW             TERM IN THE API/AI EQUATION
C
C***********************************************************************
C
C
      DIMENSION PO(*),CO(*),PX(*),RO(*),WE(*),SUBNAM(2),MODA(12),
     1          RAIN(4),IRO(4),CARY(8),CURAPI(1),CURAI(1)
      DIMENSION DESC(6)
C
      COMMON /IONUM/ IN,IPR,IPU
      COMMON /FDBUG/ IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON /FCTIME/ IDARUN,IHRRUN,LDARUN,LHRRUN,LDACPD,LHRCPD,NOW(5),
     1                LOCAL,NOUTZ,NOUTDS,NLSTZ,IDA,IHR,LDA,LHR,IDADAT
      COMMON /FCARY/ IFILLC,NCSTOR,ICDAY(20),ICHOUR(20)
      COMMON /FSMC29/ NEWSTM,IRANCO,IAICO,IROCO,IAPI,IAI,IWE,ITOT24
      INCLUDE 'common/ffgctl'
      COMMON /MOD129/ NDT29,IDT29(5),VAL29(5)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_api/RCS/ex29.f,v $
     . $',                                                             '
     .$Id: ex29.f,v 1.1 1995/09/17 18:56:57 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SUBNAM/4HEX29,4H    /,NOP/29/,
     1     MODA/31,28,31,30,31,30,31,31,30,31,30,31/,EMPTY/4H    /
C
C
C  CALL DEBUG CHECK ROUTINE.
C
      CALL FPRBUG (SUBNAM,1,NOP,IFDEB)
C
C  INITIALIZE COUNTER FOR NUMBER OF DAYS CARRYOVER IS BEING SAVED.
C  THIS COUNTER IS INCREMENTED BY 1 EACH DAY CARRYOVER IS SAVED.
C
      NOC=1
C
C  INITIALIZE ARRAY ELEMENT COUNTER FOR API CORRECTIONS.
C
      KCQN=1
C
C  SELECT REQUIRED VARIABLES FROM PO AND CO ARRAYS.
C
      IVERS=PO(1)
      IVERS = 2
      DO 1000 I=1,6
 1000 DESC(I)=PO(I+1)
      IAIADJ=PO(12)
      IDELT=PO(13)
      C1=PO(23)
      C2=PO(24)
      C3=PO(25)
      C4=PO(26)
      C5=PO(27)
      C6=PO(28)
      IREG1=(PO(29)*100.0)+0.1
      IREG2=(PO(30)*100.0)+0.1
      NEWSTM=CO(1)
      IRANCO=CO(2)
      IAICO=CO(3)
      IROCO=CO(4)
      IAPI=CO(5)
      IAI=CO(6)
      IWE=CO(7)
      NPER = 24/IDELT
      IF(IFDEB.GT.0) GO TO 133
C
C  CALL ROUTINE TO GET MONTH, DAY, AND YEAR FOR FIRST DATE BEING RUN.
C  SET NUMBER OF DAYS FOR RUN AND INITIALIZE INTERNAL DAY AND
C  HOUR COUNTER.
C
  101 CALL MDYH1 (IDA,IDELT,KMO,KDA,KYR,KHR,NLSTZ,NOUTDS,TZ)
      KYR=KYR-((KYR/100)*100)
      LAST=LDA-IDA+1
      INTDA=IDA
      INTHR = (IDA-1)*24
      IF(IFDEB.GT.0) GO TO 134
C
C  CALCULATE COMPUTATIONAL PERIOD AT OR JUST BEFORE LSTCMPDY FOR FFG
 8870 IF(IFDEB.GT.0) GOTO 170
 8872 IF(IFFG.EQ.0) GOTO 8888
      KDAFFG = LDACPD
      KHRFFG = (LHRCPD/IDELT)*IDELT
      IF(KHRFFG.GT.0) GOTO 8880
      KDAFFG = KDAFFG - 1
      KHRFFG = 24
 8880 IF(IFDEB.GT.0) GOTO 174
C
C  BEGIN DAILY LOOP.
C
 8888 DO 1005 II=1,LAST
C
C  DETERMINE LOCATION OF FIRST VALUE NEEDED IN PX ARRAY AND FIRST VALUE
C  TO BE OUTPUT IN RO ARRAY FOR THIS DATE.
C
      LOC=((INTDA-IDADAT)*4)+1
C
C  DETERMINE LOCATION IN CURAPI AND CURAI ARRAYS FOR STORING API AND/OR
C  AI VALUES FOR TIME SERIES OUTPUT.
C
      LIK=(IDA-IDADAT)+II
C
C  APPLY AI ADJUSTMENT FACTOR TO CARRYOVER VALUES OF STORM AI AND
C  CURRENT AI.
C
      IAICO=IAICO+IAIADJ
      IF(IAICO.LE.10) IAICO=11
      IAI=IAI+IAIADJ
      IF(IAI.LE.10) IAI=11
C
C  CHECK FOR LEAP YEAR.
C
      IF(((KYR/4)*4).EQ.KYR) MODA(2)=29
C
C  DETERMINE MONTH, DAY, AND YEAR FOR NEXT DATE TO BE PROCESSED.
C
      IF(KDA.GE.MODA(KMO)) GO TO 102
      NMO=KMO
      NDA=KDA+1
      NYR=KYR
      GO TO 104
  102 IF(KMO.GE.12) GO TO 103
      NMO=KMO+1
      NDA=1
      NYR=KYR
      GO TO 104
  103 NMO=1
      NDA=1
      NYR=KYR+1
C
C  DETERMINE WEEK NUMBER OF CURRENT DATE.  ALSO CALCULATE FX TERM OF
C  API/AI EQUATION.
C
  104 IDAY=0
      DO 1001 MM=1,KMO
      IDAY=IDAY+MODA(MM)
 1001 CONTINUE
      IDAY=IDAY-MODA(KMO)+KDA
      WEEK=((IDAY-1)/7)+1
      XW=WEEK*0.12083
      FX=COS(XW-C1)+(C2*COS(2.0*XW))
      IF(IFDEB.GT.0) GO TO 135
C
C  CONVERT TODAY'S PERIODS OF RAINFALL/MELT FROM METRIC TO ENGLISH.
C  THEN DETERMINE DAY'S TOTAL 24-HOUR AMOUNT.
C
  105 TOT24=0.0
      DO 1002 I=1,4
      J=LOC+I-1
      RAIN(I)=PX(J)/25.4
      TOT24=TOT24+RAIN(I)
 1002 CONTINUE
      ITOT24=(TOT24*100.0)+0.5
      IF(IFDEB.GT.0) GO TO 136
C
C  CHECK FOR NO RAINFALL/MELT.
C
  106 IF(ITOT24.GT.-1) GOTO 107
C
C  MUST BYPASS SO CARRYOVERS ARE UPDATED FOR FFG EVEN WHEN
C  NO RAIN DURING THE 24 HOURS
C 106 IF(ITOT24.GT.0) GO TO 107
C
C  IF NO RAINFALL/MELT, INITIALIZE CARRYOVERS AND SET RUNOFF AMOUNTS
C  TO ZERO.
C
      NEWSTM=4
      IRANCO=0
      IAICO=IAI-IAIADJ
      IROCO=0
      DO 1003 I=1,4
      IRO(I)=0
      J=LOC+I-1
      RO(J)=0.0
 1003 CONTINUE
      IF(IFDEB) 120,120,137
C
C  BEGIN 6-HOURLY API/AI/RUNOFF COMPUTATION.
C
  107 IPER=0
C
C  CHECK FOR STORM BREAK.
C
      IF(NEWSTM.GE.4) GO TO 108
C
C  IF STORM IS CONTINUING, USE STORM CARRYOVER VALUES.
C
      PCPN=IRANCO
      PCPN=PCPN/100.0
      PREVRO=IROCO
      PREVRO=PREVRO/100.0
      AI=IAICO
      FI=AI/10.0
      GO TO 2108
C
C  IF STORM IS BROKEN, INITIALIZE STORM VALUES.
C
  108 PCPN=0.0
      PREVRO=0.0
      AI=IAI
      FI=AI/10.0
C
C  UPDATE PERIOD COUNTER AND CHECK FOR END OF DAY.
C
 2108 IF(IFDEB.GT.0) GOTO 176
  109 IPER=IPER+1
      IF(IPER.GT.4) GO TO 118
      IF(IFDEB.GT.0) GO TO 138
C
C  CHECK FOR NO RAINFALL/MELT FOR PERIOD.
C
  110 IF(RAIN(IPER).LT.0.01) GO TO 114
C
C  IF 6-HOURLY RAINFALL/MELT EQUALS OR EXCEEDS 0.05 INCHES, STORM
C  CONTINUES.
C
      IF(RAIN(IPER).LT.0.049) GO TO 111
      NEWSTM=-1
C
C  ADD 6-HOURLY AMOUNT TO STORM TOTAL.
C
  111 PCPN=PCPN+RAIN(IPER)
C
C  IF 6-HOURLY RAINFALL/MELT EQUALS OR EXCEEDS 0.10 INCHES, UPDATE
C  STORM AI VALUE BY 0.05 AS A DURATION FACTOR.
C
      IF(RAIN(IPER).LT.0.1) GO TO 112
      FI=FI+0.05
      IF(IFDEB.GT.0) GO TO 139
C
C  CALCULATE STORM TOTAL RUNOFF AND 6-HOURLY INCREMENTAL RUNOFF.
C
  112 CALL RO29 (FI,PCPN,STRO)
      IRO(IPER)=((STRO-PREVRO)*100.0)+0.5
      PREVRO=STRO
      IF(IFDEB.GT.0) GO TO 140
  113 IF(IRO(IPER).GE.0) GO TO 115
  114 IRO(IPER)=0
C
C  UPDATE STORM PERIOD COUNTER AND CHECK TO SEE IF STORM IS BROKEN.
C
  115 NEWSTM=NEWSTM+1
      IF(NEWSTM-4) 2109,117,116
C
C  IF STORM IS BROKEN, INITIALIZE STORM VALUES.
C
  116 NEWSTM=4
  117 PCPN=0.0
      PREVRO=0.0
      AI=IAI
      FI=AI/10.0
C
C  SAVE CARRYOVER AT LSTCMPDY FOR FFG
 2109 IF(IFFG.EQ.0) GOTO 109
      KH = IPER*IDELT
      IF(INTDA.EQ.KDAFFG.AND.KH.EQ.KHRFFG) THEN
        CO(1) = NEWSTM
         JTMP = PCPN*100.
        CO(2) = JTMP + 0.5
        CO(3) = (FI*10.) + 0.5 - IAIADJ
         JTMP = PREVRO*100.
        CO(4) = JTMP + 0.5
        CO(5) = IAPI
        CO(6) = IAI - IAIADJ
        CO(7) = IWE
        IF(IFDEB.GT.0) GOTO 180
 2115   CONTINUE
      ENDIF
      GO TO 109
C
C  AT END OF EACH DAY, STORE 6-HOURLY RUNOFF VALUES IN RO ARRAY
C  AND CONVERT TO METRIC.
C
  118 DO 1004 I=1,4
      J=LOC+I-1
      RO(J)=IRO(I)
      RO(J)=(RO(J)/100.0)*25.4
 1004 CONTINUE
      IF(IFDEB.GT.0) GO TO 141
C
C  AT END OF EACH DAY, UPDATE STORM CARRYOVER VALUES.
C
  119 IRANCO=(PCPN*100.0)+0.5
      IAICO=(FI*10.0)+0.51
      IAICO=IAICO-IAIADJ
      IROCO=(PREVRO*100.0)+0.5
      IF(IFDEB.GT.0) GO TO 142
C
C  CONVERT WATER EQUIVALENT VALUE FOR CURRENT DATE FROM METRIC TO
C  ENGLISH AND STORE IN CARRYOVER VARIABLE.
C
  120 IWE=((WE(LIK)/25.4)*100.0)+0.5
      IF(IFDEB.GT.0) GO TO 143
C
C  CHECK FOR API CORRECTION TO BE APPLIED TO CURRENT COMPUTATIONAL DAY.
C  IF ALL CORRECTIONS PASSED IN COMMON BLOCK MOD129 HAVE BEEN CHECKED,
C  OR CORRECTION IS FOR FORECAST DAY, SKIP CHECK.
C
  121 IF(KCQN.GT.NDT29) GO TO 124
      IF(IDT29(KCQN).GT.(LDACPD*24)) GO TO 124
      IF(IDT29(KCQN).GT.(INTDA*24)) GO TO 124
      IAPI=(VAL29(KCQN)*100.0)+0.5
      IF(IFDEB.GT.0) GO TO 144
  122 IF(IAPI.LE.0.OR.IAPI.GT.600) GO TO 999
  123 KCQN=KCQN+1
C
C  CALCULATE API VALUE FOR CURRENT DATE.  IF WATER EQUIVALENT VALUE IS
C  NON-ZERO, HOLD API CONSTANT.
C
  124 IF(IWE.LE.0) GO TO 125
      IAPI=((IAPI*100)+(ITOT24*IREG1)+50)/100
      GO TO 126
  125 IAPI=((IAPI*IREG2)+(ITOT24*IREG1)+50)/100
  126 IF(IAPI.GT.600) IAPI=600
      API=IAPI
      API=API/100.0
C
C  CALCULATE AI VALUE FOR CURRENT DATE.  MINIMUM VALUE ALLOWED IS 1.1.
C
      AI=C3+(C4*API)+(0.07*API*API)+(C5*FX)+(C6*FX*API)
      IAI=(AI*10.0)+0.5
      IF(IAI.LE.10) IAI=11
      IF(IFDEB.GT.0) GO TO 145
C
C  IF API OR AI VALUES ARE TO BE WRITTEN TO AN OUTPUT TIME SERIES,
C  STORE THEM IN CURAPI AND CURAI ARRAYS.  CURAPI IS METRIC.
C
  127 IF(PO(34).EQ.EMPTY) GO TO 128
      CURAPI(LIK)=API*25.4
      IF(IFDEB.GT.0) GO TO 146
  128 IF(PO(37).EQ.EMPTY) GO TO 130
  129 CURAI(LIK)=IAI
      CURAI(LIK)=CURAI(LIK)/10.0
      IF(IFDEB.GT.0) GO TO 147
C
C  CHECK TO SEE IF CARRYOVER IS TO BE SAVED FOR CURRENT DATE.
C
  130 IF(IFILLC.LE.0) GO TO 132
      IF(NCSTOR.LE.0) GO TO 132
      IF(NOC.GT.NCSTOR) GO TO 132
      IF(INTDA.NE.ICDAY(NOC)) GO TO 132
C
C  IF CARRYOVER IS TO BE SAVED, STORE VALUES IN TEMPORARY ARRAY AND
C  CALL SAVE CARRYOVER ROUTINE.
C
      CARY(1)=NEWSTM+0.01
      CARY(2)=IRANCO+0.01
      CARY(3)=IAICO+0.01
      CARY(4)=IROCO+0.01
      CARY(5)=IAPI+0.01
      CARY(6)=IAI+0.01
      CARY(7)=IWE+0.01
      CARY(8)=ITOT24+0.01
      CALL FCWTCO (INTDA,24,CARY,8)
      IF(IFDEB.GT.0) GO TO 148
C
C  UPDATE COUNTER FOR NUMBER OF DAYS CARRYOVER HAS BEEN STORED.
C
  131 NOC=NOC+1
C
C  UPDATE MONTH, DAY, AND YEAR FOR PROCESSING NEXT DAY.  ALSO UPDATE
C  INTERNAL DAY COUNTER.
C
  132 KMO=NMO
      KDA=NDA
      KYR=NYR
      MODA(2)=28
      INTDA=INTDA+1
 1005 CONTINUE
C
C                     END DAILY LOOP
C
C  IF LAST DATE HAS BEEN PROCESSED, CHECK TO SEE IF CARRYOVER VALUES
C  SHOULD BE UPDATED IN CO ARRAY.
C
      IF(IFILLC.LE.0) GO TO 9999
      IF(IFFG.NE.0) GOTO 9999
      CO(1) = NEWSTM + 0.01
      CO(2) = IRANCO + 0.01
      CO(3) = IAICO + 0.01
      CO(4) = IROCO + 0.01
      CO(5) = IAPI + 0.01
      CO(6) = IAI + 0.01
      CO(7) = IWE + 0.01
      CO(8) = ITOT24 + 0.01
      IF(IFDEB) 9999,9999,149
C
C  DEBUG OUTPUT STATEMENTS.
C
  133 WRITE(IODBUG,11) SUBNAM,IVERS,DESC
      WRITE(IODBUG,12) IDARUN,IHRRUN,LDARUN,LHRRUN,LDACPD,LHRCPD,NOW,
     1                 LOCAL,NOUTZ,NOUTDS,NLSTZ,IDA,IHR,LDA,LHR,IDADAT
      WRITE(IODBUG,13) IFILLC,NCSTOR,ICDAY,ICHOUR
      WRITE(IODBUG,14) NDT29,IDT29,VAL29
      WRITE(IODBUG,15) IAIADJ,IDELT,C1,C2,C3,C4,C5,C6,IREG1,IREG2
      WRITE(IODBUG,16) NEWSTM,IRANCO,IAICO,IROCO,IAPI,IAI,IWE
      IB=((IDA-IDADAT)*4)+1
      IE=((LDA-IDADAT)+1)*4
      WRITE(IODBUG,17) (PX(I),I=1,IE)
      IB=(IDA-IDADAT)+1
      IE=IE/4
      WRITE(IODBUG,18) (WE(I),I=1,IE)
      GO TO 101
  134 WRITE(IODBUG,19) LAST
      GO TO 8870
  170 WRITE(IODBUG,50) IFFG
      GOTO 8872
  174 WRITE(IODBUG,52) KDAFFG,KHRFFG
      GOTO 8888
  135 WRITE(IODBUG,20) II,KMO,KDA,KYR,NMO,NDA,NYR,IDAY,WEEK,INTDA,LOC
      GO TO 105
  136 WRITE(IODBUG,21) RAIN,TOT24
      GO TO 106
  137 WRITE(IODBUG,22) NEWSTM,IRANCO,IAICO,IROCO
      GO TO 120
  176 WRITE(IODBUG,60)
      GOTO 109
  138 WRITE(IODBUG,23) IPER,NEWSTM,PCPN,PREVRO,FI,RAIN(IPER)
      GO TO 110
  139 WRITE(IODBUG,24) FI
      GO TO 112
  140 WRITE(IODBUG,25) STRO,IRO(IPER)
      GO TO 113
  141 IB=LOC
      IE=LOC+3
      WRITE(IODBUG,26) (RO(I),I=IB,IE)
      GO TO 119
  142 WRITE(IODBUG,27) II,NEWSTM,IRANCO,IAICO,IROCO
      GO TO 120
  143 WRITE(IODBUG,28) LIK,IWE
      GO TO 121
  144 WRITE(IODBUG,29) KCQN,VAL29(KCQN)
      GO TO 122
  145 WRITE(IODBUG,30) IAPI,IAI,IAIADJ
      GO TO 127
  146 WRITE(IODBUG,31) LIK,CURAPI(LIK)
      GO TO 128
  147 WRITE(IODBUG,32) LIK,CURAI(LIK)
      GO TO 130
  148 WRITE(IODBUG,33) KMO,KDA,KYR,ICDAY(NOC),ICHOUR(NOC),CARY
      GO TO 131
  149 WRITE(IODBUG,34) (CO(I),I=1,8)
      GO TO 9999
  180 WRITE(IODBUG,55) KMO,KDA,KYR,INTDA,KH,II,IPER
      WRITE(IODBUG,34) (CO(I),I=1,8)
      GOTO 2115
C
C  WARNING STATEMENT.
C
  999 IF(IAPI.LT.0) IAPI=0
      IF(IAPI.GT.600) IAPI=600
      API=IAPI
      API=API/100.0
      WRITE(IPR,35) VAL29(KCQN),IDT29(KCQN),API
      CALL WARN
      GO TO 123
C
C
 9999 RETURN
C
C
   11 FORMAT(/5X,2A4,' DEBUG OUTPUT.',5X,'VERSION:',I4,
     1       //,10X,'ZONE NAME:  ',6A4)
   12 FORMAT(/10X,'IDARUN   IHRRUN   LDARUN   LHRRUN   LDACPD   ',
     1       'LHRCPD   NOW(1)   NOW(2)   NOW(3)   NOW(4)   NOW(5)'/
     2       7X,11I9//
     3       11X,'LOCAL    NOUTZ   NOUTDS    NLSTZ      IDA      ',
     4       'IHR      LDA      LHR   IDADAT'/
     5       7X,9I9)
   13 FORMAT(/10X,'IFILLC:',I3,5X,'NCSTOR:',I3,5X,'ICDAY:',10I7/
     1       46X,10I7/
     2       39X,'ICHOUR:',10I7/46X,10I7)
   14 FORMAT(/10X,'NDT29:',I3,5X,'IDT29:',5I10/24X,'VAL29:',5F10.2)
   15 FORMAT(/10X,'IAIADJ     IDELT',8X,'C1',8X,'C2',8X,'C3',8X,'C4',
     1       8X,'C5',8X,'C6',5X,'IREG1',5X,'IREG2'/
     2       12X,I4,6X,I4,6F10.3,2I10)
   16 FORMAT(/10X,'NEWSTM    IRANCO     IAICO     IROCO      ',
     1       'IAPI       IAI       IWE'/6X,7I10)
   17 FORMAT(/10X,'PX ARRAY (METRIC):',2X,16F6.1,7(/30X,16F6.1))
   18 FORMAT(/10X,'WE ARRAY (METRIC):',2X,16F6.1/30X,16F6.1)
   19 FORMAT(10X,'LAST:',I4)
   20 FORMAT(10X,122('-')/10X,'DAY',I3,5X,'TODAY:',2X,3I2,
     1       5X,'TOMORROW:',2X,3I2,5X,'DAY OF YEAR:',I4,
     2       5X,'WEEK NO:',F4.0,5X,'INTERNAL DAY:',I6,5X,'LOC:',I4)
   21 FORMAT(15X,'6-HRLY RAIN:',4F8.2,5X,'24-HR TOTAL:',F8.2)
   22 FORMAT(15X,'NO RAIN TODAY     NEWSTM:',I3,5X,'IRANCO:',I3,
     1       5X,'IAICO:',I3,5X,'IROCO:',I3)
C   23 FORMAT(15X,'PERIOD:',I3,5X,'NEWSTM:',I3,5X,'STORM PCPN:',F6.2,
C     1       5X,'STORM RUNOFF:',F6.2,5X,'FI:',F5.2,5X,'INC RAIN:',F6.2)
C   24 FORMAT(30X,'FI UPDATE:',F5.2)
C   25 FORMAT(30X,'NEW STORM RUNOFF:',F6.2,5X,'6-HRLY RUNOFF:',I5)
   23 FORMAT(15X,I3,3X,I3,3X,F6.2,1X,F6.2,2X,F5.2,2X,F5.2,1X,
     +       F5.2,1X,F6.2,1X,F6.2,1X,I6)
   24 FORMAT(55X,F5.2)
   25 FORMAT(61X,F6.2,1X,I6)
   26 FORMAT(15X,'6-HRLY METRIC RUNOFF VALUES STORED IN RO ARRAY:',
     1       4F8.2)
   27 FORMAT(15X,'DAY',I3,' FINAL C/O VALUES...     NEWSTM:',I3,
     1       5X,'IRANCO:',I5,5X,'IAICO:',I3,5X,'IROCO:',I5)
   28 FORMAT(15X,'WE FOR TODAY...     LIK:',I3,5X,'IWE:',I5)
   29 FORMAT(15X,'KCQN:',I3,5X,'VAL29(KCQN):',F6.2)
   30 FORMAT(15X,'IAPI:',I4,5X,'IAI:',I3,5X,'IAIADJ:',I4)
   31 FORMAT(15X,'API TO TIME SERIES...     LIK:',I3,5X,'CURAPI:',F6.1)
   32 FORMAT(15X,'AI TO TIME SERIES...      LIK:',I3,5X,'CURAI:',F7.1)
   33 FORMAT(/15X,'CARRYOVER WAS SAVED FOR DATE:',2X,3I2,
     1       5X,'INTERNAL DAY/HOUR:',I6,'/',I2/
     2       20X,'CARY(1)   CARY(2)   CARY(3)   CARY(4)   CARY(5)   ',
     3       'CARY(6)   CARY(7)   CARY(8)'/17X,8F10.0)
   34 FORMAT(/15X,'CO ARRAY HAS BEEN UPDATED.'/
     1       20X,'CO(1)     CO(2)     CO(3)     CO(4)     CO(5)     ',
     2       'CO(6)     CO(7)     CO(8)'/15X,8F10.0)
   35 FORMAT(/10X,'**WARNING** MOD CARD FOR API CORRECTION HAS ',
     1       'ILLEGAL API VALUE',F7.2,' FOR DATE',I8,'.'/
     2       22X,'ALLOWABLE RANGE IS 0.0 THRU 6.0 INCHES.'/
     3       22X,'API IS SET TO',F5.2,' INCHES, AND PROGRAM CONTINUES.')
   50 FORMAT(/,10X,'FLASH FLOOD GUIDANCE SWITCH=',I4)
   52 FORMAT(  15X,'FFG COMPUTATIONAL DAY=',I6,/,
     1         15X,'FFG COMPUTATIONAL HOUR=',I4)
   55 FORMAT(/,10X,'CARRYOVER SAVED FOR FFG FOR DATE: ',3I2,
     1       4X,'INTERNAL DAY/HOUR:',I6,'/',I2,
     2       4X,'DAY/PERIOD:',I4,'/',I2)
   60 FORMAT(/,15X,'IPER  NEWST   PCPN  PREVRO   FI   INCRA  UFI',
     +         '    STRO  INCRO',/,
     +         15X,'====  =====  =====  ======  ====  =====  ====',
     +         '  =====  =====')
      END
