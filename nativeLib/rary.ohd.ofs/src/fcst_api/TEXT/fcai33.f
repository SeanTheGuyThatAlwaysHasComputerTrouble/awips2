C MEMBER FCAI33
C  (from old member FCEX33)
C  =====================================================================
C FROM MODULE FCAI33
C
C @PROCESS LVL(77)
C
      SUBROUTINE FCAI33(KMO,KDA,LOC,TOT24,PO,TE,AI,API,
     1                  AVGT,AT,TC,DWE)
C
C  *********************************************************
C
C  THIS ROUTINE DETERMINES THE AI FROM THE CINCINNATI RFC
C  API-SYNTHETIC TEMPERATURE RELATIONSHIP FOR BOTH SUMMER AND WINTER.
C
C  ****************************************************************
C  ROUTINE INITIALLY WRITTEN BY
C            TIM SWEENEY  -  OHRFC            NOVEMBER 1984
C  ***************************************************************
C  PRINCIPLE VARIABLES...
C     AI           CURRENT AI VALUE
C     AIADJ        INITIAL AI ADJUSTMENT FACTOR
C     API          CURRENT API VALUE
C     ARECF        API RECESSION FACTOR--NO SNOW
C     ASRF         API RECESSION FACTOR--SNOW PRESENT
C     AT           CURRENT UNCORRECTED SYNTHETIC TEMPERATURE
C     AVGT         CURRENT AVERAGE TEMPERATURE
C     DPMIN        MINIMUM API DEPLETION AMOUNT
C     DRAD         INSOLATION CORRECTION FACTOR
C     DWE          CURRENT WATER EQUIVALENT VALUE
C     KDA          MONTH
C     KMO          DAY
C     LAISUM       SUMMER API AND SYNTHETIC TEMPERATURE (TC) TABLE
C     LAIWIN       WINTER API AND SYNTHETIC TEMPERATURE (TC) TABLE
C     LAT          LATITUDE OF RUNOFF ZONE
C     LOC          LOCATION OF CURRENT DAYS FIRST ELEMENT IN THE INPUT
C                    TIME SERIES DATA FOR RAINFALL/MELT AND TEMPERATURE
C     PO(1)        INPUT PARAMETRIC DATA FROM P ARRAY
C     PRECF        PRECIPITATION RECESSION FACTOR
C     RANH         HIGHEST SYNTHETIC TEMPERATURE TO USE WINTER TABLE
C     RANL         LOWEST SYNTHETIC TEMPERATURE TO USE SUMMER TABLE
C     SUBNAM(2)    ROUTINE NAME
C     TC           CURRENT SYNTHETIC TEMPERATURE (CORRECTED)
C     TCADJ        SYNTHETIC TEMPERATURE ADJUSTMENT FACTOR
C     TE(1)        INPUT TEMPERATURE TIME SERIES DATA
C     TOT24        TOTAL 24 HOUR PRECIPITATION ENDING 12Z
C     WERCR        CRITERIA WE FOR API RECESSION FACTOR WITH SNOW
C
C  *******************************************************************
C
      DIMENSION LAIWIN(7,9),LAISUM(8,9),DPMIN(12),PO(*),TE(1),
     1          SUBNAM(2),DRAD(13)
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_api/RCS/fcai33.f,v $
     . $',                                                             '
     .$Id: fcai33.f,v 1.1 1995/09/17 18:57:33 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SUBNAM/4HFCAI,4H33  /,NOP/33/
      DATA LAIWIN/140,230,300,330,380,460,570,
     1           140,220,290,330,380,440,550,
     2           140,220,280,330,360,420,520,
     3           140,220,270,310,340,390,480,
     4           140,180,220,240,270,320,380,
     5           140,160,170,190,220,240,280,
     6           140,150,160,170,180,200,230,
     7           140,150,150,160,170,180,200,
     8           140,150,150,160,160,180,190/
C
C......WINTER......
C       10 * 1.4  1.4    -    -    -  1.4
C       20 * 2.3  2.2                 1.5
C       -  *  -    -     -    -    -   -
C    TC -  *  -    -     -    -    -   -
C       -  *  -    -     -    -    -   -
C       70 * 5.7  5.5    -    -    -  1.9
C    *************************************
C     API  *-4.0 -3.0    -    -    - +4.0
C
      DATA LAISUM/280,450,580,660,720,760,760,760,
     1           280,430,550,630,680,730,760,760,
     2           280,420,520,600,650,680,730,760,
     3           270,390,480,550,590,620,660,740,
     4           220,300,380,450,480,510,540,620,
     5           160,230,310,350,380,400,420,470,
     6           150,210,260,300,320,330,360,390,
     7           140,180,230,260,280,290,320,360,
     8           140,170,200,230,250,270,280,330/
C
C......SUMMER......
C       30 * 2.8  2.8    -    -    -  1.4
C       40 * 4.5  4.3    -    -    -  1.7
C       -  *  -    -     -    -    -   -
C  TC   -  *  -    -     -    -    -   -
C       -  *  -    -     -    -    -   -
C      100 * 7.6  7.6    -    -    -  3.3
C      ************************************
C      API *-4.0 -3.0    -    -    - +4.0
C
      DATA DPMIN/3*.04,.05,.06,3*.08,.06,.05,2*.04/
      DATA DRAD/0.,.6,3.5,6.6,10.5,13.0,12.9,11.6,8.6,4.7,1.7,0.,0./
C
C  CALL DEBUG CHECK ROUTINE.
C
      CALL FPRBUG(SUBNAM,1,NOP,IFDEB)
C  SELECT REQUIRED VARIABLES FROM PO ARRAY
C
      IVERS=PO(1)
      LAT=PO(10)
      AIADJ=PO(12)
      TCADJ=PO(26)
      PRECF=PO(27)
      ARECF=PO(28)
      ASRF=PO(29)
      WERCR=PO(30)
C
C
C  SYNTHETIC TEMPERATURE
C
C  COMPUTE CURRENT AVERAGE TEMPERATURE FROM THE CURRENT
C  FOUR 6-HOURLY VALUES FOR THE DAY AND CONVERT TO ENGLISH
C
   40 J=LOC-1
      TAV=(TE(J+1)+TE(J+2)+TE(J+3)+TE(J+4))*0.25
      TAV=1.8*TAV+32.
C
C  YESTERDAYS AVERAGE TEMPERATURE
      YT=AVGT
C
C  COMPUTE TODAYS ANTECEDENT TEMPERATURE
C
      ABAR=(TAV+YT)*0.5
      IF(ABAR-15.) 50,60,60
   50 ABAR=15.
   60 AT=AT+(ABAR-AT*AT/ABAR)/38.
C
C  COMPUTE LATITUDE CORRECTION FACTOR
      ALAT=LAT/100 + (LAT-(LAT/100)*100)/60.
      DLAT=-0.5*(ALAT-39.)
C
C  COMPUTE INSOLATION CORRECTION FACTOR
      SUN=DRAD(KMO)+((DRAD(KMO+1)-DRAD(KMO))*KDA)/31.
C
C  COMPUTE SYNTHETIC TEMPERATURE WITH SYNTHETIC TEMPERATURE ADJUSTMENT
C
      TC=AT+DLAT+SUN+TCADJ
C
C  COMPUTE API FOR CURRENT DATE
C  IF WATER EQUIVALENT IS EQUAL TO OR GREATER THAN WERCR,
C  USE A DIFFERENT RECESSION FACTOR.
C
      IF(DWE-WERCR) 110,110,100
  100 DEL=API*(1.0-ASRF)+TOT24*(1.0-PRECF)
      GO TO 120
  110 DEL=API*(1.0-ARECF)+TOT24*(1.0-PRECF)
  120 IF(DEL-DPMIN(KMO)) 140,140,150
  140 DEL=DPMIN(KMO)
  150 API=API+TOT24-DEL
C
C  CHECK THAT API IS IN ALLOWABLE RANGE,I.E. -4.00 TO 4.00
C
      IF(API+4.0) 170,170,180
  170 API=-4.0
      GO TO 210
  180 IF(API-4.0) 210,210,190
  190 API=4.0
C
C  TRANSFER SELECTED VARIABLES FOR EVENTUAL UPDATING
C
  210 AVGT=TAV
C  DETERMINE "AI" USING WINTER OR SUMMER OR COMBINATION THEREOF
C   "API","TC" VS "AI".
C
C  SET TEMP RANGE
      GO TO (230,230,230,240,220,220,220,220,220,250,250,230),KMO
C
C  TC RANGES THAT REQUIRE INTERPOLATION BETWEEN WINTER AND SUMMER
C  SUMMER RANGE
  220 RANL=48.0
      RANH=70.0
      GO TO 300
C  WINTER RANGE
  230 RANL=48.0
      RANH=75.0
      GO TO 300
C  RANGE FOR APRIL ONLY
  240 RANL=60.0
      RANH=70.0
      GO TO 300
C  RANGE FOR OCT AND NOV
  250 RANL=35.0
      RANH=55.0
C
C
C   COMPUTE COLUMN NUMBER IN TABLE AND PERCENTAGE FOR "API" IN "API",
C   "TC" VS "AI" ARRAY.
C
  300 ICF=API*100.+0.5+500
      IC=ICF/100
      DELC=ICF-100*IC
      DELC=DELC/100.
C
C   DETERMINE SEASON
C
      IF(TC-RANL) 310,310,320
  310 KSEAS=1
C
C   FULLY WINTER
      GO TO 350
C
  320 IF(TC-RANH) 330,330,340
  330 KSEAS=2
C   MUST HAVE "AI" INTERPOLATED BETWEEN SUMMER AND WINTER
      GO TO 350
C
C   FULLY SUMMER
  340 KSEAS=3
      GO TO 370
C
C   WINTER "AI" COMPUTATION
  350 IRF=TC*10.0+0.5
      IE=IRF/100
      DELR=IRF-100*IE
      DELR=DELR/100.
      IAI1=LAIWIN(IE,IC)+(LAIWIN(IE,IC+1)-LAIWIN(IE,IC))*DELC
      IAI2=LAIWIN(IE+1,IC)+(LAIWIN(IE+1,IC+1)-LAIWIN(IE+1,IC))*DELC
      AIW=(IAI1+(IAI2-IAI1)*DELR)*0.01
      GO TO (400,370,370),KSEAS
C   SUMMER "AI" COMPUTATION
  370 IRF=(TC*10.)-200.0
      IE=IRF/100
      DELR=IRF-100*IE
      DELR=DELR/100.
      IAI1=LAISUM(IE,IC)+(LAISUM(IE,IC+1)-LAISUM(IE,IC))*DELC
      IAI2=LAISUM(IE+1,IC)+(LAISUM(IE+1,IC+1)-LAISUM(IE+1,IC))*DELC
      AIS=(IAI1+(IAI2-IAI1)*DELR)*0.01
      GO TO (400,410,420),KSEAS
  400 AI=AIW
      GO TO 440
  410 PROPO=(TC-RANL)/(RANH-RANL)
      AI=AIW+(AIS-AIW)*PROPO
      GO TO 440
  420 AI=AIS
C
C  APPLY AI ADJUSTMENT
  440 AI=AI+AIADJ
C
C  CHECK RANGE OF AI:  1.2 TO 7.6
C
      IF(AI-1.2) 500,510,510
  500 AI=1.2
  510 IF(AI-7.6) 9999,9999,520
  520 AI=7.6
 9999 RETURN
      END
