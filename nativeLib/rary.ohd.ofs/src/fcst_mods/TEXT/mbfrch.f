C  MODULE MBFRCH
C-----------------------------------------------------------------------
C
C  THIS ROUTINE PERFORMS THE BFRCHNG MOD FOR THE BASEFLOW OPERATION
C
      SUBROUTINE MBFRCH (MP,P,NCARDS,MODCRD,IIDATE,NXTOPN,NXTNAM)
C
      LOGICAL FIRST
      CHARACTER*8   NXTNAM,OPID,BLANK8
      CHARACTER*60 STRNG
C
      INCLUDE 'ufreex'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fctim2'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fpwarn'
      INCLUDE 'common/fmodft'
      INCLUDE 'common/mod138'
C
      DIMENSION P(MP)
      DIMENSION OLDOPN(2),MODCRD(20,NCARDS)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mods/RCS/mbfrch.f,v $
     . $',                                                             '
     .$Id: mbfrch.f,v 1.3 2002/02/11 19:21:45 dws Exp $
     . $' /
C    ===================================================================
C
      DATA ISLASH/4H/   /,BLANK8/8H        /
C
C
      CALL FSTWHR(8HMBFRCH  ,0,OLDOPN,IOLDOP)
C
C   SET DEBUG FLAG
C
      IBUG=IFBUG(4HMODS)+IFBUG(4HBFRC)
C
      IF (IBUG.GT.0) WRITE (IODBUG,799)NXTOPN,NXTNAM
  799 FORMAT (' *** SUBROUTINE MBFRCH ENTERED *** NEXT OPERATION ',
     1 'NUMBER =',I3,', OPERATION NAME = ',A8)
C
C  CHECK IF NEXT OPERATION IS A BASEFLOW OPERATION
C  IF SO, PERFORM THIS MOD, IF NOT, LEAVE
C
      IF (NXTOPN.EQ.38) GO TO 111
      GO TO 999
C
  111 IDATE=IIDATE
C
C   SEE IF DATE IS WITHIN ALLOWED PERIOD
C
C  RULES FOR INCLUDING THIS MOD
C    1. IF START HOUR IS AT INTERNAL HOUR ZERO - DATE ENTERED
C       MUST BE GT START HOUR
C    2. IF START HOUR IS DURING A HYDROLOGIC DAY (HOUR 1-23)
C       ANY HOUR DURING THAT DAY IS VALID
C    3. ANY HOUR DURING A HYDROLOGIC DAY THAT CONTAINS THE ENDING
C       HOUR IS VALID
C    4. WILL ONLY KEEP THE LATEST VALUE FOR ANY HYDROLOGIC DAY -
C       LAST IN WINS IF THE SAME HOUR IS ENTERED AS ALREADY IN MOD138
C
C  THIS IS A PERMANENT MOD - START DATE MUST BE GREATER THAN LSTCMPDY
C 
      ISTHR=(IDA-1)*24+1
      IENHR=(LDA-1)*24+24
C
      IF (ISTHR.LE.IDATE.AND.IENHR.GE.IDATE) GO TO 11
C
C   DATE FOR CHANGES NOT IN ALLOWABLE WINDOW
C
      IF (MODWRN.EQ.0) GO TO 999
      CALL MDYH2 (IDA,1,IM1,ID1,IY1,IH1,DUM1,DUM2,MODTZC)
      CALL MDYH2 (LDA,24,IM2,ID2,IY2,IH2,DUM1,DUM2,MODTZC)
      IXDA=IDATE/24+1
      IXHR=IDATE-(IXDA-1)*24
      IF (IXHR.EQ.0)IXDA=IXDA-1
      IF (IXHR.EQ.0)IXHR=24
      CALL MDYH2 (IXDA,IXHR,IM3,ID3,IY3,IH3,DUM1,DUM2,MODTZC)
C
      IF (IDATE.GT.ISTHR) THEN
         STRNG=' '
         ELSE
         STRNG='THE START DATE MUST BE GREATER THAN TECHNIQUE LSTCMPDY.'
         ENDIF
C
      WRITE (IPR,602) IM3,ID3,IY3,IH3,MODTZC,
     * IM1,ID1,IY1,IH1,MODTZC,
     * IM2,ID2,IY2,IH2,MODTZC,
     * STRNG,
     * (MODCRD(I,NRDCRD),I=1,20)
  602 FORMAT ('0**WARNING** THE DATE FOR CHANGES IN THE ',
     *  'BFRCHNG MOD (',
     *  I2.2,'/',I2.2,'/',I4.4,'-',I2.2,1X,A4,') ',
     * 'IS NOT IN THE ALLOWED PERIOD ', /
     * 13X,'(',
     *  I2.2,'/',I2.2,'/',I4.4,'-',I2.2,1X,A4,
     *  ' TO ',
     *  I2.2,'/',I2.2,'/',I4.4,'-',I2.2,1X,A4,').',
     *  ' ',A / 
     * 13X,'THE FOLLOWING MOD CARD WILL BE IGNORED:' /
     * 13X,20A4)
      CALL WARN
      GO TO 999
C
   11 MXVALS=1
C
C   READ CARD - IF COMMAND LEAVE
C
      FIRST=.TRUE.
      IF (NRDCRD.EQ.NCARDS) GO TO 113
C
  888 IF (NRDCRD.EQ.NCARDS) GO TO 999
C
      OPID=BLANK8
C
      IF (MISCMD(NCARDS,MODCRD).EQ.0) GO TO 17
C
      IF (.NOT.FIRST) GO TO 999
C
C   HAVE FOUND COMMAND AS FIRST SUBSEQUENT CARD
C
  113 IF (MODWRN.EQ.1)THEN
         WRITE (IPR,920)
  920 FORMAT ('0**WARNING** NO SUBSEQUENT CARDS FOUND FOR THE ',
     1 'BFRCHNG MOD.  PROCESSING CONTINUES.')
         CALL WARN
         ENDIF
      GO TO 999
C
   17 FIRST=.FALSE.
C
C   CALL SUBROUTINE TO READ VALUE
C
      NFLD=1
      NRDCRD=NRDCRD+1
C
      CALL MRDVAL(NCARDS,MODCRD,NFLD,MXVALS,NVALS,VALUE,ISTAT)
C
      IF (IBUG.GT.0) WRITE (IODBUG,780)ISTAT,FIRST,NVALS,VALUE
  780 FORMAT (' **IN MBFRCH** BFRCHNG MOD, AFTER CALL TO MRDVAL - ',
     1 'ISTAT=',I3,', FIRST=',L4,', NVALS=',I2,', VALUE=',G10.2)
C
C   ISTAT RETURNED FROM MRDVAL MEANS
C     =0, VALUE READ OK, NO ADDITIONAL FIELDS ON CARD
C     =2, VALUE READ OK, ADDITIONAL FIELDS ON CARD
C     =-1, NO VALUES ENTERED
C     ELSE, MORE THAN 1 VALUE ENTERED
C
C  IF NO MORE FIELDS ENTERED WANT TO MAKE THIS BFRCHNG MOD
C  CHANGE TO ALL BASEFLOW OPERATIONS IN THIS SEGMENT, SO SINCE
C  WE KNOW THAT THE NEXT OPERATION IS A BASEFLOW GO AHEAD
C  AND MAKE CHANGE.
C
      IF (ISTAT.EQ.0) GO TO 500
      IF (ISTAT.EQ.2) GO TO 666
      IF (ISTAT.EQ.-1) GO TO 750
C
      IF (MODWRN.EQ.1)
     *   WRITE (IPR,635) (MODCRD(I,NRDCRD),I=1,20)
  635 FORMAT ('0**WARNING** IN BFRCHNG MOD - MORE THAN 1 ',
     1 'VALUE ENTERED.'/13X,'CURRENT MOD CARD IMAGE IS'/13X,20A4)
      GO TO 998
C
  750 IF (MODWRN.EQ.1)
     *   WRITE (IPR,936) (MODCRD(I,NRDCRD),I=1,20)
  936 FORMAT ('0**WARNING** ',
     1  'NO VALUES ENTERED ON A SUBSEQUENT CARD FOR BFRCHNG MOD'/
     2  13X,'THE CURRENT MOD CARD IMAGE IS'/13X,20A4)
      GO TO 998
C
C   HAVE ADDITIONAL FIELDS - LOOK FOR A SLASH (/) AND
C   AN OPERATION NAME - REPROCESS CURRENT FIELD TO SEE IF A SLASH
C
  666 ISTRT=-1
      NCHAR=1
      ICKDAT=0
C
      IF (IBUG.GT.0) WRITE (IODBUG,832)NFLD,NRDCRD
  832 FORMAT (' ABOUT TO CALL UFIEL2 - LOOKING FOR A SLASH',
     1 ', NFLD=',I3,', NRDCRD=',I3)
C
      CALL UFIEL2 (NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF (IBUG.GT.0) WRITE (IODBUG,833)NFLD,ISTRT,LEN,ITYPE,IFIELD,ISTAT
  833 FORMAT (' AFTER UFIEL2 - NFLD,ISTRT,LEN,ITYPE,IFIELD,ISTAT='/
     1 26X,I3,I6,I4,I6,3X,A4,I6)
C
      IF (IFIELD.EQ.ISLASH) GO TO 670
C
      IF (MODWRN.EQ.1)
     *   WRITE (IPR,632) (MODCRD(I,NRDCRD),I=1,20)
  632 FORMAT ('0**WARNING** IN BFRCHNG MOD - A SLASH ',
     1 'WAS NOT FOUND WHERE EXPECTED ON THE FOLLOWING CARD'/
     2 13X,20A4/13X,'NO CHANGES ENTERED ON THIS CARD WILL BE MADE')
      GO TO 998
C
C   NOW READ OPERATION NAME
C
  670 ISTRT=-3
      NCHAR=2
      ICKDAT=0
C
      CALL UFIEL2 (NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,OPID,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF (ISTRT.NE.-2) GO TO 686
      IF (MODWRN.EQ.0) GO TO 888
      WRITE (IODBUG,685) (MODCRD(I,NRDCRD),I=1,20)
  685 FORMAT ('0**WARNING** NO OPERATION NAME ENTERED ',
     1 'AFTER A SLASH ON THE FOLLOWING MOD CARD'/13X,20A4/
     2 13X,'THIS CARD IS IGNORED.')
      GO TO 998
  686 CONTINUE
C
C   CHECK THAT OPERATION NAME ENTERED MATCHES NXTNAM -
C   IF NOT, READ NEXT CARD
C
      IF (IBUG.GT.0) WRITE (IODBUG,857)OPID,NXTNAM
  857 FORMAT (' OPERATION NAME INPUT IS ',A8,', NEXT OPERATION ',
     1 'IN OPERATIONS TABLE IS ',A8,'.')
C
      IF (OPID.EQ.NXTNAM) GO TO 500
      GO TO 888
C
  500 CONTINUE
C
C   HAVE A SPECIFIC OPERATION NAME - FIND THAT OPERATION
C   IN ORDER TO SEE IF PO(9)=2 SO /MOD138/ SHOULD BE FILLED
C
      LOCP=0
      CALL FSERCH(38,NXTNAM,LOCP,P,MP)
C
      IF (LOCP.GT.0) GO TO 505
C
C   HAVE NOT FOUND THE REQUESTED OPERATION
C
      IF (MODWRN.EQ.1)
     *   WRITE (IPR,605)NXTNAM
  605 FORMAT ('0**WARNING** A BASEFLOW OPERATION WITH NAME ',
     1 A8,' HAS NOT BEEN FOUND IN THIS SEGMENT.'/13X,
     2 'YOU ARE CURRENTLY EXECUTING THE BFRCHNG MOD.'/13X,
     3 'THERE ARE PROBLEMS WITH YOUR PARAMETER FILES, CALL O/H ',
     4 'AT 8-427-7624.')
      GO TO 998
C
C  FIRST CHECK THAT THIS OPER HAS BASEFLOW RECESSION AS A TIME SERIES
C
 505  ICHECK=P(LOCP+8)
      IF (ICHECK.EQ.2) GO TO 508
C
C  BASEFLOW RECESSION TIME SERIES NOT DEFINED.  CANNOT USE THIS MOD.
C  NO NEED TO WARN BECAUSE EITHER
C   1. P(9)=0, AND WARNING WAS WRITTEN IN MBFRCP
C      OR
C   2. P(9)=1, AND PERMANENT CHANGE WAS MADE TO P() IN MBFRCP
C
      IF (IBUG.EQ.0) GO TO 888
      WRITE (IPR,610)NXTNAM
  610 FORMAT (' IN BFRCHNG MOD - ',
     1 'BASEFLOW RECESSION AS A TIME SERIES IS NOT DEFINED FOR BASEFLOW'
     2 ,' OPERATION ',A8,'.')
      GO TO 888
C
  508 IF (VALUE.GE.0.5.AND.VALUE.LT.1.0) GO TO 509
C
C   BAD TOTAL BASEFLOW RECESSION VALUE ENTERED
C
      IF (MODWRN.EQ.0) GO TO 888
      WRITE (IPR,604)NXTNAM,VALUE
  604 FORMAT ('0**WARNING** IN BFRCHNG MOD - BASEFLOW RECESSION ',
     1 'ENTERED MUST BE GE 0.5 AND LT 1.0'/13X,
     2 'FOR BASEFLOW OPERATION ',A8/13X,
     3 'VALUE ENTERED = ',G10.3/
     4 13X,'THE BASEFLOW RECESSION VALUE WILL NOT BE CHANGED.')
      GO TO 998
C
C   STORE VALUE IN COMMON BLOCK /MOD138/
C
 509  IF (NDT38.GT.0) GO TO 510
C
C   NO VALUES CURRENTLY IN COMMON BLOCK /MOD138/
C
      NDT38=1
      IDT38(1)=IDATE
      VAL38(1)=VALUE
      GO TO 530
C
C   VALUES CURRENTLY IN COMMON BLOCK /MOD138/
C   SEE IF DATE FALLS IN SAME HYDROLOGIC DAY AS ONE IN COMMON BLOCK
C
 510  IDAYCK=IDATE/24
      DO 432 I=1,NDT38
      IF (IDT38(I)/24.NE.IDAYCK) GO TO 432
C
C   MATCHES - KEEP VALUE WITH LATEST HOUR - REPLACE IF MATCHES EXACTLY
C
      IF (IDT38(I).GT.IDATE) GO TO 530
      VAL38(I)=VALUE
      GO TO 530
C
  432 CONTINUE
C
C   NO MATCH - VALUE IS NOT FOR SAME HYDROLOGIC DAY AS ANY IN /MOD138/
C   IF THERE IS ROOM - PUT THIS VALUE IN IN CHRONOLOGICAL ORDER
C
      IF (NDT38.LT.MXDT38) GO TO 520
C
C   NO ROOM
C
      IF (MODWRN.EQ.1)
     *   WRITE (IPR,646)MXDT38,(MODCRD(I,NRDCRD),I=1,20)
  646 FORMAT ('0**WARNING** IN BFRCHNG MOD - MORE THAN ',
     1 I2,' UNIQUE ENTRIES HAVE BEEN MADE'/,13X,
     2 'THE VALUE ON THE FOLLOWING MOD CARD IS IGNORED',
     3 /,13X,20A4)
      GO TO 998
C
C   THERE IS ROOM - INSERT CHRONOLOGICALLY
C
  520 IEND38=NDT38
      DO 525 I=1,IEND38
      IF (IDT38(I).LT.IDATE) GO TO 525
C
      MOVE=NDT38-I+1
      DO 523 J=1,MOVE
      K=NDT38+2-J
      IDT38(K)=IDT38(K-1)
  523 VAL38(K)=VAL38(K-1)
      IDT38(I)=IDATE
      VAL38(I)=VALUE
      NDT38=NDT38+1
      GO TO 530
C
  525 CONTINUE
C
C   ADD AT END
C
      NDT38=NDT38+1
      IDT38(NDT38)=IDATE
      VAL38(NDT38)=VALUE
C
  530 IF (IBUG.LT.1) GO TO 888
C
      WRITE (IODBUG,800) IDATE,VALUE,NDT38,NXTNAM
  800 FORMAT (' IN SUBROUTINE MBFRCH - BFRCHNG MOD'/13X,
     1 'IDATE, VALUE, NDT38, NXTNAM = ',I11,1X,G9.2,1X,I3,1X,A8)
      IF (NDT38.GT.0) WRITE (IODBUG,801) (IDT38(I),VAL38(I),I=1,NDT38)
  801 FORMAT (' IDT38, VAL38 ='/(' ',2G9.2))
      GO TO 888
C
  998 IF (MODWRN.EQ.1) CALL WARN
      GO TO 888
C
  999 CALL FSTWHR(OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C
      IF (IBUG.GT.0) WRITE (IODBUG,*) 'EXIT MBFRCH'
C
      RETURN
C
      END
