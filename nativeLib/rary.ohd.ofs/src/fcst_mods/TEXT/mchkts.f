C     MODULE MCHKTS
C
C     DESC - THIS SUBROUTINE CHECKS MOD TIME SERIES INPUT
C
C <><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><><>
C
      SUBROUTINE MCHKTS(MODID,TSID,DTYPE,IDT,KEY,MTS,TS,
     1 OPTYPE,OPNAME,NXTOPN,NXTNAM,IBUG,
     2 UNITS,DIMS,LOCD,MISALW,NVPDT,ISTAT)
C
      CHARACTER*8   MODID,TSID,OPTYPE,OPNAME,BLNK8,NXTNAM
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fpwarn'
      INCLUDE 'common/modctl'
C
      DIMENSION TS(MTS)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mods/RCS/mchkts.f,v $
     . $',                                                             '
     .$Id: mchkts.f,v 1.1 1995/09/17 19:03:41 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA BLNK8/8H        /,BLNK4/4H    /
C
      ISTAT=0
C
      UNITS=BLNK4
      DIMS=BLNK4
      LOCD=0
      MISALW=0
      NVPDT=0
C
C     IF OPERATION TYPE AND NAME ENTERED THEY MUST MATCH
C     THE NEXT OPERATION IN THE OPERATIONS TABLE
C
      IF(IBUG.GT.0)WRITE(IODBUG,875)MODID,KEY,NXTOPN,OPNAME,NXTNAM
  875 FORMAT(11X,'IN SUBROUTINE MCHKTS FROM ',A8,
     1 ' - ABOUT TO SEE IF TIME SERIES IN SEGMENT'/
     1 11X,'KEY, NXTOPN, OPNAME, NXTNAM ='/
     2 11X,I2,I8,3X,A8,1X,A8)
C
C  LEAVE IF KEYWORD IS 'FIRST' AND NOT BEFORE OPERATIONS TABLE
      IF(KEY.EQ.-1.AND.IWHERE.NE.1)GO TO 888
C
C  LEAVE IF KEYWORD IS 'LAST' AND NOT AFTER OPERATIONS TABLE
      IF(KEY.EQ.-2.AND.IWHERE.NE.3)GO TO 888
C
C  LEAVE IF OPERATION TYPE WAS ENTERED AND DOES NOT MATCH NEXT OPERATION
      IF(KEY.GT.0.AND.KEY.NE.NXTOPN)GO TO 888
C
C  OR IF OPERATION NAME WAS ENTERED AND DOES NOT MATCH NEXT OPERATION
      IF(OPNAME.NE.BLNK8.AND.OPNAME.NE.NXTNAM)GO TO 888
C
C     SEE IF TIME SERIES IS IN SEGMENT
C
      CALL FINDTS(TSID,DTYPE,IDT,LOCD,LOCTS,DIMS)
C
      IF(LOCD.GT.0)GO TO 10
C
      IF(IBUG.GT.0)WRITE(IODBUG,863)TSID,DTYPE,IDT,IWHERE
  863 FORMAT(11X,'TIME SERIES ',A8,1X,A4,I3,' NOT FOUND - ',
     1 'IWHERE=',I2)
C
      IF(MODWRN.EQ.0)GO TO 888
C
C  LEAVE IF NO KEYWORD OR 'FIRST' WAS ENTERED AND NOT BEFORE
C  OPERATIONS TABLE BECAUSE HAVE ALREADY PRINTED THIS MESSAGE
      IF(IWHERE.GT.1.AND.(KEY.EQ.0.OR.KEY.EQ.-1))GO TO 888
      WRITE(IPR,600)TSID,DTYPE,IDT
  600 FORMAT(1H0,10X,'**WARNING** TIME SERIES ',A8,1X,A4,1X,I2,
     1 ' NOT IN THIS SEGMENT.')
      GO TO 998
C
   10 CONTINUE
C
C     SEE IF THIS DATA TYPE ALLOWS MISSING DATA
C     AND GET VALUE FOR NUMBER OF VALUES PER DELTA T (NVPDT)
C
      CALL FDCODE(DTYPE,UNITS,DIMS,MISALW,NVPDT,TSCALE,NADINF,IERR)
C
C     SEE WHAT TYPE OF TIME SERIES THIS IS
C
      ITYPE=TS(LOCTS)
C
      IF(IBUG.GT.0)WRITE(IODBUG,876)TSID,DTYPE,IDT,LOCD,LOCTS,MISALW,
     1 NVPDT,ITYPE,IWHERE
  876 FORMAT(11X,'HAVE FOUND TIME SERIES IN THIS SEGMENT'/
     1 11X,'  TSID  , DTYPE, IDT, LOCD, LOCTS, MISALW, NVPDT',
     2 ', ITYPE, IWHERE'/11X,A8,2X,A4,I5,I6,I7,I8,I7,I7,I8)
C
C     ITYPE=1 - INPUT TS
C     ITYPE=2 - UPDATE TS
C     ITYPE=3 - OUTPUT TS
C     ITYPE=4 - INTERNAL TS
C
C     KEYWORD 'LAST' INVALID FOR INPUT TIME SERIES (TS)
C     KEYWORD 'FIRST', 'LAST', OR OPTYPE REQUIRED FOR UPDATE TS
C     KEYWORD 'FIRST' INVALID FOR OUTPUT TS
C     OPTYPE REQUIRED FOR INTERNAL TS
C
C     KEY INDICATES WHERE CHANGE TO TS IS TO BE MADE
C
C     KEY=0, NOT SPECIFIED
C            DEFAULT FOR INPUT TS IS 'FIRST'
C            DEFAULT FOR OUTPUT TS IS 'LAST'
C            KEY REQUIRED FOR UPDATE AND INTERNAL TS
C     KEY=-1, 'FIRST'
C     KEY=-2, 'LAST'
C     KEY>0, EQUALS THE FORECAST COMPONENT OPERATION TYPE NUMBER BEFORE
C            WHICH CHANGE TO TS WILL BE MADE.
C            NOTE THAT FOR KEY>0 THE 8-CHARACTER OPERATION TYPE IS IN
C            VARIABLE OPTYPE.
C     FOR KEY>0, VARIABLE OPNAME HOLDS THE SPECIFIC 8-CHARACTER
C                NAME OF THE OPERATION BEFORE WHICH THE TIME SERIES
C                IS TO BE CHANGED.
C                IF THE NAME IS BLANK THE TIME SERIES IS TO BE CHANGED
C                BEFORE ALL OPERATIONS OF TYPE OPTYPE.
C
      GO TO (20,30,40,50) , ITYPE
C
C     IF GET HERE ITYPE IS NOT VALID - TROUBLE IN TS ARRAY
C
      IF(MODWRN.EQ.0)GO TO 888
      IF(IWHERE.GT.1.AND.(KEY.EQ.0.OR.KEY.EQ.-1))GO TO 888
      WRITE(IPR,601)MODID,TSID,DTYPE,IDT,ITYPE,LOCTS
  601 FORMAT(1H0,10X,'**WARNING** IN ',A8,' MOD FOR TIME SERIES ',
     1 A8,1X,A4,1X,I2/11X,
     2 'A TIME SERIES TYPE OF ',I11,' WAS FOUND IN LOCATION ',I5,
     3 ' OF THE TS ARRAY.'/11X,
     4 'THIS VALUE SHOULD BE FROM 1 TO 4 FOR INPUT, UPDATE, OUTPUT, ',
     5 'AND INTERNAL TIME SERIES.')
      GO TO 998
C
   20 CONTINUE
C
C     INPUT TIME SERIES
C
      IF(KEY.NE.-2)GO TO 21
C
C     INVALID KEYWORD ENTERED FOR INPUT TIME SERIES
C
      IF(MODWRN.EQ.0)GO TO 888
      IF(IWHERE.GT.1.AND.(KEY.EQ.0.OR.KEY.EQ.-1))GO TO 888
      WRITE(IPR,611)MODID,TSID,DTYPE,IDT
  611 FORMAT(1H0,10X,'**WARNING** IN ',A8,' MOD FOR TIME SERIES ',
     1 A8,1X,A4,1X,I2/11X,
     2 'A KEYWORD OF ''LAST'' IS INVALID FOR INPUT TIME SERIES.')
      GO TO 998
C
   21 CONTINUE
C
C     IF KEYWORD WAS ENTERED IT MUST BE 'FIRST' OR OPTYPE
C     IF NO KEYWORD ENTERED ASSUME 'FIRST'
C
      IF(IWHERE.EQ.1.AND.(KEY.EQ.0.OR.KEY.EQ.-1))GO TO 100
      IF(IWHERE.EQ.2.AND.KEY.GT.0)GO TO 100
C
      GO TO 888
C
   30 CONTINUE
C
C     UPDATE TIME SERIES
C
      IF(KEY.NE.0)GO TO 31
C
C     INVALID KEYWORD ENTERED FOR UPDATE TIME SERIES
C
      IF(MODWRN.EQ.0)GO TO 888
      IF(IWHERE.GT.1.AND.(KEY.EQ.0.OR.KEY.EQ.-1))GO TO 888
      WRITE(IPR,603)MODID,TSID,DTYPE,IDT
  603 FORMAT(1H0,10X,'**WARNING** IN ',A8,' MOD FOR TIME SERIES ',
     1 A8,1X,A4,1X,I2/11X,
     2 'A KEYWORD OF ''FIRST'', ''LAST'', OR AN OPTYPE ',
     3 'IS REQUIRED FOR UPDATE TIME SERIES.')
      GO TO 998
C
   31 CONTINUE
C
C     KEYWORD OR OPTYPE WAS ENTERED
C
      IF(IWHERE.EQ.1.AND.KEY.EQ.-1)GO TO 100
      IF(IWHERE.EQ.2.AND.KEY.GT.0)GO TO 100
      IF(IWHERE.EQ.3.AND.KEY.EQ.-2)GO TO 100
C
      GO TO 888
C
   40 CONTINUE
C
C     OUTPUT TIME SERIES
C
      IF(KEY.NE.-1)GO TO 41
C
C     INVALID KEYWORD ENTERED FOR OUTPUT TIME SERIES
C
      IF(MODWRN.EQ.0)GO TO 888
      IF(IWHERE.GT.1.AND.(KEY.EQ.0.OR.KEY.EQ.-1))GO TO 888
      WRITE(IPR,604)MODID,TSID,DTYPE,IDT
  604 FORMAT(1H0,10X,'**WARNING** IN ',A8,' MOD FOR TIME SERIES ',
     1 A8,1X,A4,1X,I2/11X,
     2 'A KEYWORD OF ''FIRST'' IS INVALID FOR OUTPUT TIME SERIES.')
      GO TO 998
C
   41 CONTINUE
C
C     IF KEYWORD WAS ENTERED IT MUST BE 'LAST' OR OPTYPE
C     IF NO KEYWORD ENTERED ASSUME 'LAST'
C
      IF(IWHERE.EQ.2.AND.KEY.GT.0)GO TO 100
      IF(IWHERE.EQ.3.AND.(KEY.EQ.0.OR.KEY.EQ.-2))GO TO 100
C
      GO TO 888
C
   50 CONTINUE
C
C     INTERNAL TIME SERIES
C
      IF(KEY.GT.0)GO TO 51
C
C     INVALID KEYWORD ENTERED FOR INTERNAL TIME SERIES
C
      IF(MODWRN.EQ.0)GO TO 888
      IF(IWHERE.GT.1.AND.(KEY.EQ.0.OR.KEY.EQ.-1))GO TO 888
      WRITE(IPR,605)MODID,TSID,DTYPE,IDT,KEY
  605 FORMAT(1H0,10X,'**WARNING** IN ',A8,' MOD FOR TIME SERIES ',
     1 A8,1X,A4,1X,I2/11X,
     2 'A KEYWORD OF AN OPERATION TYPE ',
     3 'IS REQUIRED FOR INTERNAL TIME SERIES.'/11X,'KEY = ',I5)
      WRITE(IPR,941)
  941 FORMAT(11X,'IF KEY EQ  0, YOU MADE NO ENTRY'/
     2       11X,'IF KEY EQ -1, YOU ENTERED ''FIRST'''/
     3       11X,'IF KEY EQ -2, YOU ENTERED ''LAST''')
      GO TO 998
C
   51 CONTINUE
C
C     KEYWORD WAS ENTERED AND IT MUST BE AN OPERATION TYPE
C
      IF(IWHERE.EQ.2.AND.KEY.GT.0)GO TO 100
C
      GO TO 888
C
  998 CALL WARN
  888 ISTAT=1
C
  100 CONTINUE
C
      IF(IBUG.GT.0)WRITE(IODBUG,953)UNITS,DIMS,LOCD,MISALW,NVPDT,ISTAT
  953 FORMAT(11X,'LEAVING MCHKTS - UNITS=',A4,', DIMS=',A4,
     1 ', LOCD=',I8,', MISALW=',I3,', NVPDT=',I3,', ISTAT=',I2)
C
      RETURN
      END
