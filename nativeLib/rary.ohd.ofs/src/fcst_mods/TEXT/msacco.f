C MODULE MSACCO
C-----------------------------------------------------------------------
C
C
C     DESC - PERFORMS THE SACCO MOD
C
      SUBROUTINE MSACCO(NCARDS,MODCRD,IDATE,IHZERO)
C
      LOGICAL FIRST
      CHARACTER*8 OPNAME,KWRDS,IFIELD,SLASH,BLANK8,MODNAM
C
      COMMON/FSACCO/NUMCOS,SACCOS(15,10)
      INCLUDE 'common/fmodft'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fpwarn'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fctim2'
      INCLUDE 'ufreex'
C
      DIMENSION VALUES(9),KWRDS(9),MODCRD(20,NCARDS)
      DIMENSION OLDOPN(2)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mods/RCS/msacco.f,v $
     . $',                                                             '
     .$Id: msacco.f,v 1.3 1998/07/02 20:50:11 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA KWRDS /'UZTWC   ','UZFWC   ','LZTWC   ','LZFSC   ',
     1 'LZFPC   ','ADIMC   ','UZTWD   ','LZTWD   ','FGIX    '/
      DATA SLASH/'/       '/,BLANK8/'        '/
      DATA MODNAM/'SACCO   '/
C
      CALL FSTWHR(8HMSACCO  ,0,OLDOPN,IOLDOP)
C
C     SET DEBUG SWITCH
C
      IBUG=IFBUG(4HMODS)+IFBUG(4HSACC)
C
      IF(IBUG.LT.1)GO TO 20
C
      WRITE(IODBUG,10)IDATE,NUMCOS
10    FORMAT(1H0,10X,'IN SUBROUTINE MSACCO - SACCO MOD'/
     1  11X,'IDATE=',I11,', NUMCOS=',I5)
      IF(NUMCOS.GT.0)WRITE(IODBUG,430)((SACCOS(J,K),J=1,15),K=1,NUMCOS)
C
20    IF(IUMSAC.EQ.1)GO TO 30
C
C     NEED TO CONVERT FROM ENGLISH TO METRIC UNITS
C
      CALL FCONVT(4HMM  ,4HL   ,ENGUN,AMM2IN,BMM2IN,IER)
      CALL FCONVT(4HDEGC,4HDEG ,ENGUN,ADC2DF,BDC2DF,IER)
C
30    CONTINUE
C
      ICOMP=((LDACPD-1)*24)+LHRCPD
      IF (IDATE.LE.ICOMP) GOTO 40
         IF (MODWRN.EQ.0) GOTO 410
         ITYPE=2
         CALL MODS1(NCARDS,ICMND,MODNAM,MODCRD,ITYPE)
         GOTO 410
C
C     SEE IF DATE IS WITHIN - OR + 12 HOURS OF START OR END OF RUN
C
40    CONTINUE
      ISTHR=(IDA-1)*24+IHZERO
      IENHR=(LDA-1)*24+LHR
C
      IF(ISTHR-12.LE.IDATE.AND.IENHR+12.GE.IDATE)GO TO 60
C
C     DATE NOT WITHIN ALLOWABLE WINDOW
C
      CALL MDYH2(IDA,IHZERO,IM1,ID1,IY1,IH1,DUM1,DUM2,MODTZC)
      CALL MDYH2(LDA,LHR,IM2,ID2,IY2,IH2,DUM1,DUM2,MODTZC)
      IXDA=IDATE/24+1
      IXHR=IDATE-(IXDA-1)*24
      IF(IXHR.EQ.0)IXDA=IXDA-1
      IF(IXHR.EQ.0)IXHR=24
      CALL MDYH2(IXDA,IXHR,IM3,ID3,IY3,IH3,DUM1,DUM2,MODTZC)
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,50)IM3,ID3,IY3,IH3,MODTZC,IM1,ID1,IY1,IH1,MODTZC,
     1IM2,ID2,IY2,IH2,MODTZC
50    FORMAT(1H0,10X,'**WARNING** THE DATE FOR CHANGES ENTERED IN THE ',
     1 'SACCO MOD (',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/11X,
     2 'IS NOT WITHIN 12 HOURS OF THE CURRENT RUN PERIOD (',I2,1H/,I2,
     3 1H/,I4,1H-,I2,1X,A4,4H TO ,I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/
     4 11X,'THESE CHANGES WILL BE IGNORED.')
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 410
C
60    CONTINUE
C
C     READ MOD CARDS FOR THIS SEGMENT - STORE VALUES IN ARRAYS() -
C      WILL TRANSFER TO CB /FSACCO/ AFTER ALL INFO IS READ
C
C     READ CARD - IF COMMAND, LEAVE - IF COMMAND AND 1ST CARD, ERROR
C
      FIRST=.TRUE.
C
      IF(NRDCRD.EQ.NCARDS)GO TO 80
C
70    IF(NRDCRD.EQ.NCARDS)GO TO 410
C
      OPNAME=BLANK8
C
      IF(MISCMD(NCARDS,MODCRD).EQ.0)GO TO 100
C
      IF(.NOT.FIRST)GO TO 410
C
C     HAVE FOUND COMMAND AS FIRST SUBSEQUENT CARD - ERROR
C
80    IF(MODWRN.EQ.1)
     .WRITE(IPR,90)
90    FORMAT(1H0,10X,'**WARNING** NO SUBSEQUENT CARDS FOUND FOR THE ',
     1  'SACCO MOD.  PROCESSING CONTINUES')
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 410
C
100   FIRST=.FALSE.
C
      DO 110 I=1,9
110   VALUES(I)=-9999.
C
      NRDCRD=NRDCRD+1
C
C     NOW READ 2ND FIELD - MUST BE KEYWORD
C
      NFLD=1
      ISTRT=-3
      NCHAR=2
      ICKDAT=0
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,IVALUE,VALUE,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.NE.-2)GO TO 140
C
C     ERROR - DATA EXPECTED - PROCESS NEXT CARD
C
120   IF(MODWRN.EQ.1)
     .WRITE(IPR,130)(MODCRD(I,NRDCRD),I=1,20)
130   FORMAT(1H0,10X,'**WARNING** IN THE SACCO MOD - NOT ENOUGH ',
     1  'FIELDS ON SUBSEQUENT CARD.  THE CARD BEING PROCESSED IS:'/
     2  11X,20A4)
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 70
C
140   DO 150 I=1,9
      IKEY=I
      IF(KWRDS(I).EQ.IFIELD)GO TO 170
150   CONTINUE
      IF(MODWRN.EQ.1)
     .WRITE(IPR,160)(MODCRD(I,NRDCRD),I=1,20)
160   FORMAT(1H0,10X,'**WARNING** IN THE SACCO MOD - AN INVALID ',
     1  'KEYWORD FOUND ON THE FOLLOWING MOD CARD'/11X,20A4)
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 70
C
170   CONTINUE
C
C     NOW READ THE 3RD FIELD - MUST BE AN INTEGER OR REAL NUMBER
C
      ISTRT=-3
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.EQ.-2)GO TO 120
C
      IF(ITYPE.LT.2.AND.NREP.EQ.-1)GO TO 190
C
C     VALUE IS NOT AN INTEGER NOR A REAL NUMBER
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,180)(MODCRD(I,NRDCRD),I=1,20)
180   FORMAT(1H0,10X,'**WARNING** A NONNUMERIC VALUE WAS DECODED ',
     1  'WHERE ONE WAS EXPECTED'/11X,'THE CURRENT MOD CARD IS'/11X,20A4)
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 70
C
190   VALUES(IKEY)=REAL
C
C     SEE IF THERE IS ANOTHER FIELD - IF NOT PROCESS THE INFO
C     IN ARRAYS() - IF SO IT MUST BE A KEYWORD OR A SLASH (/)
C
      ISTRT=-3
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.EQ.-2)GO TO 220
C
C     ANOTHER FIELD - IS IT A SLASH ? - IS NOT SEE IF A KEYWORD
C
      IF(IFIELD.NE.SLASH)GO TO 140
C
C     GET HERE IF FIELD IS A SLASH
C     NOW READ NEXT FIELD - OPERATION NAME
C
      ISTRT=-3
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.NE.-2)GO TO 210
      IF(MODWRN.EQ.0)GO TO 70
      WRITE(IODBUG,200)(MODCRD(I,NRDCRD),I=1,20)
200   FORMAT(11X,'** WARNING ** NO OPERATION NAME ENTERED ',
     1 'AFTER A SLASH ON THE FOLLOWING MOD CARD'/11X,20A4/
     2 11X,'THIS CARD IS IGNORED.')
      CALL WARN
      GO TO 70
210   CONTINUE
C
      OPNAME=IFIELD
C
C     SEE IF ANY SETS OF CARRYOVER IN CB /FSACCO/
C
220   IF(NUMCOS.GT.0)GO TO 300
C
C     NONE IN CB - JUST ADD THIS CARRYOVER TO CB
C
      NUMCOS=1
      CALL UMEMOV(OPNAME,SACCOS(1,1),2)
      SACCOS(3,1)=IDATE+0.01
      DO 230 I=4,15
230   SACCOS(I,1)=-999.
      DO 290 I=1,9
      VALUE=VALUES(I)
      IF(VALUE.LT.-9998.)GO TO 290
      IF(IUMSAC.EQ.1)GO TO 240
      IF(I.LT.9)VALUE=VALUE/AMM2IN
      IF(I.EQ.9)VALUE=(VALUE-BDC2DF)/ADC2DF
240   IF(I.LT.9.AND.VALUE.GE.0.)GO TO 280
      IF(I.EQ.9.AND.VALUE.GE.-273..AND.VALUE.LE.0.)GO TO 280
C
C     INVALID RANGE FOR VALUE
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,250)KWRDS(I)
250   FORMAT(1H0,10X,'**WARNING** IN SACCO MOD - VALUE IS OUT OF ',
     1 'VALID RANGE FOR VARIABLE ',A8)
      IF(I.LT.9.AND.MODWRN.EQ.1)WRITE(IPR,260)VALUE
260   FORMAT(11X,'VALUE IS ',G10.3,' MM.')
      IF(I.EQ.9.AND.MODWRN.EQ.1)WRITE(IPR,270)VALUE
270   FORMAT(11X,'VALUE IS ',G10.3,' DEGC.')
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 290
C
280   SACCOS(I+3,1)=VALUE
290   CONTINUE
      GO TO 70
C
300   CONTINUE
C
C     CHECK IF NAME MATCHES ANY NAME IN CB OR IS BLANK -
C       AND IF DATE MATCHES ANY DATE IN CB
C
      DO 340 I=1,NUMCOS
      IF(IUSAME(OPNAME,SACCOS(1,I),2).EQ.0)GO TO 340
C
C     NAME IN CB MATCHES NAME TO BE STORED
C     NOW SEE IF DATES MATCH
C
      IDTCHK=SACCOS(3,I)
      IF(IDTCHK.NE.IDATE)GO TO 340
C
C     DATES MATCH TOO - ADD INFO TO THIS SPOT IN CB
C
      DO 330 J=1,9
      VALUE=VALUES(J)
      IF(VALUE.LT.-9998.)GO TO 330
      IF(IUMSAC.EQ.1)GO TO 310
      IF(J.LT.9)VALUE=VALUE/AMM2IN
      IF(J.EQ.9)VALUE=(VALUE-BDC2DF)/ADC2DF
310   IF(J.LT.9.AND.VALUE.GE.0.)GO TO 320
      IF(J.EQ.9.AND.VALUE.GE.-273..AND.VALUE.LE.0.)GO TO 320
C
C     INVALID RANGE FOR VALUE
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,250)KWRDS(J)
      IF(J.LT.9.AND.MODWRN.EQ.1)WRITE(IPR,260)VALUE
      IF(J.EQ.9.AND.MODWRN.EQ.1)WRITE(IPR,270)VALUE
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 330
C
320   SACCOS(J+3,I)=VALUE
330   CONTINUE
      GO TO 70
C
340   CONTINUE
C
C     HAVE NOT FOUND MATCH FOR NAME AND DATE -
C     ADD NEW ENTRY TO CB
C
      IF(NUMCOS.LT.10)GO TO 360
C
C     NOT ENOUGH ROOM IN CB
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,350)
350   FORMAT(1H0,10X,'**WARNING** NOT ENOUGH ROOM IN COMMON BLOCK ',
     1 '/FSACCO/'/11X,'TO HOLD A NEW ENTRY OF SAC-SMA CARRYOVER.')
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 70
C
360   CONTINUE
C
C     ENOUGH ROOM - ADD NEW ENTRY TO CB
C
      NUMCOS=NUMCOS+1
      I=NUMCOS
      CALL UMEMOV(OPNAME,SACCOS(1,I),2)
      SACCOS(3,I)=IDATE+0.01
      DO 370 J=4,15
370   SACCOS(J,I)=-999.
      DO 400 J=1,9
      VALUE=VALUES(J)
      IF(VALUE.LT.-9998.)GO TO 400
      IF(IUMSAC.EQ.1)GO TO 380
      IF(J.LT.9)VALUE=VALUE/AMM2IN
      IF(J.EQ.9)VALUE=(VALUE-BDC2DF)/ADC2DF
380   IF(J.LT.9.AND.VALUE.GE.0.)GO TO 390
      IF(J.EQ.9.AND.VALUE.GE.-273..AND.VALUE.LE.0.)GO TO 390
C
C     INVALID RANGE FOR VALUE
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,250)KWRDS(J)
      IF(J.LT.9.AND.MODWRN.EQ.1)WRITE(IPR,260)VALUE
      IF(J.EQ.9.AND.MODWRN.EQ.1)WRITE(IPR,270)VALUE
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 400
C
390   SACCOS(J+3,I)=VALUE
400   CONTINUE
      GO TO 70
C
410   IF(IBUG.EQ.0)GO TO 440
C
      WRITE(IODBUG,420)NUMCOS
420   FORMAT(11X,'IN SACCO MOD - LEAVING SUBROUTINE MSACCO - ',
     1 'NUMCOS = ',I3)
      IF(NUMCOS.GT.0)WRITE(IODBUG,430)((SACCOS(I,J),I=1,15),J=1,NUMCOS)
430   FORMAT(11X,'SACCOS=',1X,2A4,F8.0,12F8.2)
C
440   CALL FSTWHR(OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C
      RETURN
      END
