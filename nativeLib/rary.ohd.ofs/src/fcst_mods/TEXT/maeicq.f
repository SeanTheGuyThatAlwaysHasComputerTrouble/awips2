C MODULE MAEICQ
C-----------------------------------------------------------------------
C
C
C
C     DESC - THIS SUBROUTINE PERFORMS THE AEICQN MOD
C
C
C
      SUBROUTINE MAEICQ(MP,P,NCARDS,MODCRD,IIDATE,
     1  NXTOPN,NXTNAM,IHZERO)
C
      LOGICAL FIRST
      CHARACTER*8 NXTNAM,OPID,BLANK8,MODNAM
C
      INCLUDE 'ufreex'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fctim2'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fpwarn'
      INCLUDE 'common/fmodft'
      COMMON/MOD135/NDT35,IDT35(5),VAL35(5)
C
      DIMENSION P(MP)
      DIMENSION OLDOPN(2),MODCRD(20,NCARDS)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mods/RCS/maeicq.f,v $
     . $',                                                             '
     .$Id: maeicq.f,v 1.3 1998/07/02 20:34:58 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA ISLASH/4H/   /,BLANK8/'        '/
      DATA MODNAM/'AEICQN  '/
C
      CALL FSTWHR(8HMAEICQ  ,0,OLDOPN,IOLDOP)
C
C     SET DEBUG FLAG
C
      IBUG=IFBUG(4HMODS)+IFBUG(4HAEIC)
C
      IF(IBUG.GT.0)WRITE(IODBUG,10)NXTOPN,NXTNAM
10    FORMAT(11X,'*** SUBROUTINE MAEICQ ENTERED *** NEXT OPERATION ',
     1 'NUMBER =',I3,', OPERATION NAME = ',A8)
C
C  IS THE NEXT OPERATION AN API-CONT, API-HAR, OR API-HAR2 OPERATION?
C   NUMBERS ARE API-CONT=24, API-HAR=35, API-HAR2=41.
C
      IF ((NXTOPN.EQ.35).OR.(NXTOPN.EQ.41)) GO TO 20
      IF (NXTOPN.NE.24) GO TO 380
C     CHECK IF API-CONT OPERATION USES THE AEI OPTION.
      LOCP=0
      CALL FSERCH(NXTOPN,NXTNAM,LOCP,P,MP)
      IVOPT=P(LOCP+13)
      IF (IVOPT.NE.1) GO TO 380
C
20    IDATE=IIDATE
      IF(MOD(IDATE,24).EQ.0)GO TO 50
C     IF(MOD(IDATE,24).EQ.0)GO TO 222
C
C  SKIP MOD IF DATE ENTERED IS NOT AT THE END OF A HYDROLOGIC DAY
C
      IF(MODWRN.EQ.0)GO TO 40
      IXDA=IDATE/24+1
      IXHR=IDATE-(IXDA-1)*24
      CALL MDYH2(IXDA,IXHR,IM1,ID1,IY1,IH1,DUM1,DUM2,MODTZC)
C
      WRITE(IPR,30)IM1,ID1,IY1,IH1,MODTZC
30    FORMAT(1H0,10X,'**WARNING** THE DATE FOR CHANGES IN THE ',
     1 'AEICQN MOD (',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/11X,
     2 'IS NOT AT THE END OF A HYDROLOGIC DAY.'/11X,
     3 'THE DATE FOR THIS CHANGE WILL BE MOVED TO THE END OF ',
     4 'THE HYDROLOGIC DAY.')
      CALL WARN
40    IDATE=(IDATE/24)*24 + 24
C
C     SEE IF DATE IS WITHIN RUN PERIOD
C
50    CONTINUE
      ICOMP=((LDACPD-1)*24)+LHRCPD
      IF (IDATE.LE.ICOMP) GOTO 60
         IF (MODWRN.EQ.0) GOTO 380
         ITYPE=2
         CALL MODS1(NCARDS,ICMND,MODNAM,MODCRD,ITYPE)
         GOTO 380
C
60    ISTHR=(IDA-1)*24+IHZERO
      IENHR=(LDA-1)*24+LHR
C
      IF(ISTHR.LT.IDATE.AND.IENHR.GE.IDATE)GO TO 90
C
C     DATE FOR CHANGES NOT IN ALLOWABLE WINDOW
C
      IF(MODWRN.EQ.0)GO TO 380
      CALL MDYH2(IDA,IHZERO,IM1,ID1,IY1,IH1,DUM1,DUM2,MODTZC)
      CALL MDYH2(LDA,LHR,IM2,ID2,IY2,IH2,DUM1,DUM2,MODTZC)
      IXDA=IDATE/24+1
      IXHR=IDATE-(IXDA-1)*24
      IF(IXHR.EQ.0)IXDA=IXDA-1
      IF(IXHR.EQ.0)IXHR=24
      CALL MDYH2(IXDA,IXHR,IM3,ID3,IY3,IH3,DUM1,DUM2,MODTZC)
C
      IF(ISTHR.NE.IDATE)
     .WRITE(IPR,70)IM3,ID3,IY3,IH3,MODTZC,IM1,ID1,IY1,IH1,MODTZC,
     1IM2,ID2,IY2,IH2,MODTZC
70    FORMAT(1H0,10X,'**WARNING** THE DATE FOR CHANGES IN THE ',
     1 'AEICQN MOD (',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/11X,
     2 'IS NOT IN THE CURRENT RUN PERIOD (',I2,1H/,I2,
     3 1H/,I4,1H-,I2,1X,A4,4H TO ,I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/
     4 11X,'THESE CHANGES WILL BE IGNORED.')
      IF(ISTHR.EQ.IDATE)
     .WRITE(IPR,80)IM3,ID3,IY3,IH3,MODTZC
80    FORMAT(1H0,10X,'**WARNING** THE DATE FOR CHANGES IN THE ',
     1 'AEICQN MOD (',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/11X,
     2 'IS AT THE START OF THE CURRENT RUN PERIOD.'/
     3 11X,'THESE CHANGES WILL BE IGNORED.')
      CALL WARN
      GO TO 380
C
90    MXVALS=1
C
C     READ CARD - IF COMMAND LEAVE - IF COMMAND AND 1ST CARD ERROR
C
      FIRST=.TRUE.
      IF(NRDCRD.EQ.NCARDS)GO TO 110
C
100   IF(NRDCRD.EQ.NCARDS)GO TO 380
C
      OPID=BLANK8
C
      IF(MISCMD(NCARDS,MODCRD).EQ.0)GO TO 130
C
      IF(.NOT.FIRST)GO TO 380
C
C     HAVE FOUND COMMAND AS FIRST SUBSEQUENT CARD - ERROR
C
110   IF(MODWRN.EQ.1)WRITE(IPR,120)
120   FORMAT(1H0,10X,'**WARNING** NO SUBSEQUENT CARDS FOUND FOR THE ',
     1 'AEICQN MOD.  PROCESSING CONTINUES.')
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 380
C
130   FIRST=.FALSE.
C
C     CALL SUBROUTINE TO READ VALUE
C
      NFLD=1
      NRDCRD=NRDCRD+1
C
      CALL MRDVAL(NCARDS,MODCRD,NFLD,MXVALS,NVALS,VALUE,ISTAT)
C
      IF(IBUG.GT.0)WRITE(IODBUG,140)ISTAT,FIRST,NVALS,VALUE
140   FORMAT(11X,'**IN MAEICQ** AFTER CALL TO MRDVAL - ISTAT=',I3,
     1 ', FIRST=',L4,', NVALS=',I2,', VALUE=',G10.2)
C
C     ISTAT RETURNED FROM MRDVAL MEANS
C       =0, VALUE READ OK, NO ADDITIONAL FIELDS ON CARD
C       =2, VALUE READ OK, ADDITIONAL FIELDS ON CARD
C       =-1, NO VALUES ENTERED
C       ELSE, MORE THAN 1 VALUE ENTERED
C
      IF(ISTAT.EQ.0)GO TO 260
      IF(ISTAT.EQ.2)GO TO 180
      IF(ISTAT.EQ.-1)GO TO 160
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,150)(MODCRD(I,NRDCRD),I=1,20)
150   FORMAT(1H0,10X,'** WARNING ** IN AEICQN MOD - MORE THAN 1 ',
     1 'VALUE ENTERED.'/11X,'CURRENT MOD CARD IMAGE IS'/11X,20A4)
      GO TO 370
C
160   IF(MODWRN.EQ.1)
     .WRITE(IPR,170)(MODCRD(I,NRDCRD),I=1,20)
170   FORMAT(1H0,10X,'**WARNING** ',
     1  'NO VALUES ENTERED ON A SUBSEQUENT CARD FOR AEICQN MOD'/
     2  11X,'THE CURRENT MOD CARD IMAGE IS'/11X,20A4)
      GO TO 370
C
C     HAVE ADDITIONAL FIELDS - LOOK FOR A SLASH (/) AND
C     AN OPERATION NAME - REPROCESS CURRENT FIELD TO SEE IF A SLASH
C
180   ISTRT=-1
      NCHAR=1
      ICKDAT=0
C
      IF(IBUG.GT.0)WRITE(IODBUG,190)NFLD,NRDCRD
190   FORMAT(11X,'ABOUT TO CALL UFIEL2 - LOOKING FOR A SLASH',
     1 ', NFLD=',I3,', NRDCRD=',I3)
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(IBUG.GT.0)WRITE(IODBUG,200)NFLD,ISTRT,LEN,ITYPE,IFIELD,ISTAT
200   FORMAT(11X,'AFTER UFIEL2 - NFLD,ISTRT,LEN,ITYPE,IFIELD,ISTAT='/
     1 26X,I3,I6,I4,I6,3X,A4,I6)
C
      IF(IFIELD.EQ.ISLASH)GO TO 220
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,210)(MODCRD(I,NRDCRD),I=1,20)
210   FORMAT(1H0,10X,'** WARNING ** IN AEICQN MOD - A SLASH ',
     1 'WAS NOT FOUND WHERE EXPECTED ON THE FOLLOWING CARD'/
     2 11X,20A4/11X,'NO CHANGES ENTERED ON THIS CARD WILL BE MADE')
      GO TO 370
C
C     NOW READ OPERATION NAME
C
220   ISTRT=-3
      NCHAR=2
      ICKDAT=0
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,OPID,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.NE.-2)GO TO 240
      IF(MODWRN.EQ.0)GO TO 100
      WRITE(IODBUG,230)(MODCRD(I,NRDCRD),I=1,20)
230   FORMAT(11X,'** WARNING ** NO OPERATION NAME ENTERED ',
     1 'AFTER A SLASH ON THE FOLLOWING MOD CARD'/11X,20A4/
     2 11X,'THIS CARD IS IGNORED.')
      GO TO 370
240   CONTINUE
C
C     CHECK THAT OPERATION NAME ENTERED MATCHES NXTNAM -
C     IF NOT, READ NEXT CARD
C
      IF(IBUG.GT.0)WRITE(IODBUG,250)OPID,NXTNAM
250   FORMAT(11X,'OPERATION NAME INPUT IS ',A8,', NEXT OPERATION ',
     1 'IN OPERATIONS TABLE IS ',A8,'.')
C
      IF(OPID.EQ.NXTNAM)GO TO 260
      GO TO 100
C
C     CONVERT TO INCHES IF NEEDED
C  IF IUMAPI EQ 1, METRIC UNITS WERE ENTERED
C  IF IUMAPI EQ 0, ENGLISH UNITS WERE ENTERED
C
260   IF(IUMAPI.EQ.0)GO TO 270
C
C     MUST CONVERT FROM MM TO INCHES
C
      CALL FCONVT(4HMM  ,4HL   ,ENGUN,AMM2IN,BMM2IN,IER)
C
      VALUE=VALUE*AMM2IN
C
C     STORE VALUE IN COMMON BLOCK /MOD135/
C
C  ***** NOTE THAT THE DATE IS STORED IN IDT35 AS JULIAN DAYS *****
C
270   IF(NDT35.GT.0)GO TO 280
C
C     NO VALUES CURRENTLY IN COMMON BLOCK /MOD135/
C
      NDT35=1
      IDT35(1)=IDATE/24+1
      VAL35(1)=VALUE
      GO TO 340
C
C     VALUES CURRENTLY IN COMMON BLOCK /MOD135/
C     SEE IF DATE MATCHES EXACTLY ONE IN COMMON BLOCK
C
280   DO 290 I=1,NDT35
      IF(IDT35(I).NE.IDATE/24+1)GO TO 290
C
C     YES - MATCHES - REPLACE VALUE IN COMMON BLOCK
C
      VAL35(I)=VALUE
      GO TO 340
C
290   CONTINUE
C
C     NO MATCH
C     IF THERE IS ROOM - PUT THIS VALUE IN IN CHRONOLOGICAL ORDER
C
      IF(NDT35.LT.5)GO TO 310
C
C     NO ROOM
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,300)(MODCRD(I,NRDCRD),I=1,20)
300   FORMAT(1H0,10X,'** WARNING ** IN AEICQN MOD - NO ROOM IN ',
     1 'COMMON BLOCK /MOD135/'/11X,'THE VALUE ON THE FOLLOWING MOD ',
     2 'CARD IS IGNORED'/11X,20A4)
      GO TO 370
C
C     THERE IS ROOM - INSERT CHRONOLOGICALLY
C
310   IEND35=NDT35
      DO 330 I=1,IEND35
      IF(IDT35(I).LT.IDATE/24+1)GO TO 330
C
      MOVE=NDT35-I+1
      DO 320 J=1,MOVE
      K=NDT35+2-J
      IDT35(K)=IDT35(K-1)
320   VAL35(K)=VAL35(K-1)
      IDT35(I)=IDATE/24+1
      VAL35(I)=VALUE
      NDT35=NDT35+1
      GO TO 340
C
330   CONTINUE
C
C     ADD AT END
C
      NDT35=NDT35+1
      IDT35(NDT35)=IDATE/24+1
      VAL35(NDT35)=VALUE
C
340   IF(IBUG.LT.1)GO TO 100
C
      WRITE(IODBUG,350)IDATE,VALUE,NDT35,NXTNAM
350   FORMAT(11X,'IN SUBROUTINE MAEICQ - AEICQN MOD'/11X,
     1 'IDATE, VALUE, NDT35, NXTNAM = ',I11,1X,G9.2,1X,I3,1X,A8)
      IF(NDT35.GT.0)WRITE(IODBUG,360)(IDT35(I),VAL35(I),I=1,NDT35)
360   FORMAT(11X,'IDT35, VAL35 ='/(11X,2G9.2))
      GO TO 100
C
370   IF(MODWRN.EQ.1)CALL WARN
      GO TO 100
C
380   IF(IBUG.GT.0)WRITE(IODBUG,390)
390   FORMAT(11X,'**LEAVING SUBROUTINE MAEICQ**')
      CALL FSTWHR(OLDOPN,IOLDOP,OLDOPN,IOLDOP)
      RETURN
      END
