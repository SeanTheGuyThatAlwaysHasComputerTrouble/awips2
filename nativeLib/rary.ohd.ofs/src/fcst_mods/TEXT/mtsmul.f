C MODULE MTSMUL
C-----------------------------------------------------------------------
C
C
C     DESC - THIS SUBROUTINE PERFORMS THE TSMULT MOD
C
      SUBROUTINE MTSMUL(MTS,TS,MD,D,NCARDS,MODCRD,IIDATE,LLDATE,KKDATE,
     1  NXTOPN,NXTNAM,IHZERO)
C
      LOGICAL FIRST
      CHARACTER*8 OPTYPE,OPNAME,NXTNAM,MODNAM
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fctim2'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fpwarn'
      INCLUDE 'common/fmodft'
      INCLUDE 'common/modctl'
      INCLUDE 'ufreex'
C
      DIMENSION TS(MTS),D(MD),VALUES(1),TSID(2)
      DIMENSION OLDOPN(2),MODCRD(20,NCARDS)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mods/RCS/mtsmul.f,v $
     . $',                                                             '
     .$Id: mtsmul.f,v 1.3 1998/07/02 20:51:05 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA MODNAM/'TSMULT  '/
C
      CALL FSTWHR(8HMTSMUL  ,0,OLDOPN,IOLDOP)
C
C     SET DEBUG FLAG
C
      IBUG=IFBUG(4HMODS)+IFBUG(4HTSMU)
C
      IF(IBUG.GT.0)WRITE(IODBUG,10)IIDATE,LLDATE,KKDATE,NXTOPN,NXTNAM
10    FORMAT(1H0,10X,'*** SUBROUTINE MTSMUL ENTERED ***  IIDATE=',I9,
     1 ', LLDATE=',I9,' KKDATE=',I9,', NXTOPN=',I3,', NXTNAM= ',A8)
C
      FIRST=.TRUE.
      IDATE=IABS(IIDATE)
      LDATE=IABS(LLDATE)
      KDATE=IABS(KKDATE)
C
C     NOW SEE IF LSTCMPDY VALUE EQUAL LASTCMPDATE VALUE
C
      ICOMP=((LDACPD-1)*24)+LHRCPD
C
      IF ((IDATE.LE.KDATE).AND.(IDATE.LE.ICOMP)) GOTO 20
      IF ((IDATE.GT.KDATE).AND.(KDATE.EQ.ICOMP)) GOTO 60
      GOTO 50
C
20    IF (KDATE.EQ.ICOMP) GOTO 60
      IF (LDATE.LE.KDATE) GOTO 60
C
      IF ((IWHERE.GT.1).OR.(MODWRN.EQ.0)) GOTO 40
C
      WRITE(IPR,30)(MODCRD(I,NRDCRD),I=1,20)
30    FORMAT(1H0,10X,'** WARNING ** IN TSMULT MOD - THIS MOD IS ',
     1 'ONLY ENABLED BETWEEN THE START DATE AND THE EFFECTIVE DATE ',
     2 'ON THE MOD CARD.'/14X,'THE CURRENT MOD CARD IMAGE IS: ',20A4)
C
40       LDATE=KDATE
         GOTO 60
C
50       IF ((IWHERE.GT.1).OR.(MODWRN.EQ.0)) GOTO 310
         ITYPE=4
         CALL MODS1(NCARDS,ICMND,MODNAM,MODCRD,ITYPE)
         GOTO 310
C
C     SEE IF DATE IS WITHIN RUN PERIOD
C
60    ISTHR=(IDA-1)*24+IHZERO
      IENHR=(LDA-1)*24+LHR
C
      IF(IDATE.GT.IENHR.OR.LDATE.LT.ISTHR)GO TO 70
      GO TO 90
C
C     PERIOD FOR CHANGES NOT WITHIN RUN PERIOD
C
70    IF(IWHERE.GT.1.OR.MODWRN.EQ.0)GO TO 310
      CALL MDYH2(IDA,IHZERO,IM1,ID1,IY1,IH1,DUM1,DUM2,MODTZC)
      CALL MDYH2(LDA,LHR,IM2,ID2,IY2,IH2,DUM1,DUM2,MODTZC)
      IXDA=IDATE/24+1
      IXHR=IDATE-(IXDA-1)*24
      IF(IXHR.EQ.0)IXDA=IXDA-1
      IF(IXHR.EQ.0)IXHR=24
      CALL MDYH2(IXDA,IXHR,IM3,ID3,IY3,IH3,DUM1,DUM2,MODTZC)
      LXDA=LDATE/24+1
      LXHR=LDATE-(LXDA-1)*24
      IF(LXHR.EQ.0)LXDA=LXDA-1
      IF(LXHR.EQ.0)LXHR=24
      CALL MDYH2(LXDA,LXHR,IM4,ID4,IY4,IH4,DUM1,DUM2,MODTZC)
C
      WRITE(IPR,80)IM3,ID3,IY3,IH3,MODTZC,IM4,ID4,IY4,IH4,MODTZC,
     1IM1,ID1,IY1,IH1,MODTZC,IM2,ID2,IY2,IH2,MODTZC
80    FORMAT(1H0,10X,'**WARNING** THE DATES FOR CHANGES IN THE ',
     1 'TSMULT MOD (',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,
     1 4H TO ,I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/11X,
     2 'ARE NOT WITHIN THE CURRENT RUN PERIOD (',I2,1H/,I2,
     3 1H/,I4,1H-,I2,1X,A4,4H TO ,I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/
     4 11X,'THESE CHANGES WILL BE IGNORED.')
      CALL WARN
      GO TO 310
C
C     CALL SUBROUTINE TO READ INFO FOR CHANGING ONE TIME SERIES
C
90    MXVALS=1
C
100   CALL MRDA2(NCARDS,MODCRD,TSID,DTYPE,IDT,
     1 MXVALS,NVALS,VALUES,KEY,OPTYPE,OPNAME,ISTAT)
C
      IF(IBUG.GT.0)WRITE(IODBUG,110)TSID,DTYPE,IDT,MXVALS,NVALS,KEY,
     1 OPTYPE,OPNAME,ISTAT
110   FORMAT(11X,'BACK FROM MRDA2'/11X,'TSID,DTYPE,IDT,MXVALS,NVALS,',
     1 'KEY, OPTYPE, OPNAME, ISTAT ='/9X,2A4,1X,A4,I3,I7,I6,I4,1X,A8,
     2 1X,A8,I5)
      IF(IBUG.GT.0.AND.NVALS.GT.0.AND.ISTAT.NE.-1)WRITE(IODBUG,120)
     1 (VALUES(I),I=1,NVALS)
120   FORMAT(11X,'VALUES='/(11X,10G10.2))
C
      IF(ISTAT.EQ.0)GO TO 140
      IF(ISTAT.EQ.1)GO TO 100
      IF(.NOT.FIRST)GO TO 310
C
      IF(IWHERE.GT.1.OR.MODWRN.EQ.0)GO TO 310
      WRITE(IPR,130)
130   FORMAT(1H0,10X,'**WARNING** NO SUBSEQUENT CARDS FOUND FOR ',
     1  'TSMULT MOD')
      CALL WARN
      GO TO 310
C
140   IF(IBUG.LT.1)GO TO 170
C
      WRITE(IODBUG,150)IDATE,LDATE,TSID,DTYPE,IDT,NVALS,KEY,OPTYPE,
     1 OPNAME
150   FORMAT(11X,'IN SUBROUTINE MTSMUL - TSMULT MOD'/11X,
     1 'IDATE,    LDATE, TSID, DTYPE, IDT, NVALS, KEY, OPTYPE, OPNAME ='
     1 /6X,2I10,1X,2A4,1X,A4,I4,I6,I6,1X,A8,1X,A8)
      IF(NVALS.GT.0)WRITE(IODBUG,160)(VALUES(I),I=1,NVALS)
160   FORMAT(11X,'VALUES ='/(11X,10G9.2))
C
170   CONTINUE
C
      FIRST=.FALSE.
C
      CALL MCHKTS(8HTSMULT  ,TSID,DTYPE,IDT,KEY,MTS,TS,
     1 OPTYPE,OPNAME,NXTOPN,NXTNAM,IBUG,
     2 UNITS,DIMS,LOCD,MISALW,NVPDT,ISTATT)
C
      IF(ISTATT.EQ.1)GO TO 100
C
C     CHECK TIME PERIOD AGAINST RUN PERIOD - PRINT WARNING ONLY IF
C     ENTIRE PERIOD BEING CHANGED IS OUTSIDE OF RUN PERIOD
C
C     IF START HOUR NOT ENTERED - USE DEFAULT VALUE OF 12Z
C     IF START HOUR ENTERED - SET TO CLOSEST DT OF TIME SERIES
C
C     IF HOUR ENTERED IIDATE LESS THAN ZERO
C
      IF(IIDATE.LT.0)GO TO 180
C
C     HOUR NOT ENTERED - IDATE SHOULD BE DIVISIBLE BY 24
C
      JSTHR=IDATE
      GO TO 190
C
180   CONTINUE
C
C     HOUR ENTERED - SET TO NEAREST DT
C
      JSTHR=MISTDT(IDATE,IDT)
C
190   CONTINUE
C
C     IF END HOUR ENTERED LLDATE LESS THAN ZERO
C
      IF(LLDATE.LT.0)GO TO 200
C
C     HOUR NOT ENTERED
C
      JENHR=LDATE
      GO TO 210
C
C     HOUR ENTERED - SET TO NEAREST DT
C
200   JENHR=MISTDT(LDATE,IDT)
C
210   CONTINUE
C
C     NVALS IS NOW THE NUMBER OF VALUES FOR TOTAL PERIOD REQUESTED
C
      NVALS=((JENHR-JSTHR)/IDT+1)*NVPDT
C
C  GET LOCATIONS TO START CHANGES AND NUMBER OF VALUES TO MOVE
C
      CALL MLOCCH(8HTSMULT  ,ISTHR,IENHR,JSTHR,JENHR,
     1 TSID,DTYPE,IDT,NVALS,NVPDT,LOCD,IBUG,
     2 IOFFST,LDSTRT,NMOVE,ISTART,ISTATT)
C
      IF(ISTATT.EQ.1)GO TO 100
C
C     CHECK THAT VALUE TO BE MULTIPLIED IS NOT THE MISSING DATA VALUE
C
      IF(IFMSNG(VALUES(1)).EQ.0)GO TO 230
      IF(MODWRN.EQ.0)GO TO 100
      IF(MODWRN.EQ.1)WRITE(IODBUG,220)TSID,DTYPE,IDT
220   FORMAT(1H0,10X,'** WARNING ** THE MISSING DATA VALUE WAS ENTERED '
     1 ,'IN A TSMULT MOD FOR TIME SERIES ',2A4,1X,A4,I3,
     2 '.  THIS IS ILLEGAL.'/11X,'THIS MOD CARD IS IGNORED.')
      GO TO 300
C
230   IGOTO = MISALW+1
C
C     GO TO ONE OF TWO CASES
C      IGOTO = 1, MISSING NOT ALLOWED
C              2, MISSING ALLOWED
C
      IF(IBUG.GT.0)WRITE(IODBUG,240)VALUES(1),NMOVE,TSID,DTYPE,IDT,
     1 LDSTRT,IOFFST,IGOTO,NXTOPN,NXTNAM
240   FORMAT(11X,'ABOUT TO MULTIPLY ',F9.2,' BY ',I4,' VALUES IN TIME ',
     1 'SERIES ',2A4,1X,A4,I3/11X,'LDSTRT,IOFFST,IGOTO,NXTOPN,NXTNAM ='/
     2 5X,I11,I7,I6,I7,2X,A8)
C
      GO TO (260,280) , IGOTO
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,250)MISALW
250   FORMAT(1H0,10X,'** WARNING ** IN TSMULT MOD - ILLEGAL VALUE ',
     1 'FOR MISALW ',I5,').'/11X,'NO VALUES CHANGED.')
      GO TO 300
C
260   DO 270 I=1,NMOVE
270   D(LDSTRT+I)=D(LDSTRT+I)*VALUES(1)
      GO TO 100
C
280   DO 290 I=1,NMOVE
      TEMP=D(LDSTRT+I)
      IF(IFMSNG(TEMP).EQ.1)GO TO 290
      D(LDSTRT+I)=TEMP*VALUES(1)
290   CONTINUE
      GO TO 100
C
300   CONTINUE
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 100
C
310   CALL FSTWHR(OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C
      IF(IBUG.GT.0)WRITE(IODBUG,320)
320   FORMAT(11X,'*** LEAVING SUBROUTINE MTSMUL ***')
C
      RETURN
      END
