C  MODULE MBFRCP
C
C  DESC - PERFORMS THE PERMANENT BFRCHNG MOD FOR THE BASEFLOW OPERATION
C
C  DESC - SUBROUTINE MBFRC2 CALLED FOR UNIT-HG OPERATION
C  DESC - SUBROUTINE MBFRCH PERFORMS THE BFRCHNG MOD FOR
C  DESC - THE BASEFLOW OPERATION WHEN PO(9)=2 AND THE
C  DESC - /MOD138/ COMMON BLOCK MUST BE FILLED DURING
C  DESC - EXECUTION OF THE OPERATIONS TABLE
C
      SUBROUTINE MBFRCP(NCARDS,MODCRD,P,MP,IDATE,ISTATT,IHZERO)
C
      CHARACTER*8   OPNAME,OPN,BLANK8
      LOGICAL FIRST
      INCLUDE 'ufreex'
      INCLUDE 'common/fmodft'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fctim2'
      INCLUDE 'common/fpwarn'
      INCLUDE 'common/modctl'
C
      DIMENSION MODCRD(20,NCARDS),P(MP),IFIELD(3)
      DIMENSION OLDOPN(2)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mods/RCS/mbfrcp.f,v $
     . $',                                                             '
     .$Id: mbfrcp.f,v 1.2 1998/07/02 20:37:12 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA ISLASH/4H/   /,BLANK8/8H        /
C
      CALL FSTWHR(8HMBFRCP  ,0,OLDOPN,IOLDOP)
C
C  SET DEBUG SWITCH
C
      IBUG=IFBUG(4HMODS)+IFBUG(4HBFRC)
C
      IF(IBUG.GT.0)WRITE(IODBUG,900)IDATE
  900 FORMAT(11X,'*** ENTERING SUBROUTINE MBFRCP ***  IDATE=',I11)
C
      ISTATT=1
C
C  SEE IF DATE IS WITHIN RUN PERIOD
C
      ISTHR=(IDA-1)*24+IHZERO
      IENHR=(LDA-1)*24+LHR
C
      IF(ISTHR.LE.IDATE.AND.IENHR.GE.IDATE)GO TO 11
C
C  DATE NOT WITHIN ALLOWABLE WINDOW
C
      CALL MDYH2(IDA,IHZERO,IM1,ID1,IY1,IH1,DUM1,DUM2,MODTZC)
      CALL MDYH2(LDA,LHR,IM2,ID2,IY2,IH2,DUM1,DUM2,MODTZC)
      IXDA=IDATE/24+1
      IXHR=IDATE-(IXDA-1)*24
      IF(IXHR.EQ.0)IXDA=IXDA-1
      IF(IXHR.EQ.0)IXHR=24
      CALL MDYH2(IXDA,IXHR,IM3,ID3,IY3,IH3,DUM1,DUM2,MODTZC)
C
      IF(MODWRN.EQ.0)GO TO 999
      WRITE(IPR,602)IM3,ID3,IY3,IH3,MODTZC,IM1,ID1,IY1,IH1,MODTZC,
     1 IM2,ID2,IY2,IH2,MODTZC
  602 FORMAT(1H0,10X,'**WARNING** THE DATE FOR CHANGES ENTERED IN THE ',
     1 'BFRCHNG MOD (',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/11X,
     2 'IS NOT WITHIN THE CURRENT RUN PERIOD (',I2,1H/,I2,
     3 1H/,I4,1H-,I2,1X,A4,4H TO ,I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/
     4 11X,'THESE CHANGES WILL BE IGNORED.')
      CALL WARN
      GO TO 999
C
   11 CONTINUE
C
C  READ CARD - IF COMMAND, LEAVE - IF COMMAND AND 1ST CARD, ERROR
C
      OPNAME=BLANK8
      OPN=BLANK8
      FIRST=.TRUE.
C
      IF(NRDCRD.EQ.NCARDS)GO TO 13
C
    1 IF(NRDCRD.EQ.NCARDS)GO TO 999
C
      IF(MISCMD(NCARDS,MODCRD).EQ.0)GO TO 17
C
      IF(.NOT.FIRST)GO TO 999
C
C  HAVE FOUND COMMAND AS FIRST SUBSEQUENT CARD - ERROR
C
   13 IF(MODWRN.EQ.0)GO TO 999
      WRITE(IPR,920)
  920 FORMAT(1H0,10X,'**WARNING** NO SUBSEQUENT CARDS FOUND FOR THE ',
     1  'BFRCHNG MOD.  PROCESSING CONTINUES')
      CALL WARN
      GO TO 999
C
   17 FIRST=.FALSE.
      NRDCRD=NRDCRD+1
C
C  NOW READ 2ND FIELD - MUST BE A REAL NO. OR INTEGER
C
      NFLD=1
      ISTRT=-3
      NCHAR=3
      ICKDAT=0
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,IVALUE,VALUE,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTAT.EQ.0)GO TO 28
C
C  ERROR - DATA EXPECTED - PROCESS NEXT CARD
C
      IF(MODWRN.EQ.0)GO TO 1
      WRITE(IPR,921)(MODCRD(I,NRDCRD),I=1,20)
  921 FORMAT(1H0,10X,'**WARNING** IN THE BFRCHNG MOD - NOT ENOUGH ',
     1  'FIELDS ON SUBSEQUENT CARD.  THE CARD BEING PROCESSED IS:'/
     2  11X,20A4)
      CALL WARN
      GO TO 1
C
   28 IF(ITYPE.LT.2.AND.NREP.EQ.-1)GO TO 40
C
      IF(MODWRN.EQ.0)GO TO 1
      WRITE(IPR,922)(MODCRD(I,NRDCRD),I=1,20)
  922 FORMAT(1H0,10X,'**WARNING** IN BFRCHNG MOD - INVALID VALUE ',
     1  'FOUND, THE CARD BEING PROCESSED IS:'/11X,20A4)
      CALL WARN
      GO TO 1
C
   40 IF(VALUE.GE.0.5.AND.VALUE.LT.1.0)GO TO 15
C
C  BAD RECESSION FACTOR ENTERED
C
      IF(MODWRN.EQ.0)GO TO 1
      WRITE(IPR,604)VALUE
  604 FORMAT(1H0,10X,'**WARNING** IN BFRCHNG MOD - VALUE IS OUT OF ',
     1 'VALID RANGE (VALUE GE 0.5 AND LT 1.0)'/11X,'VALUE = ',G10.4)
      CALL WARN
      GO TO 1
C
   15 CONTINUE
C
C  LOOK FOR BASEFLOW OPERATIONS IN THIS SEGMENT WITH NAME OPNAME
C  IF OPNAME BLANK - CHANGE FOR ALL BASEFLOW OPERS IN THIS SEGMENT
C
C  READ NEXT FIELD - IF NONE CHANGE ALL BASEFLOW OPERS IN SEGMENT
C  IF A SLASH FOUND - READ OPERATION NAME IN FOLLOWING FIELD
C
      ISTRT=-3
      NCHAR=3
      ICKDAT=0
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTEGR,REAL,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.EQ.-2)GO TO 20
C
C  FIELD FOUND - MUST BE A SLASH
C
      IF(LEN.EQ.1.AND.IFIELD(1).EQ.ISLASH)GO TO 55
C
C  FIELD IS NOT SLASH - ERROR
C
      IF(MODWRN.EQ.0)GO TO 1
      WRITE(IPR,930)(MODCRD(I,NRDCRD),I=1,20)
  930 FORMAT(1H0,10X,'**WARNING** A SLASH WAS EXPECTED AS THE THIRD ',
     1 'FIELD OF THE FOLLOWING MOD CARD:'/11X,20A4,/11X,
     2 'THIS CARD IS IGNORED AND PROCESSING CONTINUES')
      CALL WARN
      GO TO 1
C
   55 CONTINUE
C
C  SLASH FOUND - NOW READ OPERATION NAME
C
      ISTRT=-3
      NCHAR=2
      ICKDAT=0
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTEGR,REAL,
     1  NCHAR,OPNAME,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.NE.-2)GO TO 57
C
C  NO FIELD FOUND FOR OPERATION NAME - ERROR
C
      IF(MODWRN.EQ.0)GO TO 1
      WRITE(IPR,933)(MODCRD(I,NRDCRD),I=1,20)
  933 FORMAT(1H0,10X,'**WARNING** NO OPERATION NAME WAS FOUND AFTER ',
     1 'THE SLASH ON THE FOLLOWING MOD CARD:'/11X,20A4/11X,
     2 'THIS CARD IS IGNORED AND PROCESSING CONTINUES')
      CALL WARN
      GO TO 1
C
C  HAVE A SPECIFIC OPERATION NAME - FIND THAT OPERATION
C
   57 LOCP=0
      CALL FSERCH(38,OPNAME,LOCP,P,MP)
C
      IF(LOCP.GT.0)GO TO 18
C
C  HAVE NOT FOUND THE REQUESTED OPERATION - ERROR
C
      IF(MODWRN.EQ.0)GO TO 1
      WRITE(IPR,605)OPNAME
  605 FORMAT(1H0,10X,'**WARNING** A BASEFLOW OPERATION WITH NAME ',
     1 A8,' HAS NOT BEEN FOUND IN THIS SEGMENT.'/11X,
     2 'THE BFRCHNG MOD CANNOT BE PERFORMED FOR THIS OPERATION.')
      CALL WARN
      GO TO 1
C
   18 CONTINUE
C
C  HAVE FOUND OPERATION - SEE IF VARIABLE BASEFLOW IS CURRENTLY
C  DEFINED FOR THIS OPERATION
C
      ICHECK=P(LOCP+8)
      IF(ICHECK.GT.0)GO TO 19
C
C  VARIABLE BASEFLOW NOT CURRENTLY DEFINED
C
      IF(MODWRN.EQ.0)GO TO 1
      WRITE(IPR,610)OPNAME,VALUE
  610 FORMAT(1H0,10X,'** WARNING ** IN BFRCHNG MOD - THE VARIABLE ',
     1 'BASEFLOW RECESSION FEATURE IS NOT CURRENTLY DEFINED'/42X,'FOR ',
     2 'BASEFLOW OPERATION ',A8,'.'/11X,
     3 'THE RECESSION FACTOR OF ',F8.2,' WILL NOT BE ENTERED.'/11X,
     4 'YOU CAN USE THE RESEGDEF COMMAND IN FCINIT TO ADD THE ',
     5 'VARIABLE BASEFLOW OPTION TO THIS BASEFLOW OPERATION.')
      CALL WARN
      GO TO 1
C
C  VARIABLE BASEFLOW DEFINED - NOW SEE IF SINGLE RECESSION COEF
C  (ICHECK=1) OR TIME SERIES OF COEF'S (ICHECK=2)
C
 19   IF(ICHECK.EQ.1)GO TO 21
C
C  TIME SERIES OF COEF'S NEEDED - MUST BE DONE DURING OPERATIONS
C  TABLE (IWHERE=2) IN SUBROUTINE MBFRCH WHICH FILLS CB /MOD138/
C
      IBTWEN=1
      GO TO 1
C
 21   OLDRF=P(LOCP+17)
      P(LOCP+17)=VALUE
      ISTATT=0
C
      IF(IBUG.GT.0)WRITE(IODBUG,902)VALUE,OPNAME,OLDRF
  902 FORMAT(11X,'IN BFRCHNG MOD - SUBROUTINE MBFRCP'/11X,
     1 'A VALUE OF ',F8.2,' HAS BEEN STORED IN THE P ARRAY OF ',
     2 'OPERATION ',A8,' AS THE RECESSION FACTOR.'/11X,
     3 'THE OLD RECESSION FACTOR WAS ',F8.2)
      GO TO 1
C
   20 CONTINUE
C
C  CHANGE ALL OCCURENCES OF BASEFLOW OPERATION IN THIS SEGMENT
C
      NCHNGE=0
      LOCP=1
   30 CALL FSERCH(38,OPN,LOCP,P,MP)
C
      IF(LOCP.EQ.0.AND.NCHNGE.GT.0)GO TO 1
      IF(LOCP.GT.0)GO TO 35
C
C  IF GET HERE HAVE NOT FOUND ANY BASEFLOW OPERATIONS IN THIS SEGMENT
C
      IF(MODWRN.EQ.0)GO TO 999
      WRITE(IPR,606)
  606 FORMAT(1H0,10X,'**WARNING** A BFRCHNG MOD WAS REQUESTED FOR ',
     1 'THIS SEGMENT'/11X,'BUT NO BASEFLOW OPERATIONS WERE FOUND.')
      CALL WARN
      GO TO 999
C
   35 NCHNGE=NCHNGE+1
C
C  HAVE FOUND A BASEFLOW OPERATION - SEE IF VARIABLE BF CURRENTLY ON
C
      ICHECK=P(LOCP+8)
      IF(ICHECK.GT.0)GO TO 36
C
C  VARIABLE BASEFLOW NOT CURRENTLY DEFINED
C
      IF(MODWRN.EQ.0)GO TO 30
      WRITE(IPR,610)OPN,VALUE
      CALL WARN
      GO TO 30
C
C  VARIABLE BASEFLOW DEFINED - NOW SEE IF SINGLE RECESSION COEF
C  (ICHECK=1) OR TIME SERIES OF COEF'S (ICHECK=2)
C
 36   IF(ICHECK.EQ.1)GO TO 37
C
C  TIME SERIES OF COEF'S NEEDED - MUST BE DONE DURING OPERATIONS
C  TABLE (IWHERE=2) IN SUBROUTINE MBFRCH WHICH FILLS CB /MOD138/
C
      IBTWEN=1
      GO TO 30
C
C  CHANGE THE BASEFLOW RECESSION IN THE P ARRAY
C
 37   OLDRF=P(LOCP+17)
      P(LOCP+17)=VALUE
      ISTATT=0
C
      IF(IBUG.GT.0)WRITE(IODBUG,902)VALUE,OPN,OLDRF
      GO TO 30
C
  999 CALL FSTWHR(OLDOPN,IOLDOP,OLDOPN,IOLDOP)
      IF(IBUG.GT.0)WRITE(IODBUG,842)
  842 FORMAT(11X,'*** LEAVING MBFRCP ***')
C
      RETURN
      END
