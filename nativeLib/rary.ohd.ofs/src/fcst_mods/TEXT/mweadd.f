C MODULE MWEADD
C-----------------------------------------------------------------------
C
C
C     DESC - PERFORMS THE WEADD MOD
C
      SUBROUTINE MWEADD(NCARDS,MODCRD,IDATE,IHZERO)
C
      LOGICAL FIRST
C
      CHARACTER*8 OPNAME,IFIELD,BLANK8,SLASH,MODNAM
C
      INCLUDE 'common/fmodft'
      COMMON/FRARSW/NRARSW,RARESW(6,10)
      INCLUDE 'common/ionum'
      INCLUDE 'common/fpwarn'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fctim2'
      INCLUDE 'ufreex'
C
      DIMENSION OLDOPN(2),MODCRD(20,NCARDS)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mods/RCS/mweadd.f,v $
     . $',                                                             '
     .$Id: mweadd.f,v 1.3 2006/04/25 14:17:03 wkwock Exp $
     . $' /
C    ===================================================================
C
C
      DATA BLANK8/'        '/,SLASH/'/       '/
      DATA MODNAM/'WEADD   '/
C
      CALL FSTWHR(8HMWEADD  ,0,OLDOPN,IOLDOP)
C
C     SET DEBUG SWITCH
C
      IBUG=IFBUG(4HMODS)+IFBUG(4HWEAD)
C
      IF(IBUG.GT.0)WRITE(IPR,10)IDATE
10    FORMAT(1H0,10X,'** IN SUBROUTINE MWEADD - IDATE = ',I11)
C
      ICOMP=((LDACPD-1)*24)+LHRCPD
      IF (IDATE.LE.ICOMP) GOTO 20
         IF (MODWRN.EQ.0) GOTO 350
         ITYPE=2
         CALL MODS1(NCARDS,ICMND,MODNAM,MODCRD,ITYPE)
         GOTO 350
C
C     SEE IF DATE IS WITHIN - OR + 12 HOURS OF START OR END OF RUN
C
20    CONTINUE
C
      ISTHR=(IDA-1)*24+IHZERO
      IENHR=(LDA-1)*24+LHR
C
      IF(ISTHR-12.LE.IDATE.AND.IENHR+12.GE.IDATE)GO TO 50
C
C     DATE NOT WITHIN ALLOWABLE WINDOW
C
      CALL MDYH2(IDA,IHZERO,IM1,ID1,IY1,IH1,DUM1,DUM2,MODTZC)
      CALL MDYH2(LDA,LHR,IM2,ID2,IY2,IH2,DUM1,DUM2,MODTZC)
      IXDA=IDATE/24+1
      IXHR=IDATE-(IXDA-1)*24
      IF(IXHR.EQ.0)IXDA=IXDA-1
      IF(IXHR.EQ.0)IXHR=24
      CALL MDYH2(IXDA,IXHR,IM3,ID3,IY3,IH3,DUM1,DUM2,MODTZC)
C
      IF(MODWRN.EQ.1)
     .WRITE(IPR,30)IM3,ID3,IY3,IH3,MODTZC,IM1,ID1,IY1,IH1,MODTZC,
     1IM2,ID2,IY2,IH2,MODTZC
30    FORMAT(1H0,10X,'**WARNING** THE DATE FOR CHANGES ENTERED IN THE ',
     1 'WEADD MOD (',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/11X,
     2 'IS NOT WITHIN 12 HOURS OF THE CURRENT RUN PERIOD (',I2,1H/,I2,
     3 1H/,I4,1H-,I2,1X,A4,4H TO ,I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/
     4 11X,'THESE CHANGES WILL BE IGNORED.')
      WRITE(IPR,40)(MODCRD(I,NRDCRD),I=1,20)
40    FORMAT(15X,'THE MOD CARD IMAGE IS: ',20A4)
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 350
C
50    IF(IUMGEN.EQ.1)GO TO 60
C
C     MUST CONVERT UNITS FROM INCHES TO MM
C
      CALL FCONVT(4HMM  ,4HL   ,ENGUN,AMM2IN,BMM2IN,IER)
C
C     READ MOD CARDS FOR THIS SEGMENT - STORE VALUES IN ARRAYS() -
C      WILL TRANSFER TO CB /FRARSW/ AFTER ALL INFO IS READ
C
C     READ CARD - IF COMMAND, LEAVE - IF COMMAND AND 1ST CARD, ERROR
C
60    FIRST=.TRUE.
C
      IF(NRDCRD.EQ.NCARDS)GO TO 80
C
70    IF(NRDCRD.EQ.NCARDS)GO TO 350
C
      OPNAME=BLANK8
C
      IF(MISCMD(NCARDS,MODCRD).EQ.0)GO TO 100
C
      IF(.NOT.FIRST)GO TO 350
C
C     HAVE FOUND COMMAND AS FIRST SUBSEQUENT CARD - ERROR
C
80    IF(MODWRN.EQ.1)WRITE(IPR,90)
90    FORMAT(1H0,10X,'**WARNING** NO SUBSEQUENT CARDS FOUND FOR THE ',
     1  'SACCO MOD.  PROCESSING CONTINUES')
      IF(MODWRN.EQ.1)CALL WARN
      GO TO 350
C
100   FIRST=.FALSE.
      NRDCRD=NRDCRD+1
C
C     NOW READ THE 2ND FIELD - MUST BE AN INTEGER OR REAL NUMBER
C
      NFLD=1
      ISTRT=-3
      NCHAR=2
      ICKDAT=0
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,VALUE,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
ckwz.r26-13.5/17/05.ADD IF STARTRUN==MOD DATE, DO NOT APPLY IT.
	  if (ISTHR.EQ.IDATE) then
        IF (MODWRN.EQ.1) WRITE (IPR,119)(MODCRD(I,NRDCRD),I=1,20)
119     FORMAT(1H0,10X,'**WARNING** IN THE WEADD MOD - MOD DATE ',
     1  'EQUALS STARTRUN.  THESE CHANGES WILL BE IGNORED.'/11X,
     2  'THE CURRENT MOD CARD IS'/11X,20A4)
        VALUE=0.0
      endif
C
      IF(ITYPE.NE.-1)GO TO 130
C
110   IF(MODWRN.EQ.1)WRITE(IPR,120)(MODCRD(I,NRDCRD),I=1,20)
120   FORMAT(1H0,10X,'**WARNING** IN THE WEADD MOD - NOT ENOUGH ',
     1  'FIELDS ON SUBSEQUENT CARD.  THE CARD BEING PROCESSED IS:'/
     2  11X,20A4)
      GO TO 340
C
130   IF(ITYPE.LT.2.AND.NREP.EQ.-1)GO TO 150
C
C     VALUE IS NOT AN INTEGER NOR A REAL NUMBER
C
      IF(MODWRN.EQ.1)WRITE(IPR,140)(MODCRD(I,NRDCRD),I=1,20)
140   FORMAT(1H0,10X,'**WARNING** A NONNUMERIC VALUE WAS DECODED ',
     1  'WHERE ONE WAS EXPECTED'/11X,'THE CURRENT MOD CARD IS'/11X,20A4)
      GO TO 340
C
C     SEE IF THERE IS ANOTHER FIELD - IF NOT PROCESS THE INFO
C     IN ARRAYS() - IF SO IT MUST BE A KEYWORD OR A SLASH (/)
C
150   ISTRT=-3
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,IFIELD,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.EQ.-2)GO TO 200
C
C     ANOTHER FIELD - IS IT A SLASH ? - IS NOT SEE IF A KEYWORD
C
      IF(IFIELD.EQ.SLASH)GO TO 170
C
      IF(MODWRN.EQ.1)WRITE(IPR,160)(MODCRD(I,NRDCRD),I=1,20)
160   FORMAT(1H0,10X,'**WARNING** IN THE WEADD MOD - AN INVALID ',
     1  'FIELD WAS FOUND ON THE FOLLOWING MOD CARD'/11X,20A4)
      GO TO 340
C
C     GET HERE IF FIELD IS A SLASH
C     NOW READ NEXT FIELD - OPERATION NAME
C
170   ISTRT=-3
C
      CALL UFIEL2(NCARDS,MODCRD,NFLD,ISTRT,LEN,ITYPE,NREP,INTGER,REAL,
     1  NCHAR,OPNAME,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,ISTAT)
C
      IF(ISTRT.NE.-2)GO TO 190
      IF(MODWRN.EQ.0)GO TO 70
      WRITE(IODBUG,180)(MODCRD(I,NRDCRD),I=1,20)
180   FORMAT(11X,'** WARNING ** NO OPERATION NAME ENTERED ',
     1 'AFTER A SLASH ON THE FOLLOWING MOD CARD'/11X,20A4/
     2 11X,'THIS CARD IS IGNORED.')
      GO TO 340
190   CONTINUE
C
C     CONVERT TO MM IF NEEDED
C
200   IF(IUMGEN.EQ.0)VALUE=VALUE/AMM2IN
C
C     SEE IF ANY SETS OF CARRYOVER IN CB /FRARSW/
C
      IF(NRARSW.GT.0)GO TO 250
C
C     NONE IN CB - JUST ADD THIS WATER EQUIVALENT TO CB
C
      NRARSW=1
      CALL UMEMOV(OPNAME,RARESW(1,1),2)
      RARESW(3,1)=IDATE+0.01
      DO 210 I=4,6
210   RARESW(I,1)=-99.
      IF(VALUE.GE.0.)GO TO 230
C
C     BAD WATER EQUIVALENT ENTERED
C
      IF(MODWRN.EQ.1)WRITE(IPR,220)VALUE
220   FORMAT(1H0,10X,'**WARNING** IN WEADD MOD - VALUE IS OUT OF ',
     1 'VALID RANGE '/11X,'VALUE = ',G10.4)
      GO TO 340
C
230   RARESW(6,1)=VALUE
      IF(IBUG.GT.0)WRITE(IODBUG,240)VALUE
240   FORMAT(11X,'WATER EQUIVALENT VALUE OF ',G10.2,' MM ',
     1 'STORED IN THE 1ST ROW OF ARRAY RARESW.')
      GO TO 70
C
C     CHECK IF NAME MATCHES ANY NAME IN CB OR IS BLANK -
C       AND IF DATE MATCHES ANY DATE IN CB
C
250   DO 280 I=1,NRARSW
      IF(IUSAME(OPNAME,RARESW(1,I),2).EQ.0)GO TO 280
C
C     NAME IN CB MATCHES NAME TO BE STORED
C     NOW SEE IF DATES MATCH
C
      IDTCHK=RARESW(3,I)
      IF(IDTCHK.NE.IDATE)GO TO 280
C
C     DATES MATCH TOO - ADD INFO TO THIS SPOT IN CB
C
      IF(VALUE.GE.0)GO TO 260
C
C     BAD WATER EQUIVALENT INPUT
C
      IF(MODWRN.EQ.1)WRITE(IPR,220)VALUE
      GO TO 340
C
260   RARESW(6,I)=VALUE
      IF(IBUG.GT.0)WRITE(IODBUG,270)VALUE,I
270   FORMAT(11X,'WATER EQUIVALENT VALUE OF ',G10.2,' MM ',
     1 'REPLACED IN THE ROW ',I2,' OF ARRAY RARESW.')
      GO TO 70
C
280   CONTINUE
C
C     HAVE NOT FOUND MATCH FOR NAME AND DATE -
C     ADD NEW ENTRY TO CB
C
      IF(NRARSW.LT.10)GO TO 300
C
C     NOT ENOUGH ROOM IN CB
C
      IF(MODWRN.EQ.1)WRITE(IPR,290)(MODCRD(I,NRDCRD),I=1,20)
290   FORMAT(1H0,10X,'**WARNING** NOT ENOUGH ROOM IN COMMON BLOCK ',
     1 '/FRARSW/'/11X,'TO HOLD A NEW WATER EQUIVALENT.  THE CURRENT ',
     2 'MOD CARD IMAGE IS'/11X,20A4)
      GO TO 340
C
C     ENOUGH ROOM - ADD NEW ENTRY TO CB
C
300   NRARSW=NRARSW+1
      I=NRARSW
      CALL UMEMOV(OPNAME,RARESW(1,I),2)
      RARESW(3,I)=IDATE+0.01
      DO 310 J=4,6
310   RARESW(J,I)=-99.
      IF(VALUE.GE.0)GO TO 320
C
C     BAD WATER EQUIVALENT ENTERED
C
      IF(MODWRN.EQ.1)WRITE(IPR,220)VALUE
      GO TO 340
C
320   RARESW(6,I)=VALUE
      IF(IBUG.GT.0)WRITE(IODBUG,330)VALUE,I
330   FORMAT(11X,'WATER EQUIVALENT VALUE OF ',G10.2,' MM ',
     1 'ADDED IN THE ROW ',I2,' OF ARRAY RARESW.')
      GO TO 70
C
340   IF(MODWRN.EQ.1)CALL WARN
      GO TO 70
C
350   IF(IBUG.EQ.0)GO TO 380
C
      WRITE(IODBUG,360)NRARSW
360   FORMAT(11X,'***LEAVING SUBROUTINE MWEADD*** NRARSW=',I3)
      IF(NRARSW.GT.0)WRITE(IODBUG,370)((RARESW(I,J),I=1,6),J=1,NRARSW)
370   FORMAT(11X,'ARRAY RARESW ='/(11X,2A4,1X,F11.0,3F9.2))
C
380   CALL FSTWHR(OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C
      RETURN
      END
