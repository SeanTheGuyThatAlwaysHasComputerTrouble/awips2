C  MODULE MLOCCH
C
C  DESC THIS SUBROUTINE LOCATES WHERE CHANGES WILL BE MADE
C  DESC BY THE ROCHNG, ROMULT, RRICHNG, RRIMULT,
C  DESC        TSADD, TSCHNG, TSMULT, AND TSREPL MODS
C
      SUBROUTINE MLOCCH(MODID,ISTHR,IENHR,JSTHR,JENHR,
     1 TSID,DTYPE,IDT,NVALS,NVPDT,LOCD,IBUG,
     2 IOFFST,LDSTRT,NMOVE,ISTART,ISTAT)
C
C  THIS SUBROUTINE ORIGINALLY WRITTEN BY
C        GEORGE F. SMITH - HRL - OCTOBER 1985
C
      CHARACTER*8   MODID,TSID
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fpwarn'
      INCLUDE 'common/fmodft'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mods/RCS/mlocch.f,v $
     . $',                                                             '
     .$Id: mlocch.f,v 1.2 1997/01/29 21:10:08 page Exp $
     . $' /
C    ===================================================================
C
C
      ISTAT=0
      IOFFST=0
      LDSTRT=0
      NMOVE=0
      ISTART=0
C
C  SEE IF ANY OF PERIOD ENTERED IS WITHIN THE CURRENT RUN PERIOD
C
      IF(IBUG.GT.0)WRITE(IODBUG,891)MODID,ISTHR,IENHR,JSTHR,JENHR,
     1 IDA,IHR,LDA,LHR,IDADAT,IDT,NVALS,NVPDT,LOCD
  891 FORMAT(11X,'IN MLOCCH FROM ',A8,' -  ISTHR, IENHR, JSTHR, JENHR'/
     1 36X,4I7/11X,'IDA,  IHR,  LDA,  LHR,  IDADAT,  IDT'/
     2 8X,4I6,I8,I6/11X,'NVALS, NVPDT, LOCD'/
     3 11X,I4,I7,I6)
C
cc      IF(JSTHR.GT.IENHR.OR.JENHR.LE.ISTHR)GO TO 130
      IF(JSTHR.GT.IENHR.OR.JENHR.LT.ISTHR)GO TO 130
C
C  SOME OF PERIOD BEING CHANGED IS WITHIN THE RUN PERIOD
C
      GO TO 140
C
  130 CONTINUE
C
C  NONE OF THE PERIOD BEING CHANGED IS WITHIN THE RUN PERIOD
C
      ISTAT=1
      IF(MODWRN.EQ.0)GO TO 999
C
      CALL MDYH2(IDA,IHR,IM1,ID1,IY1,IH1,DUM1,DUM2,MODTZC)
      CALL MDYH2(LDA,LHR,IM2,ID2,IY2,IH2,DUM1,DUM2,MODTZC)
      JXSTD=JSTHR/24+1
      JXSTH=MOD(JSTHR,24)
      CALL MDYH2(JXSTD,JXSTH,IM3,ID3,IY3,IH3,DUM1,DUM2,MODTZC)
      JXEND=JENHR/24+1
      JXENH=MOD(JENHR,24)
      CALL MDYH2(JXEND,JXENH,IM4,ID4,IY4,IH4,DUM1,DUM2,MODTZC)
C
      WRITE(IPR,606)MODID,IM3,ID3,IY3,IH3,MODTZC,IM4,ID4,IY4,IH4,MODTZC,
     1 TSID,DTYPE,IDT,IM1,ID1,IY1,IH1,MODTZC,IM2,ID2,IY2,IH2,MODTZC
  606 FORMAT(1H0,10X,'**WARNING** IN THE ',A8,' MOD - THE PERIOD FOR ',
     1 'CHANGES (',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,' TO ',
     2 I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H)/11X,12X,
     3 'FOR TIME SERIES ',A8,1X,A4,1X,I2/11X,12X,'IS NOT WITHIN THE ',
     4 'PERIOD THAT CAN BE CHANGED FOR THIS RUN (',I2,1H/,I2,1H/,I4,1H-,
     5 I2,1X,A4,' TO ',I2,1H/,I2,1H/,I4,1H-,I2,1X,A4,1H))
      CALL WARN
      GO TO 999
C
  140 CONTINUE
C
C  COMPUTE LOCATION IN D ARRAY OF DATA FOR START OF CHANGES
C
C  ISTHR = HOUR OF START OF RUN
C  JSTHR = HOUR OF START OF MOD - VALUE(1)
C  ISTART = HOUR OF START OF CHANGES IN D ARRAY
C  IENHR = HOUR OF END OF RUN
C  JENHR = HOUR OF END OF MOD - VALUE(NVALS)
C
      ISTART=ISTHR + IDT
      IF(ISTHR.LT.JSTHR)ISTART=JSTHR
C
C  IOFFST IS NUMBER OF VALUES TO SKIP AT BEGINNING OF VALUES ARRAY
C
      IOFFST=((ISTART-JSTHR-1)/IDT)*NVPDT
      if (ioffst.lt.0) ioffst=0
C
C  LDSTRT IS THE LOCATION IN D ARRAY FOR START OF CHANGES
C
      LDSTRT = (ISTART-(IDADAT-1)*24-1)/IDT + 1
      LDSTRT = LOCD + NVPDT*(LDSTRT-1) - 1
C
C  NEND IS NUMBER OF VALUES REQUESTED TO BE CHANGED THAT
C  ARE AFTER THE END OF DATA IN THE D ARRAY
C
      NEND=0
      IF(JENHR.GT.IENHR)NEND=((JENHR-IENHR)/IDT)*NVPDT
      NMOVE=NVALS-IOFFST-NEND
C
      IF(NMOVE.LT.1)GO TO 130
C
  999 IF(IBUG.EQ.1)WRITE(IODBUG,892)ISTAT,IOFFST,LDSTRT,NMOVE,ISTART
  892 FORMAT(11X,'LEAVING MLOCCH - ISTAT=',I2,', IOFFST=',I3,
     1 ', LDSTRT=',I8,', NMOVE=',I4,', ISTART=',I8)
C
      RETURN
      END
