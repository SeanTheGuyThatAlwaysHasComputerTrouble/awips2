C$PRAGMA C (ICP18)
cfan added for pgf90 7/3/01
C$PRAGMA C (ICPI18)
C MEMBER EX18
C-----------------------------------------------------------------------
C   THIS ROUTINE IS THE EXECUTION ROUTINE FOR
C   THE PLOT-TS OPERATION.
C
C   THIS ROUTINE WAS INITIALLY PROGRAMMED BY
C   GERALD N. DAY  -  HRL  MAY 1980
C
      SUBROUTINE EX18(PO,D,ILOCD,LZERO,LTENS,ORD,LOCPT,TORD,IPLOT,NLOCD)
C
      DIMENSION PO(*),D(1),ILOCD(1),NDAYS(24),AMONTH(12),
     1 LZERO(1),LTENS(12),ORD(121),LOCPT(1),TORD(1),UNITS(2),IPLOT(1),
     2 NLOCD(1)
      COMMON/FCTIME/IDARUN,IHRRUN,LDARUN,LHRRUN,LDACPD,
     1 LHRCPD,NOW(5),LOCAL,NOUTZ,NOUTDS,NLSTZ,IDA,IHR,
     2 LDA,LHR,IDADAT
      COMMON/FNOPR/NOPROT
      COMMON/FPLTAB/IPLHY,IPRHY
      COMMON/IONUM/IN,IPR,IPU
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/FPROG/MAINUM,VERS,VDATE(2),PNAME(5),NDD
      COMMON/FWYDS/IRWY,NXWY,NTWY,IDEFWY
      COMMON/FWYDAT/WY(372)
C common where and outctl added 12/97 to provide opname to icpi18 and icp18
C and to use ioutyp  to select (or not) the graphic output.    hs
      COMMON/WHERE/ISEG(2),IOPNUM,OPNAME(2)
      COMMON/OUTCTL/IOUTYP
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_plotts/RCS/ex18.f,v $
     . $',                                                             '
     .$Id: ex18.f,v 1.5 2002/02/11 12:48:48 michaelo Exp $
     . $' /
C    ===================================================================
C
      DATA NDAYS/31,31,28,29,31,31,30,30,31,31,30,30,31,31,31,31,30,30,
     1 31,31,30,30,31,31/
      DATA AMONTH/4HJAN ,4HFEB ,4HMAR ,4HAPR ,4HMAY ,4HJUN ,4HJUL ,
     1 4HAUG ,4HSEP ,4HOCT ,4HNOV ,4HDEC /
      DATA UNITS/4HCMSD,4HCMS /
      IF(ITRACE.GE.1) WRITE(IODBUG,900)
C   CHECK IF DEBUG OUTPUT IS REQUESTED.
C
      IBUG=0
      IF(IDBALL.GT.0) IBUG=1
      IF(NDEBUG.EQ.0) GO TO 12
      DO 10 I=1,NDEBUG
      IF(IDEBUG(I).EQ.18) GO TO 11
   10 CONTINUE
      GO TO 12
   11 IBUG=1
   12 CONTINUE
C
      NOPT=PO(7)
      NPLOTS=PO(8)
      NTTS=PO(9)
      NPER=PO(10)
      NPREQ=11+9*NPLOTS+12*NTTS
      IF(NOPT.EQ.1) NPREQ=NPREQ+1
      IF(NOPT.EQ.2) NPREQ=NPREQ+2*NPER
      IF(NOPT.EQ.4) NPREQ=NPREQ+3
      IF(IBUG.EQ.0) GO TO 20
      WRITE(IODBUG,905)
      WRITE(IODBUG,910) (PO(I),I=1,NPREQ)
C
C   CHECK IF OUTPUT IS REQUESTED
C
   20 IF(NOPROT.EQ.1) GO TO 300
C
C   CHECK IF HYDROGRAPHS SHOULD BE PLOTTED
C
      IF(IPLHY.EQ.0) GO TO 50
      NHY=0
C
C   COUNT THE NUMBER OF HYDROGRAPHS
C
      DO 30 I=1,NPLOTS
      IPO=11+9*(I-1)
      IF(PO(IPO+6).EQ.UNITS(1).OR.PO(IPO+6).EQ.UNITS(2)) NHY=NHY+1
   30 CONTINUE
C
C   IF ALL PLOTS ARE HYDROGRAPHS AND HYDROGRAPHS ARE NOT
C   TO BE PLOTTED - RETURN
C
      IF(NHY.EQ.NPLOTS.AND.IPLHY.EQ.-1) GO TO 300
      IF(NOPT.EQ.1.OR.NOPT.EQ.3) GO TO 50
C
C   IF AT LEAST ONE PLOT IS A HYDROGRAPH AND HYDROGRAPHS ARE
C   TO BE PLOTTED NO MATTER WHAT THE CRITERIA ARE - MAKE THE
C   PLOT OPTION = 3
C
      IF(NHY.GT.0.AND.IPLHY.EQ.1) NOPT=3
C
   50 IF(NOPT.NE.1) GO TO 100
C
C   PLOT OPTION 1 - WATER YEAR PLOT
C
C   CALCULATE THE FIRST AVAILABLE RECORD ON THE
C   SCRATCH FILE FOR THIS OPERATION.
C
      IPO=11+9*NPLOTS+12*NTTS
      NFREC=PO(IPO+1)
      CALL MDYH1(IDA,IHR,IMOP,IDAP,IYRP,IHRP,NOUTZ,NOUTDS,TZONE)
      CALL MDYH1(LDA,LHR,LMOP,LDAP,LYRP,LHRP,NOUTZ,NOUTDS,TZONE)
C
C   CALCULATE THE NUMBER OF MONTHS BEFORE THE CURRENT MONTH
C   ON THE SCRATCH FILE.
C
      NMOWY=2+IMOP
      IF(IMOP.GE.10) NMOWY=IMOP-10
C
C   CALCULATE THE LAST COMPLETE RECORD.
C
      LREC=(NMOWY*31*NTTS)/372+NFREC-1
C
C   CALCULATE THE NEXT AVAILABLE RECORD FOR WRITING.
C
      IREC=LREC+1
      RNMOWY=NMOWY
      RNTTS=NTTS
      RREC=RNMOWY*31.*RNTTS/372.+NFREC-1
      IF(IDA.EQ.IDARUN) GO TO 55
C
C   READ THE NEXT RECORD
C
      READ (IRWY,REC=IREC) WY
C
C   CALCULATE THE LAST POSITION IN THE WY ARRAY.
C
   55 LWY=(RREC-LREC)*372.+0.5
      DO 70 I=1,NTTS
      IPO=11+9*NPLOTS+12*(I-1)
      NVPDT=PO(IPO+11)
      LOCV=PO(IPO+12)
      DO 60 J=1,31
C
C   CALCULATE THE LOCATION OF THE TS IN THE D ARRAY.
C
      LOCD=ILOCD(I)+(J-1)*NVPDT+LOCV-1
C
C   COPY THE TIME SERIES FROM THE D ARRAY TO THE WY ARRAY.
C
      WY(LWY+J)=D(LOCD)
   60 CONTINUE
C
C   UPDATE THE LAST POSITION IN THE WY ARRAY.
C
      LWY=LWY+31
      IF(LWY.LT.372) GO TO 70
C
C   WRITE THE FILLED RECORD TO THE SCRATCH FILE.
C
      WRITE (IRWY,REC=IREC) WY
C
C   INCREMENT THE NEXT AVAILABLE RECORD
C
      IREC=IREC+1
C
C   RESET THE LAST POSITION IN THE WY ARRAY.
C
      LWY=0
   70 CONTINUE
      IF(LWY.EQ.0) GO TO 75
C
C   WRITE THE LAST FRACTIONAL RECORD TO THE SCRATCH FILE.
C
      WRITE (IRWY,REC=IREC) WY
C
C   IF THIS IS NOT THE END OF THE RUN OR THE END OF THE WATER YEAR -
C   RETURN
C
   75 IF(LDA.NE.LDARUN.AND.LMOP.NE.9) GO TO 300
      IDATA=IDADAT
      IWYR=LYRP
      IF(LMOP.GE.10) IWYR=LYRP+1
C
C   WRITE HEADER INFO
C
      CALL HEAD18(PO,IWYR,AMONTH(10),TZONE)
C
C   CALCULATE THE JULIAN DAY OF THE FIRST DAY OF THE
C   CURRENT WATER YEAR.
C
      IYPLOT=IWYR-1
	  CALL FCTZC(100,0,TZONE)
      CALL JULDA(JDAY,JHOUR,10,1,IYPLOT,IHR,100,0,TZONE)
      IF(JDAY.GT.IDARUN) GO TO 80
C
C   ITS THE FIRST WATER YEAR
C
      J1PLOT=IDARUN
      GO TO 82
   80 J1PLOT=JDAY
   82 CALL MDYH1(J1PLOT,JHOUR,IMPLOT,IDPLOT,IYPLOT,IHPLOT,NOUTZ,NOUTDS,
     1 TZONE)
C
C   CHECK FOR LEAP YEAR
C
      LY=0
      IF(IWYR.EQ.(4*(IWYR/4))) LY=1
      IF(IWYR.NE.(100*(IWYR/100))) GO TO 85
      IF(IWYR.EQ.(400*(IWYR/400))) LY=0
C
C   WRITE THE MONTH LABEL
C
   85 WRITE(IPR,700) AMONTH(IMPLOT)
C
C   IF ITS THE FIRST MONTH OR AN EVEN NUMBERED MONTH
C   DRAW THE AXIS.
C
      IF(J1PLOT.EQ.IDARUN.OR.IMPLOT.EQ.(2*(IMPLOT/2)))
     1 CALL AXIS18(PO,LZERO,LTENS,NTENS,IPLOT,ORD(1))
C
C   CALCULATE THE NUMBER OF MONTHS BEFORE THE CURRENT MONTH
C   ON THE SCRATCH FILE.
C
      NMOWY=2+IMPLOT
      IF(IMPLOT.GE.10) NMOWY=IMPLOT-10
C
C   CALCULATE THE LAST COMPLETE RECORD
C
      LREC=(NMOWY*31*NTTS)/372+NFREC-1
C
C   CALCULATE THE RECORD CONTAINING THE CURRENT MONTH.
C
      IREC=LREC+1
      RNMOWY=NMOWY
      RNTTS=NTTS
      RREC=RNMOWY*31.*RNTTS/372.+NFREC-1
C
C   READ THE RECORD CONTAINING THE CURRENT MONTH.
C
      READ (IRWY,REC=IREC) WY
C
C   CALCULATE THE LAST POSITION IN THE WY ARRAY.
C
      LWY=(RREC-LREC)*372.+0.5
      DO 95 I=1,NTTS
      IPO=11+9*NPLOTS+12*(I-1)
      NVPDT=PO(IPO+11)
      LOCV=PO(IPO+12)
      DO 90 J=1,31
C
C   CALCULATE THE POSITION IN THE D ARRAY.
C
      LOCD=ILOCD(I)+(J-1)*NVPDT+LOCV-1
C
C   COPY THE TS DATA FROM THE WY ARRAY TO THE D ARRAY.
C
      D(LOCD)=WY(LWY+J)
   90 CONTINUE
C
C   UPDATE THE LAST POSITION IN THE WY ARRAY.
C
      LWY=LWY+31
      IF(LWY.LT.372.OR.I.EQ.NTTS) GO TO 95
C
C   INCREMENT THE RECORD.
C
      IREC=IREC+1
C
C   READ THE RECORD.
C
      READ (IRWY,REC=IREC) WY
C
C   RESET THE LAST POSITON IN THE WY ARRAY.
C
      LWY=0
   95 CONTINUE
C
C   CALCULATE THE NUMBER OF DAYS TO PLOT.
C
      NDPLOT=NDAYS(2*IMPLOT+LY-1)
      IF(J1PLOT.EQ.IDARUN) NDPLOT=NDPLOT-IDPLOT+1
      J2PLOT=J1PLOT+NDPLOT-1
      IF(J2PLOT.GT.LDARUN) J2PLOT=LDARUN
      IDADAT=J1PLOT
      IF(J1PLOT.EQ.IDARUN) IDADAT=J1PLOT-IDPLOT+1
C
C   PLOT THE MONTH
C
      CALL PLOT18(PO,D,ILOCD,J1PLOT,J2PLOT,LZERO,LTENS,NTENS,ORD,LOCPT,
     1 TORD,NLOCD)
      IF(J2PLOT.EQ.LDARUN) GO TO 98
C
C   INCREMENT THE JULIAN DAY AND MONTH.
C
      J1PLOT=J1PLOT+NDPLOT
      IMPLOT=IMPLOT+1
      IF(IMPLOT.EQ.10) GO TO 98
      IF(IMPLOT.GT.12) IMPLOT=IMPLOT-12
      GO TO 85
   98 IDADAT=IDATA
      GO TO 300
  100 IF(NOPT.NE.2) GO TO 130
C
C   PLOT OPTION 2 - PLOT BY PERIOD
C
      IHEAD=0
      IPO=11+9*NPLOTS+12*NTTS
      DO 120 I=1,NPER
      IJDAP=PO(IPO+1)
      LJDAP=PO(IPO+2)
C
C   CHECK STARTING AND ENDING DATES OF PERIOD.
C
      IF(LJDAP.LT.IDA.OR.IJDAP.GT.LDA) GO TO 115
      IF(IJDAP.LT.IDA) IJDAP=IDA
      IF(LJDAP.GT.LDA) LJDAP=LDA
      IF(IHEAD.GT.0) GO TO 105
      CALL MDYH1(IJDAP,IHR,IMPLOT,IDPLOT,IYPLOT,IHPLOT,NOUTZ,NOUTDS,
     1 TZONE)
C
C   WRITE HEADER INFO
C
      CALL HEAD18(PO,IYPLOT,AMONTH(IMPLOT),TZONE)
C
C   DRAW AXIS
C
  105 CALL AXIS18(PO,LZERO,LTENS,NTENS,IPLOT,ORD(1))
C
C   PLOT PERIOD
C
  110 CALL PLOT18(PO,D,ILOCD,IJDAP,LJDAP,LZERO,LTENS,NTENS,ORD,LOCPT,
     1 TORD,NLOCD)
      IHEAD=1
  115 IPO=IPO+2
  120 CONTINUE
      GO TO 300
  130 IF(NOPT.NE.3) GO TO 140
C
C   PLOT OPTION 3 - NORMAL OPTION
C
      CALL MDYH1(IDA,IHR,IMPLOT,IDPLOT,IYPLOT,IHPLOT,NOUTZ,NOUTDS,TZONE)
C     PASS NEEDED VALUES TO GRAPHICS INTERFACE ROUTINE
cew  you have to skip over this plot-ts stuff when you are running in ifp
cew   mcp3 mainum = 3
      IF(IOUTYP.EQ.0 .or. mainum .ne. 3) GO TO 135
C     ONLY PASS PARAMETRIC DATA ONCE
      IF(IDA.NE.IDARUN) GO TO 132
      CALL icpi18(opname,po, idarun, ihrrun, ldarun, lhrrun)
  132 CALL icp18 (opname,po, d, ilocd, ida, idadat, ihr, noutz, noutds)
      IF (IOUTYP.eq.2 .and. mainum .eq. 3) go to 300
C
C   WRITE HEADER INFO
C
  135 CALL HEAD18(PO,IYPLOT,AMONTH(IMPLOT),TZONE)
C
C   DRAW AXIS
C
      CALL AXIS18(PO,LZERO,LTENS,NTENS,IPLOT,ORD(1))
C
C   PLOT MONTH
C
      CALL PLOT18(PO,D,ILOCD,IDA,LDA,LZERO,LTENS,NTENS,ORD,LOCPT,
     1 TORD,NLOCD)
      GO TO 300
  140 IF(NOPT.NE.4) GO TO 300
C
C   PLOT OPTION 4 - PLOT BY CRITERIA
C
      IPO=11+9*NPLOTS+12*NTTS
      ITS=PO(IPO+1)
      CRIT=PO(IPO+2)
      KODE=PO(IPO+3)
      IPO=11+9*NPLOTS+12*(ITS-1)
      IDT=PO(IPO+4)
      NVPDT=PO(IPO+11)
      LOCV=PO(IPO+12)
C
C   LOCATE CRITERIA TS DATA IN THE D ARRAY
C
      IHR1=IHR
      IF(IHR1.LT.IDT) IHR1=IDT
      I1=((IDA-IDADAT)*24/IDT)*NVPDT+((IHR-1)/IDT)*
     1 NVPDT+LOCV+ILOCD(ITS)-1
      I2=((LDA-IDADAT)*24/IDT)*NVPDT+((LHR-1)/IDT)*
     1 NVPDT+LOCV+ILOCD(ITS)-1
C
      IF(KODE.EQ.1) GO TO 160
C
C   CHECK TO SEE IF TS HAS DATA GREATER THAN THE CRITERIA.
C
      DO 150 I=I1,I2
      IF(D(I).GT.CRIT) GO TO 180
  150 CONTINUE
      GO TO 300
  160 CONTINUE
C
C   CHECK TO SEE IF TS HAS DATA LESS THAN THE CRITERIA.
C
      DO 170 I=I1,I2
      IF(D(I).LT.CRIT) GO TO 180
  170 CONTINUE
      GO TO 300
C
  180 CALL MDYH1(IDA,IHR,IMPLOT,IDPLOT,IYPLOT,IHPLOT,NOUTZ,NOUTDS,TZONE)
C
C   WRITE HEADER INFO
C
      CALL HEAD18(PO,IYPLOT,AMONTH(IMPLOT),TZONE)
C
C   DRAW AXIS
C
      CALL AXIS18(PO,LZERO,LTENS,NTENS,IPLOT,ORD(1))
C
C   PLOT MONTH
C
      CALL PLOT18(PO,D,ILOCD,IDA,LDA,LZERO,LTENS,NTENS,ORD,LOCPT,
     1 TORD,NLOCD)
  300 CONTINUE
      IF(ITRACE.GE.1) WRITE(IODBUG,915)
      RETURN
  700 FORMAT(1H0,A4)
  705 FORMAT(1X,A4,I4)
  900 FORMAT(1H0,15H** EX18 ENTERED)
  905 FORMAT(1H0,31H** EX18 CONTENTS OF THE P ARRAY )
  910 FORMAT(1X,10G12.5)
  915 FORMAT(1H0,14H** EX18 EXITED)
      END
