C MEMBER FSACTR
C  (from old member FCFSACTR)
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 02/14/94.08:25:40 BY $WC20SV
C
C @PROCESS LVL(77)
C
      SUBROUTINE FSACTR(PL,IDT)
C.......................................
C     THIS ROUTINE ONLY USED BY THE OPERATIONAL PROGRAM. IT CHECKS
C     THE FSACCO COMMON BLOCK TO SEE IF ANY MODS EXIST FOR THE
C     SAC-SMA OPERATION THAT IS TO BE EXECUTED.  IF MODS EXIST,
C     VALUES ARE MOVED TO THE FCOSAC COMMON BLOCK.  TIMES
C     ARE ADJUSTED TO CORRESPOND WITH THE TIME INTERVAL FOR
C     THE OPERATION.
C.......................................
C     ROUTINE WRITTEN BY ...
C         ERIC ANDERSON - HRL - DEC. 1982
C.......................................
      DIMENSION SNAME(2),PL(*)
      REAL LZTWM,LZTWC
C
C     COMMON BLOCKS
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/WHERE/ISEG(2),IOPNUM,OPNAME(2)
      COMMON/FSACCO/NUMCOS,SACCOS(15,10)
      COMMON/FCOSAC/NSCV,JHSAC(10),ZONEC(6,10),BFMULT(10),FGIX(10)
      COMMON/FCTIME/IDARUN,IHRRUN,LDARUN,LHRRUN,LDACPD,LHRCPD,NOW(5),
     1 LOCAL,NOUTZ,NOUTDS,NLSTZ,IDA,IHR,LDA,LHR,IDADAT
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_ex/RCS/fsactr.f,v $
     . $',                                                             '
     .$Id: fsactr.f,v 1.1 1995/09/17 18:58:13 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA SNAME/4HFSAC,4HTR  /
      DATA JBUG,KBUG,LBUG/4HMODS,4HSACC,4HSACB/
      DATA BLANK/4H    /
C.......................................
C     TRACE LEVEL=1, DEBUG SWITCH=IBUG.
      CALL FPRBUG(SNAME,1,1,IBUG)
      IF(IFBUG(JBUG).EQ.1) IBUG=1
      IF(IFBUG(KBUG).EQ.1) IBUG=1
      IF(IFBUG(LBUG).EQ.1) IBUG=1
C.......................................
C     SET INITIAL VALUES
      NSCV=0
      M=2
C.......................................
C     CHECK IF ANY VALUES ARE IN FSACCO.
      IF(NUMCOS.GT.0) GO TO 90
      RETURN
C.......................................
C     IF DEBUG ON, PRINT COMMON BLOCK FSACCO.
   90 IF(IBUG.EQ.0) GO TO 100
      WRITE(IODBUG,900) NUMCOS
  900 FORMAT(1H0,46HFSACTR DEBUG--CONTENTS OF FSACCO COMMON BLOCK.,2X,
     1  7HNUMCOS=,I5)
      DO 91 N=1,NUMCOS
      WRITE(IODBUG,901)(SACCOS(I,N),I=1,15)
  901 FORMAT(1H ,5X,2A4,F8.0,12F9.2)
   91 CONTINUE
C.......................................
C     COMPUTE STARTING AND ENDING JULIAN HOUR WINDOWS.
  100 JH1=(IDA-1)*24+IHR-IDT-IDT/2
      JH2=(LDA-1)*24+LHR+(IDT-1)/2
C
C     RETRIEVE UZTWM AND LZTWM.
      LPM=PL(20)
      UZTWM=PL(LPM+2)
      LZTWM=PL(LPM+10)
      IF(IBUG.EQ.1) WRITE(IODBUG,902)IDA,IHR,LDA,LHR,IDT,JH1,JH2,
     1UZTWM,LZTWM
  902 FORMAT(1H0,13HFSACTR VALUES,7I8,2F9.2)
C.......................................
C     FIND ENTRIES FOR THIS OPERATION.
      DO 101 N=1,NUMCOS
      IF((SACCOS(1,N).EQ.BLANK).AND.(SACCOS(2,N).EQ.BLANK)) GO TO 105
      IF(IUSAME(SACCOS(1,N),OPNAME,M).EQ.1) GO TO 105
      GO TO 101
C
C     ENTRY IS FOR THIS OPERATION--CHECK IF WITHIN RUN PERIOD.
  105 JH=SACCOS(3,N)
      IF((JH.LT.JH1).OR.(JH.GT.JH2)) GO TO 101
C
C     WITHIN RUN PERIOD--ADJUST TO NEAREST DELTA T.
      JH=((JH+IDT/2)/IDT)*IDT
C
C     CHECK IF ANOTHER VALUE FOR THE SAME TIME.
      IF(NSCV.EQ.0) GO TO 102
      DO 103 I=1,NSCV
      IF(JH.NE.JHSAC(I)) GO TO 103
C     ANOTHER ENTRY FOR THE SAME TIME EXISTS
      IF((SACCOS(1,N).EQ.BLANK).AND.(SACCOS(2,N).EQ.BLANK)) GO TO 120
C     SECOND ENTRY IS SPECIFICALLY FOR THIS OPERATION--IT SHOULD
C       OVERIRDE ANY PREVIOUS ENTRIES.
      UZTWC=SACCOS(4,N)
      IF(SACCOS(10,N).LT.0.0) GO TO 111
      UZTWC=UZTWM-SACCOS(10,N)
      IF(UZTWC.LT.0.0) UZTWC=0.0
  111 IF(UZTWC.GE.0.0) ZONEC(1,I)=UZTWC
      IF(SACCOS(5,N).GE.0.0) ZONEC(2,I)=SACCOS(5,N)
      LZTWC=SACCOS(6,N)
      IF(SACCOS(11,N).LT.0.0) GO TO 112
      LZTWC=LZTWM-SACCOS(11,N)
      IF(LZTWC.LT.0.0) LZTWC=0.0
  112 IF(LZTWC.GE.0.0)ZONEC(3,I)=LZTWC
      IF(SACCOS(7,N).GE.0.0) ZONEC(4,I)=SACCOS(7,N)
      IF(SACCOS(8,N).GE.0.0) ZONEC(5,I)=SACCOS(8,N)
      IF(SACCOS(9,N).GE.0.0) ZONEC(6,I)=SACCOS(9,N)
      IF(SACCOS(12,N).GT.-900.0) FGIX(I)=SACCOS(12,N)
      IF(SACCOS(13,N).GE.0.0) BFMULT(I)=SACCOS(13,N)
      GO TO 101
C     SECOND ENTRY IS FOR ALL OPERATIONS OF THIS TYPE IN
C       THE SEGMENT--ONLY REPLACE MISSING VALUES.
  120 IF(ZONEC(1,I).GE.0.0) GO TO 121
      UZTWC=SACCOS(4,N)
      IF(SACCOS(10,N).LT.0.0) GO TO 122
      UTZWC=UZTWM-SACCOS(10,N)
      IF(UZTWC.LT.0.0) UZTWC=0.0
  122 ZONEC(1,I)=UZTWC
  121 IF(ZONEC(5,N).LT.0.0) ZONEC(2,I)=SACCOS(5,N)
      IF(ZONEC(3,I).GE.0.0) GO TO 123
      LZTWC=SACCOS(6,N)
      IF(SACCOS(11,N).LT.0.0) GO TO 124
      LZTWC=LZTWM-SACCOS(11,N)
      IF(LZTWC.LT.0.0) LZTWC=0.0
  124 ZONEC(3,I)=LZTWC
  123 IF(ZONEC(4,I).LT.0.0) ZONEC(4,I)=SACCOS(7,N)
      IF(ZONEC(5,I).LT.0.0) ZONEC(5,I)=SACCOS(8,N)
      IF(ZONEC(6,I).LT.0.0) ZONEC(6,I)=SACCOS(9,N)
      IF(FGIX(I).LE.-900.0) FGIX(I)=SACCOS(12,N)
      IF(BFMULT(I).LT.0.0) BFMULT(I)=SACCOS(13,N)
      GO TO 101
  103 CONTINUE
C
C
C     STORE IN FCOSAC COMMON BLOCK.
  102 NSCV=NSCV+1
      JHSAC(NSCV)=JH
      UZTWC=SACCOS(4,N)
      IF(SACCOS(10,N).LT.0.0) GO TO 108
      UZTWC=UZTWM-SACCOS(10,N)
      IF(UZTWC.LT.0.0) UZTWC=0.0
  108 ZONEC(1,NSCV)=UZTWC
      ZONEC(2,NSCV)=SACCOS(5,N)
      LZTWC=SACCOS(6,N)
      IF(SACCOS(11,N).LT.0.0) GO TO 109
      LZTWC=LZTWM-SACCOS(11,N)
      IF(LZTWC.LT.0.0) LZTWC=0.0
  109 ZONEC(3,NSCV)=LZTWC
      ZONEC(4,NSCV)=SACCOS(7,N)
      ZONEC(5,NSCV)=SACCOS(8,N)
      ZONEC(6,NSCV)=SACCOS(9,N)
      FGIX(NSCV)=SACCOS(12,N)
      BFMULT(NSCV)=SACCOS(13,N)
  101 CONTINUE
      IF(NSCV.EQ.0) RETURN
C.......................................
C     IF DEBUG ON, PRINT FCOSAC COMMON BLOCK.
      IF(IBUG.EQ.0) RETURN
      WRITE(IODBUG,903) NSCV
  903 FORMAT(1H0,46HFSACTR DEBUG--CONTENTS OF FCOSAC COMMON BLOCK.,2X,
     1  5HNSCV=,I5)
      DO 110 N=1,NSCV
      WRITE(IODBUG,904)JHSAC(N),(ZONEC(I,N),I=1,6),BFMULT(N),FGIX(N)
  904 FORMAT(1H ,5X,I10,8F10.2)
  110 CONTINUE
C.......................................
      RETURN
      END
