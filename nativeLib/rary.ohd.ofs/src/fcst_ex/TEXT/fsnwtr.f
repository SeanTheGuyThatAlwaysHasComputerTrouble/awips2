C MEMBER FSNWTR
C  (from old member FCFSNWTR)
C
      SUBROUTINE FSNWTR(IDT,ITPX,W)
C                             LAST UPDATE: 02/20/97 BY ERB
C
C.......................................
C     THIS SUBROUTINE IS ONLY USED BY THE OPERATIONAL PROGRAM.
C     IT CHECKS THE FRARSW, FCHGRS, MOD119 & MOD219 COMMON BLOCKS TO SEE
C     IF ANY MODS EXIST FOR THE SNOW MODEL OPERATION THAT IS
C     ABOUT TO BE EXECUTED.  IF ANY MODS EXISTS WITHIN THE
C     RUN PERIOD, THE VALUES ARE MOVED TO THE FSDATA, FPXTYP,
C     FMFC19 AND FADJ19 COMMON BLOCKS.  THE TIME ASSOCIATED WITH EACH
C     MOD ENTRY IS CHECKED AGAINST THE TIME INTERVALS USED
C     IN THE OPERATION AND ADJUSTED IF NECESSARY.
C.......................................
C     SUBROUTINE WRITTEN BY ...
C          ERIC ANDERSON - HRL - DEC. 1982
C     MOD119 ADDITIONS -- ERIC ANDERSON, DEC 1993
C     FSKDATA ADDITIONS - DAVID TRAN, APR 1996
C     MOD219 ADDITIONS -- RUSS ERB, FEB. 1997
C.......................................
      REAL MFC19
      REAL UADJ19
      DIMENSION SNAME(2),W(1)
C
C     COMMON BLOCKS
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/WHERE/ISEG(2),IOPNUM,OPNAME(2)
      COMMON/FRARSW/NRARSW,RARESW(6,10)
      COMMON/FRAKSW/NRAKSW,RAREKW(6,10)
      COMMON/FSDATA/NSDV,JHSNW(10),WESNW(10),AESNW(10),WEADD(10)
      COMMON/FCHGRS/NCHGRS,ICHGRS(5,20)
      COMMON/FPXTYP/NRSV,IJHRS(20),LJHRS(20),PCTSV(20)
      COMMON/MOD119/NDT19,IOP19(2,20),JDT19(2,20),MFC19(20)
      COMMON/FMFC19/NMFC,IJHMF(20),LJHMF(20),VMFC(20)
      COMMON/MOD219/NDT219,IOP219(2,20),JDT219(2,20),UADJ19(20)
      COMMON/FADJ19/NUADJ,IJHUA(20),LJHUA(20),VUADJ(20)
      COMMON/FCTIME/IDARUN,IHRRUN,LDARUN,LHRRUN,LDACPD,LHRCPD,NOW(5),
     1LOCAL,NOUTZ,NOUTDS,NLSTZ,IDA,IHR,LDA,LHR,IDADAT
      COMMON/IONUM/IN,IPR,IPU
      include 'snow43/fskdata'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_ex/RCS/fsnwtr.f,v $
     . $',                                                             '
     .$Id: fsnwtr.f,v 1.5 2005/03/09 21:18:59 wkwock Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA SNAME/4HFSNW,4HTR  /
      DATA JBUG,KBUG,LBUG/4HMODS,4HRAIN,4HWECH/
      DATA MBUG/4HAESC/
      DATA BLANK,IBLANK/4H    ,4H    /
C.......................................
C     TRACE LEVEL=1, DEBUG SWITCH=IBUG
      CALL FPRBUG(SNAME,1,19,IBUG)
      IF(IFBUG(JBUG).EQ.1) IBUG=1
      IF((IFBUG(KBUG).EQ.1).AND.(NCHGRS.GT.0)) IBUG=1
      IF((IFBUG(LBUG).EQ.1).AND.(NRARSW.GT.0)) IBUG=1
      IF((IFBUG(MBUG).EQ.1).AND.(NRARSW.GT.0)) IBUG=1
C.......................................
C     SET INITIAL VALUES
      NSDV=0
      NKDV=0
      NRSV=0
      NMFC=0
      NUADJ=0
      M=2
C.......................................
C     CHECK IF ANY VALUES ARE IN FRARSW, FCHGRS, MOD119 OR MOD219
      IF((NRARSW.GT.0).OR.(NRAKSW.GT.0).OR.(NCHGRS.GT.0).OR.
     1   (NDT19.GT.0).OR.(NDT219.GT.0)) GOTO 90
      RETURN
C.......................................
C     IF DEBUG ON, PRINT COMMON BLOCKS.
   90 IF(IBUG.EQ.0) GO TO 100
C
      IF(NRARSW.EQ.0) GO TO 92
      WRITE(IODBUG,900) NRARSW
  900 FORMAT(1H0,46HFSNWTR DEBUG--CONTENTS OF FRARSW COMMON BLOCK.,3X,
     1  7HNRARSW=,I3)
      DO 91 N=1,NRARSW
      WRITE(IODBUG,901)(RARESW(I,N),I=1,6)
  901 FORMAT(1H ,10X,2A4,4F10.2)
   91 CONTINUE
C
   92 IF(NRAKSW.EQ.0) GO TO 95
      WRITE(IODBUG,801) NRAKSW
  801 FORMAT(1H0,46HFSNWTR DEBUG--CONTENTS OF FRAKSW COMMON BLOCK.,3X,
     1  7HNRAKSW=,I3) 
      DO 93 N=1,NRAKSW
      WRITE(IODBUG,901)(RAREKW(I,N),I=1,6)
   93 CONTINUE
C
   95 IF(NCHGRS.EQ.0) GO TO 97
      WRITE(IODBUG,902) NCHGRS
  902 FORMAT(1H0,46HFSNWTR DEBUG--CONTENTS OF FCHGRS COMMON BLOCK.,3X,
     1  7HNCHGRS=,I3)
      DO 96 N=1,NCHGRS
      WRITE(IODBUG,903) (ICHGRS(I,N),I=1,5)
  903 FORMAT(1H ,10X,2A4,3I10)
   96 CONTINUE
C
   97 IF (NDT19.EQ.0) GO TO 99
      WRITE(IODBUG,911) NDT19
  911 FORMAT(1H0,'FSNWTR DEBUG--CONTENTS OF MOD119 COMMON BLOCK.',3X,
     1 'NDT19=',I3)
      DO 98 N=1,NDT19
      WRITE(IODBUG,912) (IOP19(I,N),I=1,2),(JDT19(I,N),I=1,2),MFC19(N)
  912 FORMAT(1H ,10X,2A4,2I10,F10.3)
   98 CONTINUE
C
   99 IF (NDT219.EQ.0) GO TO 100
      WRITE(IODBUG,915) NDT219
  915 FORMAT(1H0,'FSNWTR DEBUG--CONTENTS OF MOD219 COMMON BLOCK.',3X,
     1 'NDT219=',I3)
      DO 108 N=1,NDT219
      WRITE(IODBUG,916)(IOP219(I,N),I=1,2),(JDT219(I,N),I=1,2),UADJ19(N)
  916 FORMAT(1H ,10X,2A4,2I10,F10.3)
  108 CONTINUE
C.......................................
C     COMPUTE STARTING AND ENDING JULIAN HOUR WINDOWS.
  100 JH=(IDA-1)*24+IHR-IDT
      IHPS=(ITPX+1)/2
      IHMC=(IDT+1)/2
      IJHPS=JH+IHPS
      IJHMC=JH+IHMC
      IJHSC=JH-IDT/2
      JH=(LDA-1)*24+LHR
      LJHPS=JH+IHPS-1
      LJHMC=JH+IHMC-1
      LJHSC=JH+(IDT-1)/2
      IF(IBUG.EQ.1) WRITE(IODBUG,904)IDA,IHR,LDA,LHR,IDT,ITPX,IJHPS,
     1IJHSC,IJHMC,LJHPS,LJHSC,LJHMC
  904 FORMAT(1H0,20HFSNWTR TIMING VALUES,12I8)
C.......................................
C     FRARSW COMMON BLOCK.
      IF(NRARSW.EQ.0) GO TO 140
C
C     FIND ENTRIES FOR THIS OPERATION.
      DO 101 N=1,NRARSW
      IF((RARESW(1,N).EQ.BLANK).AND.(RARESW(2,N).EQ.BLANK)) GO TO 105
      IF(IUSAME(RARESW(1,N),OPNAME,M).EQ.1) GO TO 105
      GO TO 101
C
C     ENTRY IS FOR THIS OPERATION--CHECK IF WITHIN RUN PERIOD.
  105 JH=RARESW(3,N)
      IF((JH.LT.IJHSC).OR.(JH.GT.LJHSC)) GO TO 101
C
C     WITHIN RUN PERIOD--ADJUST TO NEAREST DELTA T.
      JH=((JH+IDT/2)/IDT)*IDT
C
C     CHECK IF ANOTHER VALUE FOR THE SAME TIME, IF SO OVERRIDE.
      IF(NSDV.EQ.0) GO TO 102
      DO 103 I=1,NSDV
      IF(JH.NE.JHSNW(I)) GO TO 103
C      ANOTHER ENTRY AT THE SAME TIME EXISTS.
      IF((RARESW(1,N).EQ.BLANK).AND.(RARESW(2,N).EQ.BLANK)) GO TO 120
C      SECOND ENTRY IS SPECIFICALLY FOR THIS OPERATION--IT SHOULD
C       OVERRIDE ANY PREVIOUS ENTRIES.
      IF(RARESW(4,N).GE.0.0) WESNW(I)=RARESW(4,N)
      IF(RARESW(5,N).GE.0.0) AESNW(I)=RARESW(5,N)
      IF(RARESW(6,N).GE.0.0) WEADD(I)=RARESW(6,N)
      GO TO 101
C      SECOND ENTRY IS FOR ALL OPERATIONS OF THIS TYPE IN THE
C     SEGMENT--ONLY REPLACE MISSING VALUES.
 120  IF(WESNW(I).LT.0.0) WESNW(I)=RARESW(4,N)
      IF(AESNW(I).LT.0.0) AESNW(I)=RARESW(5,N)
      IF(WEADD(I).LT.0.0) WEADD(I)=RARESW(6,N)
      GO TO 101
  103 CONTINUE
C
C     STORE IN FSDATA COMMON BLOCK.
  102 NSDV=NSDV+1
      JHSNW(NSDV)=JH
      WESNW(NSDV)=RARESW(4,N)
      AESNW(NSDV)=RARESW(5,N)
      WEADD(NSDV)=RARESW(6,N)
  101 CONTINUE
      IF(NDSV.EQ.0) GOTO 140
C.......................................
C     IF DEBUG ON, PRINT FSDATA COMMON BLOCK.
      IF(IBUG.EQ.0) GO TO 140
      WRITE(IODBUG,905) NSDV
  905 FORMAT(1H0,46HFSNWTR DEBUG--CONTENTS OF FSDATA COMMON BLOCK.,3X,
     1  5HNSDV=,I3)
      DO 110 N=1,NSDV
      WRITE(IODBUG,906)JHSNW(N),WESNW(N),AESNW(N),WEADD(N)
  906 FORMAT(1H ,10X,I10,3F10.2)
  110 CONTINUE
C.......................................
C     FRAKSW COMMON BLOCK
  140 IF(NRAKSW.EQ.0) GOTO 150
C
C     FIND ENTRIES FOR THIS OPERATION.
      DO 141 N=1,NRAKSW
      IF((RAREKW(1,N).EQ.BLANK).AND.(RAREKW(2,N).EQ.BLANK)) GO TO 145
      IF(IUSAME(RAREKW(1,N),OPNAME,M).EQ.1) GO TO 145
      GO TO 141
C
C     ENTRY IS FOR THIS OPERATION--CHECK IF WITHIN RUN PERIOD.
  145 JH=RAREKW(3,N)
      IF((JH.LT.IJHSC).OR.(JH.GT.LJHSC)) GO TO 141
C
C     WITHIN RUN PERIOD--ADJUST TO NEAREST DELTA T.
      JH=((JH+IDT/2)/IDT)*IDT
C
C     CHECK IF ANOTHER VALUE FOR THE SAME TIME, IF SO OVERRIDE.
      IF(NSDV.EQ.0) GO TO 142
      DO 143 I=1,NKDV
      IF(JH.NE.JHSNW(I)) GO TO 143
C     ANOTHER ENTRY AT THE SAME TIME EXISTS.
      IF((RAREKW(1,N).EQ.BLANK).AND.(RAREKW(2,N).EQ.BLANK)) GO TO 149
C     SECOND ENTRY IS SPECIFICALLY FOR THIS OPERATION--IT SHOULD
C     OVERRIDE ANY PREVIOUS ENTRIES.
      WESNK(I)=RAREKW(4,N)
      VWESNK(I)=RAREKW(5,N)
      WEGAIN(I)=RAREKW(6,N)
      GO TO 141
C     SECOND ENTRY IS FOR ALL OPERATIONS OF THIS TYPE IN THE
C     SEGMENT--ONLY REPLACE MISSING VALUES.
 149  IF (WESNK(I).LT.0.0) THEN 
         WESNK(I)=RAREKW(4,N)
         VWESNK(I)=RAREKW(5,N)
         WEGAIN(I)=RAREKW(6,N)
         ENDIF
      GO TO 141
  143 CONTINUE
C
C     STORE IN FSKDATA COMMON BLOCK.
  142 NKDV=NKDV+1
      JHSNK(NKDV)=JH
      WESNK(NKDV)=RAREKW(4,N)
      VWESNK(NKDV)=RAREKW(5,N)
      WEGAIN(NKDV)=RAREKW(6,N)
  141 CONTINUE
      IF (NKDV.EQ.0) GOTO 150
C.......................................
C     IF DEBUG ON, PRINT FSKDATA COMMON BLOCK.
      IF(IBUG.EQ.0) GO TO 150
      WRITE(IODBUG,805) NKDV
  805 FORMAT(1H0,47HFSNWTR DEBUG--CONTENTS OF FSKDATA COMMON BLOCK.,3X,
     1  5HNKDV=,I3)
      DO 111 N=1,NKDV
      WRITE(IODBUG,906)JHSNK(N),WESNK(N),VWESNK(N),WEGAIN(N)
  111 CONTINUE
C.......................................
C     FCHGRS COMMON BLOCK
  150 IF(NCHGRS.EQ.0) GO TO 200
C
C     INITIALIZE WORK ARRAY
      IHZ=IJHPS-IHPS
      LW=(LJHPS-IHPS+1-IHZ)/ITPX
      DO 155 I=1,LW
 155  W(I)=-999.0
C
C     PROCESSING RULES
C      1. ENTRIES EXPLICITLY FOR THIS OPERATION WIN.
C      2. LAST ENTERED WINS.
C
C     PROCESSED ENTRIES EXPLICITLY ENTERED FOR THIS OPERATION
C      FROM LAST TO FIRST.
      DO 160 N=1,NCHGRS
      L=NCHGRS-N+1
      IF(IUSAME(ICHGRS(1,L),OPNAME,M).EQ.1) GO TO 161
      GO TO 160
C     ENTER INTO WORK SPACE
  161 VALUE=ICHGRS(5,L)
      CALL FRSW19(W,ICHGRS(3,L),ICHGRS(4,L),VALUE,
     1 IHZ,IJHPS,LJHPS,ITPX,LW,IBUG,IODBUG)
 160  CONTINUE
C
C     PROCESS IMPLICIT ENTRIES, AGAIN LAST TO FIRST
      DO 165 N=1,NCHGRS
      L=NCHGRS-N+1
      IF((ICHGRS(1,L).EQ.IBLANK).AND.(ICHGRS(2,L).EQ.IBLANK))GO TO 166
      GO TO 165
C     ENTERED INTO WORK SPACE
  166 VALUE=ICHGRS(5,L)
      CALL FRSW19(W,ICHGRS(3,L),ICHGRS(4,L),VALUE,
     1 IHZ,IJHPS,LJHPS,ITPX,LW,IBUG,IODBUG)
 165  CONTINUE
C
C     FILL FPXTYP COMMON FORM WORK ARRAY.
      FLV=-999.0
      IC=0
      DO 170 I=1,LW
      IF(W(I).EQ.FLV) GO TO 175
      IF(IC.EQ.1) GO TO 171
C     START OF A CHANGE
 172  NRSV=NRSV+1
      IJHRS(NRSV)=IHZ+I*ITPX
      PCTSV(NRSV)=W(I)
      IC=1
      GO TO 175
C     END OF A CHANGE
 171  LJHRS(NRSV)=IHZ+(I-1)*ITPX
      IF(NRSV.LT.20) GO TO 173
      JD=((LJHRS(NRSV)-1)/24)+1
      JH=LJHRS(NRSV)-((JD-1)*24)
      CALL MDYH1(JD,JH,MO,ID,IY,IH,NOUTZ,NOUTDS,TZCODE)
      WRITE(IPR,908)OPNAME,MO,ID,IY,IH,TZCODE
 908  FORMAT(1H0,10X,93H**WARNING** THE MAXIMUM OF 20 ALLOWED CHANGES TO
     1 THE FORM OF PRECIPITATION HAVE BEEN MADE FOR,/16X,
     2 22HTHE SNOW-17 OPERATION(,2A4,
     3 22H).  LAST CHANGE IS FOR,I3,1H/,I2,1H/,I4,1H-,I2,A3,1H.,
     4 /16X,56HEITHER SHORTEN RUN PERIOD OR CONSOLIDATE .RAINSNOW MODS.)
      CALL WARN
      GO TO 179
 173  IF(W(I).GE.0.0) GO TO 172
      IC=0
      FLV=-999.0
      GO TO 170
C     CHECK IF LAST VALUE
 175  IF(I.EQ.LW) GO TO 176
      FLV=W(I)
      GO TO 170
 176  IF(IC.EQ.0) GO TO 170
      LJHRS(NRSV)=IHZ+I*ITPX
 170  CONTINUE
 179  IF(NRSV.EQ.0) GO TO 200
C.......................................
C     IF DEBUG ON, PRINT FPXTYP COMMON BLOCK.
      IF(IBUG.EQ.0) GO TO 200
      WRITE(IODBUG,909) NRSV
  909 FORMAT(1H0,46HFSNWTR DEBUG--CONTENTS OF FPXTYP COMMON BLOCK.,3X,
     1  5HNRSV=,I3)
      DO 180 N=1,NRSV
      WRITE(IODBUG,910) IJHRS(N),LJHRS(N),PCTSV(N)
  910 FORMAT(1H ,10X,2I10,F10.2)
  180 CONTINUE
C.......................................
C     MOD119 COMMON BLOCK
  200 IF(NDT19.EQ.0) go to 300 
c      above statement was RETURN changed by mbs 3/14/97  
C
C     INITIALIZE WORK ARRAY
      IHZ=IJHMC-IHMC
      LW=(LJHMC-IHMC+1-IHZ)/ITPX
      DO 205 I=1,LW
 205  W(I)=-999.0
C
C     PROCESSING RULES
C      1. ENTRIES EXPLICITLY FOR THIS OPERATION WIN.
C      2. LAST ENTERED WINS.
C
C     PROCESSED ENTRIES EXPLICITLY ENTERED FOR THIS OPERATION
C      FROM LAST TO FIRST.
      DO 210 N=1,NDT19
      L=NDT19-N+1
      IF(IUSAME(IOP19(1,L),OPNAME,M).EQ.1) GO TO 211
      GO TO 210
C     ENTER INTO WORK SPACE
  211 CALL FRSW19(W,JDT19(1,L),JDT19(2,L),MFC19(L),
     1 IHZ,IJHMC,LJHMC,ITPX,LW,IBUG,IODBUG)
 210  CONTINUE
C
C     PROCESS IMPLICIT ENTRIES, AGAIN LAST TO FIRST
      DO 215 N=1,NDT19
      L=NDT19-N+1
      IF((IOP19(1,L).EQ.IBLANK).AND.(IOP19(2,L).EQ.IBLANK))GO TO 216
      GO TO 215
C     ENTERED INTO WORK SPACE
  216 CALL FRSW19(W,JDT19(1,L),JDT19(2,L),MFC19(L),
     1 IHZ,IJHMC,LJHMC,ITPX,LW,IBUG,IODBUG)
 215  CONTINUE
C
C     FILL FMFC19 COMMON FROM WORK ARRAY.
      FLV=-999.0
      IC=0
      DO 220 I=1,LW
      IF(W(I).EQ.FLV) GO TO 225
      IF(IC.EQ.1) GO TO 221
C     START OF A CORRECTION
 222  NMFC=NMFC+1
ckwz.r25-65.value of IJHMF(NMFC) is incorrect.  ITPX is the correct
ckwz.time interval variable, not IDT.
ckwz      IJHMF(NMFC)=IHZ+I*IDT
      IJHMF(NMFC)=IHZ+I*ITPX
      VMFC(NMFC)=W(I)
      IC=1
      GO TO 225
C     END OF A CORRECTION
ckwz 221  LJHMF(NMFC)=IHZ+(I-1)*IDT
 221  LJHMF(NMFC)=IHZ+(I-1)*ITPX
      IF(NMFC.LT.21) GO TO 223
      JD=((LJHMF(NMFC)-1)/24)+1
      JH=LJHMF(NMFC)-((JD-1)*24)
      CALL MDYH1(JD,JH,MO,ID,IY,IH,NOUTZ,NOUTDS,TZCODE)
      WRITE(IPR,907)OPNAME,MO,ID,IY,IH,TZCODE
 907  FORMAT(1H0,10X,'**WARNING** THE MAXIMUM OF 20 ALLOWED MELT FACTOR'
     1,' CORRECTIONS HAVE BEEN MADE FOR',/16X,
     2 'THE SNOW-17 OPERATION(',2A4,
     3 ').  LAST CORRECTION ENDS AT',I3,'/',I2,'/',I4,'-',I2,A3,'.',
     4 /16X,'EITHER SHORTEN RUN PERIOD OR CONSOLIDATE .MFC MODS.')
      CALL WARN
      GO TO 229
 223  IF(W(I).GE.0.0) GO TO 222
      IC=0
      FLV=-999.0
      GO TO 220
C     CHECK IF LAST VALUE IN WORK ARRAY
 225  IF(I.EQ.LW) GO TO 226
      FLV=W(I)
      GO TO 220
 226  IF(IC.EQ.0) GO TO 220
ckwz      LJHMF(NMFC)=IHZ+I*IDT
      LJHMF(NMFC)=IHZ+I*ITPX
 220  CONTINUE
 229  IF(NMFC.EQ.0) RETURN
C.......................................
C     IF DEBUG ON, PRINT FMFC19 COMMON BLOCK.
      IF(IBUG.EQ.0) GO TO 300
      WRITE(IODBUG,913) NMFC
  913 FORMAT(1H0,46HFSNWTR DEBUG--CONTENTS OF FMFC19 COMMON BLOCK.,3X,
     1  5HNMFC=,I3)
      DO 230 N=1,NMFC
      WRITE(IODBUG,914) IJHMF(N),LJHMF(N),VMFC(N)
  914 FORMAT(1H ,10X,2I10,F10.3)
  230 CONTINUE
C.......................................
C     MOD219 COMMON BLOCK
  300 IF(NDT219.EQ.0) RETURN
C
C     INITIALIZE WORK ARRAY
      IHZ=IJHMC-IHMC
      LW=(LJHMC-IHMC+1-IHZ)/ITPX
      DO 305 I=1,LW
 305  W(I)=-999.0
C
C     PROCESSING RULES
C      1. ENTRIES EXPLICITLY FOR THIS OPERATION WIN.
C      2. LAST ENTERED WINS.
C
C     PROCESSED ENTRIES EXPLICITLY ENTERED FOR THIS OPERATION
C      FROM LAST TO FIRST.
      DO 310 N=1,NDT219
      L=NDT219-N+1
      IF(IUSAME(IOP219(1,L),OPNAME,M).EQ.1) GO TO 311
      GO TO 310
C     ENTER INTO WORK SPACE
  311 CALL FRSW19(W,JDT219(1,L),JDT219(2,L),UADJ19(L),
     1 IHZ,IJHMC,LJHMC,ITPX,LW,IBUG,IODBUG)
 310  CONTINUE
C
C     PROCESS IMPLICIT ENTRIES, AGAIN LAST TO FIRST
      DO 315 N=1,NDT219
      L=NDT219-N+1
      IF((IOP219(1,L).EQ.IBLANK).AND.(IOP219(2,L).EQ.IBLANK))GO TO 316
      GO TO 315
C     ENTERED INTO WORK SPACE
  316 CALL FRSW19(W,JDT219(1,L),JDT219(2,L),UADJ19(L),
     1 IHZ,IJHMC,LJHMC,ITPX,LW,IBUG,IODBUG)
 315  CONTINUE
C
C     FILL FADJ19 COMMON FROM WORK ARRAY.
      FLV=-999.0
      IC=0
      DO 320 I=1,LW
      IF(W(I).EQ.FLV) GO TO 325
      IF(IC.EQ.1) GO TO 321
C     START OF A CORRECTION
 322  NUADJ=NUADJ+1
ckwz.r26-3.same as r25-65      IJHUA(NUADJ)=IHZ+I*IDT
      IJHUA(NUADJ)=IHZ+I*ITPX
      VUADJ(NUADJ)=W(I)
      IC=1
      GO TO 325
C     END OF A CORRECTION
ckwz 321  LJHUA(NUADJ)=IHZ+(I-1)*IDT
 321  LJHUA(NUADJ)=IHZ+(I-1)*ITPX
      IF(NUADJ.LT.21) GO TO 323
      JD=((LJHUA(NUADJ)-1)/24)+1
      JH=LJHUA(NUADJ)-((JD-1)*24)
      CALL MDYH1(JD,JH,MO,ID,IY,IH,NOUTZ,NOUTDS,TZCODE)
      WRITE(IPR,1007)OPNAME,MO,ID,IY,IH,TZCODE
1007  FORMAT(1H0,10X,'**WARNING** THE MAXIMUM OF 20 ALLOWED UADJ'
     1,' CORRECTIONS HAVE BEEN MADE FOR',/16X,
     2 'THE SNOW-17 OPERATION(',2A4,
     3 ').  LAST CORRECTION ENDS AT',I3,'/',I2,'/',I4,'-',I2,A3,'.',
     4 /16X,'EITHER SHORTEN RUN PERIOD OR CONSOLIDATE .UADJ MODS.')
      CALL WARN
      GO TO 329
 323  IF(W(I).GE.0.0) GO TO 322
      IC=0
      FLV=-999.0
      GO TO 320
C     CHECK IF LAST VALUE IN WORK ARRAY
 325  IF(I.EQ.LW) GO TO 326
      FLV=W(I)
      GO TO 320
 326  IF(IC.EQ.0) GO TO 320
ckwz      LJHUA(NUADJ)=IHZ+I*IDT
      LJHUA(NUADJ)=IHZ+I*ITPX
 320  CONTINUE
 329  IF(NUADJ.EQ.0) RETURN
C.......................................
C     IF DEBUG ON, PRINT FADJ19 COMMON BLOCK.
      IF(IBUG.EQ.0) RETURN
      WRITE(IODBUG,1013) NUADJ
 1013 FORMAT(1H0,46HFSNWTR DEBUG--CONTENTS OF FADJ19 COMMON BLOCK.,3X,
     1  6HNUADJ=,I3)
      DO 330 N=1,NUADJ
      WRITE(IODBUG,1014) IJHUA(N),LJHUA(N),VUADJ(N)
 1014 FORMAT(1H ,10X,2I10,F10.3)
  330 CONTINUE
C.......................................
      RETURN
      END
