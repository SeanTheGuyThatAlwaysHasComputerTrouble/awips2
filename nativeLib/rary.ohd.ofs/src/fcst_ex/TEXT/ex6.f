C MEMBER EX6
C  (from old member FCEX6)
C
      SUBROUTINE EX6(PO,CO,Q,QM)
C.......................................................................
C     THIS IS THE EXECUTION SUBROUTINE FOR THE
C        MEAN DISCHARGE OPERATION.
C.......................................................................
C     SUBROUTINE INITIALLY WRITTEN BY
C        LARRY BRAZIL -- HRL   SEPTEMBER 1979  VERSION 1
C.......................................................................
      DIMENSION PO(1),CO(1),Q(1),QM(1)
C     COMMON BLOCKS.
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/FCTIME/IDARUN,IHRRUN,LDARUN,LHRRUN,LDACPD,LHRCPD,
     1NOW(5),LOCAL,NOUTZ,NOUTDS,NLSTZ,IDA,IHR,LDA,LHR,IDADAT
      COMMON/FCARY/IFILLC,NCSTOR,ICDAY(20),ICHOUR(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_ex/RCS/ex6.f,v $
     . $',                                                             '
     .$Id: ex6.f,v 1.1 1995/09/17 18:57:21 dws Exp $
     . $' /
C    ===================================================================
C
C.......................................................................
C     CHECK TRACE LEVEL -- TRACE LEVEL FOR THIS SUBROUTINE=1.
      IF(ITRACE.GE.1) WRITE(IODBUG,900)
  900 FORMAT(1H0,14H** EX6 ENTERED)
C.......................................................................
C     CHECK TO SEE IF DEBUG OUTPUT IS NEEDED FOR THIS OPERATION.
      IBUG=0
      IF(IDBALL.GT.0) IBUG=1
      IF(NDEBUG.EQ.0) GO TO 100
      DO 10 I=1,NDEBUG
      IF(IDEBUG(I).EQ.6) GO TO 11
   10 CONTINUE
      GO TO 100
   11 IBUG=1
C.......................................................................
C     VALUES OF CONTROL VARIABLES.
  100 IDTQ=PO(5)
      IDTQB=PO(9)
      NCOMAX=IDTQB/IDTQ
      NPO=PO(11)
      IC=1
C
C.......................................................................
C     DEBUG OUTPUT - PRINT PO() AND CO()
      IF(IBUG.EQ.0) GO TO 108
      WRITE(IODBUG,102) NPO,NCOMAX
  102 FORMAT(1H0,29HCONTENTS OF PO AND CO ARRAYS.,5X,21HNUMBER OF VALUES
     1--PO=,I3,2X,3HCO=,I2)
      WRITE(IODBUG,104) (PO(I),I=1,NPO)
  104 FORMAT(1H0,15F8.3)
      WRITE(IODBUG,104) (CO(I),I=1,NCOMAX)
  108 CONTINUE
C     INITIALIZE DAY AND HOUR
      KDA=IDA
      IHRM=((IHR-1)/IDTQB)*IDTQB+IDTQB
      LHRM=((LHR-1)/IDTQB)*IDTQB+IDTQB
      KHR=IHRM
      JHR=IHR
C.......................................................................
C     BEGIN COMPUTATIONAL TIME INTERVAL LOOP
C.......................................................................
C     COMPUTE BEGINNING LOCATION IN Q() FOR CURRENT PERIOD
  110 CONTINUE
      SUM=0.0
      IQT=(KDA-IDADAT)*24/IDTQ+JHR/IDTQ
C     COMPUTE END LOCATION IN Q() FOR CURRENT PERIOD
      LQT=(KDA-IDADAT)*24/IDTQ+KHR/IDTQ
      IF(KDA.NE.IDA.OR.KHR.NE.IHRM) GO TO 200
C
C     COMPUTE NUMBER OF INITIAL CARRYOVER VALUES
      NCOIN=IDTQB/IDTQ+IQT-LQT
C
C     COMPUTE SUMS FOR INITIAL PERIOD
      IF(NCOIN.EQ.1) GO TO 130
      L=NCOIN-1
      DO 120 I=1,L
      SUM=SUM+(CO(I)+CO(I+1))/2
      IMISS=IFMSNG(CO(I))
      IMISS1=IFMSNG(CO(I+1))
      IF(IMISS.EQ.1.OR.IMISS1.EQ.1) SUM=-999.
      IF(IMISS.EQ.1.OR.IMISS1.EQ.1) GO TO 140
  120 CONTINUE
  130 CONTINUE
      SUM=SUM+(CO(NCOIN)+Q(IQT))/2
  140 IF(IDTQB.EQ.IDTQ) GO TO 244
  200 N=LQT-1
C
C     GENERAL SUM COMPUTATION FOR INTERMEDIATE PERIODS AND
C        REMAINDER OF FIRST PERIOD
      DO 240 I=IQT,N
      SUM=SUM+(Q(I)+Q(I+1))/2
      IMISS=IFMSNG(Q(I))
      IMISS1=IFMSNG(Q(I+1))
      IF(IMISS.EQ.1.OR.IMISS1.EQ.1) SUM=-999.
      IF(IMISS.EQ.1.OR.IMISS1.EQ.1) GO TO 244
  240 CONTINUE
 244  IF(KDA.EQ.LDA.AND.KHR.EQ.LHR) GO TO 256
      KHR1=KHR
      GO TO 258
  256 KHR1=LHRM
  258 LQMT=(KDA-IDADAT)*24/IDTQB+KHR1/IDTQB
      QM(LQMT)=SUM/NCOMAX*IDTQB/24
      IMISS=IFMSNG(SUM)
      IF(IMISS.EQ.1) QM(LQMT)=-999.
C
C     CHECK TO SEE IF CARRYOVER IS TO BE SAVED
      IF(IFILLC.EQ.0) GO TO 400
      IF(NCSTOR.EQ.0) GO TO 400
  275 IF(IC.GT.NCSTOR) GO TO 400
      IF(KDA.NE.ICDAY(IC)) GO TO 400
      MHR=KHR-IDTQB
      IF(KDA.EQ.LDA.AND.KHR.EQ.LHR) MHR=LHRM-IDTQB
      IF(ICHOUR(IC).LE.MHR.OR.ICHOUR(IC).GT.KHR) GO TO 400
C
C     SAVE CARRYOVER
      LCOT=(KDA-IDADAT)*24/IDTQ+ICHOUR(IC)/IDTQ
C     CHECK TO SEE IF FIRST PERIOD IS LAST PERIOD
      IF(IDA.NE.LDA.OR.IHRM.NE.LHRM) GO TO 281
      IF(ICHOUR(IC).LT.IHR.OR.ICHOUR(IC).GT.LHR) GO TO 330
C     GO TO 283
C     CHECK TO SEE IF THIS IS FIRST PERIOD
  281 IF(KDA.NE.IDA.OR.KHR.NE.IHRM) GO TO 282
      IF(ICHOUR(IC).LT.IHR) GO TO 330
      GO TO 283
  282 IF(KDA.NE.LDA.OR.KHR.NE.LHR) GO TO 285
      IF(ICHOUR(IC).GT.LHR) GO TO 330
      GO TO 285
  283 CONTINUE
C
C
C     SAVE CARRYOVER FOR FIRST PERIOD
      IF((ICHOUR(IC)/IDTQB)*IDTQB.EQ.ICHOUR(IC)) GO TO 286
      N=NCOIN+1
      GO TO 291
C
C     SAVE INTERMEDIATE PERIOD CARRYOVER
  285 IF((ICHOUR(IC)/IDTQB)*IDTQB.NE.ICHOUR(IC)) GO TO 290
  286 CO(1)=Q(LQT)
      N=2
      GO TO 310
  290 N=1
  291 CONTINUE
      DO 300 I=IQT,LCOT
      CO(N)=Q(I)
      N=N+1
  300 CONTINUE
  310 CONTINUE
      IF(N.GT.NCOMAX) GO TO 321
      DO 320 I=N,NCOMAX
      CO(I)=-999.
  320 CONTINUE
  321 CONTINUE
      CALL FCWTCO (KDA,ICHOUR(IC),CO,NCOMAX)
      IF(IBUG.EQ.0) GO TO 330
      WRITE(IODBUG,950) ICDAY(IC),ICHOUR(IC)
  950 FORMAT(1H0,24HCARRYOVER VALUES FOR DAY,I5,5H HOUR,I3)
      WRITE(IODBUG,942) (CO(I),I=1,NCOMAX)
  330 CONTINUE
      IC=IC+1
      GO TO 275
  400 CONTINUE
C     CHECK FOR LAST DAY AND HOUR
      IF(KDA.EQ.LDA.AND.(KHR.EQ.LHR.OR.KHR.EQ.LHRM)) GO TO 500
C     INCREMENT TIME STEP
      KHR=KHR+IDTQB
      JHR=KHR-IDTQB
      IF(KHR.LE.24) GO TO 450
      KHR=IDTQB
      JHR=KHR-IDTQB
      KDA=KDA+1
  450 IF(KDA.NE.LDA.OR.KHR.NE.LHRM) GO TO 110
      KHR=LHR
      GO TO 110
C.......................................................................
C     END OF COMPUTATIONAL LOOP
C.......................................................................
C     SAVE FINAL PERIOD CARRYOVER
  500 CONTINUE
      IF(LHR.NE.LHRM) GO TO 600
      IF(IFILLC.EQ.0) GO TO 700
      CO(1)=Q(LQT)
      IF(NCOMAX.EQ.1) GO TO 700
      DO 570 I=2,NCOMAX
      CO(I)=-999.
  570 CONTINUE
      GO TO 700
  600 IF(IFILLC.EQ.0) GO TO 700
C     CHECK TO SEE IF FIRST PERIOD IS LAST PERIOD
      IF(IDA.NE.LDA.OR.IHRM.NE.LHRM) GO TO 650
      N=NCOIN+1
      GO TO 651
  650 CONTINUE
      N=1
  651 CONTINUE
      DO 660 I=IQT,LQT
      CO(N)=Q(I)
      N=N+1
  660 CONTINUE
      IF(N.GT.NCOMAX) GO TO 700
      DO 680 I=N,NCOMAX
      CO(I)=-999.
  680 CONTINUE
  700 CONTINUE
C.......................................................................
C     DEBUG OUTPUT - PRINT Q(), QM(), AND CO()
      IF(IBUG.EQ.0) GO TO 800
      ITIQ=(IDA-IDADAT)*24/IDTQ+IHR/IDTQ
      LTIQ=(LDA-IDADAT)*24/IDTQ+LHR/IDTQ
      ITIQM=(IDA-IDADAT)*24/IDTQB+IHRM/IDTQB
      LTIQM=(LDA-IDADAT)*24/IDTQB+LHRM/IDTQB
      WRITE(IODBUG,940)
  940 FORMAT(1H0,16HDISCHARGE VALUES)
      WRITE(IODBUG,942) (Q(I),I=ITIQ,LTIQ)
  942 FORMAT(1H0,15F8.3)
      WRITE(IODBUG,944)
  944 FORMAT(1H0,21HMEAN DISCHARGE VALUES)
      WRITE(IODBUG,942) (QM(I),I=ITIQM,LTIQM)
      WRITE(IODBUG,946)
  946 FORMAT(1H0,16HCARRYOVER VALUES)
      WRITE(IODBUG,942) (CO(I),I=1,NCOMAX)
  800 CONTINUE
      RETURN
      END
