C MEMBER CHEKTS
C  (from old member FCCHEKTS)
C
      SUBROUTINE CHEKTS(TSID,TYPE,IDT,ICKDIM,DIM,MSGVAL,NPDT,IERR)
C.......................................
C     THIS SUBROUTINE DOES THE FOLLOWING.
C        1. CHECKS TO SEE IF A TIME SERIES HAS BEEN DEFINED,
C        2. CHECKS TO SEE THAT THE TIME SERIES HAS THE PROPER DIMENSION,
C        3. FOR TIME SERIES THAT CAN NOT CONTAIN MISSING VALUES, CHECKS
C             WHETHER THE DATA TYPE ALLOWS MISSING VALUES, AND
C        4. CHECKS TO SEE THAT THE TIME SERIES HAS THE CORRECT NUMBER
C             OF VALUES PER TIME INTERVAL
C.......................................
C     SUBROUTINE WRITTEN BY. . .
C        ERIC ANDERSON - HRL   OCT. 1979
C.......................................
      DIMENSION TSID(2)
C
C     COMMON BLOCKS
      COMMON/IONUM/IN,IPR,IPU
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_util/RCS/chekts.f,v $
     . $',                                                             '
     .$Id: chekts.f,v 1.3 2002/02/11 13:50:41 michaelo Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENT
      DATA BLANK/4H    /
C.......................................
C     TRACE LEVEL FOR THIS SUBROUTINE=3.
      IF(ITRACE.GE.3) WRITE(IODBUG,900)
  900 FORMAT(1H0,17H** CHEKTS ENTERED)
      IERR=0
C.......................................
C     CHECK TO SEE IF TIME SERIES EXISTS.
      CALL FINDTS(TSID,TYPE,IDT,I,ICK,ACTDIM)
      IF(ICK.GT.0) GO TO 100
      IERR=1
      WRITE(IPR,901) TSID,TYPE,IDT
  901 FORMAT(1H0,10X,43H**ERROR** TIME SERIES HAS NOT BEEN DEFINED.,
     13X,5HI.D.=,2A4,3X,5HTYPE=,A4,3X,3HDT=,I2,1X,5HHOURS)
      CALL ERROR
      IF (ICKDIM.EQ.0) DIM=BLANK
      RETURN
C.......................................
C     CHECK FOR THE PROPER DIMENSION.
  100 IF(ICKDIM.EQ.0) GO TO 105
      IF(ACTDIM.EQ.DIM) GO TO 110
      IERR=1
      WRITE(IPR,902) DIM,TSID,TYPE,IDT,ACTDIM
  902 FORMAT(1H0,10X,101H**ERROR** A TIME SERIES DOES NOT HAVE THE PROPE
     1R DIMENSION FOR THIS APPLICATION--DIMENSION SHOULD BE=,A4,/16X,
     25HI.D.=,2A4,3X,5HTYPE=,A4,3X,3HDT=,I2,1X,5HHOURS,5X,4HDIM=,A4)
      CALL ERROR
      GO TO 110
  105 DIM=ACTDIM
C.......................................
C     CHECK MISSING VALUE INDICATOR AND NUMBER OF VALUES PER
C        TIME INTERVAL.
  110 CALL FDCODE(TYPE,UNITS,ACTDIM,MSG,NVDT,TSCALE,N,I)
      IF(MSGVAL.EQ.1) GO TO 115
      IF(MSG.EQ.0) GO TO 115
      IERR=1
      WRITE(IPR,903) TYPE,TSID,TYPE,IDT
  903 FORMAT(1H0,10X,23H**ERROR** A DATA TYPE (,A4,86H) WHICH CAN CONTAI
     1N MISSING VALUES IS USED FOR AN APPLICATION WHERE MISSING VALUES A
     2RE,/16X,12HNOT ALLOWED.,10X,5HI.D.=,2A4,3X,5HTYPE=,A4,3X,3HDT=,I2,
     31X,5HHOURS)
      CALL ERROR
C
  115 IF(NPDT.EQ.0) GO TO 120
      IF(NPDT.EQ.NVDT) RETURN
      IERR=1
      WRITE(IPR,904) NVDT,NPDT,TSID,TYPE,IDT
  904 FORMAT(1H0,10X,26H**ERROR** A DATA TYPE WITH,1X,I2,1X,
     162HVALUES PER TIME INTERVAL IS USED FOR AN APPLICATION WHERE ONLY,
     21X,I2,1X,19HVALUES ARE ALLOWED.,/16X,17HTIME SERIES I.D.=,2A4,3X,
     35HTYPE=,A4,3X,3HDT=,I2,1X,5HHOURS)
      CALL ERROR
      RETURN
C
  120 NPDT=NVDT
C.......................................
      RETURN
      END
