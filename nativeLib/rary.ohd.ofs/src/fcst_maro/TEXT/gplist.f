C MEMBER GPLIST
C  (from old member PPGPLIST)
C
      SUBROUTINE GPLIST(PG24, APIG)
C
C.....THIS SUBROUTINE IS CALLED TO PRODUCE STATION PRECIPITATION LISTS.
C.....TWO LISTS ARE PRODUCED:
C
C..... (1) A LIST SORTED IN ALPHABETICAL ORDER BY STATE. WITHIN EACH
C.....     STATE THE ALPHABETICAL SORT IS EITHER BY NWS ID OR NAME.
C..... (2) ANOTHER LIST SORTED IN DESCENDING NUMERICAL ORDER BY
C.....     PRECIPITATION AMOUNT.
C
C.....LIST (1) CAN OPTIONALLY INCLUDE PERCENTAGE DISTRIBUTIONS...IF THEY
C.....ARE AVAILABLE.
C
C.....NOTE:  THE PRECIPITATION DARINTED IS THE PG24 ARRAY. THEREFORE
C.....THE DATA HAS HAD SOME PROCESSING DONE ON IT (FOR EXAMPLE...MORE
C.....THAN ONE REPORT FOR THE SAME GRID POINT IN THE PP24 ARRAY IS
C.....AVERAGED), THE DATA IS NOT COMPLETELY UNTOUCHED.
C
C.....THE DATA MAY OR MAY NOT INCLUDE ESTIMATES...AS THE USER DESIRES.
C
C
C.....ORIGINALLY WRITTEN BY:
C
C...JERRY M. NUNN       WGRFC FT. WORTH, TEXAS       DECEMBER 1986
C
      INCLUDE 'gcommon/explicit'
C
      INTEGER*2 APIG(1), PG24(1)
C
      DIMENSION SNAME(2), NBLNK(2)
C
      INCLUDE 'common/ionum'
      INCLUDE 'gcommon/gsize'
      INCLUDE 'gcommon/gopt'
      INCLUDE 'common/pudbug'
      INCLUDE 'common/errdat'
      INCLUDE 'common/where'
      INCLUDE 'gcommon/gdate'
      INCLUDE 'gcommon/gdispl'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_maro/RCS/gplist.f,v $
     . $',                                                             '
     .$Id: gplist.f,v 1.1 1995/09/17 19:02:14 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA NBLNK, OG24, SNAME /2*4h    , 4hOG24, 4hGPLI, 4hST  /
C
  901 FORMAT(1H0, '*** ERROR ***  UNSUCCESSFUL RETURN FROM RPPREC FOR PA
     *RAMETER TYPE ', A4, '. STATUS CODE = ', I4, '.', /, 1X, 'PROCESSIN
     *G OF THIS ROUTINE HALTED.')
  902 FORMAT(1H0, '*** ENTER SUBROUTINE GPLIST ***')
  903 FORMAT(1X, '*** EXIT SUBROUTINE GPLIST ***')
  904 FORMAT(1X, '*** IPTRCE = ', I4, ' ***')
  905 FORMAT(1H0, '*** ERROR ***  IN PRECIPITATION LIST PRINT ROUTINE...
     *WORKSPACE REQUIREMENT OF ', I5, ' SLOTS', /, 5X, 'EXCEEDS THE ',
     * I5, ' SLOTS AVAILABLE. PRECIPITATION LISTS CANNOT BE PRINTED.',
     * /, 5X, 'HOWEVER...MARO PROCESSING CONTINUES.')
C
      INCLUDE 'gcommon/setwhere'
      IF(IPTRCE .GE. 1) WRITE(IOPDBG,902)
      IF(IPTRCE .GE. 1) WRITE(IOPDBG,904) IPTRCE
      NP = 1
C
C.....READ THE OG24 PARAMETER ARRAY...WHICH CONTAINS POINTERS AND GRID
C.....POINT ADDRESSES FOR EACH STATION ON THE NAMED NETWORK. THIS IS A
C.....SINGLE RECORD PARAMETER ARRAY.
C
      IPTR = 0
      LENGTH = IPMAX + 5
      CALL RPPREC(NBLNK, OG24, IPTR, LENGTH, APIG(NP), NUMWRD, IPTRNX,
     * ISTATC)
C
      IF(ISTATC .EQ. 0) GOTO 100
      WRITE(IPR,901) OG24, ISTATC
      GOTO 999
C
  100 IBUG = IPBUG(OG24)
      CALL GOG24D(IBUG, APIG(NP), APIG(NP+10), NSTA, ISORT)
C
C.....CHECK IF THERE IS SUFFICIENT WORKSPACE TO MAKE A SORTED
C.....PRECIPITATION LIST. IF NOT...PRINT AN ERROR MESSAGE.
C
      ITOP = NSTA*4
      IF(ITOP .GT. NGRID) GOTO 175
      GOTO 150
C
  175 WRITE(IPR,905) ITOP, NGRID
      CALL ERROR
      GOTO 999
C
C.....BEFORE GOING ANY FURTHER...COMPUTE AND STORE POINTERS FOR
C.....BEGINNING ADDRESSES IN APIG ARRAY TO STORE THE FOLLOWING PIECES
C.....OF DATA.
C
C.....IPAIR  - BEGINNING ADDRESS OF UNSORTED ARRAY OF GRID POINT
C.....         ADDRESS/RECORD NUMBER PAIRS.
C.....IWORK  - BEGINNING ADDRESS OF WORK AREA. UNSORTED PAIRS ARE
C.....         PLACED HERE PRIOR TO BEING SORTED OUT IN SEPARATE ARRAYS.
C.....IREC   - BEGINNING ADDRESS OF RECORD NUMBERS.
C.....IGRID  - BEGINNING ADDRESS OF GRID POINT ADDRESSES.
C.....IPCPN  - BEGINNING ADDRESS OF PRECIPITATION.
C.....ISPTR  - BEGINNING ADDRESS OF SORT POINTERS.
C
  150 IPAIR = NP + 10
      IWORK = NSTA*2 + 1
      IREC = NP
      IGRID  = IREC + NSTA
      IPCPN = IGRID  + NSTA
      ISPTR = IPCPN + NSTA
C
C.....PUT ALL THE GRID POINTS...AND ALL THE RECORD NUMBERS...IN
C.....CONTIGUOUS LOCATIONS.
C
      CALL GPUT(APIG(IPAIR), APIG(IWORK), APIG(IREC), APIG(IGRID), NSTA)
C
C.....NOW PULL OUT PRECIPITATION IN THE SAME ORDER AS GRID POINT ADDRESS
C.....ARE IN THE ARRAY.
C
      DO 200 NP = 1, NSTA
      NPGRID = APIG(IGRID + NP - 1)
      APIG(IPCPN + NP - 1) = PG24(NPGRID)
  200 CONTINUE
C
C.....WE NOW HAVE THE FOLLOWING INFORMATION IN THE APIG DATA ARRAY:
C
C.....APIG(NP)    - APIG(IGRID-1) - RECORD NUMBERS FOR GENL P.A.
C.....APIG(IGRID) - APIG(IPCPN-1) - GRID POINT ADDRESSES.
C.....APIG(IPCPN) - APIG(ISPTR-1) PRECIPITATION AMOUNTS.
C
C.....BEGINNING IN LOCATION APIG(ISPTR), WE WILL NOW BUILD A LIST OF
C.....SORT POINTERS THAT POINT TO PRECIPITATION DATA BEGINNING IN
C.....LOCATION APIG(IPCPN). THESE POINTERS WILL BE CONSTRUCTED SUCH THAT
C.....THEY DEFINE A LIST IN DESCENDING NUMERICAL ORDER.
C
      CALL GSPFIL(NSTA, APIG(IPCPN), APIG(ISPTR))
C
C.....NOW CALL THE ROUTINE TO PRINT OUT THE LIST.
C
      CALL GLSPRT(NSTA, APIG(IREC), APIG(IGRID), APIG(IPCPN),
     * APIG(ISPTR))
C
C.....WHEN PRINTOUT IS COMPLETE...END.
C
  999 IF(IPTRCE .GE. 1) WRITE(IOPDBG,903)
      RETURN
      END
