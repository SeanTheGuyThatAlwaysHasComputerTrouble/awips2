C MEMBER GDLYS
C  (from old member PPGDLYS)
C
      SUBROUTINE GDLYS(PP24, GP24, PT24, PG24, PPSHR, NUMST)
C
C.....THIS SUBROUTINE IS CALLED TO MOVE THE PRECIPITATION DATA FROM ITS
C.....ORIGINAL TEMPORARY STORAGE LOCATION TO ITS PERMANENT LOCATIONS
C.....PRIOR TO PROCESSING OF THE DATA.
C
C.....ALSO, DATA MAY BE PLACED INTO A DIFFERENT FORM BEFORE BEING PLACED
C.....ON THE PERMANENT FILES.
C
C.....THE SUBROUTINE HAS THIS ARGUMENT LIST:
C
C.....PP24   - THE PP24 DATA ARRAY.
C.....GP24   - THE GP24 PARAMETRIC ARRAY.
C.....PG24   - THE PG24 DATA ARRAY.
C.....PT24   - THE PP24 POINTERS ARRAY.
C.....PPSHR  - THE ARRAY OF THE LOCATIONS IN THE PP24 DATA ARRAY OF THE
C.....         NAMED NETWORK STATIONS THAT SHARE THE SAME GRID POINT
C.....         ADDRESS.
C.....NUMST  - THE NUMBER OF PP24 VALUES RETURNED FROM THE PPDB.
C
C.....JERRY M. NUNN, WGRFC FT. WORTH       MAY 1986
C
C.....SUBROUTINE PARTIALLY REWRITTEN   MAY 1987   JERRY M. NUNN
C.....REWRITE REQUIRED TO CORRECT THE PROCESSING THAT IS DONE
C.....WHEN MORE THAN ONE NETWORK STATION SHARE THE SAME GRID POINT.
C
      INCLUDE 'gcommon/explicit'
      INTEGER*2 PP24(1), GP24(1), PPSHR(1), PT24(1), NRAIN, IRAIN
      INTEGER*2 PG24(1)
C
      DIMENSION SNAME(2), BLNKID(2), PCPAR(90), PCID(2)
C
      INCLUDE 'common/where'
      INCLUDE 'common/ionum'
      INCLUDE 'common/pudbug'
      INCLUDE 'gcommon/gopt'
      INCLUDE 'gcommon/gsize'
      INCLUDE 'common/errdat'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_maro/RCS/gdlys.f,v $
     . $',                                                             '
     .$Id: gdlys.f,v 1.1 1995/09/17 19:01:27 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SNAME /4hGDLY, 4hS   /
      DATA GDPP, P24TYP /4hGDPP, 4hPP24/
      DATA BLNKID, PCPN /2*4h    , 4hPCPN/
C
  900 FORMAT(1H0, '*** GDLYS ENTERED ***')
  901 FORMAT(1H0, '*** EXIT GDLYS ***')
  902 FORMAT(1H0, '*** PRECIPITATION DATA IS BEING TRANSFERRED FROM THE
     *PP24 ARRAY TO THE PG24 ARRAY ***')
  903 FORMAT(1H0, 'PP24(', I5, ') = ', I6, '. ASSOCIATED GRID ADDRESS IN
     * GP24 ARRAY = ', I5, '.')
  904 FORMAT(5X, 'SHARED GRID ARRAY POINTER', 7X, '= ', I5)
  905 FORMAT(5X, 'LOCATION OF PP24 DATA POINTER   = ', I5, /, 5X, 'NUMBE
     *R OF SHARED GRID POINTS    = ', I5)
  906 FORMAT(5X, 'PG24(', I5, ') = ', I5)
  907 FORMAT(2X, '*** PRECIPITATION AVERAGING WILL BE PERFORMED ***')
  908 FORMAT(5X, 'SHARED GRID ARRAY LOCATION   ', I5, ' CONTAINS PP24 DA
     *TA POINTER ', I6, ' . . . ENCODED PRECIPITATION VALUE = ', I6)
  910 FORMAT(5X, 'FOR SHARED LOCATION ', I5, ' OF GRID ADDRESS ', I5,
     * ' ... PP24(', I5, ') = ', I5)
  911 FORMAT(1H0, '*** WARNING ***', /, 6X, '24 HR PRECIPITATION ON PPDB
     * FOR STATION ', 2A4, ' - ', 5A4, ' (STATION NUMBER ', I4, ') IS ',
     * F6.2, ' IN.', /, 6X, 'THIS AMOUNT EXCEEDS THE MAXIMUM THRESHOLD V
     *ALUE OF ', F6.2, ' IN. THIS AMOUNT IN THE PPDB WILL NOT BE PROCESS
     *ED.', /, 6X, 'IF YOU WANT TO USE THIS AMOUNT...RE-SET THE MAXIMUM
     *THRESHOLD VALUE AND RE-RUN THE MARO FUNCTION.')
  912 FORMAT(1H0, '*** WARNING ***', /, 6X, 'UNABLE TO SUCCESSFULLY READ
     * THE PCPN PARAMETRIC ARRAY.', /, 6X, 'RECORD NO. = ', I4, 3X, 'FIL
     *E TYPE = ', A4, 3X, 'STATUS CODE = ', I4, /, 6X, 'PCPN PARAMETERS
     *CANNOT BE PROCESSED. WILL CONTINUE WITH MARO FUNCTION, HOWEVER.')
  913 FORMAT(6X, 'PARAMETRIC ARRAY NEEDS ', I4, 'WORDS. ONLY ', I4,
     * ' WORDS HAVE BEEN RESERVED...HOWEVER')
  914 FORMAT(1H0, '*** WARNING ***', /, 5X, 'STATION ', 2A4, ' - ', 5A4,
     * '(STATION NUMBER ', I5, ' AND GRID POINT ADDRESS ', I5, ')', /,
     * 5X, 'HAS ONLY A PARTIAL DAY TOTAL. PARTIAL DAY TOTAL FLAG IS ',
     * I4, '. PRECIPITATION AMOUNT OF ', F6.2, ' WILL NOT BE USED.', /,
     * 5X, 'IF YOU WANT TO USE THE PARTIAL DAY TOTAL...RE-ENTER IT AS A
     *FULL DAY TOTAL IN A SHEF MESSAGE OR TURN THE PP24TIME TECHNIQUE OF
     *F.')
C
C.....SET WHERE.
C
      OPNAME(1) = SNAME(1)
      OPNAME(2) = SNAME(2)
      IOPNUM    = -1
C
      PMAX = MAXPCP + 0.5
      PMAX = PMAX/100.
C
      LBUG = IPBUG(GDPP)
      MBUG = IPBUG(P24TYP)
C
C.....CHECK TRACE LEVEL, AND SPIT OUT OBLIGATORY MESSAGE IF DEEMED NECES
C.....TO BE NECESSARY.
C
      IF(IPTRCE .GE. 3) WRITE(IOPDBG,900)
      IF(LBUG .EQ. 1) WRITE(IOPDBG,902)
C
C.....SET CONSTANT FOR SIZE OF PCPN PARAMETRIC ARRAY.
C
      LPCPAR = 90
C
C.....FIRST MOVE THE 24 HOURLY VALUES FROM WORKING ARRAYS TO PERMANENT
C.....ARRAYS. SET ALL GRID POINT VALUES EQUAL TO MISSING.
C
      DO 100 NP = 1, NGRID
      PG24(NP) = MSGGRD
  100 CONTINUE
C
      IF(MBUG .EQ. 1) CALL GPDDMP(P24TYP, NUMST, PP24)
C
C.....FOR EACH DAILY PRECIPITATION VALUE THAT IS NOT MISSING...USE THE
C.....GRID POINT VALUES -- GP24 ARRAY -- TO GETT THE LOCATION OF THE
C.....DATA ARRAY TO STORE THE PRECIPITATION REPORT INTO.
C
      DO 600 NP = 1, NUMST
      IF(PP24(NP) .EQ. MSNG24) GOTO 600
C
C.....BREAK OUT THE PARTIAL DAY TOTAL FLAG FROM THE ENCODED
C.....PRECIPITATION VALUE.
C
      KPART = IABS(PP24(NP) - ((PP24(NP)/10)*10))
C
      IRAIN = ((PP24(NP)/10) + 3000)
      IF(IRAIN .LE. MAXPCP) GOTO 200
C
      P = IRAIN + 0.5
      P = P/100.
C
C.....PRECIPITATION AMOUNT EXCEEDS MAXIMUM THRESHOLD VALUE. GET THE
C.....STATION ID, NAME, AND NUMBER, AND WRITE WARNING MESSAGE.
C
C.....PP24 POINTER ARRAY ODD-NUMBERED SLOTS CONTAIN POINTERS TO THE
C.....PCPN PARAMETRIC ARRAY RECORD FOR THE STAITION.
C
      IPTR = PT24(2*(NP-1) + 1)
      PCID(1) = BLNKID(1)
      PCID(2) = BLNKID(2)
      CALL RPPREC(PCID, PCPN, IPTR, LPCPAR, PCPAR(1), NPCPAR, IPTRNX,
     * ISTATC)
      IF(ISTATC .GT. 0) GOTO 150
C
      ISTA = PCPAR(4)
C
      WRITE(IPR,911) (PCPAR(K), K = 2, 3), (PCPAR(K), K = 5, 9),
     * ISTA, P, PMAX
      CALL WARN
      GOTO 600
C
  150 WRITE(IPR,912) IPTR, PCPN, ISTATC
      IF(ISTATC .EQ. 3) WRITE(IPR,913) NPCPAR, LPCPAR
      CALL WARN
      GOTO 600
C
C.....GET THE GRID POINT ADDRESS.
C
  200 KP = GP24(2*NP-1)
C
C.....TEST THE PARTIAL DAY TOTAL FLAG. IF THIS IS A PARTIAL DAY TOTAL
C.....AND PP24TIME IS ENABLED (PP24TIME = 1)...DO NOT USE.
C
      IF(KPART  .EQ. 0) GOTO 250
      IF(IPTCHK .EQ. 0) GOTO 250
C
C.....PP24TIME IS ENABLED. WRITE OUT A WARNING MESSAGE...AND DO NOT
C.....USE THE PARTIAL DAY PRECIPITATION VALUE.
C
      IPTR = PT24(2*(NP-1) + 1)
      PCID(1) = BLNKID(1)
      PCID(2) = BLNKID(2)
      CALL RPPREC(PCID, PCPN, IPTR, LPCPAR, PCPAR(1), NPCPAR, IPTRNX,
     * ISTATC)
      IF(ISTATC .GT. 0) GOTO 150
C
      ISTA = PCPAR(4)
      P = IRAIN + 0.5
      P = P/100.
      WRITE(IPR,914) (PCPAR(K), K = 2, 3), (PCPAR(K), K = 5, 9),
     * ISTA, KP, KPART, P
      CALL WARN
      GOTO 600
C
  250 IF(LBUG .EQ. 1) WRITE(IOPDBG,903) NP, PP24(NP), KP
C
C.....THERE MAY BE A ZERO GRID ADDRESS ENCOUNTERED HERE. IF IT IS,
C.....DO NOT PROCESS THE DATA.
C
      IF(KP .LE. 0) GOTO 600
C
C.....CHECK TO SEE IF MORE THAN ONE NAMED NETWORK STATION SHARE THE SAME
C.....GRID POINT ADDRESS. AVERAGE THE PRECIP IF THERE IS MORE THAN ONE
C.....PRECIP STATION FOR THE SAME GRID POINT, AND MORE THAN ONE
C.....OBSERVED VALUE EXISTS.
C
      JP = GP24(2*NP)
      IF(LBUG .EQ. 1) WRITE(IOPDBG,904) JP
C
C.....JP IS AN ENCODED POINTER TO THE PP24 DATA POINTER ARRAY FOR SHARED
C.....STATIONS (THOSE STATIONS THAT SHARE THE SAME GRID POINT ADDRESS).
C.....THE NUMBER MAY BE POSITIVE, ZERO, OR NEGATIVE.  ONLY POSITIVE
C.....ENTRIES NEED TO BE PROCESSED.  SEE NWSRFS USERS MANUAL
C.....PAGE IX.3.4.3C-GP24 FOR DETAILS.
C
      IF(JP .GT. 1) GOTO 350
C
C.....CHECK TO MAKE DOUBLY SURE THAT NO GRID POINT VALUE OF PRECIP HAS
C.....BEEN ENTERED YET. (WE MAY ALREADY HAVE PROCESSED THIS GRID POINT
C.....BY HAVING PREVIOUSLY ENCOUNTERED ANOTHER GRID POINT THAT SHARES
C.....THE ADDRESS).
C
  300 IF(PG24(KP) .NE. MSGGRD) GOTO 600
C
C.....THERE IS NO GRID POINTS THAT SHARE THIS POINT. STORE A SINGLE
C.....GRID POINT PRECIPITATION VALUE.
C
      PG24(KP) = IRAIN
      IF(LBUG .EQ. 1) WRITE(IOPDBG,906) KP, PG24(KP)
      GOTO 600
C
C.....THERE IS MORE THAN ONE NAMED NETWORK STATION WITH THE SAME GRID
C.....POINT ADDRESS. USE THE SECOND FIELD OF THIS PART OF THE PARAMETRIC
C.....ARRAY TO GET THE OTHER INFORMATION.
C
  350 LOC = JP/10
      NUM = IABS(JP - LOC*10)
      IF(LBUG .EQ. 1) WRITE(IOPDBG,905) LOC, NUM
C
C.....NUM = NUMBER OF NAMED NETWORK STATIONS THAT SHARE THE SAME GRID
C.....      POINT ADDRESS AS THIS STATION.
C.....LOC = LOCATION IN THE PP24 DATA POINTER SECTION OF THIS PARAMETRIC
C.....      ARRAY WHERE PP24 LOCATION POINTERS FOR THE OTHER STATONS THA
C.....      THAT SHARE THIS GRID POINT ADDRESS CAN BE FOUND.
C
      IF(LOC .LE. 0) GOTO 300
C
C.....COMPUTE THE AVERAGE OF ALL STATIONS THAT SHARE THE SAME
C.....GRID POINT ADDRESS
C
      NTOT = 0
      NSUM = 0
      IF(LBUG .EQ. 1) WRITE(IOPDBG,907)
C
      LP = 1
      MP = LOC + NUM - 1
      KOUNT = 0
C
      DO 500 KX = LOC, MP
      NSHLOC = PPSHR(KX)
      KOUNT = KOUNT + 1
      IF(LBUG .EQ. 1) WRITE(IOPDBG,908) KX, NSHLOC, PP24(NSHLOC)
C
      IF(PP24(NSHLOC) .EQ. MSNG24) GOTO 500
      NRAIN = (PP24(NSHLOC)/10) + 3000
      IF(NRAIN .LE. MAXPCP) GOTO 450
C
      P = NRAIN
      P = P/100.
C
C.....PP24 POINTER ARRAY ODD-NUMBERED SLOTS CONTAIN POINTERS TO THE
C.....PCPN PARAMETRIC ARRAY RECORD FOR THE STAITION.
C
      IPTR = PT24(2*(KX-1) + 1)
      PCID(1) = BLNKID(1)
      PCID(2) = BLNKID(2)
      CALL RPPREC(PCID, PCPN, IPTR, LPCPAR, PCPAR(1), NPCPAR, IPTRNX,
     * ISTATC)
      IF(ISTATC .GT. 0) GOTO 400
C
      ISTA = PCPAR(4)
C
      WRITE(IPR,911) (PCPAR(K), K = 2, 3), (PCPAR(K), K = 5, 9),
     * ISTA, P, PMAX
      CALL WARN
      GOTO 500
C
  400 WRITE(IPR,912) IPTR, PCPN, ISTATC
      IF(ISTATC .EQ. 3) WRITE(IPR,913) NPCPAR, LPCPAR
      CALL WARN
      GOTO 500
C
  450 NSUM = NSUM + NRAIN
      IF(LBUG .EQ. 1) WRITE(IOPDBG,910) KOUNT, KP, PPSHR(KX), NRAIN
      NTOT = NTOT + 1
  500 CONTINUE
C
      IF(NTOT .LE. 0) GOTO 550
      PG24(KP) = NSUM/NTOT
  550 IF(LBUG .EQ. 1) WRITE(IOPDBG,906) KP, PG24(KP)
C
  600 CONTINUE
C
      IF(IPTRCE .GE. 3) WRITE(IOPDBG,901)
C
      RETURN
      END
