C MEMBER GSPQ
C  (from old member PPGSPQ)
C
      SUBROUTINE GSPQ(WEIGHT, WTQUAD, PCARAY, PWQUAD, NSQUAD)
C
C.....THIS SUBROUTINE ACCUMULATES WEIGHTED PRECIPITATION IN A GIVEN
C.....QUADRANT. THE ACCUMULATED WEIGHTED PRECIPITATION AND SUM OF THE
C.....WEIGHTS ARE PASSED BACK TO THE CALLING ROUTINE, GPPEST.
C
C.....HERE ARE THE ARGUMENTS
C
C.....WEIGHT - THE DISTANCE WEIGHTS. (STATUTE MILES).
C.....WTQUAD - THE SUM OF THE WEIGHTS FOR THIS QUADRANT.
C.....PCARAY - PRECIPITATION ARRAY FROM THE QUADRANT.
C.....PWQUAD - THE WEIGHTED PRECIPITATION FOR THIS QUADRANT.
C.....NSQUAD - THE NUMBER OF OBSERVED PRECIPITATION REPORTS THAT WERE
C.....         PROCESSED IN THIS QUADRANT.
C
C.....ORIGINALLY WRITTEN BY
C
C.....JERRY M. NUNN       WGRFC FT. WORTH, TEXAS       SEPTEMBER 1986
C
C.....06/08/92   MODIFICATIONS MADE TO CODE TO PREVENT DIVISION BY
C.....           ZERO WHEN NO WEIGHT PREVIOUSLY DEFINED.
C.....           JERRY M. NUNN      WGRFC FT. WORTH
C
      INCLUDE 'gcommon/explicit'
      INTEGER*2 PCARAY(1), PWQUAD, NSQUAD
C
      DIMENSION WEIGHT(1)
C
      INCLUDE 'gcommon/gsize'
      INCLUDE 'gcommon/gopt'
      INCLUDE 'common/pudbug'
      INCLUDE 'gcommon/boxtrc'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_maro/RCS/gspq.f,v $
     . $',                                                             '
     .$Id: gspq.f,v 1.1 1995/09/17 19:02:41 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA QUAD /4hQUAD/
C
  900 FORMAT(1X, '*** GSPQ ENTERED ***')
  901 FORMAT(1X, '*** EXIT GSPQ ***')
  902 FORMAT(1X, 'PRECIPITATION OBSERVED = ', I5, 3X, 'DISTANCE FROM GRI
     *D POINT = ', F8.4, 3X, 'DISTANCE WEIGHT = ', F8.4)
C
      IF(IPTRCE .GE. 8) WRITE(IOPDBG,900)
C
      NQBUG = IPBUG(QUAD)
      IF(IBTCON .EQ. 0) NQBUG = 0
C
C.....INITIALIZE COUNTERS AND ACCUMULATORS.
C
      PACC   = 0.0
      WACC   = 0.0
C
C.....LOOP THRU ALL ELEMENTS IN THE PRECIPITATION ESTIMATION ARRAY.
C
      DO 500 KP = 1, NSQUAD
C
      IF(WEIGHT(KP) .GT. 0.001) GOTO 100
      W = 0.0
      P = 0.0
      GOTO 500
C
C.....COMPUTE THE DISTANCE WEIGHT. MULTIPLY BY 100 TO PRESERVE THE
C.....RELATIVE MAGNITUDES OF THE DECIMAL POINTS.
C
  100 W = 100.0/(WEIGHT(KP)**PCEXP)
C
C.....ACCUMULATE THE WEIGHTED PRECIPITATION.
C
      P = PCARAY(KP)
C
      IF(NQBUG .EQ. 1) WRITE(IOPDBG,902) PCARAY(KP), WEIGHT(KP), W
C
  150 PACC = PACC + P*W
C
C.....ACCUMULATE THE WEIGHT.
C
      WACC = WACC + W
C
  500 CONTINUE
C
C.....ALL PRECIPITATION REPORTS HAVE BEEN PROCESSED. NOW COMPUTE THE
C.....WEIGHTED PRECIPITATION IN THE QUADRANT.
C
  600 PWQUAD = PACC
      WTQUAD = WACC
C
      IF(IPTRCE .GE. 8) WRITE(IOPDBG,901)
C
      RETURN
      END
