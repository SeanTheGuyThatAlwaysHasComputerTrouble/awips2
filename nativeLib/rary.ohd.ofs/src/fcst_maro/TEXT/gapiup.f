C MEMBER GAPIUP
C  (from old member PPGAPIUP)
C
      SUBROUTINE GAPIUP(APIG, PG24, ISTATC)
C
C.....THIS IS THE SUBROUTINE TO UPDATE THE API...AND WRITE THE UPDATED
C.....API ONTO THE PREPROCESSOR DATA BASE.
C
C.....ARGUMENT LIST:
C
C.....APIG   - GRID POINT API ARRAY.
C.....PG24   - GRID POINT PRECIPITATION ARRAY.
C
C.....ISTATC - A STATUS CODE RETURNED FROM THE PREPROCESSOR DATA BASE
C.....         TEST ROUTINES.
C.....              = 0   NORMAL RETURN.
C.....          NOT = 0   ABNORMAL RETURN. THERE IS A PROBLEM WITH THE
C.....                    PREPROCESSOR DATABASE.
C
C.....ORIGINALLY WRITTEN BY:
C
C.....JERRY M. NUNN       WGRFC, FT. WORTH, TEXAS       DECEMBER 1986
C
      INCLUDE 'gcommon/explicit'
C
C
      INTEGER*2 IAPI, IRAIN, APIG(1), PG24(1)
C
      DIMENSION SNAME(2)
C
      INCLUDE 'gcommon/gsize'
      INCLUDE 'gcommon/gdate'
      INCLUDE 'gcommon/gopt'
      INCLUDE 'common/where'
      INCLUDE 'common/fctime'
      INCLUDE 'common/pudbug'
      INCLUDE 'common/errdat'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_maro/RCS/gapiup.f,v $
     . $',                                                             '
     .$Id: gapiup.f,v 1.1 1995/09/17 19:01:09 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA APGI, SNAME /4hAPIG, 4hGAPI, 4hUP  /
C
  900 FORMAT(1H0, '*** ENTER SUBROUTINE GAPIUP ***')
  901 FORMAT(1H0, '*** EXIT SUBROUTINE GAPIUP -- STATUS CODE = ', I4,
     * ' ***')
  902 FORMAT(5X, 'CALLING SUBROUTINE WPDDLY. JULIAN DAY = ', I6)
  903 FORMAT(1H0, '*** WARNING ***  API RECESSION CONSTANT OF ', F8.2,
     * ' IS LESS THAN MINIMUM VALUE OF ', F8.2, /, 5X, 'OR GREATER THAN
     *MAXIMUM VALUE OF ', F8.2, '. RECESSION CONSTANT WILL BE SET TO DEF
     *AULT VALUE OF ', F8.2, '.')
      INCLUDE 'gcommon/setwhere'
C
      ISTATC = 0
C
      IF(IPTRCE .GT. 0) WRITE(IOPDBG,900)
C
C.....MAKE A TEST ON THE APIG DATA BASE BEFORE WRITING.
C
      CALL GWPTST(APGI, MAXGRD, ISTATC)
      IF(ISTATC .GT. 0) GOTO 999
C
C.....TEST THE RECESSION CONSTANT REC. IT SHOULD HAVE BEEN PROPERLY SET
C.....ALREADY...BUT SHOULD BE SET IF IT IS NOT NOW A PERMISSIBLE VALUE.
C.....THE RECESSION CONSTANT WILL DETERMINE THE MAGNITUDE OF TOMORROW'S
C.....API VALUES...AND SHOULD BE TESTED NOW.
C
      IF((REC .GE. RECMIN) .AND. (REC .LE. RECMAX)) GOTO 50
C
C.....WRITE A WARNING MESSAGE...THEN SET THE RECESSION VALUE.
C
      WRITE(IOERR,903) REC, RECMIN, RECMAX, RECDEF
      REC = RECDEF
      CALL WARN
C
C.....UPDATE THE API FOR TODAY USING THE FOLLOWING FORMULA:
C.....API (FOR TODAY) = API (FOR YESTERDAY) + RAIN (FOR YESTERDAY),
C.....QUANTITY MULTIPLIED BY THE RECESSION CONSTANT. CONVERT TO
C.....INTEGER*2 VALUE AND MULTIPLY BY 100 BEFORE STORING.
C
   50 DO 100 NPGRID = 1, NGRID
      COEFF = 1.0
      IAPI = APIG(NPGRID)
      NRAIN = PG24(NPGRID)
      IRAIN = IABS(NRAIN)
      API = IAPI
      RAIN = IRAIN
      API = API/100.
      RAIN = RAIN/100.
C
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C.....NOW COMPUTE TOMORROW'S API
C
C.....FIRST...ADD TODAY'S RAIN TO YESTERDAY'S API.
C
      TAPI2 = API + RAIN
C
C.....TEST THE SUM OF TODAY'S RAIN AND YESTERDAY'S API AGAINST THE
C.....MINIMUM VALUE.
C
      IF(TAPI2 .GE. APIMNR) GOTO 60
C
C.....THE SUM IS LESS THAN THE MINIMUM.  TO GET TODAY'S API...WE MUST
C.....SUBTRACT THE API DECREMENTATION CONSTANT FROM THE SUM.
C
      TAPI = TAPI2 - APIDEC
      GOTO 70
C
C.....THE SUM IS EQUAL TO OR ABOVE THE MINIMUM VALUE. TO GET TODAY'S
C.....API...MULTIPLY THE SUM BY THE RECESSION CONSTANT.
C
   60 TAPI = TAPI2*REC
C
C.....COMPUTE A COEFFICIENT...WHICH WILL BE EQUAL TO 1.0 IF THE TOTAL
C.....API (TAPI) IS POSITIVE...OR -1.0 IF THE TOTAL API IS NEGATIVE.
C
   70 COEFF = 1.0
      IF(TAPI .LT. 0.0) COEFF = -1.0
C
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C.....DO NOT LET THE API GO ABOVE THE MAXIMUM PERMITTED VALUE...OR LET
C.....THE API GO BELOW THE MINIMUM PERMITTED VALUE.
C
      IF(TAPI .LT. APIMIN) TAPI = APIMIN
      IF(TAPI .GT. APIMAX) TAPI = APIMAX
C
C.....STORE THE UPDATED API IN THE APIG ARRAY.
C
      APIG(NPGRID) = TAPI*100. + COEFF*0.5
  100 CONTINUE
C
C.....NOW WRITE OUT THE API DATA ONTO THE PREPROCESSOR DATA BASE.
C.....REMEMBER THIS IS TODAY'S API.
C
      IF(IPTRCE .GE. 3) WRITE(IOPDBG,902) ICURDA
      CALL WPDDLY(APGI, ICURDA, MAXGRD, APIG, ISTATC)
      IF(ISTATC .EQ. 0) GOTO 999
C
C.....WPDDLY WRITE ERROR. CALL THE ERROR PROCESSING SUBROUTINE.
C
      CALL GWPERR(APGI, MAXGRD, ISTATC)
C
  999 IF(IPTRCE .GT. 0) WRITE(IOPDBG,901) ISTATC
C
      RETURN
      END
