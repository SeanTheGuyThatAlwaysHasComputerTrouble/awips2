C MEMBER NFGRD
C  (from old member PPGNFGRD)
C
      INTEGER FUNCTION NFGRD(KPBOX, JHGC, KVGC, NXOFF, NYOFF,
     * NEWBOX, NBOX)
C
C.....THIS IS THE FUNCTION USED TO COMPUTE A GRID ADDRESS IN WHICH
C.....TO SEARCH FOR OBSERVED PRECIPITATION AS PART OF THE MARO FUNCTION
C.....PRECIPITATION SEARCH/ESTIMATION ROUTINE.
C
C.....THE GRID ADDRESS IS GENERATED FROM THE GRID BOX NUMBER OF THE
C.....GRID POINT WHICH IS BEING ESTIMATED, ALONG WITH THE X AND THE
C.....Y OFFSETS FROM THE GRID POINT.
C
C.....ARGUMENTS ARE AS FOLLOWS.
C
C.....KPBOX  - THE GRID BOX NUMBER OF THE GRID POINT CURRENTLY
C.....         BEING PROCESSED.
C.....JHGC   - THE HORIZONTAL GRID COORDINATE OF THE GRID POINT
C.....         CURRENTLY BEING PROCESSED.
C.....KVGC   - THE VERTICAL GRID COORDINATE OF THE GRID POINT
C.....         CURRENTLY BEING PROCESSED.
C.....NXOFF  - THE X OFFSET FOR THE GRID ADDRESS TO COMPUTE.
C.....NYOFF  - THE Y OFFSET FOR THE GRID ADDRESS TO COMPUTE.
C.....NEWBOX - ARRAY OF THREE ADJACENT GRID BOXES THAT SEARCH
C.....         ALGORITHM MAY FORCE THE SEARCH INTO.
C.....NBOX   - ARRAY OF GRID BOXES THAT SURROUND THE GRID BOX THAT
C.....         INCLUDES GRID POINT NPGRID. ARRAY IS TAKEN FROM THE
C.....         GBOX PARAMETRIC ARRAY.
C
C.....ORIGINALLY WRITTEN BY...
C
C.....JERRY M. NUNN       WGRFC FT. WORTH, TEXAS       JULY 25, 1991
C
      DIMENSION NEWBOX(1), NBOX(1)
C
      INCLUDE 'common/pudbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_maro/RCS/nfgrd.f,v $
     . $',                                                             '
     .$Id: nfgrd.f,v 1.1 1995/09/17 19:02:53 dws Exp $
     . $' /
C    ===================================================================
C
C
  900 FORMAT(1X, '*** NFGRD ENTERED ***')
  901 FORMAT(1X, '*** EXIT NFGRD ***')
C
      IF(IPTRCE .GE. 8) WRITE(IOPDBG,900)
C
C.....INITIALIZE THE GRID POINT ADDRESS TO BE RETURNED.
C
      NFGRD = 0
      IXCHG = 0
      IYCHG = 0
      NXBOX = KPBOX
      IP    = NEWBOX(1)
      JP    = NEWBOX(2)
      KP    = NEWBOX(3)
C
C.....COMPUTE THE X COORDINATE. IT MUST NOT BE NEGATIVE OR EXCEED 10.
C
      KXCORD = JHGC + NXOFF
      IF(KXCORD .LT.  0) GOTO 120
      IF(KXCORD .GE. 10) GOTO 140
      GOTO 200
C
  120 KXCORD = KXCORD + 10
      IXCHG  = 1
      GOTO 200
C
  140 KXCORD = KXCORD - 10
      IXCHG  = 1
      GOTO 200
C
C.....COMPUTE THE Y COORDINATE. IT MUST NOT BE NEGATIVE OR EXCEED 10.
C
  200 KYCORD = KVGC + NYOFF
      IF(KYCORD .LT.  0) GOTO 220
      IF(KYCORD .GE. 10) GOTO 240
      GOTO 300
C
  220 KYCORD = KYCORD + 10
      IYCHG  = 1
      GOTO 300
C
  240 KYCORD = KYCORD - 10
      IYCHG  = 1
      GOTO 300
C
  300 IF(IXCHG .EQ. 1) NXBOX = NBOX(IP)
      IF(IYCHG .EQ. 1) NXBOX = NBOX(JP)
      IF((IXCHG. EQ. 1) .AND. (IYCHG .EQ. 1)) NXBOX = NBOX(KP)
C
      NFGRD = NXBOX*100 + KYCORD*10 + KXCORD
C
  999 IF(IPTRCE .EQ. 8) WRITE(IOPDBG,901)
C
      RETURN
      END
