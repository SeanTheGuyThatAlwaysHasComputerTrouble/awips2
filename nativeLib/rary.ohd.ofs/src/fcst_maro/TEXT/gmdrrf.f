C MEMBER GMDRRF
C  (from old member PPGMDRRF)
C
      SUBROUTINE GMDRRF(PG24, GP6, W6, NFORD, MDR6, NFGRID, NFMBOX,
     * GMDR, WP)
C
C.....THIS SUBROUTINE CONVERTS MDR ACCUMULATIONS TO PRECIPITATON. THE
C.....CONVERSION IS DONE ON A SIX HOURLY TIME PERIOD.
C
C.....BEFORE THE CONVERSION FROM MDR TO PRECIP ESTIMATES ARE DONE...A
C.....'BEST FIT' TABLE (I.E., 30%, 50%, OR 70% PROBABILITY LEVEL) IS
C.....SELECTED.  THIS IS DONE BY COMPARING AVERAGE G/R RATIOS OVER ALL
C.....FIRST ORDER STATIONS, USING THEIR SIX HOUR PRECIP AS INPUT.  ONLY
C.....NON-ZERO OBSERVED PRECIP IS USED, AND ONLY NON-ZERO MDR
C.....ACCUMULATIONS ARE USED TO DEVELOP THE MDR PORTION OF THE INPUT.
C
C.....THE ARGUMENT LIST TO THE SUBROUTINE IS:
C
C.....NFORD  - THE NUMBER OF FIRST ORDER STATIONS TO USE IN THE
C.....         'CALIBRATION' OF THE FIRST ORDER PRECIPITATION DATA
C.....         TO FIND THE 'BEST FIT' MDR PROBABILITY CONVERSION TABLE.
C.....MDR6   - THE ARRAY OF MDR 6-HOURLY ACCUMULATIONS FOR EACH MDR
C.....         BOX ASSIGNED TO THE AREA.
C.....NFGRID - THE GRID POINT ADDRESS OF EACH OF THE FIRST ORDER
C.....         STATIONS.
C.....NFMBOX - THE MDR BOX ASSIGNED TO EACH OF THE FIRST ORDER STATIONS.
C.....GMDR   - THE GMDR PARAMETRIC ARRAY.
C.....WP     - A POINTER TO THE NEXT AVAILABLE ADDRESS IN THE 6-HOURLY
C.....         PERCENTAGE DISTRIBUTION ARRAY, W6.
C
C.....THIS SUBROUTINE WAS ADAPTED FROM SUBROUTINE XMDRRF, WHICH IS PART
C.....OF THE NWSRFS VERSION 5.0 MAP PREPROCESSOR, ORIGINALLY WRITTEN
C.....BY ERIC ANDERSON, HRL.
C
C.....ADAPTATION ORIGINALLY DONE BY
C
C.....JERRY M. NUNN       WGRFC FT. WORTH       JULY 1986
C
C
      INCLUDE 'gcommon/explicit'
      INTEGER*2 MDR6(1), NFGRID(1), GMDR(1), NFMBOX(1)
      INTEGER*2 N1, N2, N3, N4, WPTEMP, PG24(1), GP6(1), W6(1)
C
      DIMENSION ADJTBL(36), JXMDR(36), SNAME(2), PCPN6(4)
      DIMENSION THIRTY(36), FIFTY(36), SEVNTY(36), KP2(4)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/pudbug'
      INCLUDE 'gcommon/gmdr'
      INCLUDE 'gcommon/gsize'
      INCLUDE 'gcommon/gdate'
      INCLUDE 'gcommon/gdispl'
      INCLUDE 'common/where'
      INCLUDE 'common/errdat'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_maro/RCS/gmdrrf.f,v $
     . $',                                                             '
     .$Id: gmdrrf.f,v 1.1 1995/09/17 19:02:04 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA THIRTY /0.03, 0.05, 0.07, 0.10, 0.12, 0.15, 0.20, 0.35, 0.50,
     *             0.75, 1.00, 1.20, 1.40, 1.65, 2.00, 2.30, 2.75, 3.25,
     *             3.80, 4.15, 4.50, 4.65, 4.75, 5.00, 5.15, 5.25, 5.50,
     *             5.65, 5.75, 5.90, 6.00, 6.15, 6.25, 6.40, 6.50, 6.75/
      DATA FIFTY  /0.02, 0.04, 0.06, 0.08, 0.10, 0.12, 0.15, 0.20, 0.25,
     *             0.30, 0.35, 0.45, 0.55, 0.75, 0.95, 1.15, 1.35, 1.50,
     *             1.70, 1.90, 2.25, 2.75, 3.00, 3.50, 3.75, 3.95, 4.15,
     *             4.30, 4.40, 4.50, 4.65, 4.75, 4.80, 4.90, 5.00, 5.25/
      DATA SEVNTY /0.01, 0.02, 0.03, 0.05, 0.08, 0.10, 0.12, 0.15, 0.20,
     *             0.25, 0.30, 0.35, 0.40, 0.45, 0.50, 0.55, 0.70, 0.80,
     *             1.00, 1.10, 1.30, 1.50, 1.60, 1.70, 1.80, 2.00, 2.25,
     *             2.40, 2.65, 3.00, 3.30, 3.65, 3.80, 3.95, 4.00, 4.15/
C
      DATA GGP6, GPG24 /4hGP6 , 4hPG24/
      DATA SNAME, MDRF, MDRZ /4hGMDR, 4hRF  , 4hMDRF, 4hMDRZ/
      DATA JXMDR / 1,  2,  3,  4,  5,  6,  7,  8,  9, 10, 11, 12,
     *            13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23, 24,
     *            25, 26, 27, 28, 29, 30, 31, 32, 33, 34, 35, 36/
C
  900 FORMAT(1H0, '*** GMDRRF ENTERED ***')
  901 FORMAT(1H0, '*** EXIT GMDRRF ***')
  902 FORMAT(1H0, 'NUMBER OF G/R RATIOS DEVELOPED = ', I4)
  903 FORMAT(1H0, 'MDR TABLE TO BE USED = ', I4)
  904 FORMAT(1H0, 'MDR USAGE LEVEL CODE = ', I2, 3X, 'MDR 6 HOURLY USAGE
     * CODE = ', I2, 3X, 'THRESHOLD PRECIPITATION = ', F7.2)
  905 FORMAT(4X, 'THE EQUIVALENT PRECIPITATION WILL NOT BE USED...THE AM
     *OUNT EXCEEDS THE THRESHOLD AMOUNT OF ZERO.')
  906 FORMAT(4X, 'DISTRIBUTION POINTER =  GP6(', I4, ') = ', I5)
  907 FORMAT(4X, 'W6(', I4, ') THRU W6(', I4, ') IS', 4I4)
  908 FORMAT(1H0, '*** NEXT AVAILABLE SLOT IN W6 ARRAY (WP) = ', I10,
     * ' ***')
  909 FORMAT(1H0, '6 HR MDR TOTAL AT BOX ', I4, ' (LOC. ', I4, ') FOR TI
     *ME PERIOD ', I2, ' IS ', I2, /, 3X, 'PRECIP AT THE 3 PROBABILITY L
     *EVELS IS 30%- ', F6.2, '  50%- ', F6.2, '  70%- ', F6.2)
  910 FORMAT(1H0, 'PRECIP AT GRID POINT ', I4, ' IS ', F6.2, '    WEIGHT
     * AT LOCATION ', I4, ' IS ', F6.2, /, 4X, 'SIX HR PRECIP OF ',
     * F6.2, ' GIVES THESE G/R RATIOS', /, 4X, '30% = ', F7.3, 4X,
     * '50% = ', 4X, F7.3, 4X, '70% = ', F7.3)
  911 FORMAT(1H0, 'AVERAGE G/R RATIOS AT THE THREE PROBABILITY LEVELS AR
     *E', /, 4X, '30% ... ', F5.3, '  (', I4, ' RATIOS USED TO COMPUTE A
     *VERAGE)', /, 4X, '50% ... ', F5.3, '  (', I4, ' RATIOS USED TO COM
     *PUTE AVERAGE)', /, 4X, '70% ... ', F5.3, '  (', I4, ' RATIOS USED
     *TO COMPUTE AVERAGE)')
  912 FORMAT(1H0, 'THE NUMBER OF FIRST ORDER GRID POINTS TO USE IN THE M
     *DR CALIBRATION IS ', I4)
  913 FORMAT(1H0, 'FOR FIRST ORDER STATION NO. ', I4, ' GRID POINT ADDRE
     *SS IS ', I6, ' AND NATIONAL MDR BOX NO. IS ', I8)
  914 FORMAT(1H0, '*** WARNING ***  ARRAY W6 IS FILLED TO CAPACITY. ALL
     *', I5, ' SLOTS CONTAIN DISTRIBUTION INFORMATION.', /, 6X, 'PROCESS
     *ING CONTINUES...BUT NO MORE DISTRIBUTIONS CAN BE HANDLED.')
  915 FORMAT(1X, 'W6 ARRAY IS FILLED TO CAPACITY. NO MORE DISTRIBUTIONS
     *CAN BE HANDLED.')
  916 FORMAT(1H1, 'MDR DATA CALIBRATION RESULTS FOR MDR AND FIRST ORDER
     *PRECIPITATION DATA', //, 1X, 'DATE ENDING...', A3, 1X, A3, 1X, I2,
     * 1X, I4, //, 1X, 'NUMBER OF G/R RATIOS DEVELOPED = ', I4)
  917 FORMAT(1H0, 'MDR ''BEST FIT'' TABLE IS THE ', I2, ' PERCENT PROBAB
     *ILITY TABLE.')
  918 FORMAT(1H0, 'NO USER TABLE WAS GIVEN...THE TABLE GIVEN ABOVE WILL
     *BE USED IN ALL FURTHER MDR COMPUTATIONS.')
  919 FORMAT(1H0, 'AN MDR PROBABILITY TABLE OF ', I2, ' PERCENT HAS BEEN
     * SPECIFIED BY THE USER.', /, 1X, 'THE MDR PROBABILITY TABLE OF ',
     * I2, ' PERCENT DETERMINED BY CALIBRATION WILL NOT BE USED.')
  920 FORMAT(///, 1X, '6HR MDR SUMS AND EQUIVALENT 6HR PRECIPITATION TOT
     *ALS FOR THE ', I2, ' PERCENT PROBABILITY TABLE.')
  921 FORMAT(1H0, 'MDR 6HR TOTAL.......', 3X, I2, 11(6X, I2))
  922 FORMAT(1H0, 'EQUIVALENT PRECIP...', 1X, F5.2, 11(3X, F5.2))
  923 FORMAT(1H )
  924 FORMAT(1H0, 'NO FIRST ORDER PRECIPITATION DATA IS AVAILABLE FOR CA
     *LIBRATION.', /, 1H0, 'THE DEFAULT MDR PROBABILITY TABLE OF ',
     * I2, ' PERCENT WILL BE USED.')
  925 FORMAT(1H0, '*** WARNING ***  MDR 6-HOUR ACCUMULATION OF ', I6,
     * ' FOUND IN MDR BOX NUMBER ', I4, ' FOR TIME PERIOD ', I2, '.',
     * /, 5X, 'ONLY 6-HOUR ACCUMULATIONS BETWEEN 0 AND 36 ARE ALLOWED. T
     *HE ACCUMULATION ON FILE WILL NOT BE USED.')
  926 FORMAT(1H0, '*** WARNING ***  IN GMDRRF -- USER INPUT MDR-TO-PRECI
     *PITATION CONVERSION PROBABILITY TABLE OF ', I9, ' PCT IS INVALID.'
     *, /, 5X, 'CONVERSION TABLES CAN ONLY EXPRESS PROBABILITY LEVELS OF
     * 30 PCT, 50 PCT, OR 70 PCT.', /, 5X, 'THE CONVERSION TABLE IS BEIN
     *G SET TO THE CALIBRATED PROBABILITY LEVEL OF ', I4, ' PCT BEFORE C
     *ONTINUING WITH THE PROCESSING.')
  927 FORMAT(1H0, 'MDR BOX NO. ', I5, ' HAS CENTROID GRID POINT ADDRESS
     *OF ', I6, /, 4X, 'THE 6 HR MDR ACCUMULATIONS ARE  ', 4I6, /, 4X,
     * 'THE EQUIVALENT 24 HR PRECIPITATION IS ............ ', F5.2)
  928 FORMAT(4X, 'GRID BOX NO. ', I4, ' CONTAINS THE SPECIAL PROCESSING
     *FLAG', /, 4X, 'THE EQUIVALENT PRECIPITATION WILL BE USED...REGARDL
     *ESS  OF THE THRESHOLD PRECIPITATION AMOUNT')
  929 FORMAT(4X, 'THE EQUIVALENT PRECIPITATION WILL NOT BE USED...THE AM
     *OUNT EXCEEDS THE THRESHOLD AMOUNT OF ', F5.2)
  930 FORMAT(4X, 'THE EQUIVALENT PRECPIPITATION FOR MDR BOX NO. ', I4,
     * ' CANNOT BE STORED', /, 4X, 'AN OBSERVED PRECIPITATION AMOUNT OF
     *', I5, ' ALREADY EXISTS AT GRID ADDRESS ', I4)
  931 FORMAT(4X, 'EQUIVALENT PRECIPITATION OF ', F5.2, ' STORED IN GRID
     *ADDRESS ', I4, /, 4X, 'PG24(', I4, ') = ', I5)
C
C
C.....SET THE WHERE COMMON BLOCK.
C
      IOPNUM = -1
      OPNAME(1) = SNAME(1)
      OPNAME(2) = SNAME(2)
C
      IF(IPTRCE .GE. 1) WRITE(IOPDBG,900)
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,912) NFORD
      NGR30  = 0
      NGR50  = 0
      NGR70  = 0
      AVGR30 = 0.0
      AVGR50 = 0.0
      AVGR70 = 0.0
C
      MDRBUG = IPBUG(MDRF)
      NZRBUG = IPBUG(MDRZ)
      IBUG = IPBUG(GGP6)
      JBUG = IPBUG(GPG24)
      IF(IBUG .EQ. 1) CALL GDGP6(GP6)
C
C.....MDR 'CALIBRATION' ALGORITHM.
C
C.....WE ARE NOW GOING TO 'CALIBRATE THE MDR VS PRECIP CONVERSION FOR
C.....THIS DATE. WE DO THIS BY CONSTRUCTING RAIN/MDR RATIOS FOR THE 30%,
C.....50%, AND 70% LEVELS. TO GET RAIN WE USE ONLY OBSERZED NON-ZERO
C.....PRECIP FROM FIRST ORDER STATIONS FOR SIX HOUR AMOUNTS. TO GET MDR
C.....WE USE NON-ZERO ACCUMULATIONS AT THE MDR BOX IN WHICH THE FIRST
C.....ORDER STATION RESIDES.
C
C.....NEXT WE ADD UP THE RAIN/MDR RATIOS FOR EACH TIME PERIOD (WHERE
C.....PERMISSIBLE DATA EXISTS) OVER ALL TIME PERIODS FOR ALL FIRST ORDER
C.....STATIONS FOR ALL MDR LEVELS. GAGE/MDR RATIOS OF ZERO OR MORE THAN
C.....2 ARE DISCOUNTED. AN AVERAGE IS THEN PRODUCED, WHICH IS COMPARED
C.....TO THE OTHER AVERAGES.
C
C.....THE MDR TO PRECIP CONVERSION TABLE TO USE IS THE TABLE WITH AN
C.....AVERAGE RAIN/MDR RATIO CLOSEST TO 1.00.
C
C.....GO THRU THE GRID POINTS. NP IS THE GRID POINT ADDRESS.
C
C
C
      IF(JBUG .EQ. 1) CALL GDPG24(PG24)
C
C
      DO 300 NP = 1, NGRID
C
C.....PG24 IS THE GRID POINT PRECIP. DO NOT PROCESS ZERO OR MISSING
C.....GRID POINT PRECIP.
C
      IF(PG24(NP) .EQ. 0) GOTO 300
      IF(PG24(NP) .EQ. MSGGRD) GOTO 300
C
C.....PROCESS ONLY GRID POINTS AT WHICH A FIRST ORDER STATION IS LOCATED
C.....GP6 IS POSITIVE WHEN THERE IS A DISTRIBUTION, SO ALL FIRST
C.....ORDER STATIONS HAVE POSITIVE GP6S.
C
      IF(GP6(NP) .EQ. 0) GOTO 300
C
C.....WHEN GP6 > 0, GP6 IS A POINTER TO THE BEGINNING ADDRESS IN THE W6
C.....ARRAY WHERE PERCENTAGE SIX HOUR DISTRIBUTIONS FOR THE GRID POINT
C.....ARE STORED.
C
      LP = GP6(NP)
C
C.....GET THE NATIONAL MDR BOX NO. OF THE FIRST STATION, SO AS TO
C.....GET AN MDR SUM FOR THE STATION.
C
C.....NFORD  = NO. OF 1ST ORDER STATIONS.
C.....NFGRID = GRID PT. ADDRESS OF FIRST ORDER STATION.
C.....NFMBOX = MDR BOX NO. (LOCAL) OF FIRST ORDER STATION.
C
C.....NFGRID AND NFMBOX ARE ARRANGED IN ASCENDING ORDER BY GRID POINT.
C
      DO 200 M = 1, NFORD
      J = NFGRID(M)
      IF(J .EQ. NP) GOTO 210
      IF(J .GT. NP) GOTO 300
  200 CONTINUE
C
      GOTO 300
C
C.....THE GRID POINT IS A FIRST ORDER GRID POINT.
C.....GET THE LOCAL MDR BOX NO. TO USE. RECALL, THE BOX NUMBERS STORED
C.....ON FILE ARE NATIONAL BOX NUMBERS.
C
  210 JY = NFMBOX(M)/113 + 1
      JX = NFMBOX(M) - (JY-1)*113
      MDRBOX = (JY - IRMDR)*NCMDR + (JX - ICMDR + 1)
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,913) M, NFGRID(M), NFMBOX(M)
C
C.....NOW LOOP THRU ALL TIME PERIODS TO GET MDR AND PRECIP.
C
      DO 250 IP = 1, 4
C
C.....COMPUTE THE ADDRESS IN MDR6 TO LOOK UP.
C
      KP = (IP-1)*MDRDSZ + MDRBOX
C
C.....NOW GET THE MDR ACCUMULATION FOR THE SIX HOUR PERIOD.
C
      MDRACC = MDR6(KP)
C
C.....GET MDR PRECIP AT ALL THREE PROBABILITY LEVELS...PROVIDED THERE
C.....IS A NON-ZERO MDR ACCUMULATION.
C
      IF(MDRACC .EQ. 0) GOTO 240
      IF(MDRACC .EQ. MSGMDR) GOTO 240
      IF(MDRACC .LT. 0) MDRACC = -MDRACC
      IF(MDRACC .LE. 36) GOTO 215
C
      WRITE(IPR,925) MDRACC, MDRBOX, IP
      CALL WARN
      GOTO 240
C
  215 R30 = THIRTY(MDRACC)
      R50 = FIFTY(MDRACC)
      R70 = SEVNTY(MDRACC)
C
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,909) MDRBOX, KP, IP, MDRACC, R30,
     * R50, R70
C
C.....GET OBSERVED SIX HOUR PRECIP FOR FIRST ORDER STATION.
C
      PCPN = PG24(NP)
      FACT = W6(LP)
C
      R = PCPN/100.0
      F = FACT/100.0
C
      P6 = F*R
C
C.....COMPUTE THE PRECIP/MDR RATIOS (OR G/R RATIOS, IF YOU PREFER).
C
      GR30 = P6/R30
      GR50 = P6/R50
      GR70 = P6/R70
C
C.....ACCUMULATE THE G/R RATIOS. HOWEVER, DO NOT ACCUMULATE THE RATIOS
C.....IF THEY ARE ZERO OR GREATER THAN 2.
C
C
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,910) NP, R, LP, F, P6, GR30, GR50,
     * GR70
      IF((GR30 .LT. 0.01) .OR. (GR30 .GT. 2.00)) GOTO 220
      AVGR30 = AVGR30 + GR30
      NGR30 = NGR30 + 1
C
  220 IF((GR50 .LT. 0.01) .OR. (GR50 .GT. 2.00)) GOTO 230
      AVGR50 = AVGR50 + GR50
      NGR50 = NGR50 + 1
C
  230 IF((GR70 .LT. 0.01) .OR. (GR70 .GT. 2.00)) GOTO 240
      AVGR70 = AVGR70 + GR70
      NGR70 = NGR70 + 1
C
C.....INCREMENT THE W6 POINTER...THEN GO PROCESS THE NEXT TIME PERIOD.
C
  240 LP = LP + 1
C
  250 CONTINUE
C
C.....AFTER ALL 4 TIME PERIODS HAVE BEEN PROCESSED FOR THIS GRID POINT,
C.....GO PROCESS THE NEXT GRID POINT.
C
  300 CONTINUE
C
C.....COMPUTE THE NUMBER OF G/R RATIOS THAT WERE DEVELOPED.
C
      NOBS = NGR30 + NGR50 + NGR70
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,902) NOBS
C
C.....COMPUTE THE AVERAGE G/R RATIOS.
C
      XGR30  = NGR30
      XGR50  = NGR50
      XGR70  = NGR70
C
      IF(NGR30 .LE. 0) GOTO 310
      AVGR30 = AVGR30/XGR30
C
  310 IF(NGR50 .LE. 0) GOTO 320
      AVGR50 = AVGR50/XGR50
C
  320 IF(NGR70 .LE. 0) GOTO 330
      AVGR70 = AVGR70/XGR70
C
  330 IF(MDRBUG .EQ. 1) WRITE(IOPDBG,911) AVGR30, NGR30, AVGR50, NGR50,
     * AVGR70, NGR70
C
C.....IF NO G/R RATIOS WERE DEVELOPED, USE THE DEFAULT TABLE.
C
      IF(NOBS .EQ. 0) GOTO 360
C
C.....NOW FIND THE G/R RATIO WHOSE VALUE IS CLOSEST TO 1.00. THE MDR
C.....PROBABILITY TABLE THAT WAS USED TO DEVELOP THIS RATIO WILL BE
C.....THE 'TABLE OF THE DAY'.
C
      DIF30 = ABS(AVGR30 - 1.00)
      DIF50 = ABS(AVGR50 - 1.00)
      DIF70 = ABS(AVGR70 - 1.00)
C
      DIFMIN = DIF50
      NTABLE = 50
C
      IF(DIF30 .GE. DIFMIN) GOTO 350
      IF(NGR30 .LE. 0) GOTO 350
      DIFMIN = DIF30
      NTABLE = 30
C
  350 IF(DIF70 .GE. DIFMIN) GOTO 360
      IF(NGR70 .LE. 0) GOTO 360
      DIFMIN = DIF70
      NTABLE = 70
C
C.....NOW PRINT OUT THE MDR CALIBRATION RESULTS AND MDR ACCUMULATION
C.....VERSUS PRECIPITATION TABLE...PROVIDED MDR PRINTOUT IS DESIRED.
C
  360 IF(MDRPRT .EQ. 0) GOTO 400
C
      WRITE(IPR,916) IHYDAY, IHYMON, IHYDAT, IHYYR, NOBS
      IF(NOBS .GT. 0) GOTO 370
      WRITE(IPR,924) NTABLE
      GOTO 380
C
  370 WRITE(IPR,911) AVGR30, NGR30, AVGR50, NGR50, AVGR70, NGR70
      WRITE(IPR,917) NTABLE
C
  380 IF(MDRUST .EQ. 0) WRITE(IPR,918)
      IF(MDRUST .EQ. 1) WRITE(IPR,919) IUSTBL, NTABLE
C
C.....NOW FILL OUT THE MDR ADJUSTMENT TABLE.
C
  400 IF(MDRUST .EQ. 0) GOTO 420
C
C.....TEST THE USER-INPUT TABLE NUMBER. IF IT IS AN INVALID VALUE, I.E.,
C.....NOT 30, 50, OR 70, CHANGE IT TO THE CALIBRATED VALUE AND ISSUE A
C.....WARNING MESSAGE.
C
      IF(IUSTBL .EQ. 30) GOTO 410
      IF(IUSTBL .EQ. 50) GOTO 410
      IF(IUSTBL .EQ. 70) GOTO 410
C
      WRITE(IPR,926) IUSTBL, NTABLE
      CALL WARN
      GOTO 420
C
  410 NTABLE = IUSTBL
C
  420 DO 450 MP = 1, 36
      IF(NTABLE .EQ. 30) ADJTBL(MP) = THIRTY(MP)
      IF(NTABLE .EQ. 50) ADJTBL(MP) = FIFTY(MP)
      IF(NTABLE .EQ. 70) ADJTBL(MP) = SEVNTY(MP)
  450 CONTINUE
C
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,903) NTABLE
C
C.....NOW PRINT OUT THE MDR ACCUMULATION-PRECIPITATION EQUIVALENT
C.....TABLE...PROVIDED SUCH OUTPUT IS DESIRED.
C
      IF(MDRPRT .EQ. 0) GOTO 495
C
      WRITE(IPR,920) NTABLE
      WRITE(IPR,923)
      WRITE(IPR,921) (JXMDR(NP), NP = 1, 12)
      WRITE(IPR,922) (ADJTBL(NP), NP = 1, 12)
      WRITE(IPR,923)
      WRITE(IPR,921) (JXMDR(NP), NP = 13, 24)
      WRITE(IPR,922) (ADJTBL(NP), NP = 13, 24)
      WRITE(IPR,923)
      WRITE(IPR,921) (JXMDR(NP), NP = 25, 36)
      WRITE(IPR,922) (ADJTBL(NP), NP = 25, 36)
C
C.....WRITE OUT DEBUG OUTPUT (IF DESIRED).
C
  495 IF(MDRBUG .NE. 1) GOTO 496
C
      WRITE(IOPDBG,920) NTABLE
      WRITE(IOPDBG,923)
      WRITE(IOPDBG,921) (JXMDR(NP), NP = 1, 12)
      WRITE(IOPDBG,922) (ADJTBL(NP), NP = 1, 12)
      WRITE(IOPDBG,923)
      WRITE(IOPDBG,921) (JXMDR(NP), NP = 13, 24)
      WRITE(IOPDBG,922) (ADJTBL(NP), NP = 13, 24)
      WRITE(IOPDBG,923)
      WRITE(IOPDBG,921) (JXMDR(NP), NP = 25, 36)
      WRITE(IOPDBG,922) (ADJTBL(NP), NP = 25, 36)
C
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C.....M D R   P R E C I P   E N T R Y   R O U T I N E
C
C.....NOW THAT THE MDR CONVERSION TABLE HAS BEEN DETERMINED...USE THE
C.....MDR OPTIONS TO ESTIMATE PRECIPITATION AT THE MDR BOX CENTROID
C.....GRID POINTS, WHERE NO OBSERVED VALUE CURRENTLY EXISTS.
C
C.....ALSO...USE MDR TO DEVELOP SIX HOUR TIME DISTRIBUTION PERCENTAGES
C.....AT THE MDR BOX CENTROID GRID POINTS.
C
C.....USE THE VARIOUS MDR OPTIONS TO DETERMINE THE LEVEL OF MDR USAGE
C.....DESIRED.
C
C * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
C
  496 IF(MDRSTA .EQ. 0) GOTO 520
      IF(MDRSTA .EQ. 1) GOTO 500
      IF(MDRSTA .EQ. 2) GOTO 510
      IF(MDRSTA .EQ. 3) GOTO 520
      MDRSTA = 0
      GOTO 520
C
C.....THE THRESHOLD PRECIPITATION IS A PRECIPITATION AMOUNT SUCH THAT IF
C.....A 24 HOURLY MDR ESTIMATE EXCEEDS THE VALUE...THAT 24 HOUR
C.....ESTIMATE WILL NOT BE USED. SET THE THRESHOLD PRECIPITATION
C.....ACCORDINGLY.
C
  500 TPCPN = 0.0
      GOTO 530
C
  510 TPCPN = FLOAT(ISMALL)/100.0
      GOTO 530
C
  520 TPCPN = 100.0
C
C.....PROCESS EACH MDR BOX IN THE CENTROID GRID POINT MDR PARAMETRIC
C.....ARRAY.
C
  530 IF(MDRBUG .EQ. 1) WRITE(IOPDBG,904) MDRSTA, MDRST6, TPCPN
C
C.....COMPUTE THE TOTAL NUMBER OF SLOTS AVAILABLE IN W6.
C
      MAXW6 = NDIST*4
C
      DO 700 KX = 1, MDRDSZ
C
      IF(KX .GT. NMDR) GOTO 700
C
      MDRGRD = GMDR(KX)
      IF(MDRGRD .EQ. 0) GOTO 700
C
C.....IGP6 IS A FLAG DENOTING IF AN ENTRY HAS BEEN MADE IN THE GP6 FILE.
C
      IGP6 = 0
C
C
      KGMDR = MDRGRD
      IF(KGMDR .LT. 0) KGMDR = -KGMDR
      PCPN24 = 0.0
C
C.....PROCESS EACH OF THE FOUR SIX HOURLY TIME PERIODS.
C
      DO 600 IP = 1, 4
C
C.....GET THE POINTER TO THE SIX-HOURLY MDR SUMS, AND THE SUM.
C
      KP = (IP-1)*MDRDSZ + KX
      KP2(IP) = KP
      MDRACC = MDR6(KP)
C
C.....GET THE PRECIPITATION ESTIMATE.
C
      IF(MDRACC .EQ. 0) GOTO 540
      IF(MDRACC .EQ. MSGMDR) GOTO 540
      IF(MDRACC .LT. 0) MDRACC = -MDRACC
      IF(MDRACC .LE. 36) GOTO 535
C
      WRITE(IPR,925) MDRACC, KX, IP
      CALL WARN
      MDRACC = MSGMDR
      GOTO 540
C
C
  535 PCPN6(IP) = ADJTBL(MDRACC)
      GOTO 550
C
  540 PCPN6(IP) = 0.0
C
C.....SUM UP THE 24 HOUR PRECIPITATION.
C
  550 PCPN24 = PCPN24 + PCPN6(IP)
C
  600 CONTINUE
C
C.....THE MDR EQUIVALENT PRECIPITATION HAS BEEN DETERMINED FOR THIS MDR
C.....BOX.  IF DEBUG INDICATES...PRINT OUT THE EQUIVALENT PRECPITATION.
C
      IF(MDRBUG .EQ. 0) GOTO 602
C
      NP1 = KP2(1)
      NP2 = KP2(2)
      NP3 = KP2(3)
      NP4 = KP2(4)
C
      WRITE(IOPDBG,927) KX, KGMDR, MDR6(NP1), MDR6(NP2), MDR6(NP3),
     * MDR6(NP4), PCPN24
C
C.....NOW...TRY TO STORE A 24 HOUR PRECIPITATION AMOUNT THAT HAS
C.....BEEN DERIVED FROM MDR TOTALS. BUT, DO THIS ONLY IF THE OPTION
C.....IS DESIRED. THE DEGREE OF MDR-TO-PRECIPITATION CONVERSION
C.....DESIRED IS GIVEN IN THE VARIABLE MDRSTA.
C
C.....     MDRSTA = 0   DO NOT STORE ANYTHING DERIVED FROM MDR.
C.....     MDRSTA = 1   STORE THE PRECIPITATION ONLY IF THE AMOUNT
C.....                  DERIVED FROM MDR IS ZERO.
C.....     MDRSTA = 2   USE MDR TO ESTIMATE PRECIPITATION UP TO THE
C.....                  THRESHOLD AMOUNT. (THIS AMOUNT IS GIVEN IN
C.....                  TPCPN, AS WELL AS ISMALL).
C.....     MDRSTA = 3   STORE ANY MDR-ESTIMATED AMOUNT, REGARDLESS OF
C.....                  ITS MAGNITUDE, OR ITS RELATION TO TPCPN.
C
  602 IF(MDRSTA .EQ. 0) GOTO 620
      IF(MDRSTA .EQ. 1) GOTO 605
      IF(MDRSTA .EQ. 2) GOTO 606
      IF(MDRSTA .EQ. 3) GOTO 610
      GOTO 620
C
  605 IF(PCPN24 .LT. 0.01) GOTO 610
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,905)
      GOTO 620
C
  606 IF(PCPN24 .LE. TPCPN) GOTO 610
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,929) TPCPN
      GOTO 620
C
  610 IF(PG24(KGMDR) .EQ. MSGGRD) GOTO 612
      IF(MDRBUG .EQ. 0) GOTO 630
      WRITE(IOPDBG,930) KX, PG24(KGMDR), KGMDR
      GOTO 630
C
  612 PG24(KGMDR) = PCPN24*100.0 + 0.5
      IF(PG24(KGMDR) .GT. 0) GOTO 615
      IF(NZRBUG .EQ. 0) GOTO 630
  615 IF(MDRBUG .EQ. 1) WRITE(IOPDBG,931) PCPN24, KGMDR, KGMDR,
     * PG24(KGMDR)
      GOTO 630
C
C.....NORMALLY WE WON'T STORE A 24 HOUR ESTIMATE IF THE ESTIMATE EXCEEDS
C.....THE THRESHOLD. HOWEVER...WE MAY IN CERTAIN CASES (SUCH AS A
C.....MEXICAN MDR BOX) WANT TO USE AN MDR ACCUMULATION REGARDLESS OF THE
C.....THRESHOLD. A NEGATIVE GRID POINT ADDRESS DENOTES SUCH A CASE.
C
  620 IF(MDRGRD .GT. 0) GOTO 630
      IF(MDRBUG .EQ. 0) GOTO 610
      KGBOX = -MDRGRD/100
      WRITE(IOPDBG,928) KGBOX
      GOTO 610
C
C.....NOW THAT THE 24 HOUR PRECIP HAS BEEN TAKEN CARE OF...DO THE 6 HOUR
C.....PRECIP AND PERCENTAGE DISTRIBUTIONS.
C
  630 IF(MDRST6 .NE. 1) GOTO 700
      IF(WP .LE. MAXW6) GOTO 632
      WRITE(IPR,914) MAXW6
      CALL WARN
      GOTO 700
C
  632 IF(GP6(KGMDR) .GT. 0) GOTO 700
C
C.....GET THE POINTER TO THE NEXT EMPTY SLOT IN THE W6 ARRAY. PUT IT
C.....IN GP6.
C
      GP6(KGMDR) = WP
      IGP6 = 1
      WPTEMP = WP
C
C.....NOW COMPUTE THE FOUR SIX HOURLY PERCENTAGE DISTRIBUTIONS.
C
      DO 650 IP = 1, 4
C
      IF(PCPN24 .LT. 0.01) GOTO 635
      W6(WP) = (PCPN6(IP)/PCPN24)*100.0 + 0.5
      GOTO 640
C
  635 W6(WP) = 0
  640 WP = WP + 1
      IF(WP .LE. MAXW6) GOTO 650
      WRITE(IPR,914) MAXW6
      CALL WARN
C
  650 CONTINUE
C
C.....CHECK THE LATEST 4 ENTRIES IN W6.  IF ALL ARE ZERO...DO NOT STORE.
C
      KP = WPTEMP
C
      DO 675 IP = 1, 4
      IF(W6(KP) .NE. 0) GOTO 680
      KP = KP + 1
  675 CONTINUE
C
C.....WHERE ALL 4 ENTRIES ARE ZERO...DISCARD THEM.
C
      WP = WPTEMP
      GP6(KGMDR) = 0
      IGP6 = 0
C
  680 IF(PG24(KGMDR) .GT. 0) GOTO 684
      IF(NZRBUG .EQ. 0) GOTO 700
  684 IF(MDRBUG .EQ. 1) WRITE(IOPDBG,906) KGMDR, GP6(KGMDR)
      IF(IGP6 .EQ. 1) GOTO 685
      GOTO 700
C
  685 N1 = GP6(KGMDR)
      N2 = N1 + 1
      N3 = N2 + 1
      N4 = N3 + 1
C
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,907) N1, N4, W6(N1), W6(N2),
     * W6(N3), W6(N4)
C
C.....END OF GRID POINT LOOP.
C
  700 CONTINUE
C
      IF(MDRBUG .EQ. 1) WRITE(IOPDBG,908) WP
      IF((WP .GT. MAXW6) .AND. (MDRBUG .EQ. 1)) WRITE(IOPDBG,915)
C
      IF(IPTRCE .GE. 1) WRITE(IOPDBG,901)
C
      RETURN
      END
