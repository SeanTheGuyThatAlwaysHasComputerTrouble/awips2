C MEMBER GPPEST
C  (from old member PPGPPEST)
C
      SUBROUTINE GPPEST(NUM, WQ1, WQ2, WQ3, WQ4, NQP1, NQP2, NQP3,
     * NQP4, NSQUAD, PX24)
C
C.....THIS IS THE PRECIPITATION ESTIMATION COMPUTATIONAL PROCEDURE.
C.....THIS SUBROUTINE IS CALLED FROM SUBROUTINE GESTMT...WHICH HAS
C.....PLACED THE PRECIPITATION VALUES (BOTH OBSERVED AND MISSING) IN
C.....WORKING ARRAYS EQR1, EQR2, EWQR3, AND EQR4...WHICH WERE TAKEN
C.....FROM SURROUNDING GRID POINTS.
C
C.....FROM THE DATA IN THESE WORKING ARRAYS...OBSERVED VALUES FOR THE
C.....CLOSEST 'NUMQDT' STATIONS ARE FOUND IN EACH QUADRANT.
C.....USING THESE OBSERVED VALUES...AND COMPUTING THEIR DISTANCE
C.....WEIGHTS (COMPUTED IN STATUTE MILES)...THE ESTIMATION IS THE
C.....SUM OF THE PRECIPITATION TIMES THE WEIGHTS DIVIDED BY THE SUM
C.....OF THE WEIGHTS, EXCEPT WHEN THERE ARE PRECIPITATION REPORTS ONLY
C.....IN 1 OR 2 ADJACENT QUADRANTS. THEN ONE DOES NOT DIVIDE BY THE
C.....SUM OF THE WEIGHTS.
C
C.....HERE IS THE ARGUMENT LIST FOR THE SUBROUTINE.
C
C.....NUM    - THE MAXIMUM NUMBER OF PRECIPITATION OBSERVATIONS TO BE
C.....         USED OVER ALL QUADRANTS.
C.....WQ1    - ARRAY OF DISTANCE WEIGHTS FOR EACH PRECIPITATION VALUE
C.....         FOUND IN QUADRANT 1.
C.....WQ2    - ARRAY OF DISTANCE WEIGHTS FOR EACH PRECIPITATION VALUE
C.....         FOUND IN QUADRANT 2.
C.....WQ3    - ARRAY OF DISTANCE WEIGHTS FOR EACH PRECIPITATION VALUE
C.....         FOUND IN QUADRANT 3.
C.....WQ4    - ARRAY OF DISTANCE WEIGHTS FOR EACH PRECIPITATION VALUE
C.....         FOUND IN QUADRANT 4.
C.....NQP1   - ARRAY OF PRECIPITATION OBSERVATIONS FOUND IN
C.....         QUADRANT 1.
C.....NQP2   - ARRAY OF PRECIPITATION OBSERVATIONS FOUND IN
C.....         QUADRANT 2.
C.....NQP3   - ARRAY OF PRECIPITATION OBSERVATIONS FOUND IN
C.....         QUADRANT 3.
C.....NQP4   - ARRAY OF PRECIPITATION OBSERVATIONS FOUND IN
C.....         QUADRANT 4.
C.....NSQUAD - ARRAY OF NUMBER OF PRECIPITATION REPORTS FOUND IN
C.....         EACH QUADRANT.
C.....PX24   - THE PRECIPITATION ESTIMATE.  NOTE THAT THE ESTIMATE
C.....         IS RETURNED AS A NEGATIVE NUMBER.
C
C.....ORIGINALLY WRITTEN BY
C
C.....JERRY M. NUNN       WGRFC FT. WORTH, TEXAS       SEPTEMBER 1986
C
      INTEGER*2 NQP1(1), NQP2(1), NQP3(1), NQP4(1), PX24, NSQUAD(1)
      INTEGER*2 P, PQUAD(4), MPRECP(40), MWATE(40), MPCAVG, MPCLRG
C
      INCLUDE 'gcommon/explicit'
C
      DIMENSION WQ1(1), WQ2(1), WQ3(1), WQ4(1), WQUAD(4)
C
      INCLUDE 'gcommon/gsize'
      INCLUDE 'gcommon/gopt'
      INCLUDE 'common/pudbug'
      INCLUDE 'gcommon/boxtrc'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_maro/RCS/gppest.f,v $
     . $',                                                             '
     .$Id: gppest.f,v 1.1 1995/09/17 19:02:19 dws Exp $
     . $' /
C    ===================================================================
C
C
  900 FORMAT(1H0, '*** GPPEST ENTERED ***')
  901 FORMAT(1X, '*** EXIT GPPEST ***')
  902 FORMAT(1X, 'PX24 (ESTIMATED PRECIPITATION) = ', I8)
  904 FORMAT(1X, 'FOR QUADRANT ', I2, 3X, 'WEIGHTED PRECIP = ', I5, 3X,
     * 'SUM OF DISTANCE WEIGHTS = ', F6.4, 3X, 'NO. OF REPORTS FOUND = '
     *, I2)
C
      DATA QUAD /4hQUAD/
C
      IF(IPTRCE .GE. 8) WRITE(IOPDBG,900)
C
C.....GO THRU EACH OF THE 4 QUADRANTS...SUMMING UP THE CLOSEST
C.....'NUMQDT' PRECIPITATION REPORTS IN EACH QUADRANT.
C
C
      NQBUG = IPBUG(QUAD)
      IF(IBTCON .EQ. 0) NQBUG = 0
C
      DO 100 IQUAD = 1, 4
C
      IF(IQUAD .EQ. 1) GOTO 20
      IF(IQUAD .EQ. 2) GOTO 40
      IF(IQUAD .EQ. 3) GOTO 60
      IF(IQUAD .EQ. 4) GOTO 80
C
C.....COMPUTE THE SUMS OF THE PRECIPITATION AND THE SUMS
C.....WEIGHTS IN THE APPROPRIATE QUADRANTS.
C
   20 IF(NSQUAD(IQUAD) .LT. 1) GOTO 100
      CALL GSPQ(WQ1, WQUAD(IQUAD), NQP1, PQUAD(IQUAD), NSQUAD(IQUAD))
      IF(NQBUG .EQ. 1) WRITE(IOPDBG,904) IQUAD, PQUAD(IQUAD),
     * WQUAD(IQUAD), NSQUAD(IQUAD)
      GOTO 100
C
   40 IF(NSQUAD(IQUAD) .LT. 1) GOTO 100
      CALL GSPQ(WQ2, WQUAD(IQUAD), NQP2, PQUAD(IQUAD), NSQUAD(IQUAD))
      IF(NQBUG .EQ. 1) WRITE(IOPDBG,904) IQUAD, PQUAD(IQUAD),
     * WQUAD(IQUAD), NSQUAD(IQUAD)
      GOTO 100
C
   60 IF(NSQUAD(IQUAD) .LT. 1) GOTO 100
      CALL GSPQ(WQ3, WQUAD(IQUAD), NQP3, PQUAD(IQUAD), NSQUAD(IQUAD))
      IF(NQBUG .EQ. 1) WRITE(IOPDBG,904) IQUAD, PQUAD(IQUAD),
     * WQUAD(IQUAD), NSQUAD(IQUAD)
      GOTO 100
C
   80 IF(NSQUAD(IQUAD) .LT. 1) GOTO 100
      CALL GSPQ(WQ4, WQUAD(IQUAD), NQP4, PQUAD(IQUAD), NSQUAD(IQUAD))
      IF(NQBUG .EQ. 1) WRITE(IOPDBG,904) IQUAD, PQUAD(IQUAD),
     * WQUAD(IQUAD), NSQUAD(IQUAD)
      GOTO 100
C
  100 CONTINUE
C
C.....COMPUTE THE AVERAGE OF ALL PRECIPITATION REPORTS FOUND.
C
      PC = 0.0
      C  = 0.0
C
      MPCAVG = 0
C
      KP = NSQUAD(1)
      IF(KP .LE. 0) GOTO 130
      DO 120 NP = 1, KP
      PX = NQP1(NP)
      PC = PC + PX
      C  = C + 1.0
  120 CONTINUE
C
  130 KP = NSQUAD(2)
      IF(KP .LE. 0) GOTO 150
      DO 140 NP = 1, KP
      PX = NQP2(NP)
      PC = PC + PX
      C  = C + 1.0
  140 CONTINUE
C
  150 KP = NSQUAD(3)
      IF(KP .LE. 0) GOTO 170
      DO 160 NP = 1, KP
      PX = NQP3(NP)
      PC = PC + PX
      C  = C + 1.0
  160 CONTINUE
C
  170 KP = NSQUAD(4)
      IF(KP .LE. 0) GOTO 190
      DO 180 NP = 1, KP
      PX = NQP4(NP)
      PC = PC + PX
      C  = C + 1.0
  180 CONTINUE
C
  190 IF(C .LT. 0.5) GOTO 400
      MPCAVG = (PC/C) + 0.5
C
C.....FIND THE MAGNITUDE OF THE OBSERVED PRECIPITATION CLOSEST TO THE
C.....STATION BEING ESTIMATED FOR.
C
      XLARGE = WQ1(1)
      MPCLRG = NQP1(1)
C
      IF(WQ2(1) .GE. XLARGE) GOTO 220
      XLARGE = WQ2(1)
      MPCLRG = NQP2(1)
C
  220 IF(WQ3(1) .GE. XLARGE) GOTO 240
      XLARGE = WQ3(1)
      MPCLRG = NQP3(1)
C
  240 IF(WQ4(1) .GE. XLARGE) GOTO 260
      XLARGE = WQ4(1)
      MPCLRG = NQP4(1)
C
C.....ALL OF THE OBSERVED PRECIPITATION IS NOW PLACED IN THE QUADRANT
C.....ESTIMATION SLOTS. IT IS NOW TIME TO ESTIMATE THE PRECIPITATION.
C
C.....WE WILL USE THE FOLLOWING FORMULA:  THE PRECIPITATION ESTIMATE
C.....WILL BE THE SUM OF THE WEIGHTED PRECIPITATION REPORTS DIVIDED BY
C.....THE SUM OF THE WEIGHTS UNLESS:   (1)  THERE IS ONLY PRECIPITATION
C.....IN ONLY ONE QUADRANT TO ESTIMATE FROM;  OR   (2)  THERE IS ONLY
C.....OBSERVED PRECIPITATION IN TWO ADJACENT QUADRANTS, AND THE
C.....TWO QUADRANT PROCEDURE HAS BEEN ENABLED (IADJQP IN COMMON
C.....BLOCK /GOPT/ = 1 OR 2).
C
C.....IF CONDITIONS (1) OR (2) EXIST, THEN THE ESTIMATED PRECIPITATION
C.....WILL JUST BE THE SUM OF THE WEIGHTED PRECIPITATION VALUES.
C
C.....FIND OUT HOW MANY QUADRANTS HAD OBSERVED PRECIPITATION.
C
  260 J = 0
      DO 280 K = 1, 4
      IF(NSQUAD(K) .GT. 0) J = J + 1
  280 CONTINUE
C
C.....PERFORM THE NORMAL ESTIMATION PROCEDURE WHEN MORE THAN TWO
C.......QUADRANTS HAVE REPORTS.
C
      IF(J .GT. 2) GOTO 360
C
C.....IF ONLY TWO QUADRANTS HAVE REPORTS...DO A FURTHER CHECK.
C
      IF(J .EQ. 2) GOTO 300
C
C.....IF ONLY ONE QUADRANT HAS REPORTED...DO NOT DIVIDE BY THE SUM OF
C.....THE WEIGHTS.
C
      IF(J .EQ. 1) GOTO 320
C
C.....IF YOU GET HERE...J = 0. THERE ARE NO REPORTS IN ANY QUADRANT
C.....WITHIN THE SPECIFIED DISTANCE. SET THE ESTIMATE TO MISSING
C.....EXIT THE SUBROUTINE.
C
      PX24 = MSGGRD
      GOTO 999
C
C.....IF J = 2 THERE ARE TWO OPTIONS. WE CAN ESTIMATE IN THE USUAL
C.....MANNER OR WE CAN CHOOSE NOT TO DIVIDE BY THE SUM OF THE WEIGHTS.
C
C.....IF IADJQP = 1 WE WILL NOT DIVIDE BY THE SUM OF THE WEIGHTS IF
C.....THERE ARE ONLY 2 QUADRANTS WITH OBSERVED PRECIPITATION, AND
C.....THE QUADRANTS ARE ADJACENT. IF IADJQP = 2, WE WILL NOT DIVIDE
C.....BY THE SUM OF THE WEIGHTS IF THERE ARE ONLY TWO QUADRANTS WITH
C.....OBSERVED PRECIPITATION, REGARDLESS OF WHETHER THE QUADRANTS
C.....ARE ADJACENT OR NOT.
C
  300 IF(IADJQP .EQ. 2) GOTO 320
C
C.....CHECK FOR ADJACENT QUADRANTS.
C
      IF((NSQUAD(1) .GT. 0) .AND. (NSQUAD(3) .GT. 0)) GOTO 360
      IF((NSQUAD(2) .GT. 0) .AND. (NSQUAD(4) .GT. 0)) GOTO 360
C
C.....THERE IS OBSERVED PRECIPITATION ONLY IN TWO ADJACENT QUADRANTS.
C.....SEE IF WE ARE NOT GOING TO DIVIDE BY THE SUM OF THE WEIGHTS.
C
      IF(IADJQP .EQ. 1) GOTO 320
      GOTO 360
C
C.....THIS IS THE ROUTINE WHERE ESTIMATES ARE DEVELOPED BY NOT DIVIDING
C.....BY THE SUM OF THE WEIGHTS.
C
  320 P = 0
      DO 340 IQUAD = 1, 4
      P = P + PQUAD(IQUAD)
  340 CONTINUE
C
C.....MAKE THE ESTIMATE A NEGATIVE NUMBER.
C
C.....IF THE ESTIMATED PRECIPITATION IS LESS THAN .01 INCH, THEN
C.....JUST LEAVE THE GRID POINT PRECIPITATION AS MISSING.
C
      IF(P .LT. 1) GOTO 400
      PX24 = -P
C
C.....CHECK THE ESTIMATE JUST MADE. DO NOT ALLOW IT TO EXCEED THE
C.....MAGNITUDE OF THE CLOSEST ESTIMATOR AND THE AVERAGE OF ALL THE
C.....ESTIMATORS.
C
      CALL GESTCK(PX24, MPCAVG, MPCLRG)
      GOTO 999
C
C.....THIS IS THE USUAL ESTIMATION PROCEDURE. THE ESTIMATE IS THE SUM
C.....OF THE WEIGHTED PRECIPITATION DIVIDED BY THE SUM OF THE WEIGHTS.
C
  360 P = 0
      WT = 0.0
      DO 380 IQUAD = 1, 4
      P = P + PQUAD(IQUAD)
      WT = WT + WQUAD(IQUAD)
  380 CONTINUE
C
      PCPN = P
C
C.....MAKE THE ESTIMATE A NEGATIVE NUMBER.
C
      PX24 = (PCPN/WT + 0.5)
C
C.....IF THE ESTIMATE IS LESS THAN .01 INCH, LEAVE THE GRID POINT
C.....PRECIPITATION AS MISSING.
C
      IF(PX24 .LT. 1) GOTO 400
      PX24 = -PX24
C
C.....CHECK THE ESTIMATE JUST MADE. DO NOT ALLOW IT TO EXCEED THE
C.....MAGNITUDE OF THE CLOSEST ESTIMATOR AND THE AVERAGE OF ALL THE
C.....ESTIMATORS.
C
      CALL GESTCK(PX24, MPCAVG, MPCLRG)
      GOTO 999
C
C.....SET THE GRID POINT PRECIPITATION TO MISSING.
C
  400 PX24 = MSGGRD
C
  999 IF(IPTRCE .LT. 8) GOTO 998
      WRITE(IOPDBG,902) PX24
      WRITE(IOPDBG,901)
C
  998 RETURN
      END
