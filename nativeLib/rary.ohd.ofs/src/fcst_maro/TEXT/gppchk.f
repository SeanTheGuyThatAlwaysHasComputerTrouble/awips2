C MEMBER GPPCHK
C  (from old member PPGPPCHK)
C
      SUBROUTINE GPPCHK(PP24, PPVR, PTVR, NUM6)
C
C.....THIS IS A DATA CHECKING SUBROUTINE ON THE PRECIPITATION DATA, BOTH
C.....DAILY (24 HOURLY) AND LESS THAN 24 HOURLY. THIS IS DONE PRIOR TO D
C.....DATA PREPARATION OF THE PRECIPITATON DATA, AS WE CAN USE ONLY 24 H
C.....HOURLY OR SIX HOURLY DATA.
C
C.....THE ARGUMENT LIST IS AS FOLLOWS:
C
C.....PP24   - THE 24-HOURLY PRECIPITATION DATA ARRAY.
C.....PPVR   - THE LESS THAN 24-HOUR PRECIPITATION DATA ARRAY.
C.....PTVR   - THE PPVR POINTERS ARRAY.
C.....NUM6   - THE NUMBER OF PPVR ELEMENTS THAT WAS RETURNED FROM
C.....         THE PPDB.
C
C.....JERRY M. NUNN, WGRFC FT. WORTH       MAY 1986
C
C
      INTEGER*2 PP24(1), PPVR(1), PTVR(1)
C
      DIMENSION SNAME(2)
C
      INCLUDE 'common/where'
      INCLUDE 'common/pudbug'
      INCLUDE 'gcommon/gsize'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_maro/RCS/gppchk.f,v $
     . $',                                                             '
     .$Id: gppchk.f,v 1.1 1995/09/17 19:02:17 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SNAME, PPVRP /4hGPPC, 4hHK  , 4hPPVR/
C
  900 FORMAT(1H0, '*** GPPCHK ENTERED ***')
  901 FORMAT(1H0, '*** EXIT GPPCHK ***')
  902 FORMAT(1H0, '*** NOTE ***  PPVR ARRAY LOCATIONS ', I4, ' THRU ',
     * I4, ' DO NOT CONTAIN SIX-HOURLY DATA', /, 5X, 'THE TIME INTERVAL
     *FOR THE DATA IS ', I2, ' HOURS.')
C
C.....SET THE WHERE COMMON BLOCK.
C
      IOPNUM = -1
      OPNAME(1) = SNAME(1)
      OPNAME(2) = SNAME(2)
C
      IBUG = IPBUG(PPVRP)
C
C.....WRITE MESSAGE IF DEBUG IS ON.
C
      IF(IPTRCE .GE. 3) WRITE(IOPDBG,900)
C
C.....CHECK THE PRECIPITATION DATA...BY DOING THE FOLLOWING:
C
C.....(1)  IF ONLY ONE SIX HOUR AMOUNT IS MISSING, ESTIMATE ITS AMOUNT
C.....FROM THE DIFFERENCE BETWEEN THE 24 HOUR AMOUNT (PROVIDED A 24 HOUR
C.....AMOUNT IS AVAILABLE) AND THE SIX HOUR AMOUNT.
C
C.....(2)  IF ALL 4 SIX HOUR AMOUNTS ARE AVAILABLE AND ONLY TTHE 24 HOUR
C.....AMOUNT IS MISSING, ESTIMATE THE 24 HOUR TOTAL FROM THE SUM OF THE
C.....SIX HOUR AMOUNTS.
C
C......JP IS THE STARTING LOCATION TO LOOK AT IN THE PRECIPITATON ARRAY.
C......THERE ARE 24/(TIME INTERVAL OF DATA) SLOTS FOR EACH LESS THAN
C.....24 HOUR STATION, AND THE TIME INTERVAL CAN BE VARIABLE. THEREFORE,
C.....ONE MUST KEEP TRACK OF THE STARTING LOCATION OF THE PRECIPITATION
C.....DATA WITH EACH STATION.
C
      JP = 1
C
C.....LOOP THRU THE TOTAL NUMBER OF LESS THAN 24 HOURLY STATIONS.
C
      DO 170 IP = 1, NUM6, 4
C
C.....GET THE TIME INTERVAL IN THE PPVR POINTER ARRAY.
C
      LX = PTVR(IP+2)
C
C.....GET ENDING ARRAY ADDRESS FOR THIS STATION...REMEMBERING HOW MANY
C.....INDIVIDUAL REPORTS ARE STORED FOR THIS STATION.
C
      KP = JP - 1 + 24/LX
C
C.....IF THE TIME INTERVAL IS NOT SIX HOURLY, DO NOT BOTHER WITH
C.....PROCESSING THIS STATION.
C
      IF(LX .NE. 6) GOTO 140
C
C.....INITIALIZE NUMBER OF MISSING REPORTS AND LOCATION OF MISSING
C.....REPORT FOR THIS STATION.
C
      MISS = 0
      LOCN = 0
C
C.....CHECK FOR MISSING REPORT AND GET LOCATION.
C
      DO 100 NP = JP, KP
      IF(PPVR(NP) .NE. MSNG6) GOTO 100
      MISS = MISS + 1
      LOCN = NP
  100 CONTINUE
C
C.....IF MORE THAN ONE VALUE WAS MISSING, DON'T TRY TO ESTIMATE ANYTHING
C
      IF(MISS .EQ. 0) GOTO 120
      IF(MISS .GT. 1) GOTO 160
C
C.....ESTIMATE THE MISSING SIX HOUR VALUE IF ONLY 1 VALUE IS MISSING, AN
C.....AND A 24 HOUR AMOUNT IS AVAILABLE.
C
      NX = PTVR(IP + 1)
      K  = NX/5 + 1
C
C
      ISUM = 0
C
      DO 110 NP = JP, KP
      IF(NP .EQ. LOCN) GOTO 110
      ISUM = ISUM + PPVR(NP)
  110 CONTINUE
C
C
C.....ESTIMATE THE SIX HOUR AMOUNT.  REMEMBER...THE 24 HOUR PRECIP ON
C.....THE PPDB IS ENCODED.
C
      IF(PP24(K) .EQ. MSNG24) GOTO 160
      LP = PP24(K)/10 + 3000
      PPVR(LOCN) = LP - ISUM
C
C
C.....NONE OF THE SIX HOURLY DATA IS MISSING. CHECK IF THE 24 HOUR TOTAL
C.....IS AVAILABLE. IF IT IS NOT...THEN USE THE SUM OF THE SIX HOURLY
C.....AMOUNTS TO COMPUTE A 24 HOURLY AMOUNT.
C
  120 NX = PTVR(IP + 1)
      K  = NX/5 + 1
C
      IF(PP24(K) .NE. MSNG24) GOTO 160
C
      ISUM = 0
C
      DO 130 NP = JP, KP
      ISUM = ISUM + PPVR(NP)
  130 CONTINUE
C
      PP24(K) = ISUM*10 - 30000
      GOTO 160
C
C.....THE TIME INTERVAL IS NOT SIX HOURS. WRITE OUT A MESSAGE.
C
  140 IF(IBUG .EQ. 1) WRITE(IOPDBG,902) JP, KP, LX
C
C.....END OF LOOP. UPDATE STARTING LOCATION FOR THE NEXT STATION.
C.....RECALL, THE STATIONS CAN HAVE DIFFERENT TIME REPORTING INTERVALS.
C
  160 JP = KP + 1
C
  170 CONTINUE
C
C.....WRITE EXIT MESSAGE, IF ONE IS WANTED.
C
      IF(IPTRCE .GE. 3) WRITE(IOPDBG,901)
C
      RETURN
      END
