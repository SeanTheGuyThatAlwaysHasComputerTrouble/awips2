C MEMBER FWCLEX
C  (from old member FCFWCLEX)
C
C     DESC - THIS SUBROUTINE WRITES THE CALIBRATION FILES FOR THE
C     DESC - FORECAST COMPONENT EXECUTION PROGRAM.
C     DESC - IT IS CALLED FROM FCTSWT.
C     DESC - THIS ROUTINE WILL BE USED UNTIL THE PROCESSED DATA
C     DESC - FILES ARE IMPLEMENTED, AT WHICH TIME A ROUTINE
C     DESC - TO WRITE DATA TO THOSE FILES FOR THE FC EXECUTION
C     DESC - WILL BE WRITTEN.
C
C.............................
      SUBROUTINE FWCLEX(EXTL,D,W,LW,DTYPE,IDT,NPDT,UNITS,DIMS,IERR)
C.............................
C
C      THIS SUBROUTINE ORIGINALLY WRITTEN BY
C             GEORGE F SMITH - HRL - 5/19/80
C.............................
C
      INCLUDE 'common/fctime'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/where'
      INCLUDE 'common/ionum'
      INCLUDE 'common/readwt'
      COMMON/INTLRW/INTLRD,INTLWT
C.............................
C
      DIMENSION EXTL(1),D(1),W(LW)
      DIMENSION SBNAME(2),OLDOPN(2),FNAME(3),STAID(3),DESCRP(5)
      DIMENSION NDAYS(12)
C
      INTEGER YEAR
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_fdriv/RCS/fwclex.f,v $
     . $',                                                             '
     .$Id: fwclex.f,v 1.1 1995/09/17 19:08:34 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SBNAME/4HFWCL,4HEX  /
      DATA VMSNG/-999.0/,IPUNCH/0/
      DATA NDAYS/31,28,31,30,31,30,31,31,30,31,30,31/
      DATA DEBG/4HFWCL/
      IZERO=0
C
C.......................................................................
C
C         WHERE
C..............
C
      DO 5 I=1,2
      OLDOPN(I)=OPNAME(I)
    5 OPNAME(I)=SBNAME(I)
      IOLDOP=IOPNUM
      IOPNUM=0
C
C        TRACE LEVEL = 1
C.......................
C
      IF(ITRACE.GE.1)WRITE(IODBUG,900)
  900 FORMAT(1H0,17H **FWCLEX ENTERED)
C
      IBUG=0
      IF(IFBUG(DEBG).GT.0)IBUG=1
C
C.......................................................................
C
C     ZERO CALIBRATION R/W BOOKKEEPING SYSTEM
C
      INTLRD=0
      INTLWT=0
C
      DO 10 I=1,3
      FNAME(I)=EXTL(I)
      DESCRP(I)=EXTL(3+I)
   10 STAID(I)=EXTL(3+I)
      DESCRP(4)=DTYPE
      DESCRP(5)=UNITS
C
      IF(IBUG.GT.0)WRITE(IODBUG,902)FNAME,STAID,DESCRP
  902 FORMAT(1H0,10X,46HIN SUBROUTINE FWCLEX - WRITING TIME SERIES FOR/
     1   11X,12HFILE NAME = ,3A4,15H, STATION ID = ,3A4/
     2     11X,14HDESCRIPTION = ,5A4,1H.)
C
C....................
C
      NDDATA=LDARUN-IDARUN+1
      NVPD=(24/IDT)*NPDT
      IHRCK=IHRRUN
      IF(IHRCK.EQ.24)IHRCK=0
      NVDATA=NDDATA*NVPD - (IHRCK/IDT)*NPDT - ((24-LHRRUN)/IDT)*NPDT
      NDT=NVPD*31
C
      IF(IBUG.GT.0)WRITE(IODBUG,903)NDDATA,NVPD,NDT,NVDATA
  903 FORMAT(1H0,10X,31HNUMBER OF DAYS TO BE WRITTEN = ,I3/
     1   11X,27HNUMBER OF VALUES PER DAY = ,I3/
     2    11X,57HMAXIMUM NUMBER OF VALUES WRITTEN IN ONE CALL TO WTFILE
     3= ,  I5/11X,'TOTAL NUMBER OF VALUES WRITTEN ',
     4  '(NOT INCLUDING MISSING DATA SYMBOLS TO FILL OUT ',
     5  'WHOLE MONTHS) = ',I5)
C
      IF(LW.GE.NDT)GO TO 20
C
      WRITE(IPR,600)IDT,LW
  600 FORMAT(1H0,10X,44H**ERROR** THE TIME STEP FOR THE TIME SERIES ,
     1   15HBEING WRITTEN (,I2,07H HOURS)/
     2     11X,44HNEEDS MORE WORKING SPACE THAN IS AVAILABLE (,I5,
     3      25H LOCATIONS ARE AVAILABLE))
C
      CALL ERROR
      IERR=1
      GO TO 9999
C
   20 IDAX=IDARUN
      IHRX=12
      IF(IHRRUN.EQ.24)IDAX=IDAX+1
C
      CALL MDYH1(IDAX,IHRX,IMONTH,IDAY,IYEAR,IHOUR,NLSTZ,IZERO,TZC)
C
      CALL MDYH1(LDARUN,LHRRUN,LMONTH,LDAY,LYEAR,LHOUR,NLSTZ,IZERO,TZC)
C
      IF(IBUG.GT.0)WRITE(IODBUG,904)FNAME,DTYPE,DIMS,UNITS,IDT,
     1  IMONTH,IYEAR,LMONTH,LYEAR,STAID,DESCRP
  904 FORMAT(1H0,10X,47HABOUT TO CALL HEADER - ARGUMENTS IN CALL ARE --/
     1   11X,11HFNAME(3) = ,3A4/11X,08HDTYPE = ,A4/11X,07HDIMS = ,A4/
     2      11X,08HUNITS = ,A4/11X,06HIDT = ,I3/11X,15HIMONTH/IYEAR = ,I
     32,       1H/,I4/11X,15HLMONTH/LYEAR = ,I2,1H/,I4/
     4          11X,11HSTAID(3) = ,3A4/11X,12HDESCRP(5) = ,5A4,1H.)
C
      CALL HEADER(FNAME,DTYPE,DIMS,UNITS,IDT,1,IMONTH,IYEAR,
     1  LMONTH,LYEAR,STAID,1,DESCRP,1)
C
      IF(IBUG.GT.0)WRITE(IODBUG,905)
  905 FORMAT(1H0,10X,16HBACK FROM HEADER)
C
      IF(IERROR.EQ.0)GO TO 25
      IERROR=0
      IERR=1
      WRITE(IPR,601)
  601 FORMAT(1H0,10X,43H**ERROR** UNABLE TO SET UP HEADER FOR TIME ,
     1   07HSERIES ,3A4)
      CALL ERROR
      GO TO 9999
C
   25 LCMNTH=LMONTH
      IF(LMONTH.LT.IMONTH)LCMNTH=LCMNTH+12
C
      ICMNTH=IMONTH
      MONTH=IMONTH
      YEAR=IYEAR
C
      ILOCW=(IDAY-1)*NVPD + 1 + (IHRCK/IDT)*NPDT
      KOUNT=0
C
   30 IF(ICMNTH.GT.LCMNTH)GO TO 9999
C
      NLOCW=NDAYS(MONTH)
      IF(YEAR/4*4.EQ.YEAR.AND.MONTH.EQ.2)NLOCW=NLOCW+1
      NLOCW=NLOCW*NVPD
      IF(ICMNTH.EQ.LCMNTH)NLOCW = NLOCW - ((24-LHRRUN)/IDT)*NPDT
      IF(NVDATA-KOUNT.LT.NLOCW-ILOCW+1)
     1                NLOCW = ILOCW + NVDATA - KOUNT - 1
C
      LOCD=KOUNT + (IHRCK/IDT)*NPDT
      KOUNT=KOUNT+(NLOCW-ILOCW+1)
C
      J=0
C
      JXX=NLOCW-ILOCW+1
      IF(IBUG.GT.0)WRITE(IODBUG,906)ILOCW,NLOCW,LOCD,JXX,
     1  (D(LOCD+I),I=1,JXX)
  906 FORMAT(1H0,10X,44HABOUT TO TRANSFER PART OF D ARRAY TO W ARRAY/
     1   11X,28HVARIABLES OF INTEREST ARE --/
     2    11X,08HILOCW = ,I8,10H, NLOCW = ,I8,09H, LOCD = ,I8/
     3  11X,'THE ',I5,' VALUES COPIED FROM THE D ARRAY ARE'/
     4  (5X,10G10.3))
C
      DO 40 I=ILOCW,NLOCW
      J=J+1
   40 W(I)=D(LOCD+J)
C
      IF(ILOCW.EQ.1)GO TO 50
C
      ITEMP=ILOCW-1
      IF(IBUG.GT.0)WRITE(IODBUG,907)ITEMP
  907 FORMAT(1H0,10X,40HABOUT TO FILL BEGINNING OF W ARRAY WITH ,
     1   32HMISSING VALUE SYMBOLS - ITEMP = ,I8,1H.)
C
      DO 45 I=1,ITEMP
   45 W(I)=VMSNG
C
   50 IF(NLOCW.EQ.NDT)GO TO 60
C
      ITEMP=NLOCW+1
      IF(IBUG.GT.0)WRITE(IODBUG,908)ITEMP,NDT
  908 FORMAT(1H0,10X,34HABOUT TO FILL END OF W ARRAY WITH ,
     1   32HMISSING VALUE SYMBOLS - ITEMP = ,I8,08H, NDT = ,I8,1H.)
C
      DO 55 I=ITEMP,NDT
   55 W(I)=VMSNG
C
   60 IF(IBUG.GT.0)WRITE(IODBUG,909)DTYPE,IDT,MONTH,YEAR,NDT,
     1  (W(I),I=1,NDT)
  909 FORMAT(1H0,10X,39HABOUT TO CALL WTFILE - ARGUMENTS ARE --/
     1   11X,08HDTYPE = ,A4/11X,06HIDT = ,I3/
     2     11X,13HMONTH/YEAR = ,I2,1H/,I4/11X,06HNDT = ,I8/
     3  11X,'THE W ARRAY PASSED TO WTFILE IS'/(5X,10G10.3))
C
      CALL WTFILE(DTYPE,IDT,MONTH,YEAR,1,1,NDT,W,IPUNCH)
C
      IF(IBUG.GT.0)WRITE(IODBUG,910)
  910 FORMAT(1H0,10X,16HBACK FROM WTFILE)
C
      IF(IERROR.EQ.0)GO TO 65
      IERROR=0
      IERR=1
      WRITE(IPR,602)FNAME,MONTH,YEAR
  602 FORMAT(1H0,10X,47H**ERROR** UNABLE TO WRITE DATA FOR TIME SERIES ,
     1   3A4/23X,06HMONTH=,I3,07H, YEAR=,I5)
      CALL ERROR
      GO TO 9999
C
   65 ILOCW=1
      ICMNTH=ICMNTH+1
      MONTH=MONTH+1
      IF(MONTH.LE.12)GO TO 30
      MONTH=1
      YEAR=YEAR+1
      GO TO 30
C
 9999 IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      IF(ITRACE.GE.1)WRITE(IODBUG,901)
  901 FORMAT(1H0,14H **EXIT FWCLEX)
C
      RETURN
      END
