C MEMBER DRV122
C **********************************************************************
C
C  THIS SUBROUTINE IS PART OF THE TSFP PROGRAM VERSION 2
C  CREATED BY KONSTANTINE P. GEORGAKAKOS, NRC-NWS, SEPT-1982
C
C  THIS IS A MODIFIED VERSION OF THE SUBROUTINE HPCG OF THE
C  S S P  LIBRARY
C
C  IT SOLVES A SYSTEM OF FIRST ORDER ORDINARY GENERAL DIFFERENTIAL
C  EQUATIONS WITH GIVEN INITIAL VALUES.
C
C
       SUBROUTINE DVR122(PRMT,Y,DERY,NDIM,IHLF,FCT22,OUTP22,AUX)
C
       EXTERNAL FCT22
       EXTERNAL OUTP22
C
C...for debugging:
       COMMON /FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
C
       DIMENSION PRMT(5),Y(91),DERY(91),AUX(16,91)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_sssac/RCS/dvr122.f,v $
     . $',                                                             '
     .$Id: dvr122.f,v 1.2 2002/05/15 13:52:21 hank Exp $
     . $' /
C    ===================================================================
C
C
       IF (ITRACE .GT. 1) WRITE (IODBUG,990)
990    FORMAT(/10X,'** DVR122 ENTERED.')
C
       N=1
       IHLF=0
       X=PRMT(1)
       H=PRMT(3)
       PRMT(5)=0.
       DO 1 I=1,NDIM
       AUX(16,I)=0.
       AUX(15,I)=DERY(I)
 1    AUX(1,I)=Y(I)
       IF(H*(PRMT(2)-X))3,2,4
C
C  ERROR RETURNS
C
 2    IHLF=12
       GO TO 4
 3    IHLF=13
C
C  COMPUTATION OF DERY FOR STARTING VALUES
C
 4    CALL FCT22(X,Y,DERY)
C
C  RECORDING OF STARTING VALUES
C
       CALL OUTP22 (X,Y,DERY,IHLF,NDIM,PRMT)
       IF(PRMT(5))6,5,6
 5    IF(IHLF)7,7,6
 6    IF (ITRACE .GT. 1) WRITE (IODBUG,991)
991   FORMAT(/10X,'** EXIT DVR122.')
      RETURN
 7    DO 8 I=1,NDIM
 8    AUX(8,I)=DERY(I)
C
C  COMPUTATION OF AUX(2,I)
C
       ISW=1
       GO TO 100
C
C
C
 9    X=X+H
       DO 10 I=1,NDIM
 10   AUX(2,I)=Y(I)
C
C  INCREMENT H IS TESTED BY MEANS OF BISECTION
C
 11   IHLF=IHLF+1
       X=X-H
       DO 12 I=1,NDIM
 12   AUX(4,I)=AUX(2,I)
       H=.5*H
       N=1
       ISW=2
       GO TO 100
C
 13   X=X+H
       CALL FCT22 (X,Y,DERY)
       N=2
       DO 14 I=1,NDIM
       AUX(2,I)=Y(I)
 14   AUX(9,I)=DERY(I)
       ISW=3
       GO TO 100
C
C  COMPUTATION OF TEST VALUE DELT
C
 15   DELT=0.
       DO 16 I=1,NDIM
 16   DELT=DELT+AUX(15,I)*ABS(Y(I)-AUX(4,I))
       DELT=.06666667*DELT
       IF(DELT-PRMT(4))19,19,17
 17   IF(IHLF-10)11,18,18
C
C  NO SATISFACTORY ACCURACY AFTER 10 BISECTIONS
C
 18   IHLF=11
      X=X+H
      GO TO 4
C
C  THERE IS SATISFACTORY ACCURACY AFTER LESS THAN 11 BISECTIONS
C
 19   X=X+H
       CALL FCT22 (X,Y,DERY)
       DO 20 I=1,NDIM
       AUX(3,I)=Y(I)
 20   AUX(10,I)=DERY(I)
       N=3
       ISW=4
       GO TO 100
C
 21   N=1
       X=X+H
       CALL FCT22 (X,Y,DERY)
       X=PRMT(1)
       DO 22 I=1,NDIM
       AUX(11,I)=DERY(I)
 22   Y(I)=AUX(1,I)+H*(.375*AUX(8,I)+.7916667*AUX(9,I)
     *-.2083333*AUX(10,I)+.04166667*DERY(I))
 23   X=X+H
       N=N+1
       CALL FCT22 (X,Y,DERY)
       CALL OUTP22 (X,Y,DERY,IHLF,NDIM,PRMT)
       IF(PRMT(5))6,24,6
 24   IF(N-4)25,200,200
 25   DO 26 I=1,NDIM
       AUX(N,I)=Y(I)
 26   AUX(N+7,I)=DERY(I)
       IF(N-3)27,29,200
C
 27   DO 28 I=1,NDIM
       DELT=AUX(9,I)+AUX(9,I)
       DELT=DELT+DELT
 28   Y(I)=AUX(1,I)+.3333333*H*(AUX(8,I)+DELT+AUX(10,I))
       GO TO 23
C
 29   DO 30 I=1,NDIM
       DELT=AUX(9,I)+AUX(10,I)
       DELT=DELT+DELT+DELT
 30   Y(I)=AUX(1,I)+.375*H*(AUX(8,I)+DELT+AUX(11,I))
       GO TO 23
C
C  THE FOLLOWING PART OF THE SUBROUTINE COMPUTES BY MEANS OF RUNGE-
C  KUTTA METHOD STARTING VALUES FOR THE NOT SELF-STARTING PREDICTOR
C  -CORRECTOR METHOD
C
 100  DO 101 I=1,NDIM
       Z=H*AUX(N+7,I)
       AUX(5,I)=Z
 101  Y(I)=AUX(N,I)+.4*Z
C
C  Z IS AN AUXILIARY STORAGE LOCATION
C
       Z=X+.4*H
       CALL FCT22 (Z,Y,DERY)
       DO 102 I=1,NDIM
       Z=H*DERY(I)
       AUX(6,I)=Z
 102  Y(I)=AUX(N,I)+.2969776*AUX(5,I)+.1587596*Z
C
       Z=X+.4557372*H
       CALL FCT22 (Z,Y,DERY)
       DO 103 I=1,NDIM
       Z=H*DERY(I)
       AUX(7,I)=Z
 103  Y(I)=AUX(N,I)+.2181004*AUX(5,I)-3.050965*AUX(6,I)+3.832865*Z
C
       Z=X+H
       CALL FCT22 (Z,Y,DERY)
       DO 104 I=1,NDIM
 104  Y(I)=AUX(N,I)+.1747603*AUX(5,I)-.5514807*AUX(6,I)
     *+1.205536*AUX(7,I)+.1711848*H*DERY(I)
       GO TO (9,13,15,21),ISW
C
C  POSSIBLE BREAK POINT FOR LINKAGE
C
C  STARTING VALUES ARE COMPUTED
C  NOW START HAMMINGS MODIFIED PREDICTOR-CORRECTOR METHOD
C
 200  ISTEP=3
 201  IF(N-8)204,202,204
C
C  N=8 CAUSES THE ROWS OF AUX TO CHANGE THEIR STORAGE LOCATIONS
C
 202  DO 203 N=2,7
       DO 203 I=1,NDIM
       AUX(N-1,I)=AUX(N,I)
 203  AUX(N+6,I)=AUX(N+7,I)
       N=7
C
C  N LESS THAN 8 CAUSES N+1 TO GET N
C
 204  N=N+1
C
C  COMPUTATION OF NEXT VECTOR Y
C
       DO 205 I=1,NDIM
       AUX(N-1,I)=Y(I)
 205  AUX(N+6,I)=DERY(I)
       X=X+H
 206  ISTEP=ISTEP+1
       DO 207 I=1,NDIM
       DELT=AUX(N-4,I)+1.333333*H*(AUX(N+6,I)+AUX(N+6,I)-AUX(N+5,I)+
     *AUX(N+4,I)+AUX(N+4,I))
      Y(I)=DELT-.9256198*AUX(16,I)
 207  AUX(16,I)=DELT
C
C  PREDICTOR IS NOW GENERATED IN ROW 16 OF AUX, MODIFIED PREDICTOR
C  IS GENERATED IN Y.  DELT MEANS AN AUXILIARY STORAGE
C
      CALL FCT22 (X,Y,DERY)
C
C  DERIVATIVE OF MODIFIED PREDICTOR IS GENERATED IN DERY
C
      DO 208 I=1,NDIM
      DELT=.125*(9.*AUX(N-1,I)-AUX(N-3,I)+3.*H*(DERY(I)+AUX(N+6,I)+
     *AUX(N+6,I)-AUX(N+5,I)))
      AUX(16,I)=AUX(16,I)-DELT
 208  Y(I)=DELT+.07438017*AUX(16,I)
C
C  TEST WHETHER H MUST BE HALVED OR DOUBLED
C
      DELT=0.
      DO 209 I=1,NDIM
 209  DELT=DELT+AUX(15,I)*ABS(AUX(16,I))
      IF(DELT-PRMT(4))210,222,222
C
C  H MUST NOT BE HALVED.  THAT MEANS THAT Y(I) ARE GOOD
C
 210  CALL FCT22 (X,Y,DERY)
      CALL OUTP22 (X,Y,DERY,IHLF,NDIM,PRMT)
      IF(PRMT(5))212,211,212
 211  IF(IHLF-11)213,212,212
C
 212  IF (ITRACE .GT. 1) WRITE (IODBUG,991)
      RETURN
C
 213  IF(H*(X-PRMT(2)))214,212,212
 214  IF(ABS(X-PRMT(2))-.1*ABS(H))212,215,215
 215  IF(DELT-.02*PRMT(4))216,216,201
C
C  H COULD BE DOUBLED IF ALL NECESSARY PRECEEDING VALUES ARE
C  AVAILABLE
C
 216  IF(IHLF)201,201,217
 217  IF(N-7)201,218,218
 218  IF(ISTEP-4)201,219,219
 219  IMOD=ISTEP/2
      IF(ISTEP-IMOD-IMOD)201,220,201
 220  H=H+H
      IHLF=IHLF-1
      ISTEP=0
      DO 221 I=1,NDIM
      AUX(N-1,I)=AUX(N-2,I)
      AUX(N-2,I)=AUX(N-4,I)
      AUX(N-3,I)=AUX(N-6,I)
      AUX(N+6,I)=AUX(N+5,I)
      AUX(N+5,I)=AUX(N+3,I)
      AUX(N+4,I)=AUX(N+1,I)
      DELT=AUX(N+6,I)+AUX(N+5,I)
      DELT=DELT+DELT+DELT
 221  AUX(16,I)=8.962963*(Y(I)-AUX(N-3,I))-3.361111*H*(DERY(I)+DELT
     *+AUX(N+4,I))
      GO TO 201
C
C  H MUST BE HALVED
C
 222  IHLF=IHLF+1
      IF(IHLF-10)223,223,210
 223  H=.5*H
      ISTEP=0
      DO 224 I=1,NDIM
      Y(I)=.00390625*(80.*AUX(N-1,I)+135.*AUX(N-2,I)+40.*AUX(N-3,I)+
     *AUX(N-4,I))-.1171875*(AUX(N+6,I)-6.*AUX(N+5,I)-AUX(N+4,I))*H
      AUX(N-4,I)=.00390625*(12.*AUX(N-1,I)+135.*AUX(N-2,I)+
     *108.*AUX(N-3,I)+AUX(N-4,I))-.0234375*(AUX(N+6,I)+18.*AUX(N+5,I)-
     *9.*AUX(N+4,I))*H
      AUX(N-3,I)=AUX(N-2,I)
C
 224  AUX(N+4,I)=AUX(N+5,I)
      X=X-H
      DELT=X-(H+H)
      CALL FCT22 (DELT,Y,DERY)
      DO 225 I=1,NDIM
      AUX(N-2,I)=Y(I)
      AUX(N+5,I)=DERY(I)
 225  Y(I)=AUX(N-4,I)
      DELT=DELT-(H+H)
      CALL FCT22 (DELT,Y,DERY)
      DO 226 I=1,NDIM
      DELT=AUX(N+5,I)+AUX(N+4,I)
      DELT=DELT+DELT+DELT
C
      AUX(16,I)=8.962963*(AUX(N-1,I)-Y(I))-3.361111*H*(AUX(N+6,I)+DELT
     *+DERY(I))
 226  AUX(N+3,I)=DERY(I)
      GO TO 206
      END
