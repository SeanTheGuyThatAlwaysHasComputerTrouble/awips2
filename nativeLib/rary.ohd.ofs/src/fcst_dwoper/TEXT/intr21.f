C MEMBER INTR21
C  (from old member FCINTR21)
C DESC -- THIS SUBROUTINE COMPUTES COEFFICIENTS USED IN NEWTON-RALPHSON
C DESC -- ITERATIVE SOLUTION OF UNSTEADY FLOW EQUATIONS
C                             LAST UPDATE: 03/28/94.23:45:10 BY $WC30JL
C
C
C @PROCESS LVL(77)
C
      SUBROUTINE INTR21(Z,PO,NQL,NRCM1,C,KU,NB,QJ,NJUN,NUMLAD,NCM,
     1  QD,QU,YD,YU,DDX,FKC,CHCTW,LAD,POOLT,LOPLT,ITWT,LOIWT,NWJX,D,NWJ,
     2  WF,ATF,NYQ,KD,QL,LOQL,T1,NDIV,DIV,LODIV,QLSTR,LOQLS,QLEVE,QLI,
     3  DVI,PLTI,IWTI,POLTAR,NUMRCP,IRC1,LRC1,LLDJ,K6,K7,K9)
C
C           THIS SUBROUTINE WAS WRITTEN ORIGINALLY BY:
C           DR. DANNY FREAD   HRL   APRIL 1978
C
C           THIS SUBROUTINE WAS MODIFIED TO MEET VER. NO. 5 STANDARDS
C           OF THE NWSRFS BY:
C           JANICE LEWIS      HRL   NOVEMBER,1982     VERSION NO. 1
C
C   *****   UPDATED 03/10/88 BY JANICE LEWIS TO ALLOW WEIR-TYPE LOCK*DAM
C           STRUCTURES TO BE USED
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
      COMMON/XS21/LOAS,LOBS,LOHS,LOASS,LOBSS,LOHSS,LONSS,LONS1
      COMMON/WQ21/LOBBL,LOHFL,LOHML,LOHWH,LONWJ,LOTFL,LOWC,LONJX,LOKLF,
     1            LOTTL
      COMMON/CM21/LONYQ,LONM1,LONCM,LOCM,LOYCM
      COMMON/DV21/LONDV,LOLDV,LZIDV
      COMMON/OX21/LOFKC,LONT1,LONQL,LOLQ,LZRCP,LZQQD,LZYQD,LONT
      COMMON/M121/N,NU,NS1,JN,JJ,KIT,G,DT,TT,TIMF,F1,GZN,NYQD
      COMMON/M621/KTIME,DHF,J1,KX
      COMMON/SS21/NCS,A,B,DB,R,DR,AT,BT,NCSS,P,DP,ZH,NP,NPEND
      COMMON/M3221/KTERM,KPL,KPL2,JNK,TE,TM,KITPR
      COMMON/NT21/NIT,SF1
      COMMON/M3421/NCT,ICD,ITMAX
      COMMON/LCK21/LOCK,LOCKK
      COMMON/IVRSN21/IVER
C
      DIMENSION PO(*),NQL(*),NCM(*),NRCM1(*),KD(*),SNAME(2)
      DIMENSION C(*),D(4,*),KU(*),NB(*),QD(*),QJ(*),QU(*)
      DIMENSION Z(*),WF(*),YD(*),YU(*),ATF(*),DDX(*),FKC(*)
      DIMENSION NJUN(*),NWJ(*),NWJX(*),NUMLAD(*),CHCTW(*),QLEVE(*)
      DIMENSION LAD(*),POOLT(*),LOPLT(*),ITWT(*),LOIWT(*),QLSTR(*)
      DIMENSION QL(*),LOQL(*),T1(*),NDIV(*),DIV(*),LODIV(*),LOQLS(*)
      DIMENSION QLI(*),DVI(*),PLTI(*),IWTI(*),POLTAR(*),NUMRCP(*)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_dwoper/RCS/intr21.f,v $
     . $',                                                             '
     .$Id: intr21.f,v 1.3 1998/07/06 11:53:30 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA SNAME/4HINTR,4H21  /
C
C
      CALL FPRBUG(SNAME,1,21,IBUG)
C
      J=JJ
      LOCK=0
      JJ1=J-1
      IRC=IRC1
      LRC=LRC1
      L11=LCAT21(1,1,NB)-1
      LNB=LCAT21(1,J,NB)-1
      LNM1=LCAT21(1,J,NRCM1)-1
      LNWJ=LCAT21(1,J,NWJ)-1
      LLAD=LCAT21(1,J,NUMLAD)-1
      LLDJ1=1
      IF(JJ1.GT.0) THEN
        DO 5 J1J=1,JJ1
          NUM=NUMLAD(J1J)
          IF(NUM.EQ.0) GO TO 5
          LLD1=LCAT21(1,J1J,NUMLAD)-1
          DO 3 LJ1=1,NUM
            IF(IVER.LT.3) LLDJ1=LLD1+LJ1
            IF(LAD(LLDJ1).GT.0.AND.CHCTW(LLD1+LJ1).GT.0.) LOCK=LOCK+1
            IF(IVER.GE.3) LLDJ1=LLDJ1+2
    3     CONTINUE
    5   CONTINUE
      ENDIF
   10 IF(KU(J)-1) 20,20,30
   20 L1=1
      L2=2
      L3=3
      L4=4
      GO TO 40
   30 L1=2
      L2=1
      L3=4
      L4=3
   40 IF(IBUG.EQ.1.AND.JNK.GE.2.AND.NWJ(J).NE.0) WRITE(IODBUG,50)
   50 FORMAT(/)
      DO 310 IK=1,NS1
      I1=IK
      I=IK
      IR=IK+1
      LIRJ=IR+LNB
      LIJ=I+LNB
      IF(IK-1) 60,60,70
   60 II=2
      GO TO 80
   70 II=II2+1
   80 II2=II+1
      DO 180 K=1,4
      GO TO (90,100,110,120),K
   90 Y=YD(LIJ)
      IS=I
      YDL=Y
      GO TO 130
  100 Y=YU(LIJ)
      IS=I
      YUL=Y
      GO TO 130
  110 Y=YD(LIRJ)
      YDR=Y
      IS=IR
      GO TO 130
  120 Y=YU(LIRJ)
      IS=IR
      YUR=Y
  130 CALL SECT21(PO(LOAS),PO(LOBS),PO(LOHS),PO(LOASS),PO(LOBSS),
     1 PO(LOHSS),PO(LONSS),PO(LONS1),NB,J,IS,Y,K7,K9)
      GO TO (140,150,160,170),K
  140 ATDL=AT
      BTDL=BT
      ADL=A
      BDL=B
      DBDL=DB
      PDL=P
      GO TO 180
  150 AUL=A
      BUL=B
      DBUL=DB
      ATUL=AT
      BTUL=BT
      PUL=P
      DPUL=DP
      GO TO 180
  160 ATDR=AT
      BTDR=BT
      ADR=A
      BDR=B
      DBDR=DB
      PDR=P
      GO TO 180
  170 AUR=A
      BUR=B
      BTUR=BT
      ATUR=AT
      DBUR=DB
      PUR=P
      DPUR=DP
  180 CONTINUE
      DQL=0.
      DQR=0.
      IF(A.LE.0.0.OR.B.LE.0.0) NIT=1
      IF(A.LE.0.0.OR.B.LE.0.0) GO TO 500
C
      IF(NDIV(J).EQ.0) GO TO 185
      NDV=NDIV(J)
      CALL DVRG21(NDV,MDIV,NQL,PO(LOLQ),QL,LOQL,PO(LOLDV),DIV,LODIV,
     1 QU,QD,QLU,QLD,QLSTR,LOQLS,Z(LZIDV),I1,DDX,T1,QLI,DVI,NB,NDIV)
      QLV=0.0
      QLVD=0.0
      IF(MDIV.EQ.1) GO TO 210
  185 IF(NQL(J)) 190,190,200
  190 QLU=0.
      QLD=0.
      QLV=0.
      QLVD=0.
      GO TO 210
  200 TT=TT-DT/TIMF
      CALL INTP21(NU,TINP,IT1,IT2,T1)
      CALL LATQ21(NQL,PO(LOLQ),QL,LOQL,J,I1,TINP,IT1,IT2,QLU,QLV,QLI,TT)
      QLD=QLU
      QLVD=QLV
      TT=TT+DT/TIMF
      CALL INTP21(NU,TINP,IT1,IT2,T1)
      CALL LATQ21(NQL,PO(LOLQ),QL,LOQL,J,I1,TINP,IT1,IT2,QLU,QLV,QLI,TT)
C      WRITE(6,999) TT,I,QLU,QLD
C  999 FORMAT(3X,'TT    I       QLU       QLD='/ F5.0,I5,2F10.0)
  210 IF(NP.GE.1) GO TO 260
      IF(JN-2) 260,220,220
  220 IF(J-1) 230,230,260
  230 DO 240 K=2,JN
      KNB=LCAT21(1,K,NB)-1
      IF(I1-NJUN(K)) 260,250,240
  240 CONTINUE
      GO TO 260
  250 MN=NJUN(K)
      DXJ=DDX(MN+L11)
      M=NB(K)
      QLU=QJ(K)/DXJ
      QLD=QD(M+KNB)/DXJ
      IF(KD(K).EQ.2) QLU=0.0
      IF(KD(K).EQ.2) QLD=0.0
      Y=YU(M+KNB)
      CALL SECT21(PO(LOAS),PO(LOBS),PO(LOHS),PO(LOASS),PO(LOBSS),
     1 PO(LOHSS),PO(LONSS),PO(LONS1),NB,K,M,Y,K7,K9)
      QLV=QJ(K)/A*ATF(K)*QLU
      Y=YD(M+KNB)
      CALL SECT21(PO(LOAS),PO(LOBS),PO(LOHS),PO(LOASS),PO(LOBSS),
     1 PO(LOHSS),PO(LONSS),PO(LONS1),NB,K,M,Y,K7,K9)
      QLVD=QD(M+KNB)/A*ATF(K)*QLD
      GO TO 263
  260 CONTINUE
      IF(NWJ(J).EQ.0) GO TO 1100
      JM=1
      DO 1040 JK=2,JN
      IF(NWJ(JK).EQ.0) GO TO 1040
      IF(NWJ(JK).GT.0) JT=JK
 1040 CONTINUE
      NW=NWJ(J)
      DO 1050 L=1,NW
      IF(I1.NE.NWJX(L+LNWJ)) GO TO 1050
      CALL WERQ21(PO(LOHWH),PO(LOWC),NWJX,YU,YD,QLLU,QLLD,DQL,DQR,JM,JT,
     1L,DDX,PO(LOKLF),PO(LOTTL),PO(LOTFL),PO(LOBBL),PO(LOHFL),PO(LOHML),
     2NB,NWJ,QLUTOT)
      LWQ=LCAT21(I1,J,NB)
      QLEVE(LWQ)=QLUTOT
      DQL=DQL*F1*DDX(LIJ)
      DQR=DQR*F1*DDX(LIJ)
      GO TO 1200
 1050 CONTINUE
 1100 QLLU=0.
      QLLD=0.
 1200 QLU=QLU+QLLU
      QLD=QLD+QLLD
      QLV=0.
      QLVD=0.
  263 NRCM=NRCM1(J)
      DO 265 K = 1, NRCM
      IF (I.GE.NCM(K+LNM1)) GO TO 265
      IF (K.EQ.1) LL = 1
      IF (K.GT.1) LL = NCM(K-1+LNM1)
      LR = NCM(K+LNM1)
      GO TO 268
  265 CONTINUE
  268 IF (NYQ) 270,270,280
  270 YQU = 0.5*(YU(LL+LNB)+YU(LR+LNB))
      YQP = 0.5*(YD(LL+LNB)+YD(LR+LNB))
      GO TO 285
  280 YQU = 0.5*(QU(LL+LNB)+QU(LR+LNB))
      YQP = 0.5*(QD(LL+LNB)+QD(LR+LNB))
  285 CALL FRCT21(NRCM1,NCM,PO(LOCM),PO(LOYCM),
     1 J,I1,YQU,CMU,DCMU,K6)
      CALL FRCT21(NRCM1,NCM,PO(LOCM),PO(LOYCM),
     1 J,I1,YQP,CMD,DCMD,K6)
C      WRITE(IODBUG,9200)LIJ,DDX(LIJ),DT,TT
C 9200 FORMAT(1H0,3X,4HLIJ=,I5,5X,4HDDX=,F10.2,5X,3HDT=,F20.2,5X,3HTT=,
C     1 F5.1)
      FXT=0.5*DDX(LIJ)/DT
C
      NUM=NUMLAD(J)
      IF(NUM.EQ.0) GO TO 351
      DO 323 L=1,NUM
      IF(IVER.LT.3) LLDJ=L+LLAD
      IF(I1.EQ.IABS(LAD(LLDJ))) GO TO 327
  323 CONTINUE
      GO TO 351
  327 LDD=LAD(LLDJ)+1
      IF(LDD.GT.0) GO TO 328
C
C  RATING CURVE
      IRC=IRC+1
      NRCP=NUMRCP(IRC)
      YY=YU(I1+LNB)
      CALL RATC21(YY,QRT,DQRT,NRCP,Z(LZYQD+LRC),Z(LZQQD+LRC))
C     WRITE(6,9997) IRC,LRC,NRCP,I1,YY,QRT
C9997 FORMAT(2X,'IRC  LRC NRCP   I1        YY       QRT'/4I5,2F10.2)
      LRC=LRC+NRCP
      C(II)=-(QU(I1+LNB)-QRT)
      D(L1,II)=-DQRT
      D(L2,II)=1.
      D(L3,II)=0.0001
      D(L4,II)=0.
      GO TO 349
  328 IF(CHCTW(L+LLAD).LT.0) GO TO 325
      LOCK=LOCK+1
C
C          GATED LOCK AND DAM
C
      CALL INTP21(NU,TINP,IT1,IT2,T1)
      LOWT=LOIWT(LOCK)-1
      LOPT=LOPLT(LOCK)-1
C     WRITE(6,8868) LOCK,IWTI(LOCK)
C8868 FORMAT(5X,5HLOCK=,I5,5X,6H IWTI=,20I5)
C     WRITE(6,8888) (ITWT(LOWT+IZ),IZ=1,NU)
C8888 FORMAT(5X,6H ITWT=,20I5)
C     WRITE(6,8989) (POOLT(LOPT+IZ),IZ=1,NU)
C8989 FORMAT(5X,6HPOOLT=,10F10.2)
      ITW=ITWT(IT2+LOWT)+TINP*(ITWT(IT1+LOWT)-ITWT(IT2+LOWT))
      IF(TT.LT.DHF)ITW=IWTI(LOCK)+(ITWT(1+LOWT)-IWTI(LOCK))*TT/DHF
C     WRITE(IODBUG,9980) TT,LDD,ITW,YU(LDD+LNB),CHCTW(L+LLAD),
C    1 IWTI(LOCK),ITWT(1+LOWT)
C9980 FORMAT(1H0,10X,'   TT  LDD  ITW     YU  CHCTW IWTI ITWT='/
C    1 10X,F5.1,2I5,2F7.2,2I5)
      IF(ITWT(IT2+LOWT).EQ.2.OR.ITWT(IT1+LOWT).EQ.2) ITW=2
      IF(ITW.EQ.1) GO TO 350
      IF(ITW.EQ.2) GO TO 329
      IF(IVER.LT.3) THEN
        LD2=LAD(LLDJ)+1
        LD1=L+LLAD
      ELSE
        LD2=LAD(LLDJ+1)
        LD1=(LLDJ+1)/2
      ENDIF
      LD22=IABS(LD2)
      IF(LD2.GT.0.AND.YU(LD2+LNB).GT.CHCTW(LD1)) GO TO 350
      IF(LD2.LT.0.AND.QU(LD22+LNB).GT.CHCTW(LD1)) GO TO 350
 329  POLT=POOLT(IT2+LOPT)+TINP*(POOLT(IT1+LOPT)-POOLT(IT2+LOPT))
      IF(TT.LT.DHF)POLT=PLTI(LOCK)+(POOLT(1+LOPT)-PLTI(LOCK))*TT/DHF
      C(II)=-(YU(I1+LNB)-POLT)
      D(L1,II)=1.
      D(L2,II)=0.
      D(L3,II)=0.
      D(L4,II)=0.
C     WRITE(6,9981) I1,LNB,YU(I1+LNB),POLT,PLTI(LOCK),L,LLAD,II,C(II)
C9981 FORMAT(2X,'I1=',I2,'  LNB=',I3,'  YU=',F10.2,'  POLT=',F10.2/
C    1 2X,'PLTI=',F10.2,3X,'L=',I3,3X,'LLAD=',I3,' II=',I3,' C=',F10.4)
      GO TO 349
C
C        NON-GATED LOCK & DAM (WEIR TYPE)
C
  325 CWW=-CHCTW(L+LLAD)
      HCC=POLTAR(L+LLAD)
      YUU=YU(LIJ)
      YUD=YU(LIRJ)
      IF(ABS(YUU-YUD).LT.0.05) GO TO 350
      SUB=1.0
      DSUBU=0.
      DSUBD=0.
      HR=0.
      IF(YUD.LE.HCC) GO TO 326
      HR=(YUD-HCC)/(YUU-HCC)
      IF(HR.LE.0.67) GO TO 326
      SUB=1.0-27.8*(HR-0.67)**3.
      DSUBU=-83.4*(HR-0.67)**2/(YUU-HCC)
      DSUBD=-83.4*(HR-0.67)**2*(YUD-HCC)/(YUU-HCC)**2
  326 QW=CWW*SUB*(YUU-HCC)**1.5
C     WRITE(6,9992) YUU,YUD,HCC,HR,CWW,SUB,QW
C9992 FORMAT(5X,'       YUU       YUD       HCC        HR       CWW
C    1  SUB        QW='/5X,7F10.2)
      C(II)=-(QU(LIJ)-QW)
      D(L1,II)=-QW*(1.5/(YUU-HCC)+DSUBU/SUB)
      D(L2,II)=1.0
      D(L3,II)=-QW*DSUBD/SUB
      D(L4,II)=0.
  349 C(II2)=-(QU(I1+LNB)-QU(LIRJ))
      D(L1,II2)=0.
      D(L2,II2)=1.
      D(L3,II2)=0.
      D(L4,II2)=-1.
C     WRITE(6,9982) LIRJ,QU(I1+LNB),QU(LIRJ)
C9982 FORMAT(5X,'LIRJ=',I3,3X,'QU(I1)=',F10.2,3X,'QU(LIRJ)=',F10.2)
      IF(IVER.GE.3) LLDJ=LLDJ+2
      GO TO 310
 350  IF(IVER.GE.3) LLDJ=LLDJ+2
  351 CONTINUE
      F=1.
      QAU=0.5*(QU(LIJ)+QU(LIRJ))
      QAD=0.5*(QD(LIJ)+QD(LIRJ))
      BAU=0.5*(BUL+BUR)
      BAD=0.5*(BDL+BDR)
      AAU=0.5*(AUL+AUR)
      AAD=0.5*(ADL+ADR)
      VDL=QD(LIJ)/ADL
      VDR=QD(LIRJ)/ADR
      FKCC=FKC(LIJ)
      IF(QAD.GE.0.0) GO TO 356
      IF(FKCC.GT.0.01) FKCC=-(2*FKCC+0.1)
      IF(FKCC.LT.0.01) FKCC=-(FKCC+0.1)/2.
  356 SE=0.5*AAD*FKCC*(VDR*VDR-VDL*VDL)
      IF(JNK.EQ.3.AND.IBUG.EQ.1) WRITE(IODBUG,9999)
 9999 FORMAT(/1H ,18X,3HYUL,8X,3HYUR,8X,3HAAU,8X,3HBAU,8X,3HQAU,8X,
     . 3HYQU,8X,3HCMU,8X,3HQLU,8X,3HQLV)
      IF (JNK.EQ.3.AND.IBUG.EQ.1) WRITE(IODBUG,411)YUL,YUR,AAU,BAU,QAU,
     1 YQU,CMU,QLU,QLV
      RAU=(AAU/BAU)**(2./3.)
      RAD=(AAD/BAD)**(2./3.)
      PRAUHL=1./3.*RAU*(BUL/AAU-DBUL/BAU)
      PRAUHR=1./3.*RAU*(BUR/AAU-DBUR/BAU)
      SXU=G*CMU*CMU*DDX(LIJ)*QAU*ABS(QAU)/(2.21*AAU*RAU*RAU)
      SXD=G*CMD*CMD*DDX(LIJ)*QAD*ABS(QAD)/(2.21*AAD*RAD*RAD)
      FKL=0
      FKR=0
      IF(I.EQ.LL) FKL=0.5*2*DCMU/CMU
      IF(IR.EQ.LR) FKR=0.5*2*DCMU/CMU
      PSXUHL=-SXU*(0.5*BUL/AAU+2.*PRAUHL/RAU-FKL*(1-NYQ))
      PSXUHR=-SXU*(0.5*BUR/AAU+2.*PRAUHR/RAU-FKR*(1-NYQ))
      PSXUQL=SXU*(1./QAU+FKL*NYQ)
      PSXUQR=SXU*(1./QAU+FKR*NYQ)
      HXU=YU(LIRJ)-YU(LIJ)
      HXD=YD(LIRJ)-YD(LIJ)
      IF(JNK.EQ.3.AND.IBUG.EQ.1) WRITE(IODBUG,9998)
 9998 FORMAT(1H0,18X,3HRAU,5X,6HPRAUHL,8X,3HSXU,5X,6HPSXUHL,5X,6HPSXUQL,
     . 8X,3HHXU,5X,6HPRAUHR,5X,6HPSXUHR,5X,6HPSXUQR)
      IF (JNK.EQ.3.AND.IBUG.EQ.1) WRITE(IODBUG,411)RAU,PRAUHL,SXU,
     1 PSXUHL,PSXUQL,HXU,PRAUHR,PSXUHR,PSXUQR
  411 FORMAT (1H ,10X,10(1X,F10.1))
      C(II)=-(F1*(QU(LIRJ)-QU(LIJ)-QLU*DDX(LIJ))+(1.-F1)*(QD(LIRJ)-
     1QD(LIJ)-QLD*DDX(LIJ))+FXT*(ATUL+ATUR-ATDL-ATDR))
      D(L1,II)=FXT*BTUL-DQL
      D(L2,II)=-F1
      D(L3,II)=FXT*BTUR-DQR
      D(L4,II)=F1
      IF(NCT.EQ.4) GO TO 360
      IF(JNK.EQ.3.AND.IBUG.EQ.1)
     1WRITE(IODBUG,9990) J,I,IR,II,C(II),F1,FXT,DDX(LIJ),QU(LIRJ),
     1 QD(LIRJ),QU(LIJ),QD(LIJ),YU(LIRJ),YD(LIRJ),YU(LIJ),YD(LIJ),
     2 QLU,QLD,ATUL,ATDL,ATUR,ATDR
 9990 FORMAT(1H0,10X,2HJ=,I2,5X,2HI=,I2,5X,3HIR=,I2,5X,3HII=,I2,5X,
     1 6HC(II)=,G15.9/1H0,10X,9HF1      =,G15.9,5X,9HFXT     =,G15.9,5X,
     2 9HDDX(LIJ)=,G15.9/1H ,10X,9HQU(LIRJ)=,G15.9,5X,9HQD(LIRJ)=,G15.9/
     3 1H ,10X,9HQU(LIJ) =,G15.9,5X,9HQD(LIJ) =,G15.9/1H ,10X,9HYU(LIRJ)
     4=,G15.9,5X,9HYD(LIRJ)=,G15.9/1H ,10X,9HYU(LIJ) =,G15.9,5X,9HYD(LIJ
     5) =,G15.9/1H ,10X,9HQLU     =,G15.9,5X,9HQLD     =,G15.9/1H ,10X,
     6 9HATUL    =,G15.9,5X,9HATDL    =,G15.9/1H ,10X,9HATUR    =,G15.9,
     7 5X,9HATDR    =,G15.9)
C
      IF(JNK.EQ.3.AND.IBUG.EQ.1)
     1WRITE(IODBUG,9995) L1,L2,L3,L4,II,D(L1,II),D(L2,II),D(L3,II),
     1 D(L4,II)
 9995 FORMAT(1H0,10X,3HL1=,I2,5X,3HL2=,I2,5X,3HL3=,I2,5X,3HL4=,I2,5X,
     1 3HII=,I2/1H ,10X,9HD(L1,II)=,G15.9/1H ,10X,9HD(L2,II)=,G15.9/1H ,
     2 10X,9HD(L3,II)=,G15.9/1H ,10X,9HD(L4,II)=,G15.9)
C
      C(II2)=-(FXT*(QU(LIJ)+QU(LIRJ)-QD(LIJ)-QD(LIRJ))+F1*(F*(QU(LIRJ)*
     1QU(LIRJ)/AUR-QU(LIJ)*QU(LIJ)/AUL)+G*AAU*HXU+SXU+(WF(J)*BAU-QLV)*
     2DDX(LIJ))+(1.-F1)*(F*(QD(LIRJ)*QD(LIRJ)/ADR-QD(LIJ)*QD(LIJ)/ADL)+
     3G*AAD*HXD+SXD+(WF(J)*BAD-QLVD)*DDX(LIJ))+SE)
C
      IF(JNK.EQ.3.AND.IBUG.EQ.1)
     1 WRITE(IODBUG,9991) J,I,IR,II2,C(II2),FXT,F1,F,DDX(LIJ),WF(J),G,
     2 QU(LIRJ),QD(LIRJ),QU(LIJ),QD(LIJ),HXU,HXD,SXU,SXD,SE,BAU,BAD,
     3 AAU,AAD,AUL,ADL,AUR,ADR,QLV,QLVD
 9991 FORMAT(1H0,10X,2HJ=,I2,5X,2HI=,I2,5X,3HIR=,I2,5X,4HII2=,I2,5X,
     1 7HC(II2)=,G15.9/1H0,10X,7HFXT   =,G15.9,5X,7HF1    =,G15.9,5X,
     2 7HF     =,G15.9/1H ,10X,7HDDX   =,G15.9,5X,7HWF    =,G15.9,
     3 5X,7HG     =,G15.9/1H ,10X,7HQUIR  =,G15.9,5X,7HQDIR  =,G15.9/
     4 1H ,10X,7HQUI   =,G15.9,5X,7HQDI   =,G15.9/1H ,10X,7HHXU   =,
     5 G15.9,5X,7HHXD   =,G15.9/1H ,10X,7HSXU   =,G15.9,5X,7HSXD   =,
     6 G15.9,5X,7HSE    =,G15.9/1H ,10X,7HBAU   =,G15.9,5X,7HBAD   =,
     7 G15.9/1H ,10X,7HAAU   =,G15.9,5X,7HAAD   =,G15.9/1H ,10X,
     8 7HAUL   =,G15.9,5X,7HADL   =,G15.9/1H ,10X,7HAUR   =,G15.9,5X,
     9 7HADR   =,G15.9/1H ,10X,7HQLV   =,G15.9,5X,7HQLVD  =,G15.9/1H )
C
      D(L1,II2)=F1*(QU(LIJ)*QU(LIJ)*F*BUL/AUL/AUL-G*AAU+G*HXU*0.5*BUL
     1+PSXUHL+0.5*WF(J)*DBUL*DDX(LIJ))
      D(L2,II2)=FXT+F1*(-2.*F*QU(LIJ)/AUL+PSXUQL)
      D(L3,II2)=F1*(-QU(LIRJ)*QU(LIRJ)*F*BUR/AUR/AUR+G*AAU+G*HXU*0.5*BUR
     1+PSXUHR+0.5*WF(J)*DBUR*DDX(LIJ))
      D(L4,II2)=FXT+F1*(2.*F*QU(LIRJ)/AUR+PSXUQR)
C
      IF(JNK.EQ.3.AND.IBUG.EQ.1)
     1WRITE(IODBUG,9996) II2,D(L1,II2),D(L2,II2),D(L3,II2),D(L4,II2)
 9996 FORMAT(1H0,10X,4HII2=,I3/1H ,10X,10HD(L1,II2)=,G15.9/1H ,10X,
     1 10HD(L2,II2)=,G15.9/1H ,10X,10HD(L3,II2)=,G15.9/1H ,10X,
     2 10HD(L4,II2)=,G15.9)
C
      GO TO 310
  360 C(II2)=-(F1*(G*AAU*HXU+SXU)+(1.-F1)*(G*AAD*HXD+SXD))
      D(L1,II2)=F1*(-G*AAU+G*HXU*0.5*BUL+PSXUHL)
      D(L2,II2)=F1*PSXUQL
      D(L3,II2)=F1*(G*AAU+G*HXU*0.5*BUR+PSXUHR)
      D(L4,II2)=F1*PSXUQR
  310 CONTINUE
C
  500 IF(ITRACE.EQ.1) WRITE(IODBUG,9000) SNAME
 9000 FORMAT(1H0,2H**,1X,2A4,8H EXITED.)
      RETURN
      END
