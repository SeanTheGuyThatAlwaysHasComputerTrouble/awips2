C MEMBER INCD21
C  (from old member FCINCD21)
C DESC -- THIS SUBROUTINE CALCULATES THE INITIAL CONDITIONS IF ONLY THE
C DESC -- DOWNSTREAM STAGE AND UPSTREAM DISCHARGE VALUES ARE GIVEN
C                             LAST UPDATE: 03/01/94.13:10:34 BY $WC30JL
C
C @PROCESS LVL(77)
C
      SUBROUTINE INCD21(KD,NB,YDI,QDI,NRT1,NT,X,STT,LOSTT,NJUN,YQD,
     1 QYQD,GZ,NQL,LQ,QLI,QLNAM,NDIV,LDIV,DVI)
C
C           THIS SUBROUTINE WAS WRITTEN ORIGINALLY BY:
C           DR. DANNY FREAD   HRL   APRIL 1978
C
C           THIS SUBROUTINE WAS MODIFIED TO MEET VER. NO. 5 STANDARDS
C           OF THE NWSRFS BY:
C           JANICE LEWIS      HRL   NOVEMBER,1982     VERSION NO. 1
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
      COMMON/M121/N,NU,NS1,JN,JJ,KIT,G,DT,TT,TIMF,F1,GZN,NYQD
      COMMON/XCED21/NBDXCD,NCMXCD,NFRXCD,NICXCD,NINXCD,NONXCD,MTXDV
C
      DIMENSION KD(*),NB(*),YDI(*),QDI(*),NRT1(*),SNAME(2)
      DIMENSION NT(*),X(*),STT(*),LOSTT(*),NJUN(*),YQD(*)
      DIMENSION QYQD(*),NQL(*),LQ(*),QLI(*),GZ(*),QLNAM(3,*)
      DIMENSION NDIV(*),LDIV(*),DVI(*)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_dwoper/RCS/incd21.f,v $
     . $',                                                             '
     .$Id: incd21.f,v 1.2 1996/01/16 11:33:50 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA SNAME/4HINCD,4H21  /
      DATA DUMM/4HDUMM/
C
C
      CALL FPRBUG(SNAME,1,21,IBUG)
C
      L11=LCAT21(1,1,NB)-1
      I11=LCAT21(1,1,NRT1)-1
      M11=LCAT21(1,1,NQL)-1
      N11=LCAT21(1,1,NDIV)-1
C
      IF(IBUG.EQ.0) GO TO 6
C
      DO 5 J=1,JN
      L1J=LCAT21(1,J,NB)-1
      NN=NB(J)
      WRITE(IODBUG,9998) J
      WRITE(IODBUG,9999) (YDI(I+L1J), I=1,NN)
      WRITE(IODBUG,9997)
      WRITE(IODBUG,9996) (QDI(I+L1J), I=1,NN)
C
 9998 FORMAT(1H0,50X,43HINITIAL CONDITIONS AS READ IN FOR RIVER NO.,I3/
     . 1H0,10X,3HYDI)
 9997 FORMAT(1H0,10X,3HQDI)
 9999 FORMAT(1H ,10X,10F10.2)
 9996 FORMAT(1H ,10X,10F10.0)
    5 CONTINUE
C
    6 DO 42 J=1,JN
      L1J=LCAT21(1,J,NB)-1
      I1J=LCAT21(1,J,NRT1)-1
      KS=0
      NN=NB(J)
C
C        WAS ONLY DOWNSTREAM WATER SURFACE ELEVATION READ?
C
      IF(ABS(YDI(NN+L11)).GT.0.01.AND.ABS(YDI(NN-1+L11)).LT.0.01)
     1 GO TO 50
C        WAS ONLY UPSTREAM WATER SURFACE ELEVATION READ?
C
      DO 10 I=1,NN
      IF(ABS(YDI(I+L1J)).LE.0.01)KS=KS+1
      IF(KS.EQ.NN-1)GO TO 20
   10 CONTINUE
      GO TO 50
C
C        INTERPOLATE BETWEEN GAGING STATIONS TO GET INITIAL WSEL
C
C        HOW MANY GAGING STATIONS?
C
   20 NRT=NRT1(J)
      IF(NRT.EQ.0) GO TO 50
C
      DO 40 I=1,NN
C
C        IS CROSS SECTION AT A GAGING STATION?
C
      DO 30 KK=1,NRT
      K=KK
      LOST=LOSTT(K+I1J)
      IF(I.EQ.NT(KK+I1J)) GO TO 32
      IF(I.LT.NT(KK+I1J)) GO TO 34
   30 CONTINUE
C
C        INTERPOLATION IS DONE BETWEEN THE LAST TWO SECTIONS
C
      IU=NT(K+I1J)
      ID=NB(J)
      GO TO 36
C
C        INITIAL WSEL IS EQUAL TO OBSERVED VALUE
C
   32 YDI(I+L1J)=STT(LOST)+GZ(K+I1J)
      GO TO 40
C
C        FIND PROPER NODES AND OBSERVED VALUE FOR INTERPOLATION
C
   34 IU=NT(K-1+I1J)
      ID=NT(K+I1J)
      ST=STT(LOST)+GZ(K+I1J)
      GO TO 38
C
C        OBSERVED VALUE AT TRIBUTARY CONFLUENCE IS THE AVERAGE WSEL
C        BETWEEN SECTIONS ABOVE AND BELOW IT
C
   36 IJ=NJUN(J)
      ST=0.5*(YDI(IJ+L11)+YDI(IJ+1+L11))
C
C        INTERPOLATE TO GET INITIAL WATER SURFACE ELEVATION
C
   38 YDI(I+L1J)=YDI(IU+L1J)+(X(I+L1J)-X(IU+L1J))/
     1(X(ID+L1J)-X(IU+L1J))*(ST-YDI(IU+L1J))
   40 CONTINUE
   42 CONTINUE
C
C        WRITE THE INITIAL WATER SURFACE ELEVATIONS
C
      IF(IBUG.EQ.0) GO TO 50
C
      DO 45 J=1,JN
      NN=NB(J)
      L1J=LCAT21(1,J,NB)-1
      WRITE(IODBUG,1) J
      WRITE(IODBUG,2) (YDI(I+L1J),I=1,NN)
   45 CONTINUE
    1 FORMAT(1H0,10X,7H(YDI(I,,I1,8H),I=1,N))
    2 FORMAT(1H ,10X,8F10.2)
    3 FORMAT(1H0,10X,7H(QDI(I,,I1,8H),I=1,N))
C
C        WERE INITIAL DISCHARGES READ IN?
C
   50 IF(QDI(2+L11).GT.0.01) GO TO 200
C
      IF(JN.EQ.1)GO TO 80
      LQL=NQL(1)
      LDV=NDIV(1)
C
C        ADD LATERAL FLOWS TO THE INITIAL DISCHARGES
C
      DO 70 K=2,JN
      NN=NB(K)
      NM=NN-1
      NQ=NQL(K)
      L1K=LCAT21(1,K,NB)-1
      I1K=LCAT21(1,K,NQL)-1
      M1K=LCAT21(1,K,NDIV)-1
      DO 65 I=1,NM
      QQL=0.
      IF(NQ.EQ.0) GO TO 64
C
C        FIND THE LOCATION OF THE LATERAL FLOW
C
      DO 63 L=1,NQ
      LL=LQ(L+I1K)
      IF(I.NE.LL) GO TO 63
      QQL=QLI(L+LQL)
      GO TO 64
   63 CONTINUE
C
C        ADD LATERAL FLOW
C
   64 QDI(I+1+L1K)=QDI(I+L1K)+QQL
   65 CONTINUE
      LQL=LQL+NQ
C
C         FIND THE LOCATION OF THE FLOW DIVERSION
C
      NDV=NDIV(K)
      IF(NDV.EQ.0) GO TO 70
      DO 68 I=1,NM
      QQL=0.
      DO 67 L=1,NDV
      LL=LDIV(L+M1K)
      IF(I.NE.LL) GO TO 67
C
C        COMPARE FLOW DIVERSION WITH LATERAL FLOW DIVERSION
C
      IF(NQ.EQ.0) GO TO 69
      DO 66 IM=1,NQ
      IMM=LQ(IM+I1J)
      IF(IMM.EQ.LL) GO TO 68
   66 CONTINUE
   69 QQL=QDI(I+1+L1K)*DVI(L+LDV)/100.
   67 CONTINUE
C
C        SUBTRACT DIVERTED FLOW
C
      QDI(I+1+L1K)=QDI(I+L1K)-QQL
C
   68 CONTINUE
      LDV=LDV+NDV
   70 CONTINUE
C
C        FIND TOTAL FLOW ALONG MAIN STEM RIVER
C
   80 NN=NB(1)
      NM=NN-1
      NQ=NQL(1)
      NV=NDIV(1)
      DO 90 I=1,NM
      QQL=0.
      QJN=0.
      QLESS=0.
      IF(NQ.EQ.0) GO TO 85
C
C        FIND LATERAL FLOW
C
      DO 82 L=1,NQ
      IF(QLNAM(1,L).EQ.DUMM) GO TO 82
      LL=LQ(L+M11)
      IF(I.NE.LL)GO TO 82
      QQL=QLI(L)
   82 CONTINUE
   85 IF(JN.EQ.1) GO TO 88
      IF(I.GT.NJUN(JN))GO TO 88
C
C        FIND FLOW COMING IN FROM TRIBUTARIES
C
      DO 87 K=2,JN
      NNN=NB(K)
      KK=NJUN(K)
      L1K=LCAT21(1,K,NB)-1
      IF(I.EQ.KK) QJN=QDI(NNN+L1K)
   87 CONTINUE
C
C        ADD ALL FLOWS TOGETHER
C
   88 QDI(I+1+L11)=QDI(I+L11)+QQL+QJN
C
C         SUBTRACT OUT DIVERTED FLOW
C
   89 IF(NV.EQ.0) GO TO 90
      DO 91 L=1,NV
      LV=LDIV(L+N11)
      IF(I.NE.LV) GO TO 91
      IF(NQ.EQ.0) GO TO 92
      IF(LL.EQ.LV) GO TO 90
   92 QLESS=QDI(I+1+L11)*DVI(L)/100.
   91 CONTINUE
C
      QDI(I+1+L11)=QDI(I+L11)-QLESS
   90 CONTINUE
      IF(KD(1).NE.3)GO TO 110
C
C        SINGLE VALUE RATING CURVE READ IN AT THE DOWNSTREAM BOUNDARY
C
      NN=NB(1)
      Q=QDI(NN+L11)
C
C        LOCATE THE PROPER NODES FOR EXTRAPOLATION
C
      DO 52 K=2,NYQD
      KK=K
      IF(Q.LT.QYQD(K))GO TO 53
   52 CONTINUE
C
C        RATING TABLE HAS BEEN EXCEEDED -- INCREASE EXCEED COUNTER
C
      NICXCD=NICXCD+1
C
C        INTERPOLATE TO GET INITIAL DOWNSTREAM DISCHARGE CORRESPONDING
C        TO INITIAL DOWNSTREAM WSEL
C
   53 YN=YQD(KK-1)+(Q-QYQD(KK-1))/(QYQD(KK)-QYQD(KK-1))*(YQD(KK)-
     1YQD(KK-1))
      YDI(NN+L11)=YN
C
  110 CONTINUE
C
      IF(IBUG.EQ.0) GO TO 200
C
C        WRITE INITIAL DISCHARGES
C
      DO 115 J=1,JN
      NN=NB(J)
      L1J=LCAT21(1,J,NB)-1
      WRITE(IODBUG,3) J
      WRITE(IODBUG,2) (QDI(I+L1J),I=1,NN)
  115 CONTINUE
C
  200 IF(ITRACE.EQ.1) WRITE(IODBUG,9000) SNAME
 9000 FORMAT(1H0,2H**,1X,2A4,8H EXITED.)
      RETURN
      END
