C MEMBER BWCM21
C  (from old member FCBWCM21)
C
      SUBROUTINE BWCM21(PO,J,NCM,NRCM1,CM,NCML,X,XFACT,QDI,NRT1,STT,
     1 LOSTT,NB,K6,K7,K9)
C
C      THIS SUBROUTINE CALCULATES MANNING'S N VALUES TO BE USED IN
C      BACKWATER COMPUTATIONS
C
C           THIS SUBROUTINE WAS WRITTEN ORIGINALLY BY:
C           DR. DANNY FREAD   HRL   APRIL 1978
C
C           THIS SUBROUTINE WAS MODIFIED TO MEET VER. NO. 5 STANDARDS
C           OF THE NWSRFS BY:
C           JANICE LEWIS      HRL   NOVEMBER,1982     VERSION NO. 1
C
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
      COMMON/XS21/LOAS,LOBS,LOHS,LOASS,LOBSS,LOHSS,LONSS,LONS1
      COMMON/SS21/NCS,A,B,DB,R,DR,AT,BT,NCSS,P,DP,ZH,NP,NPEND
      COMMON/XCED21/NBDXCD,NCMXCD,NFRXCD,NICXCD,NINXCD,NONXCD,MTXDV
C
      DIMENSION PO(1),NCM(1),NRCM1(1),CM(K6,1),X(1),QDI(1),NB(1)
      DIMENSION NRT1(1),STT(1),LOSTT(1),SNAME(2),H(90),AA(90),BB(90)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_dwoper/RCS/bwcm21.f,v $
     . $',                                                             '
     .$Id: bwcm21.f,v 1.1 1995/09/17 18:55:56 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SNAME/4HBWCM,4H21  /
C
C
      CALL FPRBUG(SNAME,1,21,IBUG)
C
C        FIND THE INITIAL LOCATIONS FOR ARRAYS
C
      L1J=LCAT21(1,J,NB)-1
      K1J=LCAT21(1,J,NRCM1)-1
      N1J=LCAT21(1,J,NRT1)-1
C
      NMM=NRCM1(J)
C
C        DETERMINE THE MANNING'S N REACH
C
      DO 40 L=1,NMM
      IF(L.EQ.1) KJU=1
      IF(L.GT.1) KJU=NCM(L-1+K1J)
      KJD=NCM(L+K1J)
      DXL=ABS(X(KJU+L1J)-X(KJD+L1J))*XFACT
C
C        DETERMINE THE SLOPE OF THE REACH FROM OBSERVED DATA
C
      LOST=LOSTT(L+N1J)
      LST=LOSTT(L+1+N1J)
      SL=(STT(LOST)-STT(LST))/DXL
C
C        DETERMINE THE WSEL AT THE STATIONS WITHIN THE MANNING'S N REACH
C
      H(KJU)=STT(LOST)
      KJD1=KJD-1
      KJU1=KJU+1
      H(KJD)=STT(LST)
      DH=H(KJU)-H(KJD)
C
      DO 15 KJ=KJU1,KJD1
   15 H(KJ)=H(KJU)-DH/DXL*ABS(X(KJ+L1J)-
     1 X(KJU+L1J))*XFACT
C
C        DETERMINE THE AREAS AND TOP WIDTHS AT THE STATIONS IN THE REACH
C
      DO 20 KJ=KJU,KJD
      Y=H(KJ)
      CALL SECT21(PO(LOAS),PO(LOBS),PO(LOHS),PO(LOASS),PO(LOBSS),
     1 PO(LOHSS),PO(LONSS),PO(LONS1),NB,J,KJ,Y,K7,K9)
      AA(KJ)=A
   20 BB(KJ)=B
C
C        DETERMINE THE AVG AREA, TOPWIDTH, AND FLOW IN THE REACH
C
      ASUM=0.0
      BSUM=0.0
      QSUM=0.0
C
      DO 30 KJ=KJU,KJD1
      DX=ABS(X(KJ+L1J)-X(KJ+1+L1J))*XFACT
      ASUM=ASUM+0.5*DX*(AA(KJ)+AA(KJ+1))
      BSUM=BSUM+0.5*DX*(BB(KJ)+BB(KJ+1))
   30 QSUM=QSUM+0.5*DX*(QDI(KJ+L1J)+QDI(KJ+1+L1J))
C
      ABAR=ASUM/DXL
      BBAR=BSUM/DXL
      QBAR=QSUM/DXL
C
C        COMPUTE MANNING'S N
C
      CN=1.486/ABS(QBAR)*ABAR*(ABAR/BBAR)**(2./3.)*SQRT(SL)
      IF(CN.LT.0.008.OR.CN.GT.0.16) NCMXCD=NCMXCD+1
      IF(CN.LT.0.008) CN=0.008
      IF(CN.GT.0.16) CN=0.16
C
      DO 35 KJ=1,NCML
   35 CM(KJ,L+K1J)=CN
   40 CONTINUE
  100 IF(ITRACE.EQ.1) WRITE(IODBUG,9000) SNAME
 9000 FORMAT(1H0,2H**,1X,2A4,8H EXITED.)
      RETURN
      END
