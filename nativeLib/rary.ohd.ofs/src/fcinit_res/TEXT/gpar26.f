C MEMBER GPAR26
C  (from old member FCGPAR26)
C
C DESC READ AND STORE PARAMETERS FOR GENERAL SECTION OF OPERATION 26
C                             LAST UPDATE: 10/04/94.08:28:19 BY $WC30KH
C
C--------------------------------------------------------------------
C
      SUBROUTINE GPAR26(WORK,IUSEW,LEFTW,NPARG,LOCSTO)
C
C----------------------------------------------------------------------
C  ARGS:
C     WORK - ARRAY TO BE FILLED WITH THE PARAMETERS
C    IUSEW - NUMBER OF WORDS ALREADY USED IN WORK ARRAY
C    LEFTW - NUMBER OF WORDS LEFT IN WORK ARRAY
C    NPARG - NUMBER OF WORDS NEEDED TO STORE GENERAL PARAMETERS
C---------------------------------------------------------------------
C
C  JTOSTROWSKI - HRL - MARCH 1983
C----------------------------------------------------------------
      INCLUDE 'common/ionum'
      INCLUDE 'common/fld26'
      INCLUDE 'common/read26'
      INCLUDE 'common/comn26'
C
      DIMENSION KEYWDS(2,5),LKEYWD(5),VALUE(500),LOGLIN(2,2),LLOGLN(2),
     .WORK(1),UNITC(4),DIM(4),CONVT(4)
      LOGICAL CRVFND,CRVOK
      EQUIVALENCE (CONVT(1),CONVL),(CONVT(2),CONVL3),(CONVT(3),CONVLT),
     .            (CONVT(4),CONVST)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_res/RCS/gpar26.f,v $
     . $',                                                             '
     .$Id: gpar26.f,v 1.3 2000/03/13 21:03:44 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA KEYWDS/
     .            4HELVS,4HSTOR,
     .            4HINTE,4HRP  ,4HENDP,4H    ,4HENDP,4HARMS,
     .            4HENDG,4HENL /
      DATA LKEYWD/2,2,1,2,2/
      DATA NKEYWD/5/
      DATA NDKEY/2/
      DATA UNITC/4HM   ,4HCMSD,4HCMS ,4HCMSD/
      DATA DIM  /4HL   ,4HL3  ,4HL3/T,4HL3  /
C
      DATA LOGLIN/4HLINE,4HAR  ,4HLOG ,4H    /
      DATA LLOGLN/2,1/
      DATA NLOGLN/2/
      DATA NDLOG/2/
C
C  INITIALIZE LOCAL VARIABLES AND COUNTERS
C
      NSE = 0
      NPARG = 0
      LCARD = NCARD
      INTERP = 0
      CRVFND = .FALSE.
      CRVOK = .FALSE.
      USEDUP = .FALSE.
C-----------------------------------------------------------------
C  NOW LOOK FOR MATCHING KEYWORDS IN INPUT STREAM
C
   10 CONTINUE
      NUMFLD = 0
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.0) GO TO 9000
C
   20 CONTINUE
      NUMWD = (LEN-1)/4 + 1
      IDEST = IKEY26(CHAR,NUMWD,KEYWDS,LKEYWD,NKEYWD,NDKEY) + 1
C
      GO TO (100,200,300,400,400,500) , IDEST
C
C---------------------------------------------------------------------
C  NO VALID KEYWORD FOUND
C
  100 CONTINUE
      CALL STER26(1,1)
      GO TO 10
C
C-----------------------------------------------------------------------
C   'ELVSSTOR' FOUND
C  SET INDICATOR THAT CURVE HAS BEEN FOUND, IF CURVE HAS ALREADY BEEN
C  FOUND, IT IS AN ERROR
C
  200 CONTINUE
      IF (.NOT.CRVFND) GO TO 210
      CALL STER26(39,1)
C
  205 CONTINUE
      NUMFLD = 0
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.EQ.3) GO TO 9000
      IF (ITYPE.LE.1) GO TO 205
      GO TO 20
C
C  START GETTING NUMBERS FOR CURVE DEFINITION. RULES FOR INPUT ARE
C  A CONTINUATION SYMBOL (&) MUST END THE LINE AS A STAND ALONE CHAR-
C  ACTER, AND SINCE THE CURVE IS A SET OF PAIRS THE TOTAL NUMBER
C  OF VALUES MUST BE EVEN.
C
  210 CONTINUE
      CRVFND = .TRUE.
C
      NVAL=500
      CALL GLST26(1,1,X,VALUE,X,NVAL,CRVOK)
      IF (.NOT.CRVOK) GO TO 10
C
C----------------------------------------------------------------
C  ALL VALUES ARE REAL AND WE'VE NO MORE INPUT ON A LINE(NO CONTINUATION
C  NOW WE HAVE TO CHECK THE VALUES FOR A CERTAIN AMOUNT OF VALIDITY
C  BEFORE WE CAN STORE THEM. ONE IS THAT THE NUMBER OF VALUES ARE EVEN
C  AS WE HAVE TO INPUT SETS OF PAIRS, THE OTHER IS THAT EACH HALF OF THE
C  VALUES ARE IN ASCENDING ORDER.
C
  250 CONTINUE
      IF (MOD(NVAL,2).EQ.0) GO TO 255
C
      CALL STER26(40,1)
      CRVOK = .FALSE.
      GO TO 10
C
C  SEE IF EACH HALF ARE IN ASCENDING ORDER
C
  255 CONTINUE
      NHALF = NVAL/2
      DO 260 I=1,2
      J = (I-1)*NHALF
      DO 257 K = 2,NHALF
      IF (VALUE(J+K).GT.VALUE(J+K-1)) GO TO 257
      CALL STER26(41,1)
      NUMFLD = 0
      GO TO 10
C
  257 CONTINUE
  260 CONTINUE
C  SEE IF FIRST STORAGE VALUE IS ZERO.
C
      NSTOR1=NVAL/2+1
      NSTOR2=NSTOR1+1
      IF (VALUE(NSTOR1).LE.0.01) GO TO 270
C
      CALL STER26(58,1)
      GO TO 10
C
C  SEE IF RESERVOIR BOTTOM ELEVATION IS REASONABLE
C  NOW SET THE STORAGE CONVERSION FACTOR.
C  ALSO, CONVERT THE STORAGE VALUES HELD IN WORK ARRAY AND STORGE IN
C  /COMN26/ TO METRIC TIME INTERVAL MEAN DISCHARGES.
C
  270 CONTINUE
      IF (IMETEN.EQ.1) GO TO 272
C
      ISTOR = 0
      IF (IMETEN.EQ.0) ISTOR=1
      CALL CNVT26(UNITC(4),DIM(4),UENG,CONVT(4),CONST,ISTOR,IERC)
C
  272 CONTINUE
      XUPR=(VALUE(NSTOR2)-VALUE(NSTOR1))/CONVST
      XLWR=(VALUE(2)-VALUE(1))/CONVL
      DSDH=XUPR/XLWR
      IF(DSDH.GT.0.01) GO TO 280
      VALUE(1)=VALUE(2)-XUPR*CONVL*10.0
      WRITE(IPR,275)
  275 FORMAT(/1X,'ERROR: STORAGE INCREMENT RATE AT BOTTOM ',
     &'OF RESERVOIR MUST BE GREATER THEN 0.01 CMSD/M')
      CALL STER26(41,1)
      NUMFLD = 0
      GO TO 10
C
C  EVERYTHING IS OK FOR LATER STORING
C
  280 CONTINUE
      CRVOK = .TRUE.
      GO TO 10
C
C-----------------------------------------------------------------------
C  'INTERP' KEYWORD FOUND, LOOK FOR VALID FOLLOWING KEYWORD
C
  300 CONTINUE
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.1) GO TO 9000
C
C  IF NOTHING ON CARD AFTER INTERP, SET VALUE TO DEFAULT
C
      IF (IERF.EQ.1) GO TO 350
      NUMWD = (LEN-1)/4 + 1
      IDEST = IKEY26(CHAR,NUMWD,LOGLIN,LLOGLN,NLOGLN,NDLOG)
      IF (IDEST.GT.0) GO TO 320
C
C  IF NOT A VALID INTERP INDICATOR, GIVE MESSAGE AND USE DEFAULT
C
      CALL STER26(42,1)
      GO TO 350
C
  320 CONTINUE
      INTERP = IDEST - 1
C
  350 CONTINUE
      GO TO 10
C
C------------------------------------------------------------------
C  'ENDP' FOUND. THE CURVE MUST HAVE BEEN ENTERED OK, FOR US TO STORE IT
C   AND OTHER PARMS IN WORK ARRAY.
C
  400 CONTINUE
C
      IF (CRVOK) GO TO 410
C
      GO TO 9999
C
  410 CONTINUE
C
C  STORE THE NO. OF PAIRS IN THE CURVE FOLLOWED BY THE CURVE
C  VALUES, ELEVATIONS, THEN STORAGES.
C
      NPARG = NVAL + 2
      COUNT = NHALF + 0.01
      TERPIN = INTERP + 0.01
C
      CALL FLWK26(WORK,IUSEW,LEFTW,COUNT,501)
C
C  CONVERT ELEVATION VALUES BEFORE STORING THEM
C
      DO 420 I=1,NHALF
      VALUE(I) = VALUE(I)/CONVL
      CALL FLWK26(WORK,IUSEW,LEFTW,VALUE(I),501)
  420 CONTINUE
C
C  JUST STORE STORAGE CURVE WITHOUT CONVERTING VALUES. THE CONVERSION
C  IF NEEDED, WILL BE DONE IN GENL26.
C
      LOCSTO = IUSEW + 1
      NST = NHALF + 1
      DO 425 I=NST,NVAL
      CALL FLWK26(WORK,IUSEW,LEFTW,VALUE(I),501)
  425 CONTINUE
C
C  STORE INTERPOLATION SWITCH
C
      CALL FLWK26(WORK,IUSEW,LEFTW,TERPIN,501)
C
C  STORE THE ELEVATION VS STORAGE CURVE IN /COMN26/.
C
      NSE = NHALF
C
C  JUST TRANSFER INTO /COMN26/ THE ELEVATION AND STORAGE VALUES THAT
C  WERE ENTERED.
C
      DO 430 I=1,NSE
      ELSTOR(I) = VALUE(I)
      STORGE(I) = VALUE(NHALF+I)
  430 CONTINUE
C
C  ALL FINISHED WITH THE GENERAL PARMS. EXIT NOW.
C
  450 CONTINUE
      GO TO 9999
C
C---------------------------------------------------------------------
C  ERROR IN UFLD26
C
 9000 CONTINUE
      IF (IERF.EQ.1) CALL STER26(19,1)
      IF (IERF.EQ.2) CALL STER26(20,1)
      IF (IERF.EQ.3) CALL STER26(21,1)
      IF (IERF.EQ.4) CALL STER26(1,1)
C
      IF (IERF.NE.3) GO TO 10
C
C-----------------------------------------------------------------------
C  ENDGENL FOUND WHERE IT SHOULDN'T BE. SIGNAL ERROR AND RETURN
C
  500 CONTINUE
      USEDUP =.TRUE.
      CALL STER26(43,1)
      GO TO 9999
C
C--------------------------------------------------------------------
C
 9999 CONTINUE
      OKELST = CRVOK
      RETURN
      END
