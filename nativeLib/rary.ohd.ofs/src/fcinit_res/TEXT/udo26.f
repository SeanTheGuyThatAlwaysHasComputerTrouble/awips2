C MEMBER UDO26
C  (from old member FCUDO26)
C
C DESC AFTER A 'DO' RCL KEYWORD IS FOUND, THIS ROUTINE GETS THE NEXT
C DESC FIELD AND CHECKS ON THE  ID'S VALIDITY (BOTH IN NAME AND
C DESC DEFINITION
C............................................................
C
      SUBROUTINE UDO26(CODE,IRC)
C
C............................................................
C  THIS ROUTINE IS CALLED AFTER A 'DO' IS FOUND IN AN RCL
C  STATEMENT. IT SCANS FOR THE NEXT FIELD AND LOOKS FOR A VALID
C  SCHEME/UTILITY ID. VALIDITY IS DEFINED AS THE ID MATCHING AN
C  ALLOWABLE ONE AND THAT S/U HAS BEEN DEFINED WITH THE PROPER
C  PARAMETER INPUT.
C
C  THERE ARE TWO PERMISSIBLE FORMS OF THE S/U ID:
C    1) DO SCHEME
C    2) DO SCHEME(N)
C
C  THE FIRST ONE IS OBVIOUS. THE SECOND ONE STATES THAT THE N-TH
C  DEFINITION OF THE SCHEME IS TO BE EXECUTED HERE. AT LEAST N
C  DEFINITIONS OF THE SCHEME MUST HAVE BEEN GIVEN.
C
C  ANOTHER THING THAT IS CHECKED IS WHETHER A UTILITY NEEDS TO BE
C  INITIATED WITH A 'DO'. IF IT DOESN'T NEED A DO, IRC IS SET TO -1
C
C  IF ONE OF THE 'ENTRY' UTILITIES HAS BEEN SPECIFIED, WE MUST LOOK FOR
C  THE COMPANION SCHEME, EITHER 'INDSCHRGE' OR 'FLASHBDS'.
C
C......................................................................
C  ARGUMENT LIST -
C    CODE - THE NUMERIC CODE OF THE S/U IF ALL IS OK WITH THE S/U NAME
C     IRC - RETURN CODE =0, EVERYTHING IS A-OK
C                       =1, ERROR OCCURRED. PROPER ERROR CODE HAS BEEN
C                       =-1, 'DO' NOT NEEDED FOR SPECIFIED UTILITY
C                           SET IN /ERR26/
C
C.....................................................................
C
C  JTOSTROWSKI - HRL - MARCH 1983
C................................................................
      INCLUDE 'common/err26'
      INCLUDE 'common/suin26'
      INCLUDE 'common/suid26'
      INCLUDE 'common/fld26'
C
      DIMENSION TNAME(3),NEEDDO(50)
      LOGICAL NEEDDO
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_res/RCS/udo26.f,v $
     . $',                                                             '
     .$Id: udo26.f,v 1.2 1999/04/22 15:19:47 page Exp $
     . $' /
C    ===================================================================
C
      DATA NEEDDO/13*.TRUE.,7*.FALSE.,7*.TRUE.,23*.FALSE./
C
      NPACK = 3
      VALUE = 1.00
      IRC = 0
C
C  GET NEXT FIELD ON INPUT LINE
C
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IRF)
C
      IF (IRF.GT.0) GO TO 9000
C
      CALL IDWP26(TNAME,NPACK,INAME,ILEVEL,IERID)
      IF (IERID.EQ.0) GO TO 250
      IF (IERID.EQ.1) CALL STER26(7,1)
      GO TO 9900
C
  250 CONTINUE
C
C  IF A UTILTIY HAS BEEN SPECIFIED, SEE IF IT NEEDS TO BE ACTIVATED
C  A 'DO'
C
      IF (NEEDDO(INAME)) GO TO 300
      CALL STER26(16,1)
      IRC = -1
      GO TO 9999
C
  300 CONTINUE
C
C  SET CODE NUMBER OF S/U AND SEE IF IT HAS BEEN DEFINED.
C
      VALUE = ILEVEL + 0.01
      CODE = SUCODE(INAME) + VALUE
C
C  CHECK TO SEE IF THE 'ENTERISC' OR 'GOFLSH' UTILITIES HAVE BEEN
C  SPECIFIED ON THIS DO. IF SO, THEY NEED THEIR COMPANION SCHEMES,
C  EITHER 'INDSCHRGE' OR 'FLASHBDS'.
C
      IF (INAME.EQ.21.OR.INAME.EQ.24) GO TO 500
C
C  IF 'PASSFLOW' HAS BEEN SPECIFIED, JUST EXIT. THIS SCHEME HAS NO INPUT
C
      IF (INAME .EQ. 1) GO TO 9999
C
      DO 450 I=1,NSUIN
      DIFF = ABS(CODE-DEFCDE(I))
      IF (DIFF.GT.0.05) GO TO 450
      IFNDDO(I) = 1
      GO TO 9999
  450 CONTINUE
C
C  S/U HAS NOT BEEN DEFINED IF WE GET HERE.
C
      CALL STER26(8,1)
      GO TO 9900
C
C  SCAN DEFINED SCHEMES FOR THE COMPANION SCHEME.
C
  500 CONTINUE
      IF (INAME.EQ.21) COMPAN = SUCODE(11) + ILEVEL + 0.01
      IF (INAME.EQ.24) COMPAN = SUCODE(12) + ILEVEL + 0.01
      DO 550 I=1,NSUIN
      DIFF = ABS(COMPAN-DEFCDE(I))
      IF (DIFF.GT.0.05) GO TO 550
C
C  WE'VE FOUND THE COMPANION SCHEME
C
      GO TO 9999
  550 CONTINUE
C
C  NO COMPANION SCHEME IF WE REACH HERE
C
      CALL STER26(83,1)
      GO TO 9900
C
C  ERROR IN READING FIELD IN UFLD26 IF GET HERE
C
 9000 CONTINUE
      IF (IRF.EQ.1) CALL STER26(19,1)
      IF (IRF.EQ.4) CALL STER26(7,1)
C
C  SET ERROR CODE
C
 9900 CONTINUE
      IRC = 1
 9999 CONTINUE
      RETURN
      END
