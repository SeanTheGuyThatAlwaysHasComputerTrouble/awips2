C MEMBER PM2626
C
      SUBROUTINE PM2626(WORK,IUSEW,LEFTW,NP26,
     .                  LENDSU,JDEST,IERR)
C--------------------------------------------------------------------
C  ROUTINE TO GET AND CHECK PARAMETERS FOR S/U #26 -
C  RATE OF CHANGE OF RESERVOIR DISCHARGE SCHEME
C---------------------------------------------------------------------
C  KUANG HSU - HRL - NOVEMBER 1998
C----------------------------------------------------------------
C
      INCLUDE 'common/comn26'
C
C
      INCLUDE 'common/err26'
C
C
      INCLUDE 'common/fld26'
C
C
      INCLUDE 'common/read26'
C
C
      INCLUDE 'common/suky26'
C
C
      INCLUDE 'common/warn26'
C
C
      DIMENSION INPUT(2,3),LINPUT(3),IP(3),VALUE(100),
     .  QHTYPE(3),LQHT(3),WORK(1)
      LOGICAL       GETABL,ENDFND,QOK,TBLOK,CRVOK,ALLOK
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_res/RCS/pm2626.f,v $
     . $',                                                             '
     .$Id: pm2626.f,v 1.1 1999/04/22 14:55:53 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA INPUT/
     .            4HQVAL,4HUE  ,4HTBLT,4HYPE ,4HTABL,4HE   /
      DATA LINPUT/2,2,2/
      DATA NINPUT/3/
      DATA NDINPU/2/
C
      DATA TABL/4HTABL/
C
      DATA QHTYPE/4HINST,4HMEAN,4HPOOL/
      DATA LQHT/1,1,1/
      DATA NQHT/3/
C
C  INITIALIZE LOCAL VARIABLES AND COUNTERS
C
      NP26 = 0
      GETABL = .FALSE.
      QOK = .FALSE.
      TBLOK = .TRUE.
      CRVOK = .TRUE.
      ALLOK = .TRUE.
      ENDFND = .FALSE.
      QTYPE = 1.01
      TBTYP = 1.01
C
      DO 3  I =1,3
           IP(I) = 0
    3 CONTINUE
C
      IERR = 0
C
C  PARMS FOUND, LOOKING FOR ENDP
C
      LPOS = LSPEC + NCARD + 1
      LASTCD = LENDSU
      IBLOCK = 1
C
    5 IF (NCARD .LT. LASTCD) GO TO 8
           CALL STRN26(59,1,SUKYWD(1,7),3)
           IERR = 99
           GO TO 9
    8 NUMFLD = 0
      CALL UFLD26(NUMFLD,IERF)
      IF(IERF .GT. 0 ) GO TO 9000
      NUMWD = (LEN -1)/4 + 1
      IDEST = IKEY26(CHAR,NUMWD,SUKYWD,LSUKEY,NSUKEY,NDSUKY)
      IF (IDEST.EQ.0) GO TO 5
C
C  IDEST = 7 IS FOR ENDP
C
      IF (IDEST.EQ.7.OR.IDEST.EQ.8) GO TO 9
          CALL STRN26(59,1,SUKYWD(1,7),3)
          JDEST = IDEST
          IERR = 89
    9 LENDP = NCARD
C
C  ENDP CARD OR TS OR CO FOUND AT LENDP,
C  ALSO ERR RECOVERY IF NEITHER ONE OF THEM FOUND.
C
C  NOW WE'RE LOOKING FOR 'QVALUE' (REQUIRED) AND
C  'TBLTYPE' (OPTIONAL IF QVALUE IS 'TABL')
C
      IBLOCK = 2
      CALL POSN26(MUNI26,LPOS)
      NCARD = LPOS - LSPEC -1
C
   10 CONTINUE
      NUMFLD = 0
      CALL UFLD26(NUMFLD,IERF)
      IF(IERF .GT. 0) GO TO 9000
      NUMWD = (LEN -1)/4 + 1
      IDEST = IKEY26(CHAR,NUMWD,INPUT,LINPUT,NINPUT,NDINPU)
      IF(IDEST .GT. 0) GO TO 50
      IF(NCARD .GE. LENDP) GO TO 900
C
C  NO VALID KEYWORD FOUND
C
      CALL STER26(1,1)
      ALLOK = .FALSE.
      GO TO 10
C
C  NOW SEND CONTROL TO PROPER LOCATION FOR PROCESSING EXPECTED INPUT
C
   50 CONTINUE
      GO TO (100,200,300) , IDEST
C
C-----------------------------------------------------------------------
C  'QVALUE' REQUIRED.
C
  100 CONTINUE
      IP(1) = IP(1) + 1
      IF (IP(1).GT.1) CALL STER26(39,1)
C
C  'QVALUE' FOUND. NEXT FIELD MUST BE EITHER 'TABL' OR A REAL VALUE.
C
C
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.0) GO TO 9000
C
C  IF NEXT FIELD IS ALL CHARACTERS, IT MUST BE 'TABL'
C  ANOTHER FIELD INDICATING THE DATATYPE OF THE Q VALUE
C  CAN FOLLOW (OPTIONAL WITH DEFAULT OF 'INST')
C
      IF (ITYPE.EQ.2) GO TO 160
C
C  REAL VALUE FOUND AFTER 'QVALUE'
C  SET INDICATOR FOR USER SPECIFIED DISCHARGE CHANGE RATE
C  AND STORE VALUE
C
      FTBL = 0.01
      QVAL = REAL/CONVL3
      GO TO 170
C
C------------------------------
C  CHARACTER FIELD FOUND AFTER 'QVALUE'. IT MUST BE 'TABL'
C  MAKE KEYWORD 'TBLTYPE' MANDATORY FOR CLARITY
C
  160 CONTINUE
      IF (IUSAME(CHAR,TABL,1).NE.1) THEN
         CALL STER26(1,1)
         GO TO 10
      ENDIF
C
      FTBL = 1.01
      GETABL = .TRUE.
      TBLOK = .FALSE.
 170  CONTINUE
C
C  NEXT FIELD MUST BE EITHER 'INST' OR 'MEAN' TO IDENTIFY
C  DATA TYPE FOR INST/MEAN DISCHARGE CHANGE RATE
C
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.0) GO TO 9000
C
      IF (ITYPE.NE.2) THEN
         CALL STER26(1,1)
         GO TO 10
      ENDIF
      NUMWD = (LEN-1)/4 + 1
      IKEY = IKEY26(CHAR,NUMWD,QHTYPE,LQHT,NQHT,1)
      IF (IKEY.LE.0 .OR. IKEY.GE.3) THEN
         CALL STER26(1,1)
         GO TO 10
      ENDIF
C
      QTYPE = IKEY + 0.01
C
C  EVERYTHING IS OK
C
      QOK = .TRUE.
      GO TO 10
C-----------------------------------------------------------------
C  'TBLTYPE' KEYWORD EXPECTED TO SPECIFY DATA TYPE OF INDEPENDENT
C  VARIABLE OF TABLE.  ONLY ALLOWED IF DISCHARGE CHANGE RATE 
C  IS TO BE DETERMINED FROM TABLE. 
C  IF FOUND, GET NEXT FIELD ON CARD.
C
  200 CONTINUE
C
      IP(2) = IP(2) + 1
      IF (IP(2).GT.1) CALL STER26(39,1)
      IF (GETABL) GO TO 210
           IF(IP(1).GT.0) CALL STRN26(60,1,INPUT(1,IDEST),LINPUT(IDEST))
           IF(IP(1).EQ.0) CALL STRN26(59,1,INPUT(1,1),LINPUT(1))
           TBLOK = .FALSE.
           GO TO 10
C
C  NEXT FIELD MUST BE EITHER 'MEAN' OR 'INST' OR 'POOL'
C
  210 CONTINUE
      TBLOK = .FALSE.
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.0) GO TO 9000
C
      IF (ITYPE.EQ.2) GO TO 220
      CALL STER26(1,1)
      GO TO 10
C
  220 CONTINUE
      NUMWD = (LEN-1)/4 + 1
      IKEY = IKEY26(CHAR,NUMWD,QHTYPE,LQHT,NQHT,1)
      IF (IKEY.GT.0) GO TO 240
      CALL STER26(1,1)
      GO TO 10
C
  240 CONTINUE
C
C  EVERYTHING IS OK
C
      TBTYP = IKEY + 0.01
      TBLOK = .TRUE.
      GO TO 10
C
C----------------------------------------------------------------------
C  'TABLE' KEYWORD IS EXPECTED HERE. IF NOT FOUND, SIGNAL ERROR
C
  300 CONTINUE
      IP(3) = IP(3) + 1
      IF (IP(3).GT.1) CALL STER26(39,1)
C
C  GO GET LIST OF VALUES FOR DEFINING THE POOL VS Q CURVE
C
C
      NVAL=100
      CALL GLST26(1,1,X,VALUE,X,NVAL,CRVOK)
      IF (.NOT.CRVOK) GO TO 10
C
C  FOUR CHECKS MUST BE MADE ON THE CURVE:
C   1) THE TOTAL NO. OF VALUES INPUT MUST BE EVEN (PAIRS OF VALUES ARE
C      NEEDED,
C   2) THE ELEVATIONS MUST BE IN ASCENDING ORDER.
C
      IF (MOD(NVAL,2).EQ.0) GO TO 320
C
      CALL STER26(40,1)
      GO TO 10
C
  320 CONTINUE
      NHALF = NVAL/2
      NSEC = NHALF + 1
C
C  SEE IF ELEVATIONS ARE IN ASCENDING ORDER
C
      CALL ASCN26(VALUE,NHALF,0,IERA)
      IF (IERA.GT.0) GO TO 10
C
      CRVOK = .TRUE.
      GO TO 10
C
C--------------------------------------------------------------------
C  END OF INPUT. STORE VALUES IN WORK ARRAY IF EVERYTHING WAS ENTERED
C  WITHOUT ERROR.
C
  900 CONTINUE
C
      IF (IP(1).EQ.0) CALL STRN26(59,1,INPUT(1,1),LINPUT(1))
C
      IF (QOK.AND.TBLOK.AND.CRVOK.AND.ALLOK) GO TO 910
      GO TO 9999
C
C  DIFFERENT VALUES NEED TO BE STORED IF PRESCRIBED Q IS USER-DEFINED
C  OR EXPECTED FROM A TIME-SERIES.
C
  910 CONTINUE
      IF (GETABL) GO TO 950
C
C  THIS SECTION IS FOR USER-DEFINED DISCHARGE CHANGE RATE
C
      CALL FLWK26(WORK,IUSEW,LEFTW,FTBL,501)
      CALL FLWK26(WORK,IUSEW,LEFTW,QVAL,501)
      CALL FLWK26(WORK,IUSEW,LEFTW,QTYPE,501)
      NP26 = 3
      GO TO 9999
C
C  THIS SECTION IS FOR POOL/INSTQ/MEANQ VS INSTQ/MEANQ TABLE
C
  950 CONTINUE
      CALL FLWK26(WORK,IUSEW,LEFTW,FTBL,501)
      CALL FLWK26(WORK,IUSEW,LEFTW,TBTYP,501)
      CALL FLWK26(WORK,IUSEW,LEFTW,QTYPE,501)
C
C  STORE TABLE VALUES
C
C  CONVERT INDEPENDENT VARIABLES: INSTQ, OR MEANQ OR ELEVATION.
C
      ITBTYP=TBTYP
      CONVX=CONVL3
      IF(ITBTYP.EQ.3) CONVX=CONVL
      NHALF=NVAL/2
      NSEC=NHALF+1
      DO I=1,NHALF
          VALUE(I) = VALUE(I)/CONVX
      ENDDO
C
C  CONVERT DISCHARGE CHANGE RATE.
C
      DO I =NSEC,NVAL
          VALUE(I) = VALUE(I) / CONVL3
      ENDDO
C
      PAIR = NVAL/2 + 0.01
      CALL FLWK26(WORK,IUSEW,LEFTW,PAIR,501)
C
      DO I=1,NVAL
      CALL FLWK26(WORK,IUSEW,LEFTW,VALUE(I),501)
      ENDDO
C
      NP26 = 4+NVAL
C
      GO TO 9999
C
C---------------------------------------------------------------------
C  ERROR IN UFLD26
C
 9000 CONTINUE
      IF (IERF.EQ.1) CALL STER26(19,1)
      IF (IERF.EQ.2) CALL STER26(20,1)
      IF (IERF.EQ.3) CALL STER26(21,1)
      IF (IERF.EQ.4) CALL STER26( 1,1)
C
      IF (NCARD.GE.LASTCD) GO TO 9100
      IF (IBLOCK.EQ.1)  GO TO 5
      IF (IBLOCK.EQ.2)  GO TO 10
C
 9100 USEDUP = .TRUE.
C
 9999 CONTINUE
      RETURN
      END
