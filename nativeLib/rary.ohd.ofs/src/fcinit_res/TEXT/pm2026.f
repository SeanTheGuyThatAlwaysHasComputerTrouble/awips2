C MEMBER PM2026
C  (from old member FCPM2026)
C
      SUBROUTINE PM2026(WORK,IUSEW,LEFTW,NP20,
     .                  LENDSU,JDEST,IERR)
C---------------------------------------------------------------------
C  SUBROUTINE TO READ AND INTERPRET PARAMETER INPUT FOR S/U #20
C    MAXIMUM DISCHARGE RELATION
C---------------------------------------------------------------------
C  JTOSTROWSKI - HRL - MARCH 1983
C----------------------------------------------------------------
C
      INCLUDE 'common/comn26'
C
C
      INCLUDE 'common/err26'
C
C
      INCLUDE 'common/fld26'
C
C
      INCLUDE 'common/mult26'
C
C
      INCLUDE 'common/rc26'
C
C
      INCLUDE 'common/read26'
C
C
      INCLUDE 'common/suid26'
C
C
      INCLUDE 'common/suin26'
C
C
      INCLUDE 'common/suky26'
C
C
      INCLUDE 'common/warn26'
C
C
      DIMENSION INPUT(2,6),LINPUT(6),QVAL(100),QMVAL(100),
     . HVAL(100),TVAL(100),WORK(1),IP(6)
      LOGICAL ENDFND,OK(6),ALLOK
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_res/RCS/pm2026.f,v $
     . $',                                                             '
     .$Id: pm2026.f,v 1.1 1995/09/17 18:52:22 dws Exp $
     . $' /
C    ===================================================================
C
C
C
      DATA INPUT/
     .            4HCONS,4HTQ  ,4HELVS,4HQ   ,
     .            4HHEAD,4HVSQ ,4HTWCU,4HRVE ,
     .            4HCONV,4H    ,4HELVS,4HMAXQ/
      DATA LINPUT/2,2,2,2,1,2/
      DATA NINPUT/6/
      DATA NDINPU/2/
C
      DATA QMCODE/1570.01/
C
C  INITIALIZE LOCAL VARIABLES AND COUNTERS
C
      NP20 = 0
C
      DO 3 I =1,NINPUT
           OK(I) = .TRUE.
           IP(I) = 0
    3 CONTINUE
      NQVAL = 0
      NHVAL = 0
      NQMVAL = 0
C
      TOLRNS = 0.02
C
      ALLOK = .TRUE.
      ENDFND = .FALSE.
C
      CODEQM = QMCODE + SULEVL
C
C  NOW PROCESS INPUT UP TO 'ENDP'
C   LOOK FOR 'CONSTQ' FIRST, IF NOT FOUND, ASSUME THAT CURVES ARE TO BE
C    INPUT (I.E. - 'ELVSQ', HEADVSQ', AND 'TWCURVE'.)
C
C   IF 'CONSTQ' IS FOUND, ONLY NEED 'ELVSQ'.
C
      IERR = 0
C
C  PARMS FOUND, LOOKING FOR ENDP
C
      LPOS = LSPEC + NCARD + 1
      LASTCD = LENDSU
      IBLOCK = 1
C
    5 IF (NCARD .LT. LASTCD) GO TO 8
           CALL STRN26(59,1,SUKYWD(1,7),3)
           IERR = 99
           GO TO 9
    8 NUMFLD = 0
      CALL UFLD26(NUMFLD,IERF)
      IF(IERF .GT. 0 ) GO TO 9000
      NUMWD = (LEN -1)/4 + 1
      IDEST = IKEY26(CHAR,NUMWD,SUKYWD,LSUKEY,NSUKEY,NDSUKY)
      IF (IDEST.EQ.0) GO TO 5
C
C  IDEST = 7 IS FOR ENDP
C
      IF (IDEST.EQ.7.OR.IDEST.EQ.8) GO TO 9
          CALL STRN26(59,1,SUKYWD(1,7),3)
          JDEST = IDEST
          IERR = 89
    9 LENDP = NCARD
C
C  ENDP CARD OR TS OR CO FOUND AT LENDP,
C  ALSO ERR RECOVERY IF NEITHER ONE OF THEM FOUND.
C
C  NOW WE'RE LOOKING FOR KEYWORDS.
C
      IBLOCK = 2
      CALL POSN26(MUNI26,LPOS)
      NCARD = LPOS - LSPEC -1
C
   10 CONTINUE
      NUMFLD = 0
      CALL UFLD26(NUMFLD,IERF)
      IF(IERF .GT. 0) GO TO 9000
C
      NUMWD = (LEN -1)/4 + 1
      IDEST = IKEY26(CHAR,NUMWD,INPUT,LINPUT,NINPUT,NDINPU)
      IF(IDEST .GT. 0) GO TO 50
      IF(NCARD .GE. LENDP) GO TO 900
C
C  NO VALID KEYWORD FOUND
C
      CALL STER26(1,1)
      ALLOK = .FALSE.
      GO TO 10
C
C  NOW SEND CONTROL TO PROPER LOCATION FOR PROCESSING EXPECTED INPUT
C
   50 CONTINUE
      GO TO (100,200,300,400,500,600) , IDEST
C----------------------------------------------------------------------
C  'CONSTQ' FOUND. ONLY ALLOWED IF 'ELVSMAXQ', 'HEADVSQ', 'TWCURVE' OR
C  'CONV' ENTERED.
C
  100 CONTINUE
      OK(1) = .FALSE.
      IP(1) = IP(1) + 1
      IF (IP(3).GT.0) CALL STRN26(97,1,INPUT(1,3),LINPUT(3))
      IF (IP(4).GT.0) CALL STRN26(97,1,INPUT(1,4),LINPUT(4))
      IF (IP(5).GT.0) CALL STRN26(97,1,INPUT(1,5),LINPUT(5))
      IF (IP(6).GT.0) CALL STRN26(97,1,INPUT(1,6),LINPUT(6))
      IF (IP(1).GT.1) CALL STER26(39,1)
C
C  READ NEXT FIELD ON CARD TO GET CONSTANT Q VALUE
C
  110 CONTINUE
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.0) GO TO 9000
C
C  FIELD MUST BE REAL AND POSITIVE
C
      IF (ITYPE.LE.1) GO TO 120
C
      CALL STER26(4,1)
      GO TO 10
C
  120 CONTINUE
      IF (REAL.GE.0.0) GO TO 130
C
      CALL STER26(95,1)
      GO TO 10
C
C  CONSTANT Q IS OK. CONVERT IT TO METRIC VALUE
C
  130 CONTINUE
      CONSTQ = REAL/CONVLT
      OK(1) = .TRUE.
      GO TO 10
C
C-----------------------------------------------------------------------
C  'ELVSQ' FOUND. ONLY ALLOWED IF 'ELVSMAXQ' NOT ALREADY ENTERED.
C
  200 CONTINUE
      OK(2) = .FALSE.
      IP(2) = IP(2) + 1
      IF (IP(6).GT.0) CALL STRN26(97,1,INPUT(1,6),LINPUT(6))
      IF (IP(2).GT.1) CALL STER26(39,1)
C
C  READ FIRST FIELD AFTER 'ELVSQ'. IF IT'S NUMERIC, GET LIST OF NUMBERS
C   FOR DEFINING CURVE
C  IF IT'S CHARACTER, ASSUME A MULTIPLE USE REFERENCE HAS BEEN ENTERED,
C   SEE IF SPEC IS VALID.
C
C  IN THE 1ST CASE, WE MUST REPOSITION READ POINTER BACK TO READ THE FIR
C  FIELD AFTER 'ELVSQ'.
C
  210 CONTINUE
      LPOSST = LSPEC + NCARD
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.0) GO TO 9000
C
      IQTYPE = ITYPE
      IF (IQTYPE.GT.1) GO TO 260
C
C  REPOSITION TO READ FIRST FIELD AFTER 'ELVSQ' FOR GLST26
C  WHERE ALWAYS READ THE NEXT FIELD
C
      CALL POSN26(MUNI26,LPOSST)
      NCARD = LPOSST - LSPEC - 1
      NUMFLD = 0
      CALL UFLD26(NUMFLD,IERF)
C
C
C GET LIST OF REAL VALUES FOR DEFINING THE SPILLWAY RATING CURVE
C
      NQVAL=100
      CALL GLST26(1,1,X,QVAL,X,NQVAL,OK(2))
      IF (.NOT.OK(2)) GO TO 10
C
C  FIVE CHECKS MUST BE MADE ON THE CURVE:
C   1) THE TOTAL NO. OF VALUES INPUT MUST BE EVEN (PAIRS OF VALUES ARE
C      NEEDED,
C   2) THE ELEVATIONS MUST BE BETWEEN THE DEFINED BOUNDARIES OF THE ELEV
C      VS STORAGE CURVE,
C   3) THE ELEVATIONS MUST BE IN ASCENDING ORDER,
C   4) THE DISCHARGES MUST BE IN ASCENDING ORDER,
C   5) THE DISCHARGES MUST BE POSITIVE.
C
      IF (MOD(NQVAL,2).EQ.0) GO TO 220
C
      CALL STER26(40,1)
      GO TO 10
C
  220 CONTINUE
      NHALF = NQVAL/2
      NSEC = NHALF + 1
C
C  SEE IF ELEVATIONS ARE IN ASCENDING ORDER
C
      CALL ASCN26(QVAL,NHALF,0,IERA)
      IF (IERA.GT.0) GO TO 10
C
C  SEE IF DISCHARGES ARE IN ASCENDING ORDER
C
      CALL ASCN26(QVAL(NSEC),NHALF,0,IERA)
      IF (IERA.GT.0) GO TO 10
C
C  SEE IF DISCHARGES ARE ALL POSITIVE
C
      DO 240 I=NSEC,NQVAL
      QVAL(I) = QVAL(I)/CONVLT
      IF (QVAL(I).GE.0.00) GO TO 240
C
      CALL STER26(95,1)
      GO TO 10
C
  240 CONTINUE
C
C  SEE IF ELEVATIONS ARE WITHIN BOUNDS OF ELEV VS. STORAGE CURVE
C  IF STORAGE CURVE WAS NOT DEFINED, CALL IT AN ERROR
C
      DO 255 I=1,NHALF
      QVAL(I) = QVAL(I)/CONVL
  255 CONTINUE
C
      CALL ELST26(QVAL,NHALF,IERST)
      IF (IERST.GT.0) GO TO 10
C
C  STORE CODE FOR SPILLWAY RATING IN /MULT26/
C
      NMDEF(2) = NMDEF(2) + 1
      DMCODE(NMDEF(2),2) = CODEQM
C
      GO TO 295
C
C--------------------------------
C  FIRST FIELD AFTER 'ELVSQ' IS CHARACTER. SEE IF IT'S A VALID S/U ID
C  WITH OR WITHOUT PARENTHESES
C
  260 CONTINUE
      CALL MREF26(2,REFCDQ,LOCWK,LOCCDQ,IERM)
      IF (IERM.GT.0) GO TO 10
C
C  CURVE IS DEFINED OK
C
  295 CONTINUE
      OK(2) = .TRUE.
      GO TO 10
C
C-----------------------------------------------------------------------
C  'HEADVSQ' FOUND. ONLY ALLOWED IF 'ELVSMAXQ' OR 'CONSTQ' NOT ENTERED.
C
  300 CONTINUE
      OK(3) = .FALSE.
      IP(3) = IP(3) + 1
      IF (IP(1).GT.0) CALL STRN26(97,1,INPUT(1,1),LINPUT(1))
      IF (IP(6).GT.0) CALL STRN26(97,1,INPUT(1,6),LINPUT(6))
      IF (IP(3).GT.1) CALL STER26(39,1)
C
C  READ FIRST FIELD AFTER 'HEADVSQ'. IF IT'S NUMERIC, GET LIST OF NUMBER
C   FOR DEFINING CURVE
C  IF IT'S CHARACTER, ASSUME A MULTIPLE USE REFERENCE HAS BEEN ENTERED,
C   SEE IF SPEC IS VALID.
C
C  IN THE 1ST CASE, WE MUST REPOSITION READ POINTER BACK TO READ THE FIR
C  FIELD AFTER 'HEADVSQ'.
C
  310 CONTINUE
      LPOSST = LSPEC + NCARD
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.0) GO TO 9000
C
      IHTYPE = ITYPE
      IF (IHTYPE.GT.1) GO TO 360
C
C  REPOSITION TO READ FIRST FIELD AFTER 'HEADVSQ'
C
      CALL POSN26(MUNI26,LPOSST)
      NCARD = LPOSST - LSPEC - 1
      NUMFLD = 0
      CALL UFLD26(NUMFLD,IERF)
C
C
C GET LIST OF VALUES FOR DEFINING THE HEAD VS. Q CURVE
C
      NHVAL=100
      CALL GLST26(1,1,X,HVAL,X,NHVAL,OK(3))
      IF (.NOT.OK(3)) GO TO 10
C
C  FIVE CHECKS MUST BE MADE ON THE CURVE:
C   1) THE TOTAL NO. OF VALUES INPUT MUST BE EVEN (PAIRS OF VALUES ARE
C      NEEDED,
C   2) THE HEAD VALUES MUST BE IN ASCENDING ORDER,
C   3) THE DISCHARGES MUST BE IN ASCENDING ORDER,
C   4) THE DISCHARGES AND HEAD VALUES MUST BE POSITIVE.
C
      IF (MOD(NHVAL,2).EQ.0) GO TO 320
C
      CALL STER26(40,1)
      GO TO 10
C
  320 CONTINUE
      NHALF = NHVAL/2
      NSEC = NHALF + 1
C
C  SEE IF HEAD VALUES ARE IN ASCENDING ORDER
C
      CALL ASCN26(HVAL,NHALF,0,IERA)
      IF (IERA.GT.0) GO TO 10
C
C  SEE IF DISCHARGES ARE IN ASCENDING ORDER
C
      CALL ASCN26(HVAL(NSEC),NHALF,0,IERA)
      IF (IERA.GT.0) GO TO 10
C
C  SEE IF ALL VALUES ARE ALL POSITIVE
C
      DO 340 I=1,NHVAL
      CONV = CONVL
      IF (I.GT.NHALF) CONV = CONVLT
      HVAL(I) = HVAL(I)/CONV
      IF (HVAL(I).GE.0.00) GO TO 340
C
      CALL STER26(95,1)
      GO TO 10
C
  340 CONTINUE
C
C  STORE CODE FOR HEAD VS DISCHARGE CURVE IN /MULT26/
C
      NMDEF(3) = NMDEF(3) + 1
      DMCODE(NMDEF(3),3) = CODEQM
C
      GO TO 395
C
C--------------------------------
C  FIRST FIELD AFTER 'HEADVSQ' IS CHARACTER. SEE IF IT'S A VALID S/U ID
C  WITH OR WITHOUT PARENTHESES
C
  360 CONTINUE
      CALL MREF26(3,REFCDH,LOCWK,LOCCDH,IERM)
      IF (IERM.GT.0) GO TO 10
C
C  CURVE IS DEFINED OK
C
  395 CONTINUE
      OK(3) = .TRUE.
      GO TO 10
C
C-----------------------------------------------------------------------
C  'TWCURVE' FOUND. ONLY ALLOWED IF 'ELVSMAXQ' OR 'CONSTQ' NOT ENTERED.
C
  400 CONTINUE
      OK(4) = .FALSE.
      IP(4) = IP(4) + 1
      IF (IP(1).GT.0) CALL STRN26(97,1,INPUT(1,1),LINPUT(1))
      IF (IP(6).GT.0) CALL STRN26(97,1,INPUT(1,6),LINPUT(6))
      IF (IP(4).GT.1) CALL STER26(39,1)
C
C  GET THE RATING CURVE NAME FROM NEXT FIELD.
C
  410 CONTINUE
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.0) GO TO 9000
C
C  SEE IF RATING CURVE HAS BEEN DEFINED WITHIN THE FORECAST COMPONENT.
C
      CALL CHEKRC(CHAR,IERR)
      IF (IERR.GT.0) GO TO 10
C
C  SEE IF WE'VE ALREADY DEFINED A TAIL WATER RATING CURVE WITH DIFFERENT
C  NAME.
C
      CALL CKRC26(1,CHAR,IERR)
      IF (IERR.GT.0) GO TO 10
C
C  CURVE IS DEFINED OK
C
  495 CONTINUE
      OK(4) = .TRUE.
      GO TO 10
C
C------------------------------------------------------------------
C  'CONV' FOUND. ONLY ALLOWED IF 'ELVSMAXQ' OR 'CONSTQ' NOT ENTERED.
C  USE THE DEFAULT (2%) IF NOT ENTERED.
C
  500 CONTINUE
      OK(5) = .FALSE.
      IP(5) = IP(5) + 1
      IF (IP(5).GT.1) CALL STER26(39,1)
           IF(IP(1).GT.0) CALL STRN26(97,1,INPUT(1,1),LINPUT(1))
C
C  GET NEXT FIELD. ALLOWED FIELDS ARE THE NULL FIELD, OR A REAL POSITIVE
C  VALUE BETWEEN 0.01 AND 1.00
C
  515 CONTINUE
      NUMFLD = -2
      CALL UFLD26(NUMFLD,IERF)
      IF (IERF.GT.1) GO TO 9000
      IF (IERF.EQ.1) GO TO 590
C
C  LOOKING FOR REAL, POSITIVE VALUE BETWEEN 0.01 AND 1.00
C
      IF (ITYPE.EQ.1) GO TO 520
      CALL STER26(4,1)
      GO TO 10
C
  520 CONTINUE
      IF (REAL.GE.0.01.AND.REAL.LE.1.00) GO TO 530
      CALL STER26(68,1)
      GO TO 10
C
C  SAVE THE VALUE AS ALL IS OK WITH 'CONV'
C
  530 CONTINUE
      TOLRNS = REAL
C
  590 CONTINUE
      OK(5) = .TRUE.
      GO TO 10
C
C-----------------------------------------------------------------------
C  'ELVSMAXQ' FOUND. IT'S THE ELEVATION VS. MAXIMUM
C   DISCHARGE CURVE. ONLY NEEDED IF THE 'ELVSQ' CURVE WAS NOT ENTERED.
C   ALSO NOT ALLOWED IF ANY COMPONENT FOR BUILDING THE CURVE WAS
C   ENTERED (I.E. - 'HEADVSQ', 'TWCURVE', 'CONV', OR 'CONSTQ').
C
  600 CONTINUE
      IP(6) = IP(6) + 1
C
      IF (IP(1) .GT. 0) CALL STRN26(97,1,INPUT(1,1),LINPUT(1))
      IF (IP(2) .GT. 0) CALL STRN26(97,1,INPUT(1,2),LINPUT(2))
      IF (IP(3) .GT. 0) CALL STRN26(97,1,INPUT(1,3),LINPUT(3))
      IF (IP(4) .GT. 0) CALL STRN26(97,1,INPUT(1,4),LINPUT(4))
      IF (IP(5) .GT. 0) CALL STRN26(97,1,INPUT(1,5),LINPUT(5))
C
      IF (IP(6).GT.1) CALL STER26(39,1)
C
      OK(6) = .FALSE.
C
C GET LIST OF VALUES FOR DEFINING THE SPILLWAY RATING CURVE
C
      NQMVAL=100
      CALL GLST26(1,1,X,QMVAL,X,NQMVAL,OK(6))
      IF (.NOT.OK(6)) GO TO 10
C
C  FIVE CHECKS MUST BE MADE ON THE CURVE:
C   1) THE TOTAL NO. OF VALUES INPUT MUST BE EVEN (PAIRS OF VALUES ARE
C      NEEDED,
C   2) THE ELEVATIONS MUST BE BETWEEN THE DEFINED BOUNDARIES OF THE ELEV
C      VS STORAGE CURVE,
C   3) THE ELEVATIONS MUST BE IN ASCENDING ORDER,
C   4) THE DISCHARGES MUST BE IN ASCENDING ORDER,
C   5) THE DISCHARGES MUST BE POSITIVE.
C
      IF (MOD(NQMVAL,2).EQ.0) GO TO 620
C
      CALL STER26(40,1)
      GO TO 10
C
  620 CONTINUE
      NHALF = NQMVAL/2
      NSEC = NHALF + 1
C
C  SEE IF ELEVATIONS ARE IN ASCENDING ORDER
C
      CALL ASCN26(QMVAL,NHALF,0,IERA)
      IF (IERA.GT.0) GO TO 10
C
C  SEE IF DISCHARGES ARE IN ASCENDING ORDER
C
      CALL ASCN26(QMVAL(NSEC),NHALF,0,IERA)
      IF (IERA.GT.0) GO TO 10
C
C  SEE IF DISCHARGES ARE ALL POSITIVE
C
      DO 640 I=NSEC,NQMVAL
      QMVAL(I) = QMVAL(I)/CONVLT
      IF (QMVAL(I).GE.0.00) GO TO 640
C
      CALL STER26(95,1)
      GO TO 10
C
  640 CONTINUE
C
C  SEE IF ELEVATIONS ARE WITHIN BOUNDS OF ELEV VS. STORAGE CURVE
C  IF STORAGE CURVE WAS NOT DEFINED, CALL IT AN ERROR
C
      DO 655 I=1,NHALF
      QMVAL(I) = QMVAL(I)/CONVL
  655 CONTINUE
C
      CALL ELST26(QMVAL,NHALF,IERST)
      IF (IERST.GT.0) GO TO 10
  695 CONTINUE
      OK(6) = .TRUE.
      GO TO 10
C
C--------------------------------------------------------------------
C  END OF INPUT. STORE VALUES IN WORK ARRAY IF EVERYTHING WAS ENTERED
C  WITHOUT ERROR.
C
  900 CONTINUE
      IF (OK(1).AND.OK(2).AND.OK(3).AND.OK(4).AND.OK(5).AND.OK(6)
     .     .AND.ALLOK) GO TO 1010
      GO TO 9999
C
C-------------------------------------------
C  STORE INFORMATION ABOUT MAXIMUM DISCHARGE CURVE.
C  AT LEAST AN 'ELVSMAXQ' OR 'ELVSQ' CURVE MUST HAVE BEEN ENTERED.
C
 1010 CONTINUE
      IF (IP(2).EQ.1 .OR. IP(6).EQ.1) GO TO 1020
C
      CALL STER26(98,1)
      GO TO 9999
C
C  CHECK THE COMBINATION ENTERED.
C
 1020 CONTINUE
      IF (IP(6) .EQ. 0) GO TO 1030
C
C  'ELVSMAXQ' ENTERED. STORE CODE (-1) INDICATING THIS AND STORE
C   CURVE.
C
      OPT = -1.01
      CALL FLWK26(WORK,IUSEW,LEFTW,OPT,501)
C
C  STORE CURVE (FIRST STORE NO. OF PAIRS OF VALUES IN CURVE).
C
      PAIRS = NQMVAL/2 + 0.01
      CALL FLWK26(WORK,IUSEW,LEFTW,PAIRS,501)
C
      DO 1025 I=1,NQMVAL
      CALL FLWK26(WORK,IUSEW,LEFTW,QMVAL(I),501)
 1025 CONTINUE
      NP20 = NP20 + NQMVAL + 2
      GO TO 9999
C
C  IF CONSTANT Q WAS SPECIFIED, STORE A ZERO INDICATING THIS, THE VALUE
C  OF THE CONSTANT Q, AND EITHER THE SPILLWAY RATING OR THE REFERENCE TO
C  IT (THIS DEPENDS ON THE VALUE OF IQTYPE).
C
 1030 CONTINUE
      IF (IP(6).GT.0) CALL STRN26(97,1,INPUT(1,6),LINPUT(6))
      IF (IP(1).EQ.0) GO TO 1050
C
      IF (IP(1).EQ.1.AND.IP(2).EQ.1.AND.IP(3).EQ.0.AND.IP(4).EQ.0)
     .GO TO 1035
      CALL STER26(98,1)
      GO TO 9999
C
C  OPTION FOR 'CONSTQ' IS 0
C
 1035 OPT = 0.01
      CALL FLWK26(WORK,IUSEW,LEFTW,OPT,501)
      CALL FLWK26(WORK,IUSEW,LEFTW,CONSTQ,501)
C
C  IF REFERENCE WAS MADE TO SPILLWAY RATING CURVE, STORE THE NEGATIVE
C  LOCATION. IF IT WAS DEFINED HERE STORE THE CURVE.
C
      IF (IQTYPE.GT.1) GO TO 1045
C
      PAIR = NQVAL/2 + 0.01
      CALL FLWK26(WORK,IUSEW,LEFTW,PAIR,501)
C
C  STORE LOCATION OF THE DEFINITION OF THIS CURVE
C
      IPOWD(NMDEF(2),2) = NPMENT + NP20 + 3
      IWKWD(NMDEF(2),2) = IUSEW
C
C  STORE DEFINITION OF CURVE
C
      DO 1040 I=1,NQVAL
      CALL FLWK26(WORK,IUSEW,LEFTW,QVAL(I),501)
 1040 CONTINUE
      NP20 = NP20 + NQVAL + 3
      GO TO 9999
C
C  STORE REFERENCE TO CURVE IN WORK ARRAY
C
 1045 CONTINUE
      CDQLOC = - (LOCCDQ+0.01)
      CALL FLWK26(WORK,IUSEW,LEFTW,CDQLOC,501)
      NP20 = NP20 + 3
      GO TO 9999
C
C-----------------------------
C  THE COMPONENTS OF THE ELEVATION VS. Q CURVE WERE ENTERED
C  STORE IN ORDER OF SPILLWAY CURVE, HEAD VS. Q CURVE,
C  TAILWATER VS. DISCHARGE CURVE, AND CONVERGENCE CRITERION.
C
 1050 CONTINUE
      IF (IP(2).EQ.1.AND.IP(3).EQ.1.AND.IP(4).EQ.1) GO TO 1055
      CALL STER26(98,1)
      GO TO 9999
C
C  STORE OPTION OF 1.0 IN WORK ARRAY
C
 1055 OPT = 1.01
      CALL FLWK26(WORK,IUSEW,LEFTW,OPT,501)
C
C  FOR ALL THREE CURVES, STORE VALUES IF DEFINED HERE, OR STORE
C  NEGATIVE LOCATION OF DEFINITION IF REFERENCED HERE.
C
      IF (IQTYPE.GT.1) GO TO 1130
C
      PAIR = NQVAL/2 + 0.01
      CALL FLWK26(WORK,IUSEW,LEFTW,PAIR,501)
C
C  STORE LOCATION FOR THIS CURVE DEFINITION
C
      IPOWD(NMDEF(2),2) = NPMENT + NP20 + 2
      IWKWD(NMDEF(2),2) = IUSEW
C
C  STORE CURVE
C
      DO 1120 I=1,NQVAL
      CALL FLWK26(WORK,IUSEW,LEFTW,QVAL(I),501)
 1120 CONTINUE
      NP20 = NP20 + NQVAL + 2
      GO TO 1200
C
C  STORE REFERENCE TO CURVE IN WORK ARRAY
C
 1130 CONTINUE
      CDQLOC = - (LOCCDQ+0.01)
      CALL FLWK26(WORK,IUSEW,LEFTW,CDQLOC,501)
      NP20 = NP20 + 2
      GO TO 1200
C
C  STORE INFORMATION FOR HEAD VS. DISCHARGE CURVE
C
 1200 CONTINUE
      IF (IHTYPE.GT.1) GO TO 1230
C
      PAIR = NHVAL/2 + 0.01
      CALL FLWK26(WORK,IUSEW,LEFTW,PAIR,501)
C
C  STORE LOCATION FOR THIS CURVE DEFINITION
C
      IPOWD(NMDEF(3),3) = NPMENT + NP20 + 1
      IWKWD(NMDEF(3),3) = IUSEW
C
C  STORE CURVE
C
      DO 1220 I=1,NHVAL
      CALL FLWK26(WORK,IUSEW,LEFTW,HVAL(I),501)
 1220 CONTINUE
      NP20 = NP20 + NHVAL + 1
      GO TO 1300
C
C  STORE REFERENCE TO CURVE IN WORK ARRAY
C
 1230 CONTINUE
      CDHLOC = - (LOCCDH+0.01)
      CALL FLWK26(WORK,IUSEW,LEFTW,CDHLOC,501)
      NP20 = NP20 + 1
      GO TO 1300
C
C  STORE INFORMATION ON THE TAILWATER RATING CURVE
C
 1300 CONTINUE
      CALL FLWK26(WORK,IUSEW,LEFTW,RCID26(1,1),501)
      CALL FLWK26(WORK,IUSEW,LEFTW,RCID26(2,1),501)
      NP20 = NP20 + 2
C
C  STORE CONVERGENCE CRITERION
C
      CALL FLWK26(WORK,IUSEW,LEFTW,TOLRNS,501)
      NP20 = NP20 + 1
      GO TO 9999
C
C---------------------------------------------------------------------
C  ERROR IN UFLD26
C
 9000 CONTINUE
      IF (IERF.EQ.1) CALL STER26(19,1)
      IF (IERF.EQ.2) CALL STER26(20,1)
      IF (IERF.EQ.3) CALL STER26(21,1)
      IF (IERF.EQ.4) CALL STER26( 1,1)
C
      IF (NCARD.GE.LASTCD) GO TO 9100
      IF (IBLOCK.EQ.1)  GO TO 5
      IF (IBLOCK.EQ.2)  GO TO 10
C
 9100 USEDUP = .TRUE.
C
 9999 CONTINUE
      RETURN
      END
