C MEMBER WKSP26
C  (from old member FCWKSP26)
C
C DESC COMPUTE AMOUNT OF WORK SPACE NEEDED BY 'RES-SNGL'
C
C-----------------------------------------------
C
      SUBROUTINE WKSP26(PO,NWKSP)
C
C-----------------------------------------------
C  ROUTINE TO COMPUTE THE AMOUNT OF WORK SPACE NEEDED FOR THE RESERVOIR
C  OPERATION (RES-SNGL).
C--------------------------------------------------------------------
C  JTOSTROWSKI - SEPT 1983 - HRL
C--------------------------------------------------------------------
C
      INCLUDE 'common/fprog'
      INCLUDE 'common/suin26'
      INCLUDE 'common/comn26'
      INCLUDE 'common/fdbug'
C
      DIMENSION PO(1),LOCOWS(13)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_res/RCS/wksp26.f,v $
     . $',                                                             '
     .$Id: wksp26.f,v 1.1 1995/09/17 18:53:31 dws Exp $
     . $' /
C    ===================================================================
C
C
      IF (IBUG.GE.1) WRITE(IODBUG,900)
  900 FORMAT(10X,'*** ENTER WKSP26 ***')
C
C----------------------------
C  SET INITIAL VALUES AND COUNTERS
C
      LOCPTR = PO(9)
      NTIM24 = 24/MINODT
      NWKSP = 0
C
      DO 10 I=1,13
      LOCOWS(I) = 0
   10 CONTINUE
C
C-------------------------------
C  COMPUTE THE REQUIRED WORK SPACE FIRST
C
C  GET A COUNT OF ALL DEFINED S/U'S FOR THE OPERATION DEFINITION
C  THE FIRST PART OF THE WORK SPACE HOLDS INFO FOR ALL THE S/U'S USED
C  IN THIS OPERATION DEFINITION. THE CONTENTS OF THIS SECTION ARE:
C    1) THE S/U NUMBER,
C    2) THE ELEMENT IN THE IDPT ARRAY HOLDING THE LOCATION OF THE START
C       OF TIME-SERIES DATA FOR THE FIRST TIME-SERIES USED FOR THIS S/U
C       (IF ANY),
C    3) AN EXECUTION FLAG TO INDICATE IF THE S/U WAS EXECUTED DURING A
C       TIME PERIOD (USED FOR CARRYOVER UPDATING CONTROL).
C
      NWKSP = NWKSP + NSUIN*3
C
C---------------------------
C  THE NEXT PORTION OF WORK SPACE IS USED FOR HOLDING SIMULATED TRACES
C  FOR INSTANTANEOUS AND MEAN DISCHARGE, POOL ELEVATION AND STORAGE
C  CONTENTS.
C  ENOUGH SPACE IS NEEDED TO HOLD A VALUE FOR EACH QUANTITY FOR EACH
C  PERIOD IN THE RUN.
C
      NWKSP = NWKSP + 4*NDD*NTIM24
C
C-----------------------------------
C  THE NEXT PORTION WILL HOLD INFO FOR THE SCHEMES ONLY. WE NEED SIX
C  WORDS FOR EACH SCHEME PLUS ONE TO HOLD THE NUMBER OF SCHEMES.
C   (AN EXCEPTION IS FOR THE SETMIN AND SETMAX UTILITIES. SINCE THEY SET
C    VALUES FOR COMPUTATIONAL OUTPUT, THEY WILL BE CONSIDERED SCHEMES FO
C    THE PURPOSE OF TEMPORARILY STORING THESE VALUES.)
C  THE VALUES HELD IN THIS SECTION ARE THE NO.(1-24) OF THE SCHEME
C  EXECUTED, THE DISCHARGES (BOTH MEAN AND INSTANTANEOUS),
C  THE POOL ELEVATION (BOTH BEG. AND END OF PERIOD), AND STORAGE FOR
C  EACH SCHEME EXECUTED WITHIN A TIME STEP. THIS ALLOWS FOR
C  CHECKING FOR EXECUTION OF AT LEAST ONE SCHEME.
C
C  ADD TWO EXTRA SCHEMES FOR THE CASE OF MULTIPLE EXECS OF SAME SCHEME.
C
      NSCH = 0
      IF (NSUIN .EQ. 0) GO TO 25
      DO 20 I=1,NSUIN
      IBASE = PO(LOCPTR+(I-1)*4+1)/10
      IF (IBASE.LT.150.OR.IBASE.EQ.159.OR.IBASE.EQ.160) NSCH = NSCH + 1
   20 CONTINUE
C
   25 CONTINUE
      NSCH = NSCH + 3
      NWKSP = NWKSP + NSCH*6 + 1
C
C---------------------------------------------------------------
C  NEXT PORTION OF THE WORK SPACE IS RESERVED FOR CARRYOVER STORAGE.
C  RES-SNGL NEEDS TO KEEP A RECORD OF CARRYOVER FOR INTERNAL USE FOR
C  EACH TIME INTERVAL.
C  WE NEED AN AMOUNT EQUAL TO THE NUMBER OF CARRYOVER VALUES USED BY
C  THIS DEFINITION OF THE OPERATION. (HELD IN 15TH POSITION OF THE PO
C  ARRAY)
C
      NUMCO = PO(15)
      NWKSP = NWKSP + NUMCO
C
C---------------------------------------------------------------------
C  NEXT PORTION IS TO HOLD FLAGS AND OTHER INFO FOR DEFAULT CARRYOVER
C  ASSIGNMENT. THIS INFO WILL BE STORED IN SUBROUTINE XDCO26 AND WILL
C  BE USED IN SUBROUTINE XCOU26 (AFTER EACH TIME PERIOD IN RUN).
C
C  IF A SPILLWAY OPERATION IS DEFINED (SU/10 = 106) WE NEED TO SAVE
C  ASIDE THREE EXTRA SPACES FOR ITS INFO.
C  IF 'STPOOLQ' IS DEFINED (SU/10 = 108), WE NEED TO SET ASIDE SEVEN
C  EXTRA SPACES FOR CO DEFAULT INFO.
C  IF 'FLASHBDS' IS DEFINED (SU/10 = 112), WE NEED TO SET ASIDE FOUR
C  EXTRA SPACES FOR CO DEFAULT INFO.
C
      IF (NSUIN .EQ. 0) GO TO 500
      NXTRA = 0
C
      DO 50 I=1,NSUIN
      IBASE = PO(LOCPTR+(I-1)*4+1)/10
      IF (IBASE.EQ.106) NXTRA = NXTRA + 3
      IF (IBASE.EQ.108) NXTRA = NXTRA + 7
      IF (IBASE.EQ.112) NXTRA = NXTRA + 4
   50 CONTINUE
      NWKSP = NWKSP + NSUIN + NXTRA
C
C-----------------------------------------------------
C  NEXT PORTION OF WORK SPACE IS FOR THE OPTIONAL NEEDS. THEY ARE:
C   1) 'BACKFLOW' COMPUTATIONS
C   2) 'RULECURVE' STORAGE OR ADD'L WORK SPACE FOR 'ADJUST'.
C   3) 'SUMINF' INFLOW VOLUME STORAGE
C   4) INTERIM PEAK DISCHARGE VALUE STORAGE OR ADD'L WORK SPACE FOR
C      'BACKFLOW' OR 'ADJUST'.
C   5) 'RAINEVAP'. NEED TO STORE AREA VS. ELEVATION CURVE, ADD'L FLOW
C       CONTRIBUTED BY RAINEVAP, EVAP TIME SERIES, AND TEMPORARY HOLDING
C       SPACE FOR CARRYOVER.
C   6) 'ADJUST RULECURVE' MISSING OBS. AND DEVIATION STORAGE.
C   7) 'ELEV VS. MAXQ' STORAGE, STORAGE VALUES FOR 'POOLQ', OR
C       STORAGE VALUES FOR COMBINED RATING IN FLASHBOARDS.
C   8) 'TOTALQ VS. MAX GENQ' STORAGE, ENTRY INTO SURCHARGE CURVE, OR
C       ADD'L WORK SPACE FOR 'MINQ'.
C   9) TOTAL SPILLWAY RATING FOR FLASHBOARDS OR RULE CURVE STORAGE
C      FOR 'POWERGEN'.
C  10) MODIFIED PULS ROUTING CURVE (COMPUTED FROM RATING CURVES) FOR
C      'FILLSPILL', 'SPILLWAY', 'MINQ' AND 'FLASHBDS'.
C  11) RULE CURVE STORAGE AND RUNNING TIME-SERIES OF MIN AND
C      MAX DISCHARGES FOR 'POWERGEN'.
C  12) INFLOW AND OUTFLOW TIME SERIES AND ADD' WORK SPACE FOR LAG/K FOR
C      'STPOOLQ'.
C  13) INFLOW HOLDING SPACE IF 'MINQ', 'POWERGEN', 'SUMINF', 'FLASH-
C      BOARDS' OR 'RULEADJ' ARE USED.
C
C------------------------------------------------------
C  FIRST LOOP THRU ALL SU'S LOOKIN FOR NEED FOR OPTIONAL WORK SPACE.
C  THE NEEDS ARE SIGNALLED BY S/U NUMBERS:
C  OPTION 1)  SU/10 = 155
C         2)  SU/10 = 104, 107, 108, 109, 111, 113, OR 151 (154 FOR
C                     'ADJUST')
C         3)  SU/10 = 152
C         4)  SU/10 = 105, 106, 109, OR 112 ( 155 FOR 'BACKFLOW',
C                     154 FOR 'ADJUST'.)
C         5)  SU/10 = 153
C         6)  SU/10 = 151
C         7)  SU/10 = 105, 106, 109, 110, 112, OR 157 (107 FOR
C                     'POOLQ')
C         8)  SU/10 = 105, 106, OR 112 (111 FOR SURCHARGE ENTRY CURVE,
C                     109 FOR MINQ WORK SPACE)
C         9)  SU/10 = 112 OR 113
C        10)  SU/10 = 105, 106, 109, OR 112
C        11)  SU/10 = 113
C        12)  SU/10 = 108
C        13)  SU/10 = 109, 112, 113, 151, OR 152
C
      DO 110 I=1,NSUIN
      NUM = PO(LOCPTR+(I-1)*4+1)/10
      IF (NUM.EQ.155) LOCOWS(1) = 1
      IF (NUM.EQ.104.OR.NUM.EQ.107.OR.NUM.EQ.108.OR.NUM.EQ.109.OR.
     . NUM.EQ.111.OR.NUM.EQ.113.OR.NUM.EQ.151.OR.NUM.EQ.154)
     .      LOCOWS(2) = 1
      IF (NUM.EQ.152) LOCOWS(3) = 1
      IF (NUM.EQ.105.OR.NUM.EQ.106.OR.NUM.EQ.109.OR.NUM.EQ.112.OR.
     .   NUM.EQ.155.OR.NUM.EQ.154) LOCOWS(4) = 1
      IF (NUM.EQ.153) LOCOWS(5) = 1
      IF (NUM.EQ.151) LOCOWS(6) = 1
      IF (NUM.EQ.105.OR.NUM.EQ.106.OR.NUM.EQ.107.OR.NUM.EQ.109.OR.NUM
     . .EQ.110.OR.NUM.EQ.112.OR.NUM.EQ.157) LOCOWS(7) = 1
      IF (NUM.EQ.105.OR.NUM.EQ.106.OR.NUM.EQ.109.OR.NUM.EQ.111
     . .OR.NUM.EQ.112) LOCOWS(8) = 1
      IF (NUM.EQ.112.OR.NUM.EQ.113) LOCOWS(9) = 1
      IF (NUM.EQ.105.OR.NUM.EQ.106.OR.NUM.EQ.109.OR.NUM.EQ.112)
     .  LOCOWS(10) = 1
      IF (NUM.EQ.113) LOCOWS(11) = 1
      IF (NUM.EQ.108) LOCOWS(12) = 1
      IF (NUM.EQ.109.OR.NUM.EQ.112.OR.NUM.EQ.113.OR.NUM.EQ.151.OR.
     .    NUM.EQ.152) LOCOWS(13) = 1
  110 CONTINUE
C
C--------------------------------------------
C  IF A 'BACKFLOW' IS FOUND, WE MUST SET ASIDE TWO WORDS FOR EVERY RUN
C  PERIOD. ONE FOR MEAN FLOWS AND THE OTHER FOR INSTANTANEOUS.
C
      IF (LOCOWS(1).EQ.0) GO TO 120
      NWKSP = NWKSP + 2*NDD*NTIM24
C
C---------------------------------------------
C  IF 'RULECURVE' VALUES MUST BE STORED, WE NEED ONE VALUE PER TIME
C  PERIOD. ALSO USED AS WORK SPACE FOR 'ADJUST'.
  120 CONTINUE
      IF (LOCOWS(2).EQ.0) GO TO 130
C
      NWKSP = NWKSP + NDD*NTIM24
C
C--------------------------------------------
C  IF AN INFLOW SUMMATION UTILITY IS FOUND, WE NEED A VALUE PER TIME
C  PERIOD.
C
  130 CONTINUE
      IF (LOCOWS(3).EQ.0) GO TO 140
C
      NWKSP = NWKSP + NDD*NTIM24
C
C-------------------------------------------
C  IF WE HAVE NEED TO STORE INTERIM PEAK VALUES, WE NEED TO STORE TWO
C  VALUES PER TIME PERIOD.
C  ALSO, SET THE NO. OF SPACES AVAILABLE FOR HOLDING THESE INTERIM
C  VALUES. KEPT IN /RESV26/ AS NTOTPK.
C  ALSO FOR USE BY 'BACKFLOW' AS WORK SPACE.
C
  140 CONTINUE
      IF (LOCOWS(4).EQ.0) GO TO 150
C
      NWKSP = NWKSP + 2*NDD*NTIM24
C
C-----------------------------------------------
C  IF A RAIN/EVAP UTILITY WAS SPECIFIED, WE NEED TO STORE ENOUGH VALUES
C  THAT DEFINED THE STORAGE CURVE, TO HOLD THE ADD'L FLOW TS, A DAILY
C  EVAP TIME-SERIES, AND TO TEMPORARILY STORE CARRYOVER.
C
  150 CONTINUE
      IF (LOCOWS(5).EQ.0) GO TO 160
C
      NWKSP = NWKSP + NDD*NTIM24 + 2*NSE + NDD + NUMCO
C
C-------------------------------------------------------------------
C  IF 'ADJUST RULECURVE' UTILITY IS USED, WE NEED TO STORE MISSING VALUE
C  AND DEVIATION ARRAYS, WE NEED ENOUGH SPACE FOR TWO VALUES PER TIME
C  PERIOD
C
  160 CONTINUE
      IF (LOCOWS(6).EQ.0) GO TO 170
C
      NWKSP = NWKSP + 2*NDD*NTIM24
C
C---------------------------------------------------------------------
C  IF WE NEED TO STORE AN ELEV VS. MAXQ CURVE (OR 'POOLQ' STORAGES),
C  RESERVE SPACE EQUAL TO THE NUMBER OF PAIRS OF VALUES IN THE ELEV
C  VS. STORAGE CURVE.
C
  170 CONTINUE
      IF (LOCOWS(7).EQ.0) GO TO 180
C
      NWKSP = NWKSP + NSE
      GO TO 180
C
C---------------------------------------------------------------------
C  IF WE NEED TO STORE A TOTAL DISCHARGE VS. MAXIMUM GENERATION Q
C  CURVE (OR SURCHARGE ENTRY CURVE), RESERVE TWO TIMES THE NUMBER OF
C  PAIRS OF VALUES IN THE ELEV VS. STORAGE CURVE.
C
  180 CONTINUE
      IF (LOCOWS(8).EQ.0) GO TO 190
C
      NWKSP = NWKSP + 2*NSE
C
C----------------------------------------------------
C  FLASHBOARDS WERE FOUND. NEED TO SET ASIDE SPACE FOR THE COMBINED
C  SPILLWAY RATING. NEED AS MUCH SPACE AS NEEDED TO STORE HALF
C  THE STORAGE - ELEVATION CURVE.
C
  190 CONTINUE
      IF (LOCOWS(9).EQ.0) GO TO 200
C
      NWKSP = NWKSP + NSE
C
C---------------------------------------------------------------------
C  WE NEED TO STORE MODIFIED PULS CURVES. WE NEED THREE TIMES THE NO. OF
C  VALUES USED TO STORE LARGEST SPILLWAY RATING CURVE.
C
  200 CONTINUE
      IF (LOCOWS(10).EQ.0) GO TO 210
C
      CALL MAXV26(PO,MAX)
      MAXVAL = MAX0(MAX,NSE)
      NWKSP = NWKSP + 3*MAXVAL
C
C--------------------------------------------------------------------
C  NEED TO STORE RULE CURVE STORAGE, AND RUNNING TIME SERIES FOR MIN AND
C  MAX DISCHARGES FOR 'POWERGEN'.
C
  210 CONTINUE
      IF (LOCOWS(11).EQ.0) GO TO 220
C
      NWKSP = NWKSP + 3*NDD*NTIM24
C
C---------------------------------------------------
C  NEED SPACE FOR EACH TIME PERIOD OF THE RUN FOR INFLOW AND OUTFLOW
C  TIME SERIES AND ADD'L WORK SPACE FROM LAG/K FOR 'STPOOLQ'.
C
  220 CONTINUE
      IF (LOCOWS(12) .EQ. 0) GO TO 230
C
      NWKSP = NWKSP + 8*NDD*NTIM24
C------------------------------------------------
C  'MINQ', 'POWERGEN', 'SUMINF', 'FLASHBOARDS', OR 'RULEADJ' ARE USED.
C  NEED HOLDING SPACE FOR
C  BOTH INST. AND MEAN INFLOWS. THESE S/U'S USE THE ENTIRE INFLOW ARRAYS
C  AND WE NEED A CONSTANT LOCATION TO STORE ANY MODIFIED VALUES.
C
  230 CONTINUE
      IF (LOCOWS(13) .EQ. 0) GO TO 500
C
      NWKSP = NWKSP + 2*NDD*NTIM24
C
C--------------------------------------------------------
C  ALL DONE COMPUTING WORK SPACE
C
  500 CONTINUE
C
C  PRINT OUT ALLOCATION LOCATIONS FOR REQUIRED AND OPTIONAL WORK SPACE
C
      IF (IBUG.GE.2) WRITE(IODBUG,1690) NWKSP,
     .               (LOCOWS(K),K=1,13)
 1690 FORMAT(1H0,' NO. OF WORDS NEEDED FOR WORK SPACE = ',I4,
     ./5X,'       OPTIONAL WORK SPACE = ',13I5)
C
      IF (IBUG.GE.1) WRITE(IODBUG,901)
  901 FORMAT(11X,'*** EXIT WKSP26 ***')
C
      RETURN
      END
