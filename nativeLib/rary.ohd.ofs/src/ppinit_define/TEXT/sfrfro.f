C MEMBER SFRFRO
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 03/07/94.08:36:16 BY $WC20SV
C
C @PROCESS LVL(77)
C
C DESC ROUTINE FOR DEFINING RAINFALL-RUNOFF RELATION PARAMETERS
C DESC PPINIT COMMAND :  @DEFINE RFROCNST
C
      SUBROUTINE SFRFRO (LARRAY,ARRAY,DISP,PRPARM,PRNOTE,NFLD,
     *   IRUNCK,ISTAT)
C
C
      REAL XRFRO/4HRFRO/
      REAL XMSNG/4HM   /
      REAL XMINCK(10)/.0,1.,1.,.0,.5,1.,.0,.0,.0,1./
      REAL XMAXCK(10)/12.,52.,52.,30.,1.25,30.,10.,10.,1.,5./
      REAL XNEW/4HNEW /,XOLD/4HOLD /,XNO/4HNO  /
      REAL SLASH/4H/   /,CKNULL(2)/4H,,  ,4HNULL/
C
      DIMENSION ARRAY(LARRAY)
      DIMENSION CHAR(5),CHK(5)
      DIMENSION IRFRO(1000),UNUSED(5)
      INCLUDE 'scommon/dimrfro'
C
      INCLUDE 'uio'
      INCLUDE 'scommon/sudbgx'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppinit_define/RCS/sfrfro.f,v $
     . $',                                                             '
     .$Id: sfrfro.f,v 1.1 1995/09/17 19:11:34 dws Exp $
     . $' /
C    ===================================================================
C
C
C
      IF (ISTRCE.GT.0) WRITE (IOSDBG,520)
      IF (ISTRCE.GT.0) CALL SULINE (IOSDBG,1)
C
C  SET DEBUG LEVEL
      LDEBUG=ISBUG(XRFRO)
C
      ISTAT=0
C
      LCHAR=5
      LCHK=5
      ILPFND=0
      IRPFND=0
      ISTRT=-1
      MAXFLD=13
      MRFRO=1000
      MPARM=10
      TDISP=DISP
C
      NUMFLD=0
      NUMERR=0
      NUMWRN=0
      INWFLG=0
C
C  CHECK NUMBER OF LINES LEFT ON PAGE
      IF (ISLEFT(5).GT.0) CALL SUPAGE
C
C  PRINT CARD
      CALL SUPCRD
C
C  PRINT HEADER LINE
      WRITE (LP,540)
      CALL SULINE (LP,2)
      WRITE (LP,530)
      CALL SULINE (LP,2)
C
C  PRINT OPTIONS
      WRITE (LP,580) DISP,PRPARM,PRNOTE
      CALL SULINE (LP,2)
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK IF PREPROCESSOR PARAMETRIC DATA BASE ALLOCATED
      IDPPP=1
      CALL SUDALC (0,0,0,0,IDPPP,0,0,0,0,0,NUMERR,IERR)
      IF (IERR.EQ.0) GO TO 20
         WRITE (LP,590)
         CALL SUERRS (LP,2,NUMERR)
         GO TO 30
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  GET RAINFALL-RUNOFF RELATIONSHIP NUMBERS CURRENTLY DEFINED
C
20    CALL SUGTRR (LARRAY,ARRAY,MRFRO,IRFRO,NRFRO,IERR)
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  SET VALUE FOR MISSING PARAMETER
30    UNSD=-999.
      UNDEF=-997.
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK FIELDS FOR DEFINE RFRO OPTIONS
C
40    IF (INWFLG.EQ.0) GO TO 50
         INULL=1
         GO TO 80
50    INULL=0
      NULREP=0
      CALL UFIELD (NFLD,ISTRT,LENGTH,ITYPE,NREP,INTEGR,REAL,
     *   LCHAR,CHAR,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,IERR)
      IF (NFLD.EQ.-1) GO TO 130
      IF (LDEBUG.GT.0)
     *   CALL UPRFLD (NFLD,ISTRT,LENGTH,ITYPE,NREP,INTEGR,REAL,
     *   LCHAR,CHAR,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,IERR)
C
C  CHECK FOR NULL FIELD
      IF (IERR.NE.1) GO TO 70
         IF (LDEBUG.GT.0) WRITE (IOSDBG,620) NFLD
         IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
         IF (TDISP.EQ.XNEW) GO TO 60
            NUMFLD=NUMFLD+1
            IF (NFLD.EQ.1) CALL SUPCRD
            GO TO 40
60       INULL=1
         GO TO 160
C
C  CHECK FOR PARENTHESIS IN FIELD
70    IF (LLPAR.GT.0) CALL UFPACK (LCHK,CHK,ISTRT,1,LLPAR-1,IERR)
      IF (LLPAR.EQ.0) CALL UFPACK (LCHK,CHK,ISTRT,1,LENGTH,IERR)
C
C  CHECK FOR PAIRED PARENTHESES
      CALL SUPFND (ILPFND,IRPFND,NFLD,0)
      IF (LLPAR.GT.0) ILPFND=1
      IF (LRPAR.GT.0) IRPFND=1
C
C  CHECK IF DISP IS NEW AND ALL FIELD FOUND
80    IF (INWFLG.EQ.0) GO TO 90
         IF (NUMFLD.GT.MAXFLD) GO TO 450
         GO TO 150
C
C  CHECK FOR REPEATED NULL FIELD
90    IF (NREP.GT.0) NULREP=NREP-1
      IF (CHAR(1).NE.CKNULL(1).AND.CHAR(1).NE.CKNULL(2)) GO TO 120
         IF (LDEBUG.GT.0) WRITE (LP,630) NREP
         IF (LDEBUG.GT.0) CALL SULINE (LP,1)
         IF (NREP.GT.0.AND.NREP+NUMFLD.LE.MAXFLD) GO TO 100
            WRITE (LP,640) NREP,MAXFLD
            CALL SUERRS (LP,2,NUMERR)
            NREP=MAXFLD-NUMFLD
100      IF (TDISP.EQ.XOLD) GO TO 110
            NULREP=NREP-1
            INULL=1
            GO TO 150
110      NUMFLD=NUMFLD+NREP
         GO TO 40
C
C  CHECK FOR COMMAND
120   IF (LATSGN.EQ.1) GO TO 130
C
C  CHECK FOR END OF INPUT
      IF (CHK(1).EQ.SLASH) GO TO 130
C
C  IF FIRST FIELD, CONTENTS HAS BEEN CHECK FOR KEYWORD
      IF (NUMFLD.EQ.0) GO TO 150
C
      IF (LDEBUG.GT.0) WRITE (IOSDBG,660) CHK
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
C
C  CHECK FOR KEYWORD
      CALL SUIDCK ('DEFN',CHK,NFLD,0,IKEYWD,IERR)
      IF (IERR.NE.2) GO TO 150
130      IF (TDISP.EQ.XNEW.AND.NUMFLD.LT.MAXFLD) GO TO 140
            GO TO 450
140      INWFLG=1
         IF (PRNOTE.EQ.XNO) GO TO 40
            WRITE (LP,670)
            CALL SULINE (LP,2)
            GO TO 40
C
C  PRINT CARD
150   IF (NUMFLD.GT.0.AND.NFLD.EQ.1.AND.INWFLG.EQ.0) CALL SUPCRD
C
C  CHECK IF MAXIMUM NUMBER OF FIELD EXPECTED EXCEEDED
160   NUMFLD=NUMFLD+1
      IF (TDISP.EQ.XOLD.AND.INULL.EQ.1) GO TO 40
      IF (NUMFLD.GT.MAXFLD) GO TO 430
C
      IF (LDEBUG.GT.0) WRITE (IOSDBG,550) NUMFLD,MAXFLD,NUMERR
      IF (LDEBUG.GT.0) CALL SULINE (LP,1)
      GO TO (170,210,290,350,350,350,350,350,350,350,
     *       350,350,350),NUMFLD
         WRITE (LP,600) NUMFLD,MAXFLD
         CALL SULINE (LP,2)
         GO TO 40
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK PARAMETER TYPE
C
170   IF (CHK(1).EQ.XRFRO) GO TO 180
         WRITE (LP,720) XRFRO,NFLD,CHK(1)
         CALL SUWRNS (LP,2,NUMWRN)
         GO TO 190
180   TYPE=CHK(1)
      IF (LDEBUG.GT.0) WRITE (IOSDBG,610) TYPE
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
C
190   TDISP=DISP
      NPARM=0
      DO 200 I=1,MPARM
         RFROPM(I)=UNDEF
200      CONTINUE
C
      GO TO 40
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  RAINFALL-RUNOFF RELATION IDENTIFIER
C
210   IF (INULL.EQ.0) GO TO 220
         IF (TDISP.EQ.XOLD) GO TO 420
         WRITE (LP,680) NUMFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 420
220   IF (ITYPE.EQ.2) GO TO 230
         WRITE (LP,730) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 420
230   IF (LENGTH.LE.8) GO TO 240
         WRITE (LP,760) LENGTH
         CALL SUWRNS (LP,2,NUMWRN)
240   RFROID(1)=CHAR(1)
      RFROID(2)=CHAR(2)
      IF (LDEBUG.GT.0) WRITE (IOSDBG,770) RFROID
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
C
C  READ RFRO PARAMETER RECORD
      IPRERR=0
      IPTR=0
      INCLUDE 'scommon/callsrrfro'
C
C  PARAMETERS EXIST AND DISP IS NEW
      IF (IERR.EQ.0.AND.TDISP.EQ.XNEW) GO TO 250
         GO TO 260
250   WRITE (LP,560) RFROID
      CALL SUERRS (LP,2,NUMERR)
      TDISP=XOLD
      GO TO 280
C
C  PARAMETERS DO NOT EXIST AND DISP IS OLD
260   IF (IERR.GT.0.AND.TDISP.EQ.XOLD) GO TO 270
         GO TO 280
270   WRITE (LP,570)
      CALL SUWRNS (LP,2,NUMWRN)
      TDISP=XNEW
      GO TO 30
C
C  PRINT RFRO PARAMETERS
280   IF (LDEBUG.EQ.0.OR.IERR.GT.0) GO TO 420
      INCLUDE 'scommon/callsprfro'
C
      GO TO 420
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  RAINFALL-RUNOFF RELATIONSHIP NUMBER
C
290   IF (INULL.EQ.0) GO TO 300
         IF (TDISP.EQ.XOLD) GO TO 420
         WRITE (LP,680) NUMFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 420
300   IF (ITYPE.EQ.0) GO TO 310
         WRITE (LP,750) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 420
310   NURFRO=INTEGR
      IF (NRFRO.EQ.0) GO TO 340
         DO 320 I=1,NRFRO
            IF (NURFRO.EQ.IRFRO(I)) GO TO 330
320         CONTINUE
            GO TO 340
330      WRITE (LP,780) NURFRO
         CALL SUERRS (LP,2,NUMERR)
         GO TO 420
340   IF (LDEBUG.GT.0) WRITE (IOSDBG,790) NURFRO
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
      NRFRO=NRFRO+1
      IRFRO(NRFRO)=NURFRO
      GO TO 420
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  RAINFALL-RUNOFF PARAMETERS
C
350   IF (INULL.EQ.0) GO TO 360
         REAL=UNSD
         IF (PRNOTE.EQ.XNO) GO TO 390
            WRITE (LP,690) NUMFLD,REAL
            CALL SULINE (LP,2)
            GO TO 390
360   IF (NPARM.GT.0) GO TO 370
         IF (NREP.LE.0) GO TO 370
            IBEG=LASK+1
            NCHAR=LENGTH-IBEG+1
            CALL SUBSTR (CHAR,IBEG,NCHAR,CHAR,1)
            IF (CHAR(1).EQ.XMSNG) GO TO 370
               IBEG=1
               IEND=LASK-1
               CALL UFRLFX (REAL,ISTRT,IBEG,IEND,0,IERR)
               IF (LDEBUG.GT.0) WRITE (IOSDBG,800) CHAR(1),REAL
               IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
370   IF (CHAR(1).NE.XMSNG) GO TO 380
         REAL=UNSD
         GO TO 390
380   IF (ITYPE.NE.2) GO TO 390
         IF (NREP.GT.0) GO TO 390
            WRITE (LP,740) NUMFLD,NFLD
            CALL SUERRS (LP,2,NUMERR)
            GO TO 420
390   NPARM=NPARM+1
      IF (NPARM.LE.MPARM) GO TO 400
         WRITE (LP,810) MPARM
         CALL SUERRS (LP,2,NUMERR)
         GO TO 420
400   IF (REAL.EQ.UNSD) GO TO 410
      XMIN=XMINCK(NPARM)-.0001
      XMAX=XMAXCK(NPARM)+.0001
      IGT=0
      IGE=0
      IF (NPARM.EQ.1.OR.NPARM.EQ.4.OR.NPARM.EQ.7.OR.NPARM.EQ.8.OR.
     *    NPARM.EQ.9) IGT=1
      IF (NPARM.EQ.2.OR.NPARM.EQ.3.OR.NPARM.EQ.5.OR.NPARM.EQ.6.OR.
     *    NPARM.EQ.10) IGE=1
      IF (LDEBUG.GT.0) WRITE (IOSDBG,820) NPARM,IGT,IGE,REAL,XMIN,XMAX
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
      IF (IGT.EQ.1.AND.REAL.GT.XMIN.AND.REAL.LE.XMAX) GO TO 410
      IF (IGE.EQ.1.AND.REAL.GE.XMIN.AND.REAL.LE.XMAX) GO TO 410
         IF (IGT.EQ.1) WRITE (LP,830) NPARM,REAL,XMIN,XMAX
         IF (IGE.EQ.1) WRITE (LP,840) NPARM,REAL,XMIN,XMAX
         CALL SUERRS (LP,2,NUMERR)
         GO TO 420
410   RFROPM(NPARM)=REAL
      IF (LDEBUG.GT.0) WRITE (IOSDBG,850) NPARM,RFROPM(NPARM)
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
      GO TO 420
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK FOR REPEATED NULL FIELDS
420   IF (NULREP.EQ.0) GO TO 40
         NULREP=NULREP-1
         GO TO 160
C
C  MAXIMUM FIELDS PROCESSED BUT NO KEYWORD FOUND - SEARCH FOR
C  NEXT KEYWORD
430   IF (NULREP.EQ.0) ISTRT=-1
440   CALL UFIELD (NFLD,ISTRT,LENGTH,ITYPE,NREP,INTEGR,REAL,
     *   LCHAR,CHAR,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,IERR)
      IF (LATSGN.EQ.1) GO TO 450
      IF (LLPAR.GT.0) CALL UFPACK (LCHK,CHK,ISTRT,1,LLPAR-1,IERR)
      IF (LLPAR.EQ.0) CALL UFPACK (LCHK,CHK,ISTRT,1,LENGTH,IERR)
      IF (CHK(1).EQ.SLASH) GO TO 450
      CALL SUIDCK ('DEFN',CHK,NFLD,0,IKEYWD,IERR)
      IF (IERR.EQ.2) GO TO 450
         WRITE (LP,880) MAXFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 440
C
C  CHECK NUMBER OF FIELDS PROCESSED
450   IF (NUMFLD.GT.1) GO TO 460
         WRITE (LP,890)
         CALL SULINE (LP,2)
         GO TO 500
C
460   IF (NUMERR.GT.0) GO TO 480
C
C  CHECK IF ALL PARAMETER VALUES DEFINED
      NFOUND=0
      DO 470 I=1,NPARM
         IF (RFROPM(I).EQ.UNDEF) GO TO 470
         NFOUND=NFOUND+1
470      CONTINUE
         IF (NFOUND.EQ.MPARM) GO TO 480
         WRITE (LP,870) NFOUND,MPARM
         CALL SUERRS (LP,2,NUMERR)
C
C  CHECK IF RUNCHECK OPTION SPECIFIED
480   IF (IRUNCK.EQ.1) GO TO 500
C
C  WRITE PARAMETERS IF NO ERRORS ENCOUNTERED
      IF (NUMERR.EQ.0) GO TO 490
         WRITE (LP,860) NUMERR
         CALL SULINE (LP,2)
         ISTAT=1
         GO TO 500
C
C  WRITE PARAMETERS TO FILE
490   IVRFRO=1
      WDISP=TDISP
      CALL SWRFRO (IVRFRO,RFROID,NURFRO,RFROPM,
     *   UNUSED,UNSD,LARRAY,ARRAY,IPTR,WDISP,
     *   IERR)
C
      IF (IERR.GT.0) GO TO 500
C
C  READ PARAMETERS
      IPRERR=1
      IPTR=0
      CALL SRRFRO (IVRFRO,UNUSED,
     *   RFROID,NURFRO,RFROPM,
     *   LARRAY,ARRAY,IPRERR,IPTR,IPTRNX,
     *   IERR)
      IF (IERR.GT.0) GO TO 500
C
C  PRINT PARAMETERS
      CALL SPRFRO (IVRFRO,RFROID,NURFRO,RFROPM,
     *   UNUSED,ISTAT)
C
C  CHECK IF ANOTHER RAINFALL-RUNOFF RELATIONSHIP TO BE DEFINED
500   IF (LDEBUG.GT.0) WRITE (IOSDBG,900) CHK(1),XRFRO
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
      IF (CHK(1).NE.XRFRO) GO TO 510
         CALL SUPCRD
         NUMFLD=0
         INWFLG=0
         NUMERR=0
         NUMWRN=0
         GO TO 150
C
510   IF (ISTRCE.GT.0) WRITE (IOSDBG,910)
      IF (ISTRCE.GT.0) CALL SULINE (IOSDBG,1)
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
520   FORMAT (' *** ENTER SFRFRO')
530   FORMAT ('0*--> DEFINE RAINFALL-RUNOFF RELATION PARAMETERS')
540   FORMAT (' ')
550   FORMAT (' NUMFLD=',I2,3X,'MAXFLD=',I2,3X,'NUMERR=',I2)
560   FORMAT ('0*** ERROR - DISPOSITION OF NEW WAS SPECIFIED FOR ',
     *   'RFRO PARAMETERS FOR IDENTIFIER ',2A4,
     *   ' BUT PARAMETERS ALREADY EXIST.')
570   FORMAT ('0*** WARNING - RFRO PARAMETER RECORD DOES NOT EXIST.',
     *   ' DISP ASSUMED TO BE NEW.')
580   FORMAT ('0DEFINE OPTIONS IN EFFECT :  DISP=',A4,3X,
     *   'PRNTPARM=',A4,3X,'PRNTNOTE=',A4)
590   FORMAT ('0*** ERROR - PREPROCESSOR PARAMETRIC DATA BASE FILES ',
     *   'ARE NOT ALLOCATED. INPUT DATA WILL BE CHECKED FOR ERRORS.')
600   FORMAT ('0*** ERROR - FIELD ',I2,' EXCEEDS MAXIMUM ALLOWABLE ',
     *   'FIELDS (',I2,').')
610   FORMAT (' PARAMETER TYPE IS ',A4)
620   FORMAT (' *** NOTE - BLANK STRING FOUND IN FIELD ',I2)
630   FORMAT (' NUMBER OF NULL FIELDS TO BE REPEATED = ',I2)
640   FORMAT ('0*** ERROR - ',I3,' IS AN INVALID NUMBER OF NULL ',
     *   'FIELDS TO BE REPEATED. MAXIMUM FIELDS ALLOWED IS ',I2,'.')
660   FORMAT (' INPUT FIELD = ',5A4)
670   FORMAT ('0*** NOTE - REMAINING RFRO PARAMETERS WILL ',
     *   'BE FILLED WITH DEFAULT VALUES.')
680   FORMAT ('0*** ERROR - NO VALUE FOUND FOR REQUIRED FIELD ',I2,'.')
690   FORMAT ('0*** NOTE - NO VALUE FOUND FOR REQUIRED FIELD ',I2,
     *   '. DEFAULT VALUE (',F7.2,') WILL BE USED.')
720   FORMAT ('0*** ERROR - IN SFRFRO - THE CHARACTERS ',A4,
     *   'WERE EXPECTED IN FIELD ',I2,' BUT ',A4,' WERE FOUND.')
730   FORMAT ('0*** ERROR - CHARACTER DATA EXPECTED IN INPUT ',
     *   'FIELD ',I2,' (CARD FIELD ',I2,').')
740   FORMAT ('0*** ERROR - NON-CHARACTER DATA EXPECTED IN INPUT ',
     *   'FIELD ',I2,' (CARD FIELD ',I2,').')
750   FORMAT ('0*** ERROR - INTEGER DATA EXPECTED IN INPUT ',
     *   'FIELD ',I2,' (CARD FIELD ',I2,').')
760   FORMAT ('0*** WARNING - NUMBER OF CHARACTERS IN IDENTIFIER (',I2,
     *   ') EXCEEDS 8. THE FIRST 8 CHARACTERS WILL BE USE.')
770   FORMAT (' RAINFALL-RUNOFF IDENTIFIER NAME SET TO : ',2A4)
780   FORMAT ('0*** ERROR - RAINFALL-RUNOFF RELATIONSHIP NUMBER ',I4,
     *   ' IS ALREADY BEING USED.')
790   FORMAT (' RAINFALL-RUNOFF RELATIONSHIP NUMBER SET TO : ',I4)
800   FORMAT (' CHAR(1)=',A4,3X,'REAL=',G15.6)
810   FORMAT ('0*** ERROR - MAXIMUM NUMBER OF RAINFALL-RUNOFF ',
     *   'RELATIONSHIP PARAMETERS (',I2,') EXCEEDED.')
820   FORMAT (' NPARM=',I2,3X,'IGT=',I2,3X,'IGE=',I2,3X,
     *   'REAL=',G15.6,3X,'XMIN=',G15.6,3X,'XMAX=',G15.6)
830   FORMAT ('0*** ERROR - RAINFALL-RUNOFF PARAMTER NUMBER ',I2,
     *   ' (',F7.2,') BE GREATER THAN ',F7.2,
     *   ' AND LESS THAN OR EQUAL TO ',F7.2,'.')
840   FORMAT ('0*** ERROR - RAINFALL-RUNOFF PARAMTER NUMBER ',I2,
     *   ' (',F7.2,') MUST BE GREATER THAN OR EQUAL TO ',F7.2,
     *   ' AND LESS THAN OR EQUAL TO ',F7.2,'.')
850   FORMAT (' RAINFALL-RUNOFF RELATIONSHIP NUMBER ',I2,' SET TO ',
     *   F7.2)
860   FORMAT ('0*** NOTE - RFRO PARAMETERS NOT WRITTEN ',
     *   'BECAUSE ',I2,' ERRORS ENCOUNTERED.')
870   FORMAT ('0*** ERROR - ONE OR MORE RAINFALL-RUNOFF PARAMETERS ',
     *   'NOT DEFINED. ',I2,' WERE FOUND. ',I2,' WERE EXPECTED.')
880   FORMAT ('0*** ERROR - MAXIMUM NUMBER OF FIELDS ALLOWED (',I2,
     *   ') EXCEEDED IN FIELD ',I2,'.')
890   FORMAT ('0*** NOTE - NO PARAMETER VALUES FOUND.')
900   FORMAT (' CHK(1)=',A4,3X,'XRFRO=',A4)
910   FORMAT (' *** EXIT SFRFRO')
C
      END
