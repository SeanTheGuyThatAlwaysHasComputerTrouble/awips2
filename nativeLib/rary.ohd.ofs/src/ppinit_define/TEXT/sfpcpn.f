C MODULE SFPCPN
C-----------------------------------------------------------------------
C
C  ROUTINE TO DEFINE STATION PRECIPITATION PARAMETERS.
C
      SUBROUTINE SFPCPN (LARRAY,ARRAY,DISP,TDISP,IRUNCK,
     *   PRPARM,PRNOTE,SUNITS,NFLD,
     *   STAID,DESCRP,STATE,STALOC,STACOR,NBRSTA,
     *   ICSTANO,ILTLNN,IFSTAN,
     *   ITPPVR,IPPROC,MDRBOX,PCPNCF,IPTWGT,IPTWGTO,
     *   NPPCPN,IPCHARO,IPCHARN,PXCHR,
     *   NSTERR,NSTWRN,IWRITE,LPPCPN,NSPACE,ISTAT)
C
C
      CHARACTER*4 DISP,TDISP,PRPARM,PRNOTE,SUNITS,STATE
      CHARACTER*4 RDISP,WDISP,TYPE,UNITS,TSTATE
      PARAMETER (MOPTN=4)
      CHARACTER*8 OPTN(MOPTN)
     *   /'CF      ','CHAR    ','NWGT    ','WGT     '/
      CHARACTER*20 CHAR/' '/,CHK/' '/,TCHAR
C
      DIMENSION ARRAY(LARRAY)
C
C  STAN PARAMETER ARRAYS
      DIMENSION TSTALOC(2),TSTACOR(2)
      INCLUDE 'scommon/dimstan'    
C
C  PCPN PARAMETER ARRAYS
      CHARACTER*8 CSTAID
      INCLUDE 'scommon/dimpcpn'
C
      INCLUDE 'uio'
      INCLUDE 'scommon/sudbgx'
      INCLUDE 'scommon/sugnlx'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppinit_define/RCS/sfpcpn.f,v $
     . $',                                                             '
     .$Id: sfpcpn.f,v 1.3 1998/04/07 15:08:10 page Exp $
     . $' /
C    ===================================================================
C
C
C
      IF (ISTRCE.GT.0) THEN
         WRITE (IOSDBG,1230)
         CALL SULINE (IOSDBG,1)
         ENDIF
C
C  SET DEBUG LEVEL
      LDEBUG=ISBUG('PCPN')
C
      ISTAT=0
C      
      LCHAR=LEN(CHAR)/4
      LCHK=LEN(CHK)/4
C
      TDISP=DISP
C
C  SET VALUE FOR MISSING PARAMETER
      UNSD=-999.
      UNDEF=-997.
      IUNDEF=-997
C
      NUMERR=0
      NUMWRN=0
      ILPFND=0
      IRPFND=0
      ISTRT=-1
      NUMFLD=0
      MINFLD=5
      MAXFLD=7
      INWIND=0
      NOPFLD=0
      NXTFLD=0
      NPIFLD=0
      NPCFLD=NFLD
      IENDIN=0
C
      IOFLD1=0
      IOFLD2=0
      IOFLD3=0
C
      IDTIME=24
      IPPROC=0
      MDRBOX=0
      IPSWGT=1
      INDSIG=0
      NUMSTA=0
      NUMSWT=0
      DO 10 I=1,MSTASID
         CALL UREPET (' ',STASID(I),LEN(STASID(I)))
         IPDSPT(I)=IUNDEF
         STASWT(I)=UNDEF
10       CONTINUE
      DO 25 I=1,5
         DO 20 J=1,4
            IPD5PT(I,J)=0
            STA5WT(I,J)=0.0
20          CONTINUE
25       CONTINUE
      DO 35 I=1,3
         DO 30 J=1,4
            IPD3PT(I,J)=0
            STA3WT(I,J)=0.0
30          CONTINUE
35       CONTINUE
      NUMCF=0
      PCPNCF(1)=1.0
      PCPNCF(2)=-999.
      NPXCHR=0
      IPCHAR=0
C
C  CHECK NUMBER OF LINES LEFT ON PAGE
      IF (ISLEFT(5).GT.0) CALL SUPAGE
C
C  PRINT CARD
      CALL SUPCRD
C
C  PRINT HEADER LINE
      WRITE (LP,1240)
      CALL SULINE (LP,2)
      WRITE (LP,1250)
      CALL SULINE (LP,2)
C
      IF (LDEBUG.GT.0) THEN
C     PRINT OPTIONS
         WRITE (LP,1360) TDISP,PRPARM,PRNOTE
         CALL SULINE (LP,2)
         ENDIF
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK IF PREPROCESSOR PARAMETRIC DATA BASE ALLOCATED
C
      IDPPP=1
      CALL SUDALC (0,0,0,0,IDPPP,0,0,0,0,0,NUMERR,IERR)
      IF (IERR.NE.0) THEN
         WRITE (LP,1370)
         CALL SUERRS (LP,2,NUMERR)
         ENDIF
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK STATION IDENTIFIER
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,*) 'STAID=',STAID
         CALL SULINE (IOSDBG,1)
         ENDIF
      IF (STAID.EQ.'????????') THEN
         WRITE (LP,1350)
         CALL SUERRS (LP,2,NUMERR)
         ENDIF
C
C  STORE CURRENT ATTRIBUTES
      TDESCRP=DESCRP
      TSTATE=STATE
      CALL SUBSTR (STALOC,1,8,TSTALOC,1)
      CALL SUBSTR (STACOR,1,8,TSTACOR,1)
      TNBRSTA=NBRSTA
C
C  READ STATION PCPN PARAMETERS
      IPRERR=0
      IPTR=0
      IREAD=1
      INCLUDE 'scommon/callsrpcpn'
      IF (IERR.EQ.0) THEN 
         IPTWGTO=IPTWGT     
C     RESET ATTRIBUTES
         DESCRP=TDESCRP
         STATE=TSTATE
         CALL SUBSTR (TSTALOC,1,8,STALOC,1)
         CALL SUBSTR (TSTACOR,1,8,STACOR,1)
         NBRSTA=TNBRSTA
         IF (LDEBUG.GT.0) THEN
            IPRNT=1
            LEVEL=1
            IPREST=0
            UNITS=SUNITS
            INCLUDE 'scommon/callsppcpn'
            ENDIF
         ENDIF 
C
C  STATION EXIST AND DISP IS NEW
      IF (IERR.EQ.0.AND.TDISP.EQ.'NEW') THEN
         WRITE (LP,1270) STAID
         CALL SULINE (LP,2)
         TDISP='OLD'
         GO TO 100
         ENDIF
C
C  STATION DOES NOT EXIST AND DISP IS OLD
      IF (IERR.GT.0.AND.TDISP.EQ.'OLD') THEN
         WRITE (LP,1280) STAID
         CALL SUWRNS (LP,2,NUMWRN)
         TDISP='NEW'
         ENDIF
C
100   NPPCPN=IPTR
C
C  CHECK IF STATION HAS CHARACTERISTICS
      IF (IPCHAR.GT.0) THEN        
C     READ STATION CHARACTERISTICS
         CALL RPP1CH (IPCHAR,PXCHR,IERR)
         IF (IERR.NE.0) THEN
            IF (IERR.EQ.1) THEN
               WRITE (LP,1310)
               CALL SUERRS (LP,2,NUMERR)
               ENDIF
            IF (IERR.EQ.2) THEN
               WRITE (LP,1320) IPCHAR
               CALL SUERRS (LP,2,NUMERR)
               ENDIF
            IF (IERR.EQ.3) THEN
               WRITE (LP,1995)
               CALL SUERRS (LP,2,NUMERR)
               ENDIF
            IF (IERR.LE.0.OR.IERR.GT.3) THEN
               WRITE (LP,1340)
               CALL SUERRS (LP,2,NUMERR)
               ENDIF
            GO TO 110
            ENDIF
         IPCHARO=IPCHAR
         IF (LDEBUG.GT.0) THEN
            WRITE (LP,1290)
            CALL SULINE (LP,2)
            WRITE (LP,1300) (PXCHR(I),I=1,12)
            CALL SULINE (LP,1)
            ENDIF
         ENDIF
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK FIELDS FOR DEFINE STATION PCPN OPTIONS
C
110   IF (INWIND.EQ.0) GO TO 120
         INULL=1
         GO TO 160
120   INULL=0
      NULREP=0
      CALL UFIELD (NFLD,ISTRT,LENGTH,ITYPE,NREP,INTEGR,REAL,LCHAR,CHAR,
     *   LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,IERR)
      IF (NFLD.EQ.-1) THEN
         IENDIN=1
         GO TO 220
         ENDIF
      IF (LDEBUG.GT.0) THEN
         CALL UPRFLD (NFLD,ISTRT,LENGTH,ITYPE,NREP,INTEGR,REAL,LCHAR,
     *      CHAR,LLPAR,LRPAR,LASK,LATSGN,LAMPS,LEQUAL,IERR)
         ENDIF
C
C  CHECK FOR NULL FIELD
      IF (IERR.NE.1) GO TO 150
         IF (LDEBUG.GT.0) THEN
            WRITE (IOSDBG,1460) NFLD
            CALL SULINE (IOSDBG,1)
            ENDIF
         IF (TDISP.EQ.'NEW') GO TO 140
            NUMFLD=NUMFLD+1
            IF (NFLD.EQ.1) CALL SUPCRD
            GO TO 110
140      INULL=1
         GO TO 240
C
150   IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,*) 'CHAR=',CHAR
         CALL SULINE (IOSDBG,1)
         ENDIF
C
      IF (NOPFLD.GT.0) GO TO 160
C
C  CHECK FOR PAIRED PARENTHESES
      CALL SUPFND (ILPFND,IRPFND,NPIFLD,NPCFLD)
C
C  CHECK IF DISP IS NEW AND ALL FIELDS FOUND
160   IF (INWIND.EQ.0) GO TO 170
         IF (NUMFLD.NE.MINFLD) GO TO 330
            INWIND=0
            IF (IENDIN.EQ.1) GO TO 1160
            CALL SUBSTR (TCHAR,1,LEN(CHAR),CHAR,1)
            INULL=0
            GO TO 290
C
C  IF (NOPFLD.GT.0) GO TO 30
C
C  CHECK FOR REPEATED NULL FIELD
170   IF (CHAR.EQ.',,'.OR.CHAR.EQ.'NULL') THEN
         IF (LDEBUG.GT.0) THEN
            WRITE (IOSDBG,*) 'NREP=',NREP
            CALL SULINE (IOSDBG,1)
            ENDIF
         IF (NREP.GT.0.AND.NREP+NUMFLD.LE.MINFLD) GO TO 180
            WRITE (LP,1430) NREP,NUMFLD,MINFLD
            CALL SUERRS (LP,2,NUMERR)
            NREP=MAXFLD-NUMFLD
180      NULREP=NREP
         INWIND=1
         INULL=1
         GO TO 230
         ENDIF
C
C  CHECK FOR COMMAND
      IF (LATSGN.EQ.1) THEN
         IENDIN=1
         GO TO 220
         ENDIF
C
C  CHECK FOR PARENTHESIS IN FIELD
      IF (LRPAR.EQ.LENGTH.AND.LENGTH.EQ.1) GO TO 230
      IF (LLPAR.GT.0) CALL UFPACK (LCHK,CHK,ISTRT,1,LLPAR-1,IERR)
      IF (LLPAR.EQ.0) CALL UFPACK (LCHK,CHK,ISTRT,1,LENGTH,IERR)
C
C  CHECK FOR END OF INPUT
      IF (CHK.EQ.'/') GO TO 220
C
C  IF FIRST FIELD, CONTENTS HAS BEEN CHECKED FOR KEYWORD
      IF (NUMFLD.EQ.0) GO TO 230
      IF (CHAR.EQ.'PCPN') GO TO 230
C
C  CHECK FOR KEYWORD
      CALL SUIDCK ('DEFN',CHK,NFLD,0,IKEYWD,IERR)
      IF (IERR.NE.2) GO TO 230
C      
220   IF (NUMFLD.LT.MINFLD) GO TO 310
      CALL SUPFND (ILPFND,IRPFND,NPIFLD,NPCFLD)
      GO TO 1160
C
C  PRINT CARD
230   IF (NFLD.EQ.1.AND.NUMFLD.GT.0.AND.INWIND.EQ.0) CALL SUPCRD
C
C  CHECK FOR OPTIONAL FIELDS
240   IF (NUMFLD.GT.MAXFLD) GO TO 300
         IF (NOPFLD.GT.0.AND.LRPAR.EQ.LENGTH.AND.LENGTH.EQ.1)
     *      GO TO 330
         IF (NOPFLD.GT.0.AND.LRPAR.EQ.LENGTH.AND.LENGTH.EQ.1)
     *      GO TO 330
            DO 250 IOPTN=1,MOPTN
               IF (CHK.EQ.OPTN(IOPTN)) GO TO 260
250            CONTINUE
         IF (NUMFLD+1.LE.MINFLD) GO TO 330
         IF (NOPFLD.GT.0) GO TO 330
            WRITE (LP,1380) NUMFLD,NFLD,CHAR
            CALL SUERRS (LP,2,NUMERR)
            GO TO 110
260      NXTFLD=0
         IF (ILPFND.GT.0.AND.IRPFND.EQ.0) GO TO 270
            GO TO 280
270      WRITE (LP,1470) NPIFLD,NPCFLD
         CALL SULINE (LP,2)
         ILPFND=0
         IRPFND=0
         NOPFLD=0
         NXTFLD=0
280      IF (TDISP.EQ.'NEW'.AND.NUMFLD.LT.MINFLD) GO TO 320
290      GO TO (790,940,1090,1090),IOPTN
            WRITE (LP,1550) IOPTN
            CALL SUERRS (LP,2,NUMERR)
            GO TO 110
C
C  MAXIMUM FIELDS EXCEEDED
300      WRITE (LP,1400) NUMFLD,MAXFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 110
C
C  FIELDS NOT SPECIFIED - USE DEFAULT VALUES
310   IENDIN=1
      INULL=1
320   INWIND=1
      CALL SUBSTR (CHAR,1,LEN(TCHAR),TCHAR,1)
      IF (PRNOTE.EQ.'YES') THEN
         WRITE (LP,1490)
         CALL SULINE (LP,2)
         ENDIF
      GO TO 110
C
330   IF (LLPAR.GT.0) ILPFND=1
      IF (LLPAR.GT.0) IRPFND=0
      IF (LRPAR.GT.0) IRPFND=1
C
C  CHECK IF FIELD WITH MORE THAN ONE POSSIBLE PARAMETER
      IF (NOPFLD.GT.0) GO TO (540,790,940),NOPFLD
C
340   NUMFLD=NUMFLD+1
C
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1260) NUMFLD,MINFLD,MAXFLD,NUMERR
         CALL SULINE (IOSDBG,1)
         ENDIF
C
      GO TO (350,380,430,470,540),NUMFLD
      WRITE (LP,1390) CHAR
      CALL SUERRS (LP,2,NUMERR)
      GO TO 1140
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK PARAMETER TYPE
C
350   IF (CHK.NE.'PCPN') THEN
         WRITE (LP,1410) 'PCPN',NFLD,CHK(1:LENSTR(CHK))
         CALL SUERRS (LP,2,NUMERR)
         GO TO 110
         ENDIF
      TYPE=CHK
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,*) 'TYPE=',TYPE
         CALL SULINE (IOSDBG,1)
         ENDIF
C
C  CHECK IF UNITS SPECIFIED
      IF (LLPAR.EQ.0) GO TO 110
      IEND=LRPAR-1
      IF (LRPAR.GT.0) GO TO 370
         CALL SUPFND (ILPFND,IRPFND,NPIFLD,NPCFLD)
         LRPAR=LENGTH-1
370   CALL UFPACK (LCHK,CHK,ISTRT,LLPAR+1,IEND,IERR)
      WRITE (LP,1450) CHK(1:LENSTR(CHK)),NFLD
      CALL SULINE (LP,2)
      GO TO 110
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  REPORTING TIME INTERVAL
C
C  USE DEFAULT IF NULL FIELD
380   IF (INULL.EQ.1) THEN
         INTEGR=IDTIME
         IF (PRNOTE.EQ.'YES') THEN
            WRITE (LP,1510) NUMFLD,NPCFLD,INTEGR
            CALL SULINE (LP,2)
            ENDIF
         GO TO 410
         ENDIF
C
C  CHECK IF INTEGER VALUE
      IF (ITYPE.NE.0) THEN
         WRITE (LP,1540) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
         ENDIF
C
C  CHECK FOR VALID VALUE
      IF (INTEGR.EQ.1.OR.
     *    INTEGR.EQ.3.OR.
     *    INTEGR.EQ.6.OR.
     *    INTEGR.EQ.24) GO TO 410
         WRITE (LP,1560) INTEGR
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  SET REPORTING TIME INTERVAL
410   IPTIME=INTEGR
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1870) IPTIME
         CALL SULINE (IOSDBG,1)
         ENDIF
      GO TO 1140
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  PROCESSING CODE
C
C  CHECK FOR NULL FIELD
430   IF (INULL.EQ.1) THEN
         CHAR='NORM'
         IF (PRNOTE.EQ.'YES') THEN
            WRITE (LP,1520) NUMFLD,NPCFLD,CHAR(1:LENSTR(CHAR))
            CALL SULINE (LP,2)
            ENDIF
         GO TO 450
         ENDIF
C
C  CHECK IF CHARACTER DATA
      IF (ITYPE.NE.2) THEN
         WRITE (LP,1530) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
         ENDIF
C
C  CHECK IF VALID VALUE
450   IF (CHAR.EQ.'NORM'.OR.
     *    CHAR.EQ.'ZERO'.OR.
     *    CHAR.EQ.'SYN') GO TO 460
         WRITE (LP,1570) CHAR(1:LENSTR(CHAR)),NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  SET PROCESSING CODE
460   IF (CHAR.EQ.'NORM') IPPROC=0
      IF (CHAR.EQ.'ZERO') IPPROC=1
      IF (CHAR.EQ.'SYN') IPPROC=2
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1880) IPPROC,CHAR(1:LENSTR(CHAR))
         CALL SULINE (IOSDBG,1)
         ENDIF
C
C  CHECK IF SYNTHETIC STATION
      IF (IPPROC.NE.2) GO TO 1140
C
C  CHECK TIME INTERVAL
      IF (IPTIME.EQ.24) GO TO 1140
         WRITE (LP,1580)
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  MDR USAGE OPTION
C
C  CHECK IF NULL FIELD
470   IF (INULL.EQ.1) THEN
         CHAR='MDR'
         IF (PRNOTE.EQ.'YES') THEN
            WRITE (LP,1520) NUMFLD,NPCFLD,CHAR(1:LENSTR(CHAR))
            CALL SULINE (LP,2)
            ENDIF
         GO TO 490
         ENDIF
C
C  CHECK IF CHARACTER DATA
      IF (ITYPE.NE.2) THEN
         WRITE (LP,1530) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
         ENDIF
C
C  CHECK FOR VALID VALUE
490   IF (CHAR.EQ.'MDR'.OR.
     *    CHAR.EQ.'NMDR') GO TO 500
         WRITE (LP,1590) CHAR(1:LENSTR(CHAR)),NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  CHECK IF MDR TO BE USED
500   IF (CHAR.EQ.'NMDR') THEN
         MDRBOX=0
         GO TO 530
         ENDIF
C
C  CHECK IF MDR SUBSET DEFINED
      IF (MDRSUB(1).NE.-997) GO TO 520
         CHAR='NMDR'
         WRITE (LP,1600) 'NMDR'
         CALL SUWRNS (LP,2,NUMWRN)
         MDRBOX=0
         GO TO 530
C
520   IF (STACOR(1).EQ.UNDEF.OR.STACOR(2).EQ.UNDEF) GO TO 1140
C
C  COMPUTE MDR BOX NUMBER FROM NWSRFS/HRAP COORDINATES
      CALL SBMDRS (STACOR(1),STACOR(2),MDRX,MDRY,IERR)
      CALL SBMNUM (MDRX,MDRY,MDRBOX,IERR)
C
C  CHECK IF MDR BOX FALLS IN USER DEFINED SUBSET
      CALL SBNUMM (MDRBOX,NCOL,NROW,IERR)
      IF (NCOL.GE.MDRSUB(1).AND.NCOL.LE.MDRSUB(1)+MDRSUB(2)-1.AND.
     *    NROW.GE.MDRSUB(3).AND.NROW.LE.MDRSUB(3)+MDRSUB(4)-1) GO TO 530
         WRITE (LP,1610) NROW,NCOL,MDRBOX
         CALL SUWRNS (LP,2,NUMWRN)
         WRITE (LP,1620) MDRSUB
         CALL SULINE (LP,1)
C
530   IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1890) MDRBOX,CHAR(1:LENSTR(CHAR))
         CALL SULINE (IOSDBG,1)
         ENDIF
      GO TO 1140
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  TYPE OF STATION WEIGHTS
C
C  CHECK WHICH FIELD IS TO BE PROCESSED
540   NGOTO=NXTFLD+1
      GO TO (550,620,740),NGOTO
C
C  CHECK FOR NULL FIELD
550   IF (INULL.EQ.1) THEN
         CHAR='D2'
         IF (PRNOTE.EQ.'YES') THEN
            WRITE (LP,1520) NUMFLD,NPCFLD,CHAR(1:LENSTR(CHAR))
            CALL SULINE (LP,2)
            ENDIF
         GO TO 570
         ENDIF
C
C  CHECK FOR CHARACTER DATA
      IF (ITYPE.NE.2) THEN
         WRITE (LP,1530) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
         ENDIF
C
C  CHECK FOR PARENTHESES IN FIELD
570   IF (CHAR.EQ.')') THEN
         NOPFLD=0
         NXTFLD=0
         IF (NUMSTA.GT.0) GO TO 1140
            WRITE (LP,1650) NUMFLD,NFLD
            CALL SUERRS (LP,2,NUMERR)
            GO TO 1140
         ENDIF
      IF (LLPAR.EQ.0) CHK=CHAR
      IF (LLPAR.GT.0) CALL SUBSTR (CHAR,1,LLPAR-1,CHK,1)
      IF (CHK.EQ.'D2'.OR.
     *    CHK.EQ.'SIG') GO TO 590
         WRITE (LP,1630) CHK(1:LENSTR(CHK)),NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
590   IF (CHK.EQ.'SIG') GO TO 610
C
C  1/D**2 WEIGHTS
      IPTWGT=0
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1900) CHAR(1:LENSTR(CHAR))
         CALL SULINE (IOSDBG,1)
         ENDIF
      GO TO 1140
C
C  SIGNIFICANCE WEIGHTS
610   INDSIG=1
      NOPFLD=1
      NXTFLD=1
      IF (LLPAR.EQ.0.OR.LLPAR.EQ.LENGTH.AND.LENGTH.EQ.1) GO TO 1140
C
C  CHECK FOR SPECIAL CASES
620   IF (LRPAR.EQ.LENGTH.AND.LENGTH.EQ.1) GO TO 780
      IF (LRPAR.NE.LENGTH) GO TO 630
         NOPFLD=0
         NXTFLD=0
         GO TO 1140
630   IF (NUMSTA.GT.0.OR.IRPFND.EQ.0) GO TO 640
         WRITE (LP,1650) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         NOPFLD=0
         NXTFLD=0
         GO TO 1140
640   IBEG=LLPAR+1
      IEND=LENGTH
C
C  CHECK IF MAXIMUM SIGNIFICANCE STATIONS EXCEEDED
      IF (NUMSTA.GT.MSTASID) THEN
         WRITE (LP,1660) NUMSTA,MSTASID
         CALL SUERRS (LP,2,NUMERR)
         NXTFLD=2
         GO TO 1140
         ENDIF
C
C  SET STATION IDENTIFIER
      CSTAID=' '
      CALL SUBSTR (CHAR,LLPAR+1,LENGTH-LLPAR,CSTAID,1)
C
C  SAVE ATTRIBUTES OF CURRENT STATION
      CALL SUBSTR (STAID,1,8,TSTAID,1)
      TDESCRP=DESCRP
      TSTATE=STATE
      CALL SUBSTR (STALOC,1,8,TSTALOC,1)
      CALL SUBSTR (STACOR,1,8,TSTACOR,1)
      TNBRSTA=NBRSTA
C
C  READ STATION GENL PARAMETERS
      CALL SUBSTR (CSTAID,1,8,STAID,1)
      IPRERR=0
      IPTR=0
      RDISP='OLD'
      INCLUDE 'scommon/callsrstan'
      IRSTAN=IERR
      IF (LDEBUG.EQ.0.OR.IERR.GT.0) GO TO 660
         IPRNT=1
         UNITS=SUNITS
         INCLUDE 'scommon/callspstan'
C
C  RESET STATION ATTRIBUTES
660   CALL SUBSTR (TSTAID,1,8,STAID,1)
      DESCRP=TDESCRP
      STATE=TSTATE
      CALL SUBSTR (TSTALOC,1,8,STALOC,1)
      CALL SUBSTR (TSTACOR,1,8,STACOR,1)
      NBRSTA=TNBRSTA
      CALL SUBSTR (TSTALOC,1,8,STALOC,1)
      CALL SUBSTR (TSTACOR,1,8,STACOR,1)
C
C  CHECK IF STATION EXISTS
      IF (IRSTAN.EQ.0) GO TO 670
         WRITE (LP,1670) CSTAID
         CALL SUWRNS (LP,2,NUMWRN)
         GO TO 700
C
C  CHECK STATION STATUS
670   IF (ICSTAN.EQ.1) THEN
         WRITE (LP,1680) CSTAID
         CALL SUWRNS (LP,2,NUMWRN)
         GO TO 700
         ENDIF
C
C  CHECK IF STATION HAS PCPN DATA GROUP
      DO 690 I=1,NGPS
         IF (GPS(I).EQ.'PCPN') GO TO 700
690      CONTINUE
      WRITE (LP,1690) CSTAID
      CALL SUWRNS (LP,2,NUMWRN)
C
C  CHECK FOR DULICATE NAMES
700   IF (NUMSTA.GT.1) THEN
         DO 710 I=1,NUMSTA
            IF (CSTAID.EQ.STASID(I)) GO TO 720
710         CONTINUE
            GO TO 730
720      WRITE (LP,1700) CSTAID
         CALL SUWRNS (LP,2,NUMWRN)
         ENDIF
C          
730   NUMSTA=NUMSTA+1
      STASID(NUMSTA)=CSTAID
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1910) STASID(NUMSTA)
         CALL SULINE (IOSDBG,1)
         ENDIF
      IPDSPT(NUMSTA)=IPPP24
      NXTFLD=2
      GO TO 1140
C
C  SET STATION WEIGHTS
740   IF (ITYPE.NE.2) GO TO 760
         IF (LRPAR.NE.1) GO TO 750
         IF (NUMSTA.EQ.NUMSWT) GO TO 1140
            WRITE (LP,1730) NUMSTA,NUMSWT
            CALL SUERRS (LP,2,NUMERR)
            NOPFLD=0
            NXTFLD=0
            GO TO 1140
750      IF (LRPAR.GT.1) GO TO 760
         WRITE (LP,1530) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         NXTFLD=1
         GO TO 1140
760   IF (LRPAR.EQ.0) IEND=LENGTH
      IF (LRPAR.GT.0) IEND=LRPAR-1
      CALL UFRLFX (TSTAWT,ISTRT,1,IEND,0,IERR)
      IF (IERR.NE.0) THEN
         WRITE (LP,1710) NFLD
         CALL SUERRS (LP,2,NUMERR)
         ENDIF
      IF (TSTAWT.GT.0) GO TO 770
         WRITE (LP,1720) CSTAID,TSTAWT
         CALL SUERRS (LP,2,NUMERR)
         NXTFLD=1
         GO TO 1140
770   NUMSWT=NUMSWT+1
      STASWT(NUMSWT)=TSTAWT
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1920) STASWT(NUMSTA)
         CALL SULINE (IOSDBG,1)
         ENDIF
      NXTFLD=1
C
780   IF (LRPAR.EQ.0) GO TO 1140
         NOPFLD=0
         NXTFLD=0
         GO TO 1140
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CORRECTION FACTORS
C
C  CHECK WHICH FIELD TO BE PROCESSED
790   NGOTO=NXTFLD+1
      GO TO (800,860),NGOTO
C
C  CHECK IF OPTIONAL FIELD PRECIOUSLY PROCESSED
800   IF (IOFLD1.EQ.0) GO TO 810
         WRITE (LP,1740)
         CALL SUWRNS (LP,2,NUMWRN)
         NUMCF=0
C
810   IOFLD1=1
C
C  CHECK FOR NULL FIELD
      IF (INULL.EQ.0) GO TO 820
         WRITE (LP,1500) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  CHECK FOR CHARACTER DATA
820   IF (ITYPE.EQ.2) GO TO 830
         WRITE (LP,1530) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  CHECK FOR PARENTHESES IN FIELD
830   IF (CHAR.NE.')') GO TO 840
         NOPFLD=0
         NXTFLD=0
         WRITE (LP,1750) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
840   IF (LLPAR.EQ.0) CHK=CHAR
      IF (LLPAR.GT.0) CALL SUBSTR (CHAR,1,LLPAR-1,CHK,1)
      IF (CHK.NE.'CF') THEN
         WRITE (LP,1760) CHK(1:LENSTR(CHK)),NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
         ENDIF
      NOPFLD=2
      NXTFLD=1
      IF (LLPAR.EQ.0.OR.LLPAR.EQ.LENGTH.AND.LENGTH.EQ.1) GO TO 1140
C
C  CHECK FOR SPECIAL CHARACTERS
860   IF (LRPAR.EQ.LENGTH.AND.LENGTH.EQ.1) GO TO 930
      IF (ITYPE.NE.2) GO TO 880
         IF (LRPAR.NE.1) GO TO 870
         IF (NUMCF.GT.0) GO TO 1140
            WRITE (LP,1770)
            CALL SUERRS (LP,2,NUMERR)
            NOPFLD=0
            NXTFLD=0
            GO TO 1140
870      IF (LRPAR.EQ.0.OR.LRPAR.EQ.LENGTH) GO TO 880
            WRITE (LP,1530) NUMFLD,NFLD
            CALL SUERRS (LP,2,NUMERR)
            NXTFLD=1
            GO TO 1140
880   IF (LLPAR.EQ.0) IBEG=1
      IF (LLPAR.GT.0) IBEG=LLPAR+1
      IF (LASK.GT.0) IBEG=LASK+1
      IEND=LENGTH
      IF (LRPAR.EQ.0) GO TO 890
         IEND=LRPAR-1
         NOPFLD=0
         NXTFLD=0
890   CALL UFRLFX (TPXCOR,ISTRT,IBEG,IEND,0,IERR)
      IF (IERR.NE.0) THEN
         WRITE (LP,1710) NFLD
         CALL SUERRS (LP,2,NUMERR)
         ENDIF
C
C  CHECK FOR MAXIMUM CORRECTION FACTORS
900   NUMCF=NUMCF+1
      IF (NUMCF.LE.2) GO TO 910
         WRITE (LP,1640) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  CHECK IF GREATER THAN ZERO
910   IF (TPXCOR.GT..05.AND.TPXCOR.LE.4.95) GO TO 920
         WRITE (LP,1780) NUMCF,TPXCOR
         CALL SUERRS (LP,2,NUMERR)
         NXTFLD=1
         GO TO 1140
C
C  SET CORRECTION FACTOR
920   PCPNCF(NUMCF)=TPXCOR
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1930) NUMCF,PCPNCF(NUMCF)
         CALL SULINE (IOSDBG,1)
         ENDIF
C
C  CHECK FOR REPEATED VALUES
      IF (NREP.LE.1) GO TO 930
         NREP=NREP-1
         GO TO 900
C
930   NXTFLD=1
      IF (LRPAR.EQ.0) GO TO 1140
         NOPFLD=0
         NXTFLD=0
         GO TO 1140
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  STATION CHARACTERISTICS
C
C  CHECK WHICH FIELD TO BE PROCESSED
940   NGOTO=NXTFLD+1
      GO TO (950,1010),NGOTO
C
C  CHECK IF OPTIONAL FIELD ALREADY SPECIFIED
950   IF (IOFLD2.EQ.0) GO TO 960
         WRITE (LP,1790)
         CALL SUWRNS (LP,2,NUMWRN)
         NPXCHR=0
C
960   IOFLD2=1
C
C  CHECK FOR NULL FIELD
      IF (INULL.EQ.0) GO TO 970
         WRITE (LP,1500) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  CHECK IF CHARACTER DATA
970   IF (ITYPE.EQ.2) GO TO 980
         WRITE (LP,1530) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  CHECK FOR PARENTHESES IN FIELD
980   IF (LLPAR.EQ.LENGTH.AND.LENGTH.EQ.1) GO TO 1140
      IF (CHAR.NE.')') GO TO 990
         NOPFLD=0
         NXTFLD=0
         WRITE (LP,1800) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
990   IF (LLPAR.EQ.0) CHK=CHAR
      IF (LLPAR.GT.0) CALL SUBSTR (CHAR,1,LLPAR-1,CHK,1)
      IF (CHK.NE.'CHAR') THEN
         WRITE (LP,1810) CHK(1:LENSTR(CHK)),NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
         ENDIF
      NOPFLD=3
      NXTFLD=1
      IF (LLPAR.EQ.0.OR.LLPAR.EQ.LENGTH) GO TO 1140
C
C  CHECK FOR SPECIAL CHARACTERS
1010  IF (LRPAR.EQ.LENGTH.AND.LENGTH.EQ.1) GO TO 1080
      IF (ITYPE.NE.2) GO TO 1030
         IF (LRPAR.NE.1) GO TO 1020
         IF (NUMCF.GT.0) GO TO 1140
            WRITE (LP,1830)
            CALL SUERRS (LP,2,NUMERR)
            NOPFLD=0
            NXTFLD=0
            GO TO 1140
1020     IF (LRPAR.EQ.0.OR.LRPAR.EQ.LENGTH) GO TO 1030
            WRITE (LP,1530) NUMFLD,NFLD
            CALL SUERRS (LP,2,NUMERR)
            NXTFLD=1
            GO TO 1140
1030  IF (LLPAR.EQ.0) IBEG=1
      IF (LLPAR.GT.0) IBEG=LLPAR+1
      IF (LASK.GT.0) IBEG=LASK+1
      IEND=LENGTH
      IF (LRPAR.EQ.0) GO TO 1040
         IEND=LRPAR-1
         NOPFLD=0
         NXTFLD=0
1040  CALL UFRLFX (TPXCHR,ISTRT,IBEG,IEND,0,IERR)
      IF (IERR.NE.0) THEN
         WRITE (LP,1710) NFLD
         CALL SUERRS (LP,2,NUMERR)
         ENDIF
C
C  CHECK FOR MAXIMUM NUMBER VALUES
1050  NPXCHR=NPXCHR+1
      IF (NPXCHR.GT.MPXCHR) THEN
         WRITE (LP,1820) MPXCHR,NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
         ENDIF
      IF (TPXCHR.LE.0) THEN
         WRITE (LP,1840) NPXCHR,TPXCHR
         CALL SUERRS (LP,2,NUMERR)
         NXTFLD=1
         GO TO 1140
         ENDIF
C
C  SET CHARACTERISTIC
      PXCHR(NPXCHR)=TPXCHR
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1940) NPXCHR,PXCHR(NPXCHR)
         CALL SULINE (IOSDBG,1)
         ENDIF
C
C  CHECK FOR REPEATED VALUES
      IF (NREP.LE.1) GO TO 1080
         NREP=NREP-1
         GO TO 1050
C
1080  NXTFLD=1
      IF (LRPAR.EQ.0) GO TO 1140
         NOPFLD=0
         NXTFLD=0
         GO TO 1140
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  INDICATOR WHETHER STATION TO BE USED ONLY FOR ESTIMATION
C
C  CHECK IF OPTIONAL FIELD PRECIOUSLY PROCESSED
1090  IF (IOFLD3.EQ.0) GO TO 1100
         WRITE (LP,1950)
         CALL SUWRNS (LP,2,NUMWRN)
C
1100  IOFLD3=1
C
C  CHECK FOR NULL FIELD
      IF (INULL.EQ.1) THEN
         CHAR='WGT'
         IF (PRNOTE.EQ.'YES') THEN
            WRITE (LP,1520) NUMFLD,NPCFLD,CHAR(1:LENSTR(CHAR))
            CALL SULINE (LP,2)
            ENDIF
         GO TO 1120
         ENDIF
C
C  CHECK IF CHARACTER DATA
      IF (ITYPE.NE.2) THEN
         WRITE (LP,1530) NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
         ENDIF
C
C  CHECK FOR VALID VALUE
1120  IF (CHAR.EQ.'WGT'.OR.
     *    CHAR.EQ.'NWGT') GO TO 1130
         WRITE (LP,1960) CHAR(1:LENSTR(CHAR)),NUMFLD,NFLD
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1140
C
C  CHECK IF NWGT TO BE USED
1130  IPSWGT=1
      IF (CHAR.EQ.'NWGT') IPSWGT=0
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,1970) IPSWGT,CHAR(1:LENSTR(CHAR))
         CALL SULINE (IOSDBG,1)
         ENDIF
      GO TO 1140
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK FOR REPEATED NULL FIELDS
1140  IF (NULREP.GT.0) THEN
         NULREP=NULREP-1
         IF (NULREP.GT.0) GO TO 340
         INWIND=0
         ENDIF
C
      IF (INWIND.EQ.1) GO TO 110
C      
      NPIFLD=NUMFLD
      NPCFLD=NFLD
C
C  CHECK FOR END OF INPUT
      IF (IENDIN.EQ.1) GO TO 1160
C      
      GO TO 110
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C  CHECK NUMBER OF FIELDS PROCESSED
1160  IF (NUMFLD.GE.2.OR.
     *    IOFLD1.GT.0.OR.
     *    IOFLD2.GT.0.OR.
     *    IOFLD3.GT.0) THEN
         ELSE
            WRITE (LP,2060)
            CALL SUWRNS (LP,2,NUMWRN)
            GO TO 1220
         ENDIF
C
C  CHECK NUMBER OF SIGNIFICANCE STATION IDS AND WEIGHT PROCESSED
      IF (INDSIG.EQ.1.AND.NUMSTA.EQ.0) THEN
         WRITE (LP,1725)
         CALL SUERRS (LP,2,NUMERR)
         ENDIF
      IF (NUMSTA.NE.NUMSWT) THEN
         WRITE (LP,1730) NUMSTA,NUMSWT
         CALL SUERRS (LP,2,NUMERR)
         ENDIF
C
C  CHECK NUMBER OF STATION CHARACTERISTICS
      IF (NPXCHR.EQ.0.OR.NPXCHR.EQ.12) GO TO 1190
         WRITE (LP,1850) NPXCHR
         CALL SUERRS (LP,2,NUMERR)
         GO TO 1200
C
C  CHECK IF RUNCHECK OPTION SET
1190  IF (IRUNCK.EQ.1) THEN
         WRITE (LP,2050) STAID
         CALL SULINE (LP,2)
         GO TO 1220
         ENDIF
C
      IF (IFSTAN.GT.1) GO TO 1200
      IF (NUMERR.GT.0) GO TO 1200
      IF (NPXCHR.EQ.0) GO TO 1200
C      
      IPCHARN=-1
      IF (IWRITE.EQ.0) GO TO 1200
C
C  WRITE PRECIPITATION CHARACTERISTICS
      CALL WPP1CH (PXCHR,IPCHAR,IERR)
      IF (IERR.EQ.0) THEN
         WRITE (LP,1975) STAID
         CALL SULINE (LP,2)
         IPCHARN=IPCHAR
         ELSE
            IF (IERR.EQ.1) THEN
               WRITE (LP,1980)
               CALL SUERRS (LP,2,NUMERR)
               ENDIF
            IF (IERR.EQ.2) THEN
               WRITE (LP,1990)
               CALL SUERRS (LP,2,NUMERR)
               ENDIF
            IF (IERR.EQ.3) THEN
               WRITE (LP,1995)
               CALL SUERRS (LP,2,NUMERR)
               ENDIF
            IF (IERR.GT.3) THEN
               WRITE (LP,2000) IERR
               CALL SUERRS (LP,2,NUMERR)
               ENDIF
         ENDIF
C
C  WRITE PCPN PARAMETERS IF NO ERRORS ENCOUNTERED
1200  IF (NUMERR.GT.0) THEN
         IF (IRUNCK.EQ.0) THEN
            WRITE (LP,2010) STAID,NUMERR
            CALL SULINE (LP,2)
            IRUNCK=1
            ELSE
               WRITE (LP,2020) STAID,NUMERR
               CALL SULINE (LP,2)
            ENDIF
         ISTAT=1
         GO TO 1220
         ENDIF
C
C  CHECK IF STATION GENERAL PARAMETERS DEFINED
      IF (IFSTAN.EQ.0.OR.IFSTAN.EQ.1) GO TO 1210
         IF (DISP.EQ.'NEW') THEN
            WRITE (LP,2030) STAID
            CALL SUWRNS (LP,2,NUMWRN)
            ENDIF
         IF (DISP.EQ.'OLD') THEN
            WRITE (LP,2040) STAID
            CALL SUWRNS (LP,2,NUMWRN)
            ENDIF
         ISTAT=1
         GO TO 1220
C
C  WRITE PCPN PARAMETERS TO FILE
1210  IVPCPN=1
      IF (TDISP.EQ.'NEW') IPNTWK=0
      IF ((TDISP.EQ.'OLD'.AND.ILTLNN.EQ.1).AND.
     *    (IPNTWK.EQ.2.OR.IPNTWK.EQ.3)) IPNTWK=1
      IPTWGT=NUMSTA
      WDISP=TDISP
      INCLUDE 'scommon/callswpcpn'
      IF (IERR.NE.0) THEN
         ISTAT=1
         GO TO 1220
         ENDIF
C
      IF (IWRITE.EQ.0.OR.PRPARM.EQ.'NO') GO TO 1220
C
C  READ PCPN PARAMETERS
      IPTR=0
      IPRERR=1
      IREAD=1
      INCLUDE 'scommon/callsrpcpn'
      IF (IERR.GT.0) GO TO 1220
C
C  PRINT PCPN PARAMETERS
      IPRNT=1
      IPREST=0
      UNITS=SUNITS
      INCLUDE 'scommon/callsppcpn'
C
C  CHECK DATA TIME INTERVAL
1220  ITPPVR=0
      IF (IPTIME.LT.24) ITPPVR=IPTIME
      IF (LDEBUG.GT.0) THEN
         WRITE (IOSDBG,2070) IPTIME,ITPPVR
         CALL SULINE (IOSDBG,1)
         ENDIF
      NSTERR=NSTERR+NUMERR
      NSTWRN=NSTWRN+NUMWRN
C
      IF (ISTRCE.GT.0) THEN
         WRITE (IOSDBG,2080) ISTAT
         CALL SULINE (IOSDBG,1)
         ENDIF
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
1230  FORMAT (' *** ENTER SFPCPN')
1240  FORMAT (' ')
1250  FORMAT ('0*--> DEFINE STATION PCPN PARAMETERS')
1360  FORMAT ('0DEFINE OPTIONS IN EFFECT :  '
     *   'DISP=',A,3X,
     *   'PRPARM=',A,
     *   'PRNOTE=',A)
1260  FORMAT (' NUMFLD=',I2,3X,'MINFLD=',I2,3X,'MAXFLD=',I2,3X,
     *   'NUMERR=',I2)
1270  FORMAT ('0*** NOTE - DISPOSITION OF NEW WAS SPECIFIED ',
     *   'FOR STATION ',A,' BUT PCPN PARAMETERS ALREADY EXIST.')
1280  FORMAT ('0*** WARNING - PCPN PARAMETERS FOR STATION ',A,
     *   ' DO NOT EXIST. DISP ASSUMED TO BE NEW FOR THIS DATA GROUP.')
1290  FORMAT ('0CHARACTERISTICS:  JAN   FEB   MAR   APR   MAY   JUN   ',
     *   'JUL   AUG   SEP   OCT   NOV   DEC')
1300  FORMAT (18X,12(F4.1,2X))
1310  FORMAT ('0*** ERROR - IN SFPCPN - SYSTEM ERROR ACCESSING ',
     *   'PRECIPITATION CHARACTERISTICS FILE.')
1320  FORMAT ('0*** ERROR - IN SFPCPN - INVALID VALUE OF POINTER TO ',
     *   'LOCATION OF CHARACTERISTICS. IPCHAR=',I5)
1340  FORMAT ('0*** ERROR - IN SFPCPN - STATUS CODE FROM RPP1CH ',
     *   'NOT RECOGNIZED: ',I4)
1350  FORMAT ('0*** ERROR - STATION IDENTIFIER NOT SPECIFIED. ',
     *   'CHECK FOR MISSING OR INCOMPLETE ''STAN'' INPUT.')
1370  FORMAT ('0*** ERROR - PREPROCESSOR PARAMETRIC DATA BASE FILES ',
     *   'ARE NOT ALLOCATED. INPUT DATA WILL BE CHECKED FOR ERRORS.')
1380  FORMAT ('0*** ERROR - INVALID DEFINE PCPN STATION OPTION ',
     *   'IN INPUT FIELD ',I2,' (CARD FIELD ',I2,') : ',A)
1390  FORMAT ('0*** ERROR - PROCESSING OPTION : ',A)
1400  FORMAT ('0*** ERROR - FIELD ',I2,' EXCEEDS MAXIMUM ALLOWABLE ',
     *   'FIELDS (',I2,').')
1410  FORMAT ('0*** ERROR - IN SFPCPN - THE CHARACTERS ''',A,
     *   ' ''WERE EXPECTED IN FIELD ',I2,' BUT ''',A,''' WERE FOUND.')
1430  FORMAT ('0*** ERROR - ',I3,' IS AN INVALID NUMBER OF NULL ',
     *   'TO BE REPEATED. CURRENT FIELD IS ',I2,'. MAXIMUM REQUIRED ',
     *   'FIELDS ALLOWED IS ',I2,'.')
1450  FORMAT ('0*** NOTE - UNITS CODE (',A,') FOUND IN CARD FIELD ',
     *   I2,' NOT USED FOR DEFINING PCPN PARAMETERS.')
1460  FORMAT (' BLANK STRING FOUND IN FIELD ',I2)
1470  FORMAT ('0*** NOTE - RIGHT PARENTHESES ASSUMED IN INPUT FIELD ',
     *   I2,'+ (CARD FIELD ' ,I2,').')
1490  FORMAT ('0*** NOTE - REMAINING PCPN PARAMETERS WILL ',
     *   'BE SET TO DEFAULT VALUES.')
1500  FORMAT ('0*** ERROR - NO VALUE FOUND FOR OPTIONAL INPUT FIELD ',
     *   I2,' (CARD FIELD ',I2,').')
1510  FORMAT ('0*** NOTE - NO VALUE FOUND FOR REQUIRED INPUT FIELD ',
     *   I2,' (CARD FIELD ',I2,'+). DEFAULT VALUE (',I4,
     *   ') WILL BE USED.')
1520  FORMAT ('0*** NOTE - NO VALUE FOUND FOR REQUIRED INPUT FIELD ',
     *   I2,' (CARD FIELD ',I2,'+). DEFAULT VALUE (',A,
     *   ') WILL BE USED.')
1530  FORMAT ('0*** ERROR - NON-CHARACTER DATA EXPECTED IN INPUT ',
     *   'FIELD ',I2,' (CARD FIELD ',I2,').')
1540  FORMAT ('0*** ERROR - INTEGER DATA EXPECTED IN INPUT ',
     *   'FIELD ',I2,' (CARD FIELD ',I2,').')
1550  FORMAT ('0*** ERROR - PROCESSING DEFINE PCPN OPTION ',
     *   'NUMBER ',I2,'.')
1560  FORMAT ('0*** ERROR - REPORTING TIME INTERVAL (',I2,
     *   ') IS INVALID. VALID VALUES ARE 1, 3, 6 OR 24.')
1570  FORMAT ('0*** ERROR - INVALID PROCESSING CODE (',A,') IN ',
     *   'INPUT FIELD ',I2,' (CARD FIELD ',I2,').')
1580  FORMAT ('0*** ERROR - SYNTHETIC PCPN STATION MUST HAVE A ',
     *   'REPORTING TIME INTERVAL OF 24 HOURS.')
1590  FORMAT ('0*** ERROR - INVALID MDR USAGE OPTION (',
     *   A,') IN INPUT FIELD ',I2,' (CARD FIELD ',I2,').')
1600  FORMAT ('0*** WARNING - MDR SUBSET NOT DEFINED IN UGNL ',
     *   'PARAMETERS. MDR USAGE OPTION ASSUMED TO BE ',A,'.')
1610  FORMAT ('0*** WARNING - MDR BOX FALLS OUTSIDE OF USER DEFINED ',
     *   'SUBSET. ROW NUMBER IS ',I3,' AND COLUMN NUMBER IS ',I3,
     *   ' FOR MDR BOX NUMBER ',I5,'.')
1620  FORMAT (T16,'USER DEFINED MDR SUBSET IS :   WESTERN COL=',I3,3X,
     *   'NUMBER COLS=',I2,3X,'SOUTHERN ROW=',I2,3X,'NUMBER ROWS=',
     *   I2)
1630  FORMAT ('0*** ERROR - INVALID TYPE OF STATION WEIGHTS OPTION (',
     *   A,') IN INPUT FIELD ',I2,' (CARD FIELD ',I2,').')
1640  FORMAT ('0*** ERROR - MAXIMUM NUMBER OF CORRECTION FACTORS ',
     *   '(2) EXCEEDED IN INPUT FIELD ',I2,'+ (CARD FIELD ',I2,').')
1650  FORMAT ('0*** ERROR - NO STATION IDENTIFIERS OR STATION ',
     *   'WEIGHTS FOUND IN INPUT FIELD ',I2,' (CARD FIELD ',I2,').')
1660  FORMAT ('0*** ERROR - MAXIMUM NUMBER OF STATIONS WITH ',
     *   'SIGNIFICANCE WEIGHTS ALLOWED (',I2,') EXCEEDED.')
1670  FORMAT ('0*** WARNING - STATION ',A,' IS NOT DEFINED.')
1680  FORMAT ('0*** WARNING - STATION ',A,' HAS A STATUS OF ',
     *   'INCOMPLETE.')
1690  FORMAT ('0*** WARNING - STATION ',A,' DOES NOT HAVE PCPN DATA ',
     *   'GROUP SPECIFIED.')
1700  FORMAT ('0*** WARNING - STATION ',A,' HAS ALREADY BEEN ',
     *  'SPECIFIED AS A SIGNIFICANCE STATION.')
1710  FORMAT ('0*** ERROR - INVALID CHARACTER FOUND IN ',
     *   'CARD FIELD ',I2,'.')
1720  FORMAT ('0*** ERROR - WEIGHT FOR STATION ',A,' MUST BE ',
     *   'GREATER THAN ZERO. VALUE INPUT IS ',F9.2,'.')
1725  FORMAT ('0*** ERROR - ''SIG'' WAS SPECIFIED FOR TYPE OF ',
     *   'STATION WEIGHTS BUT NO STATION IDENTIFIERS AND WEIGHTS ',
     *   'WERE SPECIFIED.')
1730  FORMAT ('0*** ERROR - NUMBER OF SIGNIFICANCE STATION ',
     *   'IDENTIFIERS (',I2,') DOES NOT EQUAL THE NUMBER OF ',
     *   'SIGNIFICANCE STATION WEIGHTS (',I2,').')
1740  FORMAT ('0*** WARNING - PRECIPITATION CORRECTION FACTORS HAVE ',
     *   'ALREADY BEEN DEFINED.')
1750  FORMAT ('0*** ERROR - NO PRECIPITATION CORRECTION FACTORS ',
     *   'FOUND IN INPUT FIELD ',I2,' (CARD FIELD ',I2,').')
1760  FORMAT ('0*** ERROR - PROCESSING PRECIPITATION CORRECTION ',
     *   'FACTOR INDICATOR (',A,') IN INPUT FIELD ',I2,' (CARD FIELD ',
     *   I2,').')
1770  FORMAT ('0*** ERROR - NO PRECIPITATION CORRECTION FACTORS FOUND.')
1780  FORMAT ('0*** ERROR - CORRECTION FACTOR ',I2,' MUST BE GREATER ',
     *   'THAN OR EQUAL TO .05 AND LESS THAN OR EQUAL TO 4.95. ',
     *   'VALUE INPUT IS ',F9.2,'.')
1790  FORMAT ('0*** WARNING - STATION CHARACTERISTICS HAVE ALREADY ',
     *   'BEEN DEFINED.')
1800  FORMAT ('0*** ERROR - NO PRECIPITATION STATION CHARACTERISTICS ',
     *   'FOUND IN INPUT FIELD ',I2,' (CARD FIELD ',I2,').')
1810  FORMAT ('0*** ERROR - PROCESSING STATION CHARACTERISTICS ',
     *   'INDICATOR (',A,') IN INPUT FIELD ',I2,' (CARD FIELD ',
     *   I2,').')
1820  FORMAT ('0*** ERROR - MAXIMUM NUMBER OF STATION CHARACTERISTICS ',
     *   '(',I2,') EXCEEDED IN INPUT FIELD ',I2,'+ (CARD FIELD ',I2,
     *   ').')
1830  FORMAT ('0*** ERROR - NO STATION CHARACTERISTICS FOUND.')
1840  FORMAT ('0*** ERROR - STATION CHARACTERISTIC ',I2,' MUST BE ',
     *   'GREATER THAN ZERO. VALUE INPUT IS ',F9.2,'.')
1850  FORMAT ('0*** ERROR - ',I2,' STATION CHARACTERISTICS WERE ',
     *   'PROCESSED. 12 MUST BE INPUT (ONE FOR EACH MONTH).')
1870  FORMAT (' REPORTING TIME INTERVAL SET TO : ',I2)
1880  FORMAT (' PROCESSING CODE SET TO : ',I2,2X,'(',A,')')
1890  FORMAT (' MDR BOX NUMBER SET TO : ',I6,2X,'(',A,')')
1900  FORMAT (' TYPE OF STATION WEIGHTS SET TO : ',A)
1910  FORMAT (' SIGNIFICANCE STATION IDENTIFIER SET TO : ',A)
1920  FORMAT (' SIGNIFICANCE STATION WEIGHT SET TO : ',F9.2)
1930  FORMAT (' CORRECTION FACTOR ',I1,' SET TO : ',F9.2)
1940  FORMAT (' STATION CHARACTERISTIC ',I2,' SET TO : ',F9.2)
1950  FORMAT ('0*** WARNING - WEIGHTING INDICATOR HAS ',
     *   'ALREADY BEEN DEFINED.')
1960  FORMAT ('0*** ERROR - INVALID PCPN WEIGHTING ',
     *   'OPTION (',A,') IN INPUT FIELD ',I2,' (CARD FIELD ',I2,').')
1970  FORMAT (' WGHT INDICATOR SET TO : ',I6,2X,'(',A,')')
1975  FORMAT ('0*** NOTE - PCPN CHAR VALUES SUCCESSFULLY WRITTEN ',
     *   'FOR STATION ',A,'.')
1980  FORMAT ('0*** ERROR - SYSTEM ERROR WRITING PCPN CHARACTERISTICS ',
     *   'TO THE PPPDB.')
1990  FORMAT ('0*** ERROR - PCPN CHARACTERISTICS FILE IS ',
     *   'FULL.')
1995  FORMAT ('0*** ERROR - PRECIPITATION CHARACTERISTIS NOT ',
     *   'DEFINED IN PREPROCESSOR PARAMETRIC DATA BASE.')
2000  FORMAT ('0*** ERROR - IN SFPCPN - STATUS CODE RETURNED FROM ',
     *   'WPP1CH (',I2,') NOT RECOGNIZED.')
2010  FORMAT ('0*** NOTE - PCPN PARAMETERS FOR STATION ',A,
     *   ' NOT WRITTEN BECAUSE ',I2,' ERRORS ENCOUNTERED. ',
     *   'RUNCHECK OPTION SET FOR THIS STATION.')
2020  FORMAT ('0*** NOTE - PCPN PARAMETERS FOR STATION ',A,
     *   ' NOT WRITTEN BECAUSE ',I2,' ERRORS ENCOUNTERED.')
2030  FORMAT ('0*** WARNING - PCPN PARAMETERS FOR STATION ',A,
     *   ' NOT WRITTEN BECAUSE STAN PARAMETERS NOT SUCCESSFULLY ',
     *   'DEFINED.')
2040  FORMAT ('0*** WARNING - PCPN PARAMETERS FOR STATION ',A,
     *   ' NOT WRITTEN BECAUSE STAN PARAMETERS NOT SUCCESSFULLY ',
     *   'REDEFINED.')
2050  FORMAT ('0*** NOTE - PCPN PARAMETERS FOR STATION ',A,
     *   ' NOT WRITTEN BECAUSE RUNCHECK OPTION IS SET.')
2060  FORMAT ('0*** WARNING - NO PCPN PARAMETER VALUES FOUND.')
2070  FORMAT (' IPTIME=',I2,3X,'ITPPVR=',I2)
2080  FORMAT (' *** EXIT SFPCPN - ISTAT=',I2)
C
      END
