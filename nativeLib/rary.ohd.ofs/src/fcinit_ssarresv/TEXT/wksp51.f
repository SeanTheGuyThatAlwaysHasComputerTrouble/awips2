C MEMBER WKSP51
C--------------------------------------------------------------------
C
C@PROCESS LVL(77)
C
      SUBROUTINE WKSP51(PO,NWKSP)
C
C DESC COMPUTE AMOUNT OF WORK SPACE NEEDED BY 'SSARRESV'
C
C  ROUTINE TO COMPUTE THE AMOUNT OF WORK SPACE NEEDED FOR THE SSARRESV
C  OPERATION.
C--------------------------------------------------------------------
C  KUANG HSU - HRL - OCTOBER 1994
C--------------------------------------------------------------------
C
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fprog'
      INCLUDE 'common/comn51'
C
      DIMENSION PO(*)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_ssarresv/RCS/wksp51.f,v $
     . $',                                                             '
     .$Id: wksp51.f,v 1.3 2006/03/16 16:25:06 xfan Exp $
     . $' /
C    ===================================================================
C
C
      IF (IBUG.GE.1) WRITE(IODBUG,900)
  900 FORMAT(10X,'*** ENTER WKSP51 ***')
C
C----------------------------
C  SET INITIAL VALUES AND COUNTERS
C
      NTIM24 = 24/MINODT
      NWKSP = 0
      NRES = PO(9)
C
C-------------------------------
C  THE FIRST PORTION OF WORK SPACE IS USED FOR HOLDING
C  THE ELEVATION/STORAGE CURVE IN THE ARMY CORPS FORMAT
C  DISCHARGE, ELEVATION, TIME OF STORAGE (NOT USED), STORAGE
C
C  FOR BACKWATER AFFECTED RESERVOIR, ONLY ELEVATION AND STORAGE ARE
C  NEEDED.  DISCHARGE WILL BE DETERMINED FROM BACKWATER TABLE
C
      DO 100 II=1,NRES
      IRES = II
C
C  72 WORD FOR HEADER IN IS ARRAY
      NWKSP = NWKSP +72
C  7 WORD FOR HEADER IN FCN ARRAY
C    WORD IN FCN ARRAY:
C      1        LENGTH OF ARRAY IN WORDS.
C      2  SCNI -SCAN INDEX MAINTAINED BY STLU2, POINTS TO POSITION IN
C               ARRAY USED BY PREVIOUS REFERENCE.
C      3        BASETIME RELATION WAS LOADED TO FILE (SYSTEM CLOCK TIME)
C      4        RESERVED
C      5-6      RELATION NAME
C      7  FC - FORMAT CODE (REAL);
      NWKSP = NWKSP +7
      IPTX = NRES - IRES +10
      LPP = PO(IPTX)
      ISTYP = PO(LPP)
      LPP = LPP+1
C
C  ELEVATION/STORAGE/DISCHARGE CURVE
      NVAL = PO(LPP)
      LPP = LPP+3*NVAL+1
      NWKSP = NWKSP + NVAL*4
      IF(ISTYP.EQ.0) GO TO 100
C
C  STORE BACKWATER TABLE
      NVAL = PO(LPP)
C
C  THE THREE VARIABLES FOR EACH POINT OF THE BACKWATER CURVE ARE ENTERED
C  IN THE SEQUENCE OF THE BACKWATER OUTFLOW, THE BACKWATER ELEVATION,
C  AND THE CONTROL PARAMETER (DISCHARGE OR ELEV OF DOWNSTREAM RESERVOIR)
      NWKSP = NWKSP + NVAL*3
 100  CONTINUE
C
C---------------------------
C  THE NEXT PORTION OF WORK SPACE IS USED FOR HOLDING SIMULATED TRACES
C  FOR INSTANTANEOUS DISCHARGE AT PERIOD START AND END,
C  MEAN DISCHARGE, POOL ELEVATION AND STORAGE CONTENTS.
C  ENOUGH SPACE IS NEEDED TO HOLD A VALUE FOR EACH QUANTITY FOR EACH
C  PERIOD IN THE RUN.
C  EACH QUANTITY WILL BE FILLED CONTIGUOUSLY FOR TIME-SERIES PURPOSES.
C
C dws    PO(12) was placed into an integer to replace it in the next
C dws     statement to avoid compiler warnings ... 2006-01-23
 
      NUPO12 = PO(12)

      NTTS = PO(NUPO12)
      NTS = (NTTS-2)/NRES-5
      NWKSP = NWKSP + NRES*NTS*NDD*NTIM24
C
C---------------------------------------------------------------
C  NEXT PORTION OF THE WORK SPACE IS RESERVED FOR CARRYOVER STORAGE.
C
      NWKSP = NWKSP + 4*NRES + 1
C
C
C
C  STORE ALLOCATION LOCATIONS FOR REQUIRED AND OPTIONAL WORK SPACE
C
      NWKSP = NWKSP + NRES*200
      PO(16) = NWKSP
C
C
C  PRINT OUT ALLOCATION LOCATIONS FOR REQUIRED WORK SPACE
C
      IF (IBUG.GE.2) WRITE(IODBUG,1690) NWKSP
 1690 FORMAT(1H0,' NO. OF WORDS NEEDED FOR WORK SPACE = ',I4)
C
      IF (IBUG.GE.1) WRITE(IODBUG,901)
  901 FORMAT(11X,'*** EXIT WKSP51 ***')
C
      RETURN
      END
