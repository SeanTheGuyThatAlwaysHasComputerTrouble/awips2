C MODULE QDTYPE
C-----------------------------------------------------------------------
C
C  ROUTINE QDTYPE CHECKS TO SEE IF THE DATA TYPE IS A VALID RRS DATA
C  TYPE AND RETURNS ATTRIBUTES FOR THE DATA TYPE.
C
C  ORIGINALLY CODED BY DEBBIE VAN DEMARK - 2/29/84
C
C----------------------------------------------------------------------
C
C  INPUT ARGUMENT
C        DTYPE - DATA TYPE
C        IUDTR - INDICATES WHETHER ROUTINE UDTRRS HAS BEEN CALLED
C       MXTYPE - MAXIMUM NUMBER OF DATA TYPES
C         TYPE - ARRAY TO HOLD DATA TYPES
C       IPRTYP - ARRAY TO HOLD DATA PRINT CATEGORIES
C        JMEAN - ARRAY TO HOLD MEAN DATA INDICATORS
C        JMISS - ARRAY TO HOLD MISSING DATA ALLOWED INDICATORS
C       AUNITI - ARRAY TO HOLD DATA INPUT UNITS
C       AUNITO - ARRAY TO HOLD DATA OUTPUT UNITS
C       DISTRB - ARRAY TO HOLD DISTRIBUTION INDICATORS
C       LFIELD - ARRAY TO HOLD THE FIELD LENGTH TO BE USED
C                WHEN PRINTING VALUES
C       NUMDEC - ARRAY TO HOLD THE NUMBER OF DECIMAL PLACES TO BE
C                USED WHEN PRINTING VALUES
C      CHKMIN  - ARRAY TO HOLD THE MINIMUM VALUES
C      CHKMAX  - ARRAY TO HOLD THE MAXIMUM VALUES
C  OUTPUT ARGUMENT
C       NUTYPE - NUMBER OF DATA TYPES RETURNED FROM UDTRRS
C        ITYPE - PRINT TYPE INDICATOR
C       IMEAN - MEAN DATA TYPE INDICATOR
C               0 - INITIALIZATION VALUE
C               3 - MEAN DATA TYPE
C               2 - INSTANTANEOUS DATA TYPE
C      UNITIN - DATA INPUT UNITS
C      UNITOT - DATA OUTPUT UNITS
C      NFIELD - FIELD LENGTH TO BE USED IN PRINTING
C        NDEC - NUMBER OF DECIMAL PLACES TO BE USED IN PRINTING
C       ISTAT - STATUS CODE
C               0 - OK
C               1 - NOT A VALID RRS DATA TYPE
C
C----------------------------------------------------------------------
C
      SUBROUTINE QDTYPE (DTYPE,IUDTR,MXTYPE,TYPE,IPRTYP,JMEAN,
     $ JMISS,AUNITI,AUNITO,DISTRB,LFIELD,NUMDEC,CHKMIN,CHKMAX,
     $ NUTYPE,ITYPE,IMEAN,UNITIN,UNITOT,NFIELD,NDEC,ISTAT)
C     
      DIMENSION TYPE(1),JMEAN(1),AUNITI(1),AUNITO(1),JMISS(1),IPRTYP(1),
     $ LFIELD(1),NUMDEC(1),CHKMIN(1),CHKMAX(1),DISTRB(1)
      DIMENSION OLDOPN(2)
C      
      INCLUDE 'common/pudbug'
      INCLUDE 'common/ionum'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_rrs/RCS/qdtype.f,v $
     . $',                                                             '
     .$Id: qdtype.f,v 1.3 1999/07/06 15:54:30 page Exp $
     . $' /
C    ===================================================================
C
C
      IBUG=IPBUG('QDTP')
C      
      ISTAT=1
C
      IOPNUM=-3
      CALL FSTWHR ('QDTYPE  ',IOPNUM,OLDOPN,IOLDOP)
C
      IF (IPTRCE.GT.2) WRITE (IOPDBG,60)
C      
      IF (IUDTR.EQ.1) GO TO 20
C
C  CALL UDTRRS TO GET THE VALID RRS DATA TYPE CODES AND INFORMATION
      CALL UDTRRS (MXTYPE,NUTYPE,TYPE,IPRTYP,JMEAN,JMISS,
     $   AUNITI,AUNITO,DISTRB,LFIELD,NUMDEC,CHKMIN,CHKMAX,ISUDTR)
C
      IUDTR=1
C
C CHECK STATUS FROM UDTRRS
      IF (ISUDTR.EQ.0) GO TO 20
      IF (ISUDTR.EQ.2) GO TO 10
         WRITE(IPR,70)
         CALL WARN
         GO TO 20
10    WRITE (IPR,80)NUTYPE,MXTYPE
         CALL WARN
         NUTYPE=MXTYPE
20    IF (IBUG.GT.0) WRITE (IOPDBG,90) DTYPE
      DO 30 I=1,NUTYPE
         IF (DTYPE.EQ.TYPE(I)) GO TO 40
30       CONTINUE
      WRITE(IPR,100) DTYPE
      CALL WARN
      GO TO 50
40    ISTAT=0
      ITYPE=IPRTYP(I)
      IMEAN=JMEAN(I)
      UNITIN=AUNITI(I)
      UNITOT=AUNITO(I)
      NPRTYP=IPRTYP(I)
      NFIELD=LFIELD(I)
      NDEC=NUMDEC(I)
      IF (IBUG.GT.0) WRITE(IOPDBG,110)IMEAN,UNITIN,UNITOT,NPRTYP,NFIELD,
     $  NDEC
C
50    IF (IPTRCE.GT.2) WRITE (IOPDBG,120)
      CALL FSTWHR (OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C      
      RETURN
C
60    FORMAT ('0*** ENTER QDTYPE ')
70    FORMAT ('0**WARNING** THERE WAS AN INVALID PRINT CLASS OR TIME',
     $ ' INDICATOR DECODED FROM THE RRS DATA TYPE INFORMATION FILE.'/
     $ T12,' THIS WILL CAUSE SOME DATA NOT TO BE PRINTED.')
80    FORMAT ('0**WARNING** ',I3,' RRS DATA TYPES EXIST AND ',
     $  'THE DATA TYPE INFORMATION ARRAYS ARE DIMENSIONED AT ',I3,'.'/
     $ T15,'PROCESSING WILL CONTINUE. THIS MAY CAUSE SOME VALID DATA ',
     $  'TYPES TO BE REJECTED, AS ALL THE VALID DATA TYPES WILL NOT ',
     $  'FIT IN THE ARRAYS AS DIMENSIONED')
90    FORMAT ('0PROCESSING DATA TYPE ',A4)
100   FORMAT (' ',A4,' IS NOT A VALID RRS DATA TYPE, PROCESSING WILL',
     $ ' CONTINUE WITH THE NEXT DATA TYPE.')
110   FORMAT ('0IMEAN= ',I1,' UNITIN= ',A4,' UNITOT= ',A4,' IPRTYP=',I1,
     $ ' NFIELD= ',I3,' NDEC= ',I3)  
120   FORMAT ('0*** EXIT QDTYPE ')
C
      END
