C MODULE QXWPRD
C-----------------------------------------------------------------------
C
C  ROUTINE QXWPRD SETS UP AND CALLS WPRD.
C
C  ORIGINALLY CODED BY ED VANBLAGAN - HRL - 7/31/85
C
C  MODIFIED BY ERIC ANDERSON - HRL - FEB 1987
C    FUTURE POINTER PASSED TO WPRD SET TO ZERO IF A TIME SERIES
C    MUST BE TRUNCATED BECAUSE OF SPACE ON THE PDB AND SPACE ON THE
C    PDB EQUALS MINDAY
C
C-----------------------------------------------------------------------
C
C  INPUT ARGUMENTS:
C
C        STAID - STATION ID
C        DTYPE - DATA TYPE CODE
C        JHOUR - FIRST HOUR OF DATA IN TSDAT
C       INTVAL - TIME INTERVAL
C       UNITOT - OUTPUT DATA UNITS
C        NSTEP - NUMBER OF VALUES IN TSDAT
C       LTADAT - LENGTH OF ARRAY TSDAT
C        TSDAT - TIME SERIES DATA ARRAY
C        IFPTR - FIRST HOUR OF FUTURE DATA
C       LWKBUF - LENGTH OF ARRAY IWKBUF
C       IWKBUF - WORK ARRAY
C         IREC - RECORD NUMBER OF TIME SERIES IN PROCESSED DATA BASE
C
C-----------------------------------------------------------------------
C
      SUBROUTINE QXWPRD (STAID,DTYPE,JHOUR,INTVAL,UNITOT,NSTEP,
     $  LTSDAT,TSDAT,IFPTR,LWKBUF,IWKBUF,IREC,LERDTP,ERDTP,NERDTP)
C
      CHARACTER*4 DTYPE,UNITOT,ERDTP
      CHARACTER*8 STAID,OLDOPN
C
      DIMENSION TSDAT(LTSDAT),IWKBUF(LWKBUF)
C 
      INCLUDE 'common/ionum' 
      INCLUDE 'common/fctime'    
      INCLUDE 'common/qprint'
      INCLUDE 'common/pudbug'
      INCLUDE 'prdcommon/pmaxdm'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_rrs/RCS/qxwprd.f,v $
     . $',                                                             '
     .$Id: qxwprd.f,v 1.4 1999/07/06 16:14:48 page Exp $
     . $' /
C    ===================================================================
C
C
      IF (IPTRCE.GT.0) WRITE (IOPDBG,50)
C      
      IOPNUM=-3
      CALL FSTWHR ('QXWPRD  ',IOPNUM,OLDOPN,IOLDOP)
C
      IBUG=IPBUG('QXWP')
C
C  INITIALIZE NUMBER OF VALUES ALLOWED (NUMVAL) TO BE NSTEP
      NUMVAL=NSTEP
C      
C  COMPUTE VARIABLES
C   NPER  - NUMBER OF PERIODS IN A DAY
C   MAXNF - MAX NUMBER OF FUTURE VALUES PDB CAN ACCEPT
C   NUMR  - NUMBER OF REGULAR VALUES IN TSDAT
C   NUMF  - NUMBER OF FUTURE VALUES IN TSDAT
      NPER=24/INTVAL
      MAXNF=(IPRDMD(DTYPE)-MINDAY)*NPER
      NUMR=(LDACPD-IDARUN)*NPER+LHRCPD/INTVAL
      NUMF=NSTEP-NUMR
      IF (NUMF.LE.MAXNF) GO TO 30
C      
C  MUST TRUNCATE DATA - CHECK IF ANY TRUNCATED ARE NON-MISSING
      NUMVAL=NUMR+MAXNF
      IF (MAXNF.LE.0) IFPTR=0
      IF (IBUG.GT.0) WRITE (IOPDBG,70) NPER,MAXNF,NUMR,NUMF,
     $   NUMVAL,INTVAL,STAID,DTYPE,(TSDAT(I),I=1,NSTEP)
      J=NUMVAL+1
      DO 10 I=J,NSTEP
         IF (IFMSNG(TSDAT(I)).EQ.0) GO TO 20
10       CONTINUE
C
C  TRUNCATED DATA IS ALL MISSING
      GO TO 30
C      
C  TRUNCATED DATA HAD OBSEVATIONS
20    IF (IRWARN.EQ.1) THEN
         WRITE (IPR,40) DTYPE,STAID
         CALL WARN
         ENDIF
C
C  WRITE DATA TO PROCESSED DATA BASE
30    ICALL=0
      CALL WPRDD (STAID,DTYPE,JHOUR,INTVAL,NUMVAL,UNITOT,NUMVAL,
     $   LTSDAT,TSDAT,IFPTR,ICALL,LWKBUF,IWKBUF,IREC,ISTAT)
      IF (ISTAT.GT.0) THEN
         CALL QSWPRD (ISTAT,STAID,DTYPE,JHOUR,INTVAL,NUMVAL,
     $      NUMVAL,UNITOT,LWKBUF,LTSDAT,LERDTP,ERDTP,NERDTP)
         ENDIF
C
      CALL FSTWHR (OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C      
      IF (IPTRCE.GT.0) WRITE (IOPDBG,60)
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -      
C
40    FORMAT ('0**WARNING** SOME DATA WAS TRUNCATED ',
     $   'FOR DATA TYPE ',A4,' FOR STATION ',2A4,' ',
     $   'TO PRESERVE MINIMUM DAYS OF OBSERVED DATA IN THE PDB.')
50    FORMAT ('0*** ENTER QXWPRD')
60    FORMAT ('0*** EXIT QXWPRD')
70    FORMAT (' QXWPRD DEBUG',6I5,3A4 / (1X,10F10.1) )
C
      RETURN
C
      END
