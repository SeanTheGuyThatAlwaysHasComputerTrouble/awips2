C MODULE PRP32
C-----------------------------------------------------------------------
C
      SUBROUTINE PRP32 (PO)
C
C  THIS ROUTINE PRINTS PARAMETRIC DATA WHICH WAS STORED IN THE
C  PO ARRAY BY THE PIN ROUTINE AND PRINTS A SUMMARY OF THIS DATA.
C  THIS SUBROUTINE PRINTS PARAMETER VALUES FROM THE P ARRAY FOR THE
C  FFG OPERATION.
C
C  INITIALLY WRITTEN BY - JANICE LEWIS - HRL - 9/1991
C  ADDED MINIMUM AND MAXIMUM THRESHOLD RUNOFF VALUES - TIM SWEENEY - HRL
C     1/1995
C
C  PRINCIPAL VARIABLES:
C     BASNID      ID OF BASIN BOUNDARY
C     FFGAID      FLASH FLOOD GUIDANCE AREA IDENTIFIER
C     FFGDSC      FLASH FLOOD GUIDANCE AREA DESCRIPTION
C     IBUG        DEBUG OUTPUT SWITCH, 0 = OFF, 1 = ON
C     IDUR        DURATION FLAG, 0 = 1-, 3-, AND 6-HOUR DURATIONS ONLY,
C                   1 = 12- AND 24-HOUR DURATIONS ALSO.
C     NOP         NUMBER OF THIS OPERATION (32)
C     OPNARR(2)   OPERATION NAME OF RAINFALL-RUNOFF MODEL
C     OPNASN(2)   OPERATION NAME OF SNOW MODEL
C     OPTYRR(2)   OPERATION TYPE OF RAINFALL-RUNOFF MODEL
C     OPTYSN(2)   OPERATION TYPE OF SNOW MODEL
C     PO(1)       INPUT PARAMETRIC DATA FROM P ARRAY
C
      CHARACTER*4 FFGTYP
      CHARACTER*8 RTNNAM
      CHARACTER*8 SEGID,FFGAID,BASNID,OPNARR,OPTYRR,OPNASN,OPTYSN
      CHARACTER*20 FFGDSC
C
      DIMENSION PO(*)
      PARAMETER (LARRY=100)
      DIMENSION PARRY(LARRY)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/where'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_prpc/RCS/prp32.f,v $
     . $',                                                             '
     .$Id: prp32.f,v 1.3 2003/12/16 15:28:29 scv Exp $
     . $' /
C    ===================================================================
C
C
      RTNNAM='PRP32'
C
      IF (ITRACE.GT.0) WRITE (IODBUG,*) 'ENTER ',RTNNAM
C
      IBUG=IFBUG('FFG')
C
      IF (IBUG.GT.0) WRITE (IODBUG,'(A,2A4)') ' IN PRP32 - ISEG=',ISEG
      CALL UMEMOV (ISEG,SEGID,2)
      IF (IBUG.GT.0) WRITE (IODBUG,*) 'IN PRP32 - SEGID=',SEGID
C
      CALL UMEMOV (PO(2),FFGAID,LEN(FFGAID)/4)
      CALL UMEMOV (PO(4),FFGDSC,LEN(FFGDSC)/4)
      CALL UMEMOV (PO(9),OPTYRR,2)
      CALL UMEMOV (PO(11),OPNARR,2)
      CALL UMEMOV (PO(13),OPTYSN,2)
      CALL UMEMOV (PO(15),OPNASN,2)
      CALL UMEMOV (PO(17),BASNID,LEN(BASNID)/4)
C
      IDUR = PO(19)
C
      LMMRO = PO(24)
      IF (LMMRO.GT.0) THEN
         ROMIN = PO(LMMRO)
         ROMAX = PO(LMMRO+1)
         ELSE
            ROMIN = 0.10
            ROMAX = 2.50
         ENDIF
C
      WRITE (IPR,100) FFGAID,FFGDSC,IDUR,ROMIN,ROMAX,
     +                BASNID,OPTYRR,OPNARR,OPTYSN,OPNASN
  100 FORMAT (11X,'FFG ID: ',A,5X,'DESC: ',A,5X,'IDUR:',I2 //
     +  14X,'MINIMUM THRESHOLD RUNOFF:',F5.2 /
     +  14X,'MAXIMUM THRESHOLD RUNOFF:',F5.2 //
     +  14X,' BASIN     RAINFALL-RUNOFF MODEL       SNOW MODEL' /
     +  14X,'BNDRY ID     OP TYPE   OP NAME     OP TYPE   OP NAME' /
     +  14X,'--------     --------  --------    --------  --------' /
     +  14X, A, 5X, A,2X,A,4X,A,2X,A )
C
C  CHECK IF FFG PARAMETER RECORD EXISTS
      FFGTYP='FFG'
      IREC=0
      CALL RPPREC (FFGAID,FFGTYP,IREC,LARRY,PARRY,NFILL,IRECNX,ISTAT)
      IF (ISTAT.NE.0) THEN
         CALL PSTRDC (ISTAT,FFGTYP,FFGAID,IREC,LARRY,NFILL)
         ENDIF
C
      RETURN
C
      END
