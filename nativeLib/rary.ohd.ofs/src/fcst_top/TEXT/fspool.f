C MODULE FSPOOL
C-----------------------------------------------------------------------
C  DESC: ROUTINE TO COPY CARRYOVER FROM TEMPORARY TO PERMANENT DATASET
C
      SUBROUTINE FSPOOL
C
      INCLUDE 'common/fccgd1'
      INCLUDE 'common/fcunit'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fctim2'
      INCLUDE 'common/where'
      INCLUDE 'common/fccgd'
      INCLUDE 'common/errdat'
      INCLUDE 'common/sysbug'
      INCLUDE 'common/fc'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fcio'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fcary'
      INCLUDE 'common/fcsegn'
C
      DIMENSION OPNOLD(2),SEG(2),CGID(2)
C
      EQUIVALENCE (ISEG(1),SEG(1))
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_top/RCS/fspool.f,v $
     . $',                                                             '
     .$Id: fspool.f,v 1.4 1999/01/20 13:55:42 page Exp $
     . $' /
C    ===================================================================
C
C
C
      IBUG=0
      IF (IFBUG('COTR').EQ.1) IBUG=1
      IF (IFBUG('COBG').EQ.1) IBUG=2
C
      IF (IBUG.GT.0) WRITE (IODBUG,140)
C
      IOPNUM=0
C
C
      CALL UMEMOV (OPNAME,OPNOLD,2)
      CALL UMEMOV ('FSPOOL  ',OPNAME,2)
C
      NWST=NWARN
      NEST=NERRS
C
      IF (NCSTOR.EQ.0) THEN
         WRITE (IPR,150)
         CALL ERROR
         GO TO 130
         ENDIF
C
C  PROCESS EACH TEMPORARY FILE
      DO 120 I=1,NCSTOR
         IUNIT=ICFNUM(I)
         IF (IBUG.GT.1) WRITE (IODBUG,*)
     *      ' NCSTOR=',NCSTOR,
     *      ' I=',I,
     *      ' IUNIT=',IUNIT,
     *      ' '
         REWIND IUNIT
         READ (IUNIT,END=115) ISLOT,CGID,IPROT,ICDAYT,ICHR
         IF (IBUG.GT.1) WRITE (IODBUG,170) ISLOT,ICDAYT,ICHR,CGID,IPROT
C     CHECK IF THE CARRYOVER GROUP ID STORED ON THE TEMP FILE IS DEFINED
         DO 30 J=1,25
            IF (CGID(1).NE.CGIDS(1,J).OR.CGID(2).NE.CGIDS(2,J)) GO TO 30
            KREC=ICOREC(J)
            GO TO 40
30          CONTINUE
         WRITE (IPR,180) CGID
         CALL ERROR
         GO TO 130
C     SET THE COMPLETE SWITCH TO INCOMPLETE AND FORCE WRITE THIS STATUS
C     ON THE CARRYOVER GROUP DEFINITION FILE
40       CALL FCTZC (100,0,TZ)
         CALL JULDA (LUPD,IK,NOW(1),NOW(2),NOW(3),1,100,0,TZ)
         LUPT=NOW(4)*100000+NOW(5)
         IPC(ISLOT)=IPROT*2
         CALL UWRITT (KFCGD,KREC,CGIDC(1),IERR)
         IF (IBUG.GT.1) WRITE (IODBUG,190) KREC,IPC(ISLOT),IPROT
         CALL MDYH2 (ICDAYT,ICHR,MMO,MMDA,MMYR,MMHR,NZXX,NDXX,INPTZC)
         WRITE (IPR,200) MMO,MMDA,MMYR,MMHR,INPTZC
50       NER1=NERRS
C     READ SEGMENT HEADER FROM TEMPORARY FILE
         READ (IUNIT,END=100) SEG,IWOCRY,ICDAYT,ICHR,NCITMS,NCR
         IF (IBUG.GT.1) WRITE (IODBUG,210) SEG,IWOCRY,ICDAYT,ICHR,
     *      NCITMS,NCR
         CALL FLOCSG (SEG,IRSEG)
         CALL FGETSG (SEG,IRSEG,0,X,0,X,0,X,1,1,IERR)
         INCSEG(ISLOT)=0
         CALL UWRITT (KFSGST,IRSEG,IDSEGN,IERR)
         NCLNTH=0
         DO 70 K=1,NCR
C        READ INDIVIDUAL CARRYOVER VALUES
            READ (IUNIT,END=115) IC1,IC2,(C(JK),JK=IC1,IC2)
            IF (IBUG.GT.1) WRITE (IODBUG,220) K,IC1,IC2
            NCLNTH=NCLNTH+IC2-IC1+1
            IF (NCLNTH.LE.NC) GO TO 70
               NCULP=K
               GO TO 80
70          CONTINUE
         C(NC)=-1.01
         GO TO 90
80       WRITE (IPR,230) NCLNTH,NCULP,IUNIT,NC
         CALL ERROR
         CALL UCLOST (KFSGST)
         GO TO 130
C     WRITE THE CARRYOVER VALUES TO THE PERMANENT CARRYOVER FILE
90       CALL FPUTCO (SEG,ICDAYT,ICHR,NOW,ISLOT,IWOCRY,C,NC,0)
C     CHECK IF ERRORS ENCOUNTERED
         IF (NERRS.GT.NER1) GO TO 50
C     SET INCOMPLETE SWITCH TO COMPLETE
         INCSEG(ISLOT)=1
         CALL UWRITT (KFSGST,IRSEG,IDSEGN,IERR)
         GO TO 50
C     IF ANY ERRORS HAVE OCCURRED IN WRITING CARRYOVER, MAINTAIN
C     INCOMPLETE STATUS OF CARRYOVER ON PERMANENT CARRYOVER FILE
100      IF (NERRS.EQ.NEST) GO TO 110
            WRITE (IPR,240)
            CALL UCLOST (KFSGST)
            GO TO 130
C     NO ERRORS HAVE OCCURRED - SET COMPLETION SWITCH TO COMPLETE
C     AND FORCE WRITE THE CARRYOVER GROUP DEFINITION ONTO THE CGDEF FILE
C     AND PLACE THE CARRYOVER DATE AND TIME AND THE UPDATE DATE AND TIME
110      IPC(ISLOT)=IPC(ISLOT)+1
         ICODAY(ISLOT)=ICDAYT
         ICOTIM(ISLOT)=ICHR
         LUPDAY(ISLOT)=LUPD
         LUPTIM(ISLOT)=LUPT
C     FORCE WRITE INCOMPLETE SWITCH FOR SLOT IN FCCGD1 COMMON BLOCK
         CALL UWRITT (KFCGD,KREC,CGIDC(1),IERR)
         CALL UCLOST (KFCRY)
         CALL UCLOST (KFSGST)
         WRITE (IPR,250) MMO,MMDA,MMYR,MMHR,INPTZC
         GO TO 120
115      WRITE (IPR,255) IUNIT
         CALL ERROR
120      CONTINUE
C
130   IF (IBUG.GT.0) WRITE (IODBUG,260)
C
      CALL UMEMOV (OPNOLD,OPNAME,2)
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
140   FORMAT (' *** ENTER FSPOOL')
150   FORMAT ('0**ERROR** NUMBER OF CARRYOVER SAVE DATES IS ZERO.')
170   FORMAT (' FILE HEADER - ',
     *   'ISLOT=',I2,' ICDAYT=',I7,' ICHR=',I2,' CGID=',2A4,
     *   'IPROT=',I2)
180   FORMAT ('0**ERROR* CARRYOVER GROUP IDENTIFIER ',2A4,
     *   ' IS INVALID.')
190   FORMAT (' INCOMPLETE STATUS FORCE WRITTEN - KREC=',I3,
     *  ' IPC(ISLOT)=',I3,' IPROT=',I3)
200   FORMAT ('0**NOTE** BEGINNING CARRYOVER FILE UPDATE FOR ',
     *   I2.2,'/',I2.2,'/',I4,'-',I2.2,A4,'. ',
     *   'PROGRAM TERMINATION AT THIS POINT MAY DAMAGE ',
     *   'FILE CONTENTS.')
210   FORMAT (' SEGMENT HEADER - ',
     *   'SEG=',2A4,' IWOCRY=',I4,' ICDAYT=',I7,
     *   ' ICHR=',I2,' NCITMS=',I4,' NCR=',I5)
220   FORMAT (' CARRYOVER RECORD READ - CO OPER. NO.=',I2,
     *  ' FIRST POSITION=',I3,' LAST POSITION=',I3)
230   FORMAT ('0**ERROR** THE NUMBER OF CARRYOVER VALUES (',I5,
     *   ') READ FROM RECORD ',I5,' OF UNIT ',I2,
     *   ' EXCEEDS THE NUMBER OF VALUES IN THE C ARRAY (',I5,').')
240   FORMAT ('0**NOTE** ERRORS ENCOUNTERED WRITING CARRYOVER TO ',
     *  'PERMANEMT FILES. STATUS OF CARRYOVER ON PERMANENT FILE ',
     *  'REMAINS INCOMPLETE.')
250   FORMAT ('0**NOTE** CARRYOVER HAS BEEN SUCCESSFULLY SAVED FOR ',
     *   I2.2,'/',I2.2,'/',I4,'-',I2.2,A4,'.')
255   FORMAT ('0**ERROR** UNEXPECTED END OF FILE ENCOUNTERED READING ',
     *   'CARRYOVER FROM UNIT ',I2,'.')
260   FORMAT (' *** EXIT FSPOOL')
C
C
      END
