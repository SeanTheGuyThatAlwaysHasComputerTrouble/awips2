C MODULE FCFAZE0
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 10/19/94.07:53:03 BY $WC20SV
C
C @PROCESS LVL(77)
C
C   ROUTINE TO INITIALIZE COMMON BLOCKS FOR FUNCTION FCEXEC
C
      SUBROUTINE FAZE0
C
C.......................................................................
C
C  STARTUP ROUTINE FOR PROGRAM FCEXEC
C    SETS INITIAL VALUES IN NUMEROUS COMMON BLOCKS.
C    NOTE THAT A NUMBER OF COMMON BLOCKS HAVE INITIAL VALUES
C    SET WITH BLOCK DATA ROUTINES
C
C  ROUTINE ORIGINALLY WRITTEN BY --
C    GEORGE F SMITH - HRL - 19 MARCH 1980
C  DEBUG ADDED 1/14/88 - GEORGE SMITH - HRL
C  FIXED MAX RATING CURVE POINTERS READ - GEORGE SMITH - HRL - 1/21/88
C
C  IGNORE THE MRC VALUE WHEN READING FROM THE FIRST RECORD IN THE
C  FCRCPTR FILE BECAUSE THIS VALUE IS SET IN MEMBER FCBLOCK.
C    BY DAVID TRAN -HRL- 2/23/94
C.......................................................................
      INCLUDE 'common/fccgd'
      INCLUDE 'common/fccgd1'
      INCLUDE 'common/fcfgs'
      INCLUDE 'common/fcsegp'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/fprog'
      INCLUDE 'common/fsglst'
      INCLUDE 'common/fcunit'
      INCLUDE 'common/where'
      INCLUDE 'common/killcd'
      INCLUDE 'common/frcptr'
C  DATA STATEMENTS USED TO SET VDATE AND PNAME IN COMMON PROG
      DIMENSION PNAMEX(5)
C  DATA STATEMENTS TO SET ROUTINE NAME IN COMMON BLOCK WHERE
      DIMENSION SBNAME(2),OLDOPN(2)
      DIMENSION IBUF(3)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_top/RCS/faze0.f,v $
     . $',                                                             '
     .$Id: faze0.f,v 1.1 1995/09/17 19:08:02 dws Exp $
     . $' /
C    ===================================================================
C
      DATA SVDAT1/4H????/,SVDAT2/4H????/
      DATA PNAMEX/4HFC E,4HXECU,4HTION,4H PRO,4HGRAM/
      DATA SBNAME/4HFAZE,4H0   /
      DATA BLANK/4H    /
C
C  SET DEBUG FLAG
C
      IBUG=IFBUG(4HPHZ0)
C
C  WHERE
C-------
      IOLDOP=IOPNUM
      IOPNUM=0
      DO 10 I=1,2
      OLDOPN(I)=OPNAME(I)
10    OPNAME(I)=SBNAME(I)
C
C  TRACE LEVEL = 1
C
      IF(ITRACE.GE.1)WRITE(IODBUG,20)
20    FORMAT(' *** ENTER FAZE0')
C
C.......................................................................
C
      IF(IBUG.EQ.1)WRITE(IODBUG,30)
30    FORMAT(' ABOUT TO FILL COMMON /FCCGD/')
C
C  FCCGD
C      COPY 1ST RECORD OF FILE FCCOGDEF INTO CB FCCGD
C-------
      CALL UREADT (KFCGD,1,NSLOTS,IERR)
C
      IF(IBUG.EQ.1)WRITE(IODBUG,40)
40    FORMAT(' ABOUT TO FILL COMMON /FCCGD1/')
C
C  FCCGD1
C     FILL CARRYOVER GROUP ID IN CB FCCGD1 WITH BLANKS
C--------
      CGIDC(1)=BLANK
      CGIDC(2)=BLANK
C
      IF(IBUG.EQ.1)WRITE(IODBUG,50)
50    FORMAT(' ABOUT TO FILL COMMON /FCFGS/')
C
C  FCFGS
C     COPY 1ST RECORD OF FILE FCFGSTAT INTO CB FCFGS
C     COPY VALUE IDUMYG INTO NFGREC (TOTAL NUMBER OF F GROUPS ON FILE)
C-------
      CALL UREADT (KFFGST,1,FGID,IERR)
      NFGREC=IDUMYG
C
      IF(IBUG.EQ.1)WRITE(IODBUG,60)
60    FORMAT(' ABOUT TO FILL COMMON /FCSEGP/')
C
C  FCSEGP
C     COPY 1ST TWO RECORDS OF FILE FCSEGPTR INTO CB FCSEGP
C--------
      CALL UREADT (KFSGPT,1,NS,IERR)
      CALL UREADT (KFSGPT,2,NRP,IERR)
C
      IF(IBUG.EQ.1)WRITE(IODBUG,70)
70    FORMAT(' ABOUT TO FILL COMMON /FSGLST/')
C
C  FSGLST
C     COPY UP TO 1ST 500 RECORDS OF FILE FCFGLIST INTO CB FSGLST
C--------
      NLIST=NS
      IF(MLIST.LT.NLIST)NLIST=MLIST
      DO 80 I=1,NLIST
      J=I+2
      CALL UREADT (KFSGPT,J,IDZ(1,I),IERR)
80    CONTINUE
C
      IF(IBUG.EQ.1)WRITE(IODBUG,90)
90    FORMAT(' ABOUT TO FILL COMMON /FRCPTR/')
C
C  FRCPTR
C--------
      CALL UREADT (KFRCPT,1,IBUF,IERR)
      NRC=IBUF(1)
      MRCF=IBUF(3)
C
C  CAN ONLY READ MINIMUM OF MRC OR MRCF RATING CURVES FROM FILE
C
C     MRC IS THE MAXIMUM NUMBER OF RATING CURVES THAT CAN FIT
C       IN COMMON BLOCK /FRCPTR/
C     MRCF IS THE MAXIMUM NUMBER OF RATING CURVES THAT CAN BE
C       HELD IN FCRCPTR FILE
C
      IRCXX=MIN0(MRC,MRCF)
      IRCRD=IRCXX+1
C
      IF(IBUG.EQ.1)WRITE(IODBUG,100)IRCXX,IRCRD
100   FORMAT(' ABOUT TO STORE ',I5,' RATING CURVES IN THE /FRCPTR/ ',
     1 'COMMON BLOCK'/3X,'READING RECORDS 2 THROUGH ',I5,' OF THE ',
     2 'FCRCPTR FILE.')
C
      DO 110 J=2,IRCRD
      I = J-1
      CALL UREADT (KFRCPT,J,RCZZ(1,I),IERR)
110   CONTINUE
C
      IF(IBUG.EQ.1)WRITE(IODBUG,120)
120   FORMAT(' ABOUT TO FILL COMMON /FPROG/')
C
C  FPROG
C     SET VALUES TO TELL WHICH PROGRAM  IS BEING EXECUTED
C------
      MAINUM=1
      VERS=5.000
      VDATE(1)=SVDAT1
      VDATE(2)=SVDAT2
      DO 130 I=1,5
130   PNAME(I)=PNAMEX(I)
      NDD=31
C
      IF(IBUG.EQ.1)WRITE(IODBUG,140)
140   FORMAT(' ABOUT TO FILL COMMON /KILLCD/')
C
C     SET KLSTOP = 0 IN CB KILLCD SO THAT PROGRAM WILL RETURN
C         FROM CALL TO ROUTINE KILLPM
C------
      KLSTOP=0
C
      IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      IF(ITRACE.GT.0)WRITE(IODBUG,150)
150   FORMAT(' *** EXIT FAZE0')
C
      RETURN
C
      END
