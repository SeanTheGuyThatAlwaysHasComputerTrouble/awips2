C   MODULE TESTM
C
      SUBROUTINE TESTM(IARY,MIARY,NTOTM,IMONTH,IDAY,IYEAR,IPART,IFUT,
     1 IBLAST,MSNG,NSTA,IORD,IER)
C
C   THIS SUBROUTINE ESTIMATES THE MISSING DATA AND CALCULATES
C   THE 6 HOUR MEANS FOR THE MAX/MIN STATIONS.
C
C   THIS SUBROUTINE WAS ORIGINALLY WRITTEN BY GERALD N. DAY .
C
      LOGICAL*1 KODE,O,B,E,C,M
      LOGICAL LBUG
C
      INTEGER*2 MAXT,MINT,MINE,MAXE,MEANE,MEANMX,MEANMN,IARY,
     1 MINP,MAXP,MEANP,MSNG,IT12,IORD

      character*8 curr_staid
C
      DIMENSION STAID(2,2),PTM(3,4),WTM(3,4),DESCR(5,2),NUM(2),DAY(4),
     1 PTI(3,4),WTI(3,4),PTF(2,4),WTF(2,4),DUM(2),SBNAME(2),OLDOPN(2),
     2 KODE(3,2),MAXT(2),MINT(2),IARY(1),IORD(1),STATE(2)
C
      INCLUDE 'common/tscrat'
      INCLUDE 'common/pudbug'
      INCLUDE 'common/tloc'
      INCLUDE 'common/tout'
      INCLUDE 'common/tuser'
      INCLUDE 'common/tary'
      INCLUDE 'common/fctime'
      INCLUDE 'common/where'
      INCLUDE 'common/ionum'
C
C
      EQUIVALENCE (ARY(18),PTM(1,1)),(ARY(30),WTM(1,1))
      EQUIVALENCE (ARY(42),PTI(1,1)),(ARY(54),WTI(1,1))
      EQUIVALENCE (ARY(66),PTF(1,1)),(ARY(74),WTF(1,1))
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mat/RCS/testm.f,v $
     . $',                                                             '
     .$Id: testm.f,v 1.2 1997/12/31 19:46:22 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA TEMP/4HTEMP/,BLANK/4H    /,O/1H /,B/1HB/,E/1HE/,C/1HC/,M/1HM/
      DATA DAY/4HOBSE,4HRVED,4HFUTU,4HRE  /
      DATA DCODE/4HESTM/,SBNAME/4HTEST,4HM   /
C
      IF(IPTRCE.GE.1) WRITE(IOPDBG,900)
  900 FORMAT(1H0,18H** PTESTM ENTERED.)
C
      LBUG=.FALSE.
      IF(IPBUG(DCODE).EQ.1) LBUG=.TRUE.
C
      IOLDOP=IOPNUM
      IOPNUM=-1
      DO 5 I=1,2
      OLDOPN(I)=OPNAME(I)
    5 OPNAME(I)=SBNAME(I)
C
      IF(.NOT.LBUG) GO TO 8
      WRITE(IOPDBG,910)
  910 FORMAT(1H0,26HBEFORE TESTM, IARY AND ARY)
      WRITE(IOPDBG,920) (IARY(I),I=1,LAST)
  920 FORMAT(5X,20I6)
      WRITE(IOPDBG,930) (ARY(I),I=ISTDAT,IUSE)
  930 FORMAT(5X,10F10.2)
    8 CONTINUE
C
      IFLAG=1
      ISTA=0
      IER=0
C
      NWTS=3
      IF(IFUT.EQ.1) NWTS=2
      II=2*IFUT+1
C
C   INITIALIZE IBTHIS = 0 , IF A STATION IS FOUND THAT IS NOT
C   BLENDING OR USING MEANS, IBTHIS WILL BE SET TO 1. (ONLY NEEDED
C   FOR FUTURE DAYS WHEN IPRMF=0.)
C
      IBTHIS=0
C
C   INITIALIZE IBLEND = 0 , IF A STATION IS FOUND THAT IS
C   BLENDING, IBLEND WILL BE SET TO 1.
C
      IBLEND=0
C
C   LOOP THROUGH MAX/MIN ARRAY SEARCHING FOR MAX/MIN ONLY STATIONS.
C   IF NSTA=0 PROCESS STATIONS IN THE ORDER THEY ARE STORED IN
C   THE MAX/MIN POINTER ARRAY. IF NSTA>0 PROCESS STATIONS IN THE
C   ORDER SPECIFIED BY THE IORD ARRAY.
C
      LOC=LOCMP
      MAXSTA=NTOTM
      IF(NSTA.EQ.0) GO TO 10
      MAXSTA=NSTA
   10 CONTINUE
      DO 800 I=1,MAXSTA
      IPOS=I
      IF(NSTA.EQ.0) GO TO 12
      LOC=LOCMP+IORD(I)-1
      IPOS=IORD(I)/5+1
   12 IF(IARY(LOC).EQ.0) GO TO 790
C
C
      NINST=0
      IESTMX=0
      IESTMN=0
C
      LIP=IARY(LOC+2)
C
C   CHECK TO SEE IF STATION HAS BEEN TREATED AS AN
C   INSTANTANEOUS STATION, THAT IS POSTIVE POINTER ON
C   A REGULAR DAY.
C
      IF(LIP.GT.0.AND.IFUT.EQ.0) GO TO 790
C
      ISTA=ISTA+1
C
C   CHECK IF FUTURE DAY OR MAX/MIN DATA ONLY
C
      IF(IFUT.EQ.1.OR.LIP.EQ.0) GO TO 14
C
C  INST. STATION , BUT TREATED AS A MAX/MIN STA. - GET LAST
C  INST. VALUE SO THAT IT CAN BE USED TO UPDATE THE PREV. VALUE ARRAY.
C
      LIPTMP=IABS(LIP)
      LID=IARY(LOCIP+LIPTMP+1)
      IDT=IARY(LOCIP+LIPTMP)
      IDT=IABS(IDT)
      IF(IDT.EQ.1) IDT=3
      NINST=24/IDT
      IT12=IARY(LOCID+LID-2+NINST)
C   VALUE IS EITHER O OR C - DOES NOT MATTER
      KODE(3,ISTA)=O
C
C
C   TREAT AS A MAX/MIN STATION
C
   14 IPREC=IARY(LOC)
      DUM(1)=BLANK
      DUM(2)=BLANK
C
C   READ TEMPERATURE PARAMETERS
C
      LIMIT=ISTDAT-1
      NPREC=-IPREC
      CALL RPPREC(DUM,TEMP,NPREC,LIMIT,ARY,NFILL,PTRNXT,ISTAT)
      IF(ISTAT.EQ.0) GO TO 16
C
      CALL PSTRDC(ISTAT,TEMP,DUM,IPREC,LIMIT,NFILL)
C
      IER=1
      GO TO 999
   16 IF(LBUG) CALL PDUMPA(NFILL,ARY,TEMP,DUM,1)
C
C   CHECK IF NETWORK HAS BEEN RUN ON THIS STATION.
C
      INETWK=ARY(16)
      IF(INETWK.EQ.0) GO TO 790
C
C
C   MAX AND MIN DATA
      LMEAN=IARY(LOC+1)
      ICFMAX=IARY(LOC+3)
      ICFMIN=IARY(LOC+4)
      LMD=IPOS*2-1
      MAXT(ISTA)=IARY(LOCMD+LMD-1)
      MINT(ISTA)=IARY(LOCMD+LMD)
C
      MEANMX=IARY(LOCMMX+LMEAN-1)
      MEANMN=IARY(LOCMMN+LMEAN-1)
      KODE(1,ISTA)=O
      KODE(2,ISTA)=O
      IF(IFUT.EQ.1) GO TO 18
      IF(ICFMAX.NE.0) KODE(1,ISTA)=C
      IF(ICFMIN.NE.0) KODE(2,ISTA)=C
C
   18 STAID(1,ISTA)=ARY(2)
      STAID(2,ISTA)=ARY(3)
      DO 20 J=1,5
      DESCR(J,ISTA)=ARY(4+J)
   20 CONTINUE
      NUM(ISTA)=ARY(4)
      STATE(ISTA)=ARY(83)
C
      IF(MAXT(ISTA).GT.MSNG) GO TO 150
C
C   ESTIMATE MAXIMUM TEMPERATURE
C
      IESTMX=1
      SUMD=0.
      SUMN=0.
      DO 100 J=1,4
      DO 50 K=1,NWTS
      IF(IFUT.NE.1) GO TO 30
C
C   FUTURE DATA
C
      LFPE=PTF(K,J)
      IF(LFPE.LE.0) GO TO 50
      LMPE=IARY(LOCFP+LFPE-1)
      IE=(LMPE-1)/5
      LMDE=IE*2+1
      MAXE=IARY(LOCMD+LMDE-1)
      W=WTF(K,J)
      GO TO 40
C
C   REGULAR DATA
C
   30 LMPE=PTM(K,J)
      IF(LMPE.LE.0) GO TO 50
      IE=(LMPE-1)/5
      LMDE=IE*2+1
      MAXE=IARY(LOCMD+LMDE-1)
      W=WTM(K,J)
C
   40 IF(MAXE.LE.MSNG) GO TO 50
      LMEANE=IARY(LMPE+1)
      MEANE=IARY(LOCMMX+LMEANE-1)
      Q=(MAXE+MEANMX-MEANE)*W
      SUMN=SUMN+Q
      SUMD=SUMD+W
      GO TO 100
   50 CONTINUE
  100 CONTINUE
C
      IF(SUMD.GT.5.0E-08) GO TO 120
C
C   MAX COULD NOT BE ESTIMATED - MUST BLEND
C
      IBLEND=1
      LOCP=(IPOS-1)*3+LOCPMX
      MAXP=IARY(LOCP)
      MEANP=IARY(LOCP+1)
      NDAYS=IARY(LOCP+2)
      IF(MAXP.LE.MSNG.OR.MAXP.EQ.MEANP) GO TO 110
      FACTOR=NDAYS*BLEND
      IF(FACTOR.GT.1.0) GO TO 110
      MAXT(ISTA)=MEANMX+(MAXP-MEANP)*(1.-FACTOR)
      KODE(1,ISTA)=B
      GO TO 200
C
C   MEAN IS USED
C
  110 MAXT(ISTA)=MEANMX
      KODE(1,ISTA)=M
      GO TO 200
C
C
  120 MAXT(ISTA)=SUMN/SUMD
      KODE(1,ISTA)=E
C
C   EITHER HAD DATA OR COULD ESTIMATE IT.
C
  150 IBTHIS=1
C
  200 IF(MINT(ISTA).GT.MSNG) GO TO 270
C
C   ESTIMATE MIN TEMP.
C
      IESTMN=1
      SUMD=0.
      SUMN=0.
      DO 240 J=1,4
      DO 230 K=1,NWTS
      IF(IFUT.NE.1) GO TO 210
C
C   FUTURE DATA
C
      LFPE=PTF(K,J)
      IF(LFPE.LE.0) GO TO 230
      LMPE=IARY(LOCFP+LFPE-1)
      IE=(LMPE-1)/5
      LMDE=IE*2+1
      MINE=IARY(LOCMD+LMDE)
      W=WTF(K,J)
      GO TO 220
C
C   REGULAR DATA
C
  210 LMPE=PTM(K,J)
      IF(LMPE.LE.0) GO TO 230
      IE=(LMPE-1)/5
      LMDE=IE*2+1
      MINE=IARY(LOCMD+LMDE)
      W=WTM(K,J)
  220 IF(MINE.LE.MSNG) GO TO 230
      LMEANE=IARY(LMPE+1)
      MEANE=IARY(LOCMMN+LMEANE-1)
      Q=(MINE+MEANMN-MEANE)*W
      SUMN=SUMN+Q
      SUMD=SUMD+W
      GO TO 240
  230 CONTINUE
  240 CONTINUE
C
      IF(SUMD.GT.5.0E-08) GO TO 260
C
C   MIN COULD NOT BE ESTIMATED - MUST BLEND
C
      IBLEND=1
      LOCP=(IPOS-1)*3+LOCPMN
      MINP=IARY(LOCP)
      MEANP=IARY(LOCP+1)
      NDAYS=IARY(LOCP+2)
      IF(MINP.LE.MSNG.OR.MINP.EQ.MEANP) GO TO 250
      FACTOR=NDAYS*BLEND
      IF(FACTOR.GT.1.0) GO TO 250
      MINT(ISTA)=MEANMN+(MINP-MEANP)*(1.-FACTOR)
      KODE(2,ISTA)=B
      GO TO 300
C
C   MEAN IS USED
C
  250 MINT(ISTA)=MEANMN
      KODE(2,ISTA)=M
      GO TO 300
C
  260 MINT(ISTA)=SUMN/SUMD
      KODE(2,ISTA)=E
C
C   EITHER HAD DATA OR COULD ESTIMATE IT.
C
  270 IBTHIS=1
C
C   MAX AND MIN ARE COMPLETE
C
  300 IF(LIP.EQ.0.OR.IFUT.EQ.1) GO TO 308
      IF(IESTMX.EQ.0) GO TO 304
      DO 302 N=1,NINST
      IF(IARY(LOCID+LID-2+N).LE.MSNG) GO TO 302
      IF(IARY(LOCID+LID-2+N).GT.MAXT(ISTA))
     1 MAXT(ISTA)=IARY(LOCID+LID-2+N)
  302 CONTINUE
  304 IF(IESTMN.EQ.0) GO TO 308
      DO 306 N=1,NINST
      IF(IARY(LOCID+LID-2+N).LE.MSNG) GO TO 306
      IF(IARY(LOCID+LID-2+N).LT.MINT(ISTA))
     1 MINT(ISTA)=IARY(LOCID+LID-2+N)
  306 CONTINUE
  308 IF(ISTA.LT.2) GO TO 350
C
      IF(IFUT.EQ.1) GO TO 310
C
C   REGULAR DAY
C
      IF(IPRMO.EQ.0) GO TO 350
      IF(IPRLST.EQ.1.AND.IDA.NE.LDACPD) GO TO 350
      GO TO 320
C
C   FUTURE DAY
C
  310 IF(IPRMF.EQ.0) GO TO 350
C
C   CALL DISPLAY
C
  320 CALL TDSPM(NUM,STAID,STATE,DESCR,MAXT,MINT,KODE,IFLAG,MSNG)
      IFLAG=0
C
C   CALCULATE 6 HOUR MEANS FOR THE MAX/MIN STATIONS.
C
  350 LOC6=LOC6M+(IPOS-1)*4
      LMINP=LOCPMN+(IPOS-1)*3
      MINP=IARY(LMINP)
      NDAYS=IARY(LMINP+2)
      IF(MINP.LE.MSNG.OR.NDAYS.NE.1) MINP=MINT(ISTA)
      MMP=LOCMD-LOCMP
      MIP=LOCID-LOCIP
C
cew added staid to tmeanm.f call
cew unalbe to understand the staid array so ...
      write(curr_staid,351) (ary(jj),jj=2,3)
 351  format(2a4)

      CALL TMEANM(MAXT(ISTA),MINT(ISTA),MINP,IARY(LOCMP),MMP,
     1 IARY(LOCIP),MIP,ARY(ISTDAT),PTI,WTI,IARY(LOC6),IPART,IFUT,
     2 MSNG,curr_staid)
C
C   UPDATE THE PREVIOUS DAYS ARRAY FOR THE STATION.
C
      CALL TUPDAT(MAXT(ISTA),MINT(ISTA),IT12,1,IARY,LOC,IFUT,
     1 KODE(1,ISTA),MSNG)
      IF(ISTA.EQ.2) ISTA=0
C
  790 IF(NSTA.GT.0) GO TO 800
      LOC=LOC+5
      IF(LOC.LE.MIARY) GO TO 800
      WRITE(IPR,620) MIARY
  620 FORMAT(1H0,10X,45H**ERROR** ATTEMPT TO EXCEED THE LENGTH OF THE,
     1 11H IARY ARRAY,I6)
      CALL ERROR
      IER=1
      GO TO 999
C
  800 CONTINUE
C
      IF(ISTA.GT.0) GO TO 805
C
C   NO STATIONS LEFT TO PRINT
C
      IF(IFUT.EQ.1.AND.IPRMF.EQ.0) GO TO 840
      GO TO 830
  805 IF(IFUT.EQ.1) GO TO 810
C
C   REGULAR DAY
C
      IF(IPRMO.EQ.0) GO TO 830
      IF(IPRLST.EQ.1.AND.IDA.NE.LDACPD) GO TO 830
      GO TO 820
C
C   FUTURE DAY
C
  810 IF(IPRMF.EQ.0) GO TO 840
  820 NUM(2)=MSNG
      CALL TDSPM(NUM,STAID,STATE,DESCR,MAXT,MINT,KODE,IFLAG,MSNG)
C
C
C   NO STATIONS LEFT TO PRINT
C
  830 IF(IBLEND.NE.1) GO TO 860
  835 WRITE(IPR,630) DAY(II),DAY(II+1),IMONTH,IDAY,IYEAR
  630 FORMAT(1H0,10X,42H**WARNING** A BLEND OR A MEAN WAS USED ON ,
     1 2A4,10H DATA DAY ,I2,1H/,I2,1H/,I4,1H.)
      CALL WARN
      GO TO 860
C
C
C   FUTURE DAY WITH NO PRINTOUT
C
  840 IF(IBLEND.NE.1.OR.IBLAST.EQ.0) GO TO 860
      IF(IBTHIS.EQ.1) GO TO 835
      WRITE(IPR,640) IMONTH,IDAY,IYEAR
  640 FORMAT(1H0,10X,43H**WARNING** A BLEND OR A MEAN WAS USED FOR ,
     1 47HALL STATIONS ON ALL FUTURE DATA DAYS BEGINNING ,
     2 I2,1H/,I2,1H/,I4,1H.)
      CALL WARN
C
  860 IBLAST=IBTHIS
C
      IF(.NOT.LBUG) GO TO 999
      WRITE(IOPDBG,940)
  940 FORMAT(1HO,25HAFTER TESTM, IARY AND ARY)
      WRITE(IOPDBG,920) (IARY(I),I=1,LAST)
      WRITE(IOPDBG,930) (ARY(I),I=ISTDAT,IUSE)
C
  999 CONTINUE
C
      IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      RETURN
      END
