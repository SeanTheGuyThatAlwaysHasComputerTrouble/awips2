C   MODULE TFUTUR
C
      SUBROUTINE TFUTUR(IARY,IARYTMP,MIARY,NTOTM,NMMMT,IMONTH,
     1 IDAY,IYEAR,IPART,IFUT,IBLAST,MSNG,NSTA,IORD)
C
C   THIS SUBROUTINE READS TEMPERATURE DATA, ESTIMATES MISSING
C   DATA, AND CALCULATES 6 HR MEANS FOR FUTURE TEMP. DAYS.
C
C   THIS SUBROUTINE WAS ORIGINALLY WRITTEN BY GERALD N DAY .
C
C   MODIFIED BY JAY BREIDENBACH 12/14/1998 TO INTERPOLATE MEAN DAILY 
C   TEMPERATURES FROM MONTHLY MEANS. 
C
      INTEGER* 2 IARY,IARYTMP,MSNG,IORD
C
      LOGICAL LBUG
C
      DIMENSION SBNAME(2),OLDOPN(2),IARY(1),IARYTMP(1),IORD(1)
C
      INCLUDE 'common/tloc'
      INCLUDE 'common/fctime'
      INCLUDE 'common/fctim2'
      INCLUDE 'common/pudbug'
      INCLUDE 'common/tary'
      INCLUDE 'common/where'
      INCLUDE 'common/ionum'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mat/RCS/tfutur.f,v $
     . $',                                                             '
     .$Id: tfutur.f,v 1.2 1999/01/20 14:03:48 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA TF24/4HTF24/,SBNAME/4HTFUT,4HUR  /,DCODE/4HTEMP/
C
      IF(IPTRCE.GE.1) WRITE(IOPDBG,900)
  900 FORMAT(1H0,18H** TFUTUR ENTERED.)
C
      LBUG=.FALSE.
      IF(IPBUG(DCODE).EQ.1) LBUG=.TRUE.
C
      IOLDOP=IOPNUM
      IOPNUM=-1
      DO 10 I=1,2
      OLDOPN(I)=OPNAME(I)
   10 OPNAME(I)=SBNAME(I)
C
      IER=0
      LIMIT=ISTDAT-1
C
      IF(IPART.EQ.1) GO TO 25
C
C   INITIALIZE MAX/MIN DATA AS MISSING.
C
      L2=LOCMMX-1
      DO 20 L=LOCMD,L2
      IARY(L)=MSNG
   20 CONTINUE
C
   25 IPTRS=1
      IF(IPART.EQ.-1) IPTRS=0
C
C   GET THE NUMBER OF DAYS IN THE CURRENT MONTH
C
      CALL DDGCDM(IYEAR,IMONTH,NODIM)
C
C   FOR EACH DAY OF THE MONTH WHERE A FUTURE VALUE MUST BE COMPUTED,
C   CALCULATE THE INTERPOLATED MONTHLY MEAN VALUE
C
      CALL RPPMT(IMONTH,NMMMT,IARY(LOCMMX),IARY(LOCMMN),NFILL,ISTAT)
C
C   INTERPOLATE DAILY MEAN VALUES BY ASSIGNING EACH MONTHS
C   MEAN VALUE TO THE MIDDLE OF THE MONTH AND LINEARLY INTERPOLATING
C   FROM THE MID MONTH (15TH) TO MID MONTH (15TH).  MID FEB=14TH
C
      MIDMON=NODIM/2
C     
      IF (IDAY.GT.MIDMON) THEN
        IMONTH1=IMONTH
        IMONTH2=IMONTH + 1
      ELSE    
        IMONTH1=IMONTH - 1
        IMONTH2=IMONTH
      ENDIF
C
C   CHECK FOR BEGINING AND END OF YEAR
C
      IF(IMONTH2.EQ.13) IMONTH2=1
      IF(IMONTH1.EQ.0)  IMONTH1=12
C       
C    HANDLE CASE WHERE WE ALREADY HAVE READ IN MONTH1 MEAN TEMPERATURES
C    AND NOW WE NEED TO READ IN MEAN TEMPERATURES FOR MONTH 2
C    THESE MEAN TEMPERATURES WILL BE READ INTO THE ARRAY IARYTMP WHICH IS
C    A TEMPORARY ARRAY WITH A STRUCTURE IDENTICAL TO IARY, EXCEPT THAT IT
C    WILL BE FILLED WITH 0'S AT ALL LOCATIONS THAT ARE NOT MEAN TEMPS.
C
      IF (IDAY.GT.MIDMON) THEN
      CALL RPPMT(IMONTH2,NMMMT,IARYTMP(LOCMMX),IARYTMP(LOCMMN),
     +           NFILL,ISTAT)
C
C   INTERPOLATE BETWEEN THIS MONTH AND NEXT MONTH
C      
      WF=REAL(NODIM-IDAY)/(REAL(NODIM)/2.0)
      LMX=LOCMMX+NFILL-1
      LMN=LOCMMN+NFILL-1
      DO 90 I=LOCMMX,LMX
      IARY(I)=IARY(I)*WF + ((IARYTMP(I)+IARY(I))/2)*(1-WF)
   90 CONTINUE
      DO 91 I=LOCMMN,LMN
      IARY(I)=IARY(I)*WF + ((IARYTMP(I)+IARY(I))/2)*(1-WF) 
   91 CONTINUE
C
      ELSE
C   
      CALL RPPMT(IMONTH1,NMMMT,IARYTMP(LOCMMX),IARYTMP(LOCMMN),
     +           NFILL,ISTAT)
C
C   INTERPOLATE BETWEEN LAST MONTH AND THIS MONTH
C
      WF=REAL(IDAY)/(REAL(NODIM)/2.0) 
      LMX=LOCMMX+NFILL-1
      LMN=LOCMMN+NFILL-1
      DO 92 I=LOCMMX,LMX
      IARY(I)=IARY(I)*WF + ((IARYTMP(I)+IARY(I))/2)*(1-WF)  
   92 CONTINUE
      DO 93 I=LOCMMN,LMN
      IARY(I)=IARY(I)*WF + ((IARYTMP(I)+IARY(I))/2)*(1-WF) 
   93 CONTINUE
C   
      ENDIF
C
      IF(.NOT.LBUG) GO TO 30
      WRITE(IOPDBG,910)
  910 FORMAT(1H0,5X,23HMEAN MAX AND MIN ARRAYS)
      WRITE(IOPDBG,920) (IARY(I),I=LOCMMX,LMX)
  920 FORMAT(5X,20I6)
      WRITE(IOPDBG,920) (IARY(I),I=LOCMMN,LMN)
   30 IF(ISTAT.EQ.0) GO TO 40
      IF(ISTAT.EQ.1) WRITE(IPR,610)
  610 FORMAT(1H0,10X,38H**ERROR** SYSTEM ERROR ACCESSING FILE.)
      IF(ISTAT.EQ.2) WRITE(IPR,620) IMONTH
  620 FORMAT(1H0,10X,33H**ERROR** INVALID VALUE FOR MONTH,I6)
      IF(ISTAT.EQ.3) WRITE(IPR,630) NMMMT,NFILL
  630 FORMAT(1H0,10X,43H**ERROR** MEAN MONTHLY ARRAYS ARE TOO SMALL,
     1 I6,6H. ONLY,I6,17H COULD BE FILLED.)
      CALL ERROR
      GO TO 999
C
C   READ FORECAST POINTER AND MAX/MIN DATA FOR FUTURE DAY
C
   40 LENP=LOCFD-LOCFP
      LEND=LAST-LOCFD+1
      IPPDRD=IDA+1
      CALL RPDDLY(TF24,IPPDRD,IPTRS,LENP,IARY(LOCFP),LPFILL,LEND,
     1 IARY(LOCFD),LDFILL,NSTAF,MSNG,LIMIT,ARY(1),ISTAT)
      IF(LBUG) CALL PDMPDY(TF24,IPPDRD,MSNG,IARY(LOCFP),
     1 LPFILL,IARY(LOCFD),LDFILL)
      IF(ISTAT.EQ.4) GO TO 300
      IF(ISTAT.EQ.0) GO TO 110
      CALL PSTRDD(IPPDRD,ISTAT,LENP,LEND,TF24)
      GO TO 999
C
C   COPY FUTURE DATA TO THE MAX/MIN ARRAY
C
  110 LOC=LOCFP
      DO 200 I=1,LPFILL
      IF(IARY(LOC).LE.0) GO TO 190
      LMP=IARY(LOC)
      IE=(LMP-1)/5
      LMD=IE*2+LOCMD
      LFD=(I-1)*2+LOCFD
      IARY(LMD)=IARY(LFD)
      IARY(LMD+1)=IARY(LFD+1)
  190 LOC=LOC+1
  200 CONTINUE
C
  300 CALL TESTM(IARY,MIARY,NTOTM,IMONTH,IDAY,IYEAR,IPART,IFUT,IBLAST,
     1 MSNG,NSTA,IORD,IER)
C
  999 CONTINUE
C
      IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      RETURN
      END
