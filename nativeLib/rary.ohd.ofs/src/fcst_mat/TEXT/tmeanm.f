C   MODULE TMEANM
C
      SUBROUTINE TMEANM(MAXT,MINT,MINTP,MXMNP,MMXMNP,INSTP,MINSTP,DV,
     1 PTI,WTI,MEAN6,IPART,IFUT,MSNG,curr_staid)
C
C   THIS SUBROUTINE CALCULATES THE 6 HR MEANS FOR THE MAX/MIN
C   STATION TEMPERATURE DATA.
C
C   THIS SUBROUTINE WAS ORIGINALLY WRITTEN BY W. GILREATH
C
C
C THIS SUBROUTINE COMPUTES THE 6 HOUR MEANS FOR MAX/MIN STATIONS FOR A
C FULL DAY. THE 6 HOUR MEANS ARE COMPUTED FROM THE WEIGHTED AVERAGE OF
C DIURNAL VARIATION VALUES AT SURROUNDING INSTANTANEOUS STATIONS. IF NO
C SURROUNDING INSTANTANEOUS STATIONS ARE FOUND THE 6 HOUR MEANS ARE
C COMPUTED FROM A FIXED DIURNAL VARIATION.
C
      INTEGER*2 INSTP,MAXT,MINT,MINTP,MTP,MEAN6,MXMNP,MSNG
      LOGICAL LBUG
      character*8 curr_staid
      DIMENSION DV(1),PTI(3,4),WTI(3,4),SDVWT(4),SBNAME(2),OLDOPN(2)
      DIMENSION INSTP(1),MEAN6(1),MXMNP(1),STAID(2,2)
      INCLUDE 'common/pudbug'
      INCLUDE 'common/ionum'
      INCLUDE 'common/where'
CFAN
C      INTEGER DIURNAL
C      REAL K1,K2,K3,K4,K5,K6,K7,K8,K9,K0(9,4)

C jgg-replaced the above line for DR 18651,allows 3 more sites-May,2007
C     note this change was also made in fun008.f, tiluvo.f, temp.f
      INTEGER DIURNAL
      REAL K1,K2,K3,K4,K5,K6,K7,K8,K9,K0(9,16)
C jgg
      COMMON/DIURNAL0/DIURNAL,K1,K2,K3,K4,K5,K6,K7,K8,K9,K0
CFAN
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob82/ohd/ofs/src/fcst_mat/RCS/tmeanm.f,v $
     . $',                                                             '
     .$Id: tmeanm.f,v 1.4 2005/03/17 18:44:26 xfan Exp jgofus $
     . $' /
C    ===================================================================
C
C
      DATA DCODE/4HTMNM/,SBNAME/4HTMEA,4HNM  /
      IF(IPTRCE.GE.1) WRITE(IOPDBG,900)
  900 FORMAT(1H0,18H** TMEANM ENTERED.)
      LBUG = .FALSE.
      IF(IPBUG(DCODE).EQ.1) LBUG = .TRUE.
C
CFAN
C Check DIURNAL and the selected coefficients
C
C jgg - DR 18651 
C      CALL FTEKCK (DIURNAL,8HDIURNAL ,0,DIURNAL,0,4)
      CALL FTEKCK (DIURNAL,8HDIURNAL ,0,DIURNAL,0,16)
C jgg
C 
      IF (ABS(K1+K2-1.0)    .GT. 0.01 .OR.
     &    ABS(K3+K4+K5-1.0) .GT. 0.01 .OR.
     &    ABS(K6+K7-1.0)    .GT. 0.01 .OR.
     &    ABS(K8+K9-1.0)    .GT. 0.01) THEN
        CALL WARN
        WRITE(IPR,530)
530   FORMAT(1H0,50H**WARNING** WRONG COEFFICIENTS, USE DEFAULT VALUES)
        K1=0.55
        K2=0.45
        K3=0.10
        K4=0.80
        K5=0.10
        K6=0.55
        K7=0.45
        K8=0.75
        K9=0.25
      WRITE(IPR,540)DIURNAL,K1,K2,K3,K4,K5,K7,K6,K9,K8
540   FORMAT(2H0 ,15HDIURNAL         ,I2,/
     *       2H0 ,45HCOEFFICIENTS   WN1   WX1   WN2a  WX2   WN2b  ,
     *            21HWX3   WN3   WX4   WN4/
     *       15X, 9F6.2)
      ENDIF
CFAN
     
C
      IOLDOP=IOPNUM
      IOPNUM=-1
      DO 5 I=1,2
      OLDOPN(I)=OPNAME(I)
    5 OPNAME(I)=SBNAME(I)
C
      IF(IFUT.EQ.0) GO TO 8
C
C   SET MTP FOR FUTURE DAY
C
      MTP=MINTP
C
C   FUTURE DAY - CHECK IF PARTIAL DAY ALSO
C
      IF(IPART.EQ.0.OR.IPART.EQ.-1) GO TO 300
C
C   IPART MUST BE ONE (FUTURE PORTION OF A PARTIAL DAY)
C
      GO TO 350
C
C   REGULAR DATA DAY
C
    8 CONTINUE
C
C   SET MTP FOR REGULAR DAY
C
      MTP=MINT
C
      DO 10 I = 1,4
   10 SDVWT(I) = 0.
      SUMWT = 0.
C
C THE FOLLOWING STATEMENTS (THRU DO LOOP 100) SELECT UP TO 4 SURROUNDING
C STATIONS THAT HAVE DIURNAL VARIATION AND WEIGHT VALUES. IF AVAILABLE,
C ONE STATION FROM 3 PREDETERMINED STATIONS IN EACH OF 4 QUADRANTS IS
C SELECTED. EACH STATION DIURNAL VARIATION IS MULTIPLIED BY THE STATION
C WEIGHT AND SUMMED FOR THE SURROUNDING STATIONS FOR EACH 6 HOUR TIME
C PERIOD IN A FULL DAY. ALSO THE STATION WEIGHTS ARE SUMMED.
C
      DO 100 J = 1,4
      DO 50 I = 1,3
C
C PTI(I,J) POINTS TO INSTANTANEOUS TEMPERATURE POINTER INFORMATION
      ILOC = PTI(I,J)
      IF(ILOC.LE.0) GO TO 50
C
C INSTP(ILOC) IS INSTANTANEOUS TEMPERATURE POINTER LOCATION THAT POINTS
C TO MAX/MIN POINTER INFORMATION
C
      MMLOC = INSTP(ILOC)
C
C MXMNP(MMLOC +2) IS MAX/MIN POINTER THAT POINTS TO INSTANTANEOUS
C TEMPERATURE POINTER INFORMATION. IF IT IS LESS THAN OR EQUAL TO ZERO
C NO DIURNAL VALUES ARE AVAILABLE.
C
      IF(MXMNP(MMLOC+2).LE.0) GO TO 50
C
C CALCULATE THE LOCATION OF THE DIURNAL VARIATIONS
C
      ILOCDV = (ILOC-1)/3*4+1
C
      DO 20 K = 1,4
   20 SDVWT(K) = SDVWT(K) + DV(ILOCDV+K-1)*WTI(I,J)
      SUMWT=SUMWT+WTI(I,J)
      IF(LBUG)WRITE(IOPDBG,920) I,J,WTI(I,J),(DV(ILOCDV+K-1),K=1,4)
  920 FORMAT(1H0,'  WTI(',I1,',',I1,') =',F6.2,'  DV(12Z-18Z) THRU DV(6Z
     1-12Z) =',4F6.2)
      GO TO 100
   50 CONTINUE
  100 CONTINUE
C
C IF NO SURROUNDING STATIONS ARE FOUND WITH DV AND WT VALUES, DEFAULT
C 6 HOUR MEANS ARE COMPUTED STARTING WITH STATEMENT 300.
C
      IF(SUMWT.LE.5.0E-08) GO TO 300
cew patch to keep the mat values from going negative
      do 199 i=1,4
       if(abs(SDVWT(I)).gt.abs(SUMWT)) then
c       if(SDVWT(I).gt.SUMWT) then
        write(ipr,'(A,A)') '*** WARNING  *** PROBLEM WITH WEIGHTS FOR TI
     +ME DISTRIBUTION, USING DEFAULTS FOR STATION ', curr_staid
        goto 300
       endif
  199 continue

      DO 200 I = 1,4
  200 MEAN6(I) = MINT + (SDVWT(I)/SUMWT)*(MAXT-MINT)
      IF(LBUG) WRITE(IOPDBG,930) (MEAN6(I),I=1,4),MAXT,MINT,
     1(SDVWT(K),K=1,4),SUMWT
  930 FORMAT(1H ,'  6 HR MEANS =',4I6,'  MAXT =',I6,'  MINT =',I6,
     1'  SDVWT"S =',4F6.2,'  SUMWT =',F6.2)
      GO TO 400
C
C DEFAULT 6 HR MEAN FOR 12Z TO 18Z
C
CFAN  300 MEAN6(1) = .55*MTP + .45*MAXT
  300 MEAN6(1) = K1*MTP + K2*MAXT
C
C DEFAULT 6 HR MEAN FOR 18Z TO 00Z
C
CFAN      MEAN6(2) = .1*MTP+.8*MAXT+.1*MINT
      MEAN6(2) = K3*MTP+K4*MAXT+K5*MINT
C
C DEFAULT 6 HR MEAN FOR 00Z TO 06Z
C
CFAN      MEAN6(3) = .55*MINT+.45*MAXT
      MEAN6(3) = K6*MINT+K7*MAXT
C
C DEFAULT 6 HR MEAN FOR 06Z TO 12Z
C
CFAN      MEAN6(4) = .75*MINT + .25*MAXT
      MEAN6(4) = K8*MINT + K9*MAXT
C
      GO TO 380
C
C   FUTURE PORTION OF A PARTIAL DAY CHECK IF MEAN IS MSNG
C
  350 IF(MEAN6(1).NE.MSNG) GO TO 355
CFAN      MEAN6(1)=.55*MTP+.45*MAXT
      MEAN6(1)=K1*MTP+K2*MAXT
  355 IF(MEAN6(2).NE.MSNG) GO TO 360
CFAN      MEAN6(2)=.1*MTP+.8*MAXT+.1*MINT
      MEAN6(2)=K3*MTP+K4*MAXT+K5*MINT
  360 IF(MEAN6(3).NE.MSNG) GO TO 365
CFAN      MEAN6(3)=.55*MINT+.45*MAXT
      MEAN6(3)=K6*MINT+K7*MAXT
  365 IF(MEAN6(4).NE.MSNG) GO TO 380
CFAN      MEAN6(4)=.75*MINT+.25*MAXT
      MEAN6(4)=K8*MINT+K9*MAXT
C
  380 IF(LBUG)WRITE(IOPDBG,940)(MEAN6(I),I=1,4),MAXT,MINT,MINTP
  940 FORMAT(1H ,'  DEFAULT 6 HR MEANS =',4I6,'  MAXT =',I6,'  MINT =',
     1I6,'  MINTP =',I6)
C
  400 IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      RETURN
      END
