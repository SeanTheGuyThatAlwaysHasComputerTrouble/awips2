C MODULE PDLRRS
C-----------------------------------------------------------------------
C
      SUBROUTINE PDLRRS (ISTAFL,LWBUFF,WBUFF)
C
C  THIS ROUTINE DELETES RRS DATA VALUES BY TYPE AND STATION
C  FROM THE PPDB.  THE DATE IS CONVERTED TO JULIAN HOUR AND SET
C  TO NEGATIVE.  THE WRITE ROUTINE WPDRRS CHECK FOR THIS NEGATIVE
C  HOUR AND DELETES THE VALUES.
C
C  ARGUMENT LIST:
C
C     NAME    TYPE   I/O   DIM   DESCRIPTION
C     ----    ----   ---   ---   -----------
C     ISTAFL    I     I     1    STATION IDENTIFIER TYPE INDICATOR:
C                                  0=STATION IDENTIFIER
C                                  1=STATION NUMBER
C
      CHARACTER*4 DTYPE,UNITS
      CHARACTER*8 XCHAR,STAID
      PARAMETER (LMINS=1)
      DIMENSION MINS(LMINS)
      PARAMETER (LOBS=3)
      REAL OBS(LOBS)
      INTEGER WBUFF(LWBUFF)
C
      INCLUDE 'uio'
      INCLUDE 'udebug'
      INCLUDE 'udatas'
      INCLUDE 'ufreei'
      INCLUDE 'hclcommon/hdflts'
      INCLUDE 'pdbcommon/pdrrsc'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppdutil/RCS/pdlrrs.f,v $
     . $',                                                             '
     .$Id: pdlrrs.f,v 1.3 2000/12/18 22:21:15 dws Exp $
     . $' /
C    ===================================================================
C
C
      IF (IPDTR.GT.0) WRITE (IOGDB,10)
10    FORMAT (' *** ENTER PDLRRS')
C
C  SET REVISION FLAG FUTURE WRITE FLAG
      IREV=1
      IFUT=0
C
C  READ CARD
      CALL RWCARD(ISTAT)
      IF (ISTAT.NE.0) GO TO 220
      NFLD=1
      NCHAR=IFSTOP(NFLD)-IFSTRT(NFLD)+1
      IF (NCHAR.NE.5) THEN
         WRITE (LP,20)
20    FORMAT ('0**ERROR** INPUT CARD IS NOT IDENTIFIER ''STAID''')
         GO TO 280
         ENDIF
      XCHAR=' '
      CALL UPACK1 (IBUF(IFSTRT(NFLD)),XCHAR,NCHAR)
C
30    IF (XCHAR.NE.'STAID') THEN
         WRITE (LP,20)
         GO TO 280
         ENDIF
C
C  GET STATION ID
      NFLD=NFLD+1
      STAID=' '
      IF (ISTAFL.EQ.0) THEN
         NCHAR=IFSTOP(NFLD)-IFSTRT(NFLD)+1
         IF (NCHAR.GT.LEN(STAID)) NCHAR=LEN(STAID)
         CALL UPACK1 (IBUF(IFSTRT(NFLD)),STAID,NCHAR)
         GO TO 50
         ENDIF
      IF (IFTYPE(NFLD).NE.1) THEN
         WRITE (LP,40) NFLD
40    FORMAT ('0**ERROR** FIELD ',I2,' IS NOT AN INTEGER')
         GO TO 130
         ENDIF
      CALL UNUMIC (IBUF,IFSTRT(NFLD),IFSTOP(NFLD),STAID)
C
C  GET TYPE CODE
50    NFLD=NFLD+1
      NCHAR=IFSTOP(NFLD)-IFSTRT(NFLD)+1
      IF (NCHAR.GT.4) NCHAR=4
      CALL UPACK1 (IBUF(IFSTRT(NFLD)),DTYPE,NCHAR)
C
C  CHECK IF VALID TYPE
      IRX=IPDCKR(DTYPE)
      IF (IRX.EQ.0) THEN
         WRITE (LP,60) DTYPE
60    FORMAT ('0**ERROR** INVALID RRS DATA TYPE ',A4)
         GO TO 280
         ENDIF
C
C  GET DATE
      CALL RWCARD(ISTAT)
      IF (ISTAT.NE.0) GO TO 240
      NFLD=1
C
C  CONVERT DATE (CHANGE TO NEGATIVE FOR DELETE)
70    CALL HDATEC (IFSTRT(NFLD),IFSTOP(NFLD),0,0,1,JULST,INTHR,JULHR,
     *   ISTAT)
      IF (ISTAT.NE.0) THEN
         WRITE (LP,80)
80    FORMAT ('0**ERROR** INVALID DATE CARD.')
         GO TO 130
         ENDIF
      JULHR=JULHR+NHOPDB
      IF (IPDDB.GT.0) WRITE (IOGDB,90) JULHR
90    FORMAT (' JULHR=',I7)
      JULHR=-JULHR
      CALL UMEMOV (JULHR,OBS(1),1)
      NVLPOB=2
      IF (NFIELD.EQ.2) THEN
         NFLD=NFLD+1
         IF (IFTYPE(NFLD).NE.1) THEN
            WRITE (LP,40) NFLD
            GO TO 280
            ENDIF
         CALL UNUMIC (IBUF,IFSTRT(NFLD),IFSTOP(NFLD),IPER)
         NVLPOB=3
         CALL UMEMOV (IPER,OBS(3),1)
         ENDIF
C
C  GET METRIC UNITS CODE FOR DATA TYPE
      IUNFLG=1
      CALL PFDUNT (DTYPE,1,UNITS)
C
C  DELETE RRS DATA
      NTYPES=1
      NUMOBS=1
      MINS(LMINS)=0
      CALL WPDRRS (STAID,ISTAFL,NTYPES,DTYPE,NVLPOB,UNITS,NUMOBS,
     *   LOBS,OBS,LMINS,MINS,LWBUFF,WBUFF,
     *   IWRITE,IFUT,LSTHR,IREV,ISTAT)
      IF (ISTAT.EQ.0) GO TO 100
      IF (ISTAT.EQ.11) GO TO 200
      IF (ISTAT.GT.7) GO TO 220
      GO TO (140,160,180,220,160,220,160),ISTAT
100   IF (ISTAFL.EQ.0) THEN
         WRITE (LP,110) DTYPE,STAID
110   FORMAT ('0**NOTE** ',A,' DATA VALUE FOR STATION ',A,
     *   ' SUCCESSFULLY DELETED.')
         GO TO 130
         ENDIF
      WRITE (LP,120) DTYPE,STAID
120   FORMAT ('0**NOTE** ',A,' DATA VALUE FOR STATION ',I6,
     *   ' SUCCESSFULLY DELETED.')
C
C  CHECK IF ANOTHER TYPE OR ANOTHER STATION FOR DELETE
130   CALL RWCARD(ISTAT)
      IF (ISTAT.NE.0) GO TO 240
      NFLD=1
      IF (IFTYPE(NFLD).EQ.1) GO TO 70
      NCHAR=IFSTOP(NFLD)-IFSTRT(NFLD)+1
      IF (NCHAR.GT.LEN(XCHAR)) GO TO 70
      XCHAR=' '
      CALL UPACK1 (IBUF(IFSTRT(NFLD)),XCHAR,NCHAR)
      IF (XCHAR.EQ.'END') GO TO 260
      IF (XCHAR.EQ.'STAID') GO TO 30
      GO TO 70
C
140   WRITE (LP,150)
150   FORMAT ('0**ERROR** STATION NOT FOUND.')
      GO TO 130
160   WRITE (LP,170) ISTAT
170   FORMAT ('0**ERROR** DATA TYPE NOT FOUND. ISTAT=',I2)
      GO TO 130
180   WRITE (LP,190)
190   FORMAT ('0**ERROR** TIME PERIOD INVALID.')
      GO TO 130
200   WRITE (LP,210)
210   FORMAT ('0**WARNING** NO DATA FOUND FOR SPECIFIED DATE.')
      GO TO 130
220   WRITE (LP,230) ISTAT,IWRITE
230   FORMAT ('0**ERROR** IN PDLRRS - UNEXPECTED ERROR IN WPDRRS. ',
     *   'ISTAT=',I2,' IWRITE=',I2)
      GO TO 130
240   WRITE (LP,250)
250   FORMAT ('0**ERROR** MISSING END CARD.')
      GO TO 260
C
260   IF (IPDTR.GT.0) WRITE (IOGDB,270)
270   FORMAT (' *** EXIT PDLRRS')
C
280   CONTINUE
C
      RETURN
C
      END
