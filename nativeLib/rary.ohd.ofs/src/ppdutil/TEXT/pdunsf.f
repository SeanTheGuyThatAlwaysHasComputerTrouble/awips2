C MEMBER PDUNSF
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 06/10/94.15:23:29 BY $WC20SV
C
C @PROCESS LVL(77)
C
      SUBROUTINE PDUNSF
C
C  THIS ROUTINE SETS THE LAST FREEPOOL RECORD FOR A DATA TYPE FOR
C  A STATION TO USED IF IT IS NOW MARKED UNUSED
C
C
      CHARACTER*72 XSTRNG
C
      DIMENSION IRBUF(16),ISTAID(2)
C
      INCLUDE 'uio'
      INCLUDE 'udebug'
      INCLUDE 'ufreei'
      INCLUDE 'pdbcommon/pdunts'
      INCLUDE 'pdbcommon/pdrrsc'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppdutil/RCS/pdunsf.f,v $
     . $',                                                             '
     .$Id: pdunsf.f,v 1.1 1995/09/17 19:09:37 dws Exp $
     . $' /
C    ===================================================================
C
C
C
      LRECL=16
C
C  READ CARD
10    CALL RPCARD (IBUF,IERR)
      CALL WPCARD (IBUF)
C
C  FIND FIELDS ON CARD
      CALL UFREE (1,72)
C
C  CHECK IF NO FIELDS ON CARD
      IF (NFIELD.EQ.0) GO TO 10
C
C  GET FIRST FIELD
      XSTRNG=' '
      NFLD=1
      NCHAR=IFSTOP(NFLD)-IFSTRT(NFLD)+1
      IF (NCHAR.GT.LEN(XSTRNG)) NCHAR=LEN(XSTRNG)
      CALL UPACK1 (IBUF(IFSTRT(NFLD)),XSTRNG,NCHAR)
      IF (IPDDB.GT.0) THEN
         CALL ULINE (LPD,1)
         WRITE (LPD,*) 'XSTRNG=',XSTRNG
         ENDIF
C
      IF (XSTRNG.EQ.'END') GO TO 130
C
      CALL SUBSTR (XSTRNG,1,8,ISTAID,-1)
      IF (NCHAR.GT.8) THEN
         CALL SUBSTR (XSTRNG,9,4,IDTYPE,-1)
         ELSE
            NFLD=2
            NCHAR=IFSTOP(NFLD)-IFSTRT(NFLD)+1
            CALL UPACK1 (IBUF(IFSTRT(NFLD)),IDTYPE,NCHAR)
         ENDIF
C
      IF (IPDDB.GT.0) THEN
         CALL ULINE (LPD,1)
         WRITE (LPD,'(1X,A,2A4)') 'ISTAID=',ISTAID
         WRITE (LPD,'(1X,A,2A4)') 'IDTYPE=',IDTYPE
         ENDIF
C
C  READ PRIMARY FILE CONTROL RECORD
      IREC=1
      CALL UREADT (KPDRRS,IREC,IRBUF,IERR)
      IF (IERR.GT.0) GO TO 100
      LASTRC=IRBUF(2)-1
      IF (LASTRC.GT.2) GO TO 20
         WRITE (LP,140)
         GO TO 130
C
C  READ PRIMARY FILE READING HEADERS
20    IREC=2
30    CALL UREADT (KPDRRS,IREC,IRBUF,IERR)
      IF (IERR.GT.0) GO TO 100
C
C  CHECK IF AT END OF PRIMARY RRS FILE
      LPRIM=IRBUF(1)
      IF (LPRIM.GT.0) GO TO 40
      WRITE (LPE,150) IDTYPE,ISTAID
      GO TO 10
C
40    NXREC=IREC+1+(LPRIM-1)/LRECL
C
C  CHECK STATION IDENTIFIER AND DATA TYPE
      IF (IRBUF(2).EQ.ISTAID(1).AND.IRBUF(3).EQ.ISTAID(2).AND.
     *   IRBUF(5).EQ.IDTYPE) GO TO 50
         IREC=NXREC
         IF (IREC.LE.LASTRC) GO TO 30
            WRITE (LPE,150) IDTYPE,ISTAID
            GO TO 10
C
C  CHECK IF FREEPOOL RECORDS EXISTS
50    IFREC=IRBUF(13)
      IF (IFREC.GT.0) GO TO 60
      WRITE (LPE,160) IDTYPE,ISTAID
      GO TO 10
C
C  READ FREEPOOL RECORDS TO LAST CHAINED RECORD
60    KUNIT=KPDDDF(LUFREE)
70    CALL UREADT (KUNIT,IFREC,IRBUF,IERR)
      IF (IERR.GT.0) GO TO 110
      NEXTFP=IRBUF(1)
      IF (IPDDB.GT.0) WRITE (LP,*) 'IFREC=',IFREC,
     *   ' NEXTFP=',NEXTFP
      IF (NEXTFP.NE.0) GO TO 80
C
C  FREEPOOL RECORD MARKED USED AT END OF CHAIN
      WRITE (LPE,170) IDTYPE,ISTAID
      GO TO 10
C
80    IF (NEXTFP.LT.0) GO TO 90
C
C  SET NEXT RECORD NUMBER IN FREEPOOL CHAIN
      IFREC=NEXTFP
      GO TO 70
C
C  FOUND RECORD MARKED NOT IN USE AT END OF FREEPOOL CHAIN
90    IRBUF(1)=0
      CALL UWRITT (KUNIT,IFREC,IRBUF,IERR)
      IF (IERR.GT.0) GO TO 120
      WRITE (LP,180) IFREC,IDTYPE,ISTAID
      GO TO 10
C
100   WRITE (LPE,190) 'READING PRIMARY',IREC
      GO TO 130
110   WRITE (LPE,190) 'READING FREEPOOL',IFREC
      GO TO 130
120   WRITE (LPE,190) 'WRITING FREEPOOL',IFREC
      GO TO 130
C
130   RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
140   FORMAT ('0**WARNING** NO RRS STATIONS ARE DEFINED.')
150   FORMAT ('0**WARNING** NO RRS DATA RECORDS FOUND ',
     *   'FOR DATA TYPE ',A4,' FOR STATION ',2A4,'.')
160   FORMAT ('0**NOTE** NO FREEPOOL RECORDS FOUND ',
     *   'FOR DATA TYPE ',A4,' FOR STATION ',2A4,'.')
170   FORMAT ('0**NOTE** FREEPOOL CHAIN DOES NOT END WITH A RECORD ',
     *   'MARKED IN USE ',
     *   'FOR DATA TYPE ',A4,' FOR STATION ',2A4,'.')
180   FORMAT ('0**NOTE** LAST CHAINED FREEPOOL RECORD (',I7,
     *   ') HAS BEEN MARKED IN USE ',
     *   'FOR DATA TYPE ',A4,' FOR STATION ',2A4,'.')
190   FORMAT ('0**ERROR** ',A,' RECORD ',I7,'.')
C
      END
