C MEMBER PUC33
C  (from old member FCPUC33)
C  VERSION 1
C
      SUBROUTINE PUC33 (PO,CO)
C
C***********************************************************************
C
C  THIS SUBROUTINE PUNCHES CARDS WHICH CAN BE READ IN THE PIN ROUTINE
C  TO DEFINE OR RE-DEFINE A SEGMENT OR SEGMENTS FOR THE API-CIN
C  OPERATION.
C
C  PARAMETRIC DATA IS THAT WHICH IS CURRENTLY IN THE P ARRAY.  CARRYOVER
C  DATA MAY BE DEFAULTED OR THAT WHICH IS CURRENTLY IN THE C ARRAY.
C
C***********************************************************************
C  SUBROUTINE INITIALLY WRITTEN BY
C        TIM SWEENEY - OHRFC     NOVEMBER 1984
C***********************************************************************
C  PRINCIPAL VARIABLES...
C
C  FOR DEFINITION OF VARIABLES IN COMMON BLOCKS, SEE SECTION IX.3.3C
C  OF THE NWSRFS USER'S MANUAL.
C
C     AI             CURRENT AI VALUE
C     AIADJ          AI ADJUSTMENT FACTOR
C     AICO           STORM AI VALUE
C     API            CURRENT API VALUE
C     CO(1)          INPUT CARRYOVER DATA FROM C ARRAY
C     IDELT          DELTA-T OF TIME SERIES FOR RAINFALL/MELT AND RUNOFF
C     IFDEB          DEBUG OUTPUT SWITCH, 0 = OFF, 1 = ON
C     INCOFL         READ/NO READ CARRYOVER FLAG, 0 = NO READ, 1 = READ
C     IOUTFL         API/AI OUTPUT TIME SERIES FLAG,
C                      0 = NO READ, 1 = READ
C     NEWSTM         STORM PERIOD COUNTER
C     NOP            NUMBER OF OPERATION ASSIGNED TO THIS OPERATION
C     NOZON          RUNOFF ZONE NUMBER
C     PO(1)          INPUT PARAMETRIC DATA FROM P ARRAY
C     RAINCO         STORM RAINFALL VALUE
C     ROCO           STORM RUNOFF VALUE
C     SUBNAM(2)      SUBROUTINE NAME
C     WE             CURRENT WATER EQUIVALENT VALUE
C
C***********************************************************************
C
C
      DIMENSION PO(1),CO(1),SUBNAM(2)
C
      COMMON/IONUM/ IN,IPR,IPU
      COMMON/FDBUG/ IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/PUDFLT/ IPDFLT
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_puc/RCS/puc33.f,v $
     . $',                                                             '
     .$Id: puc33.f,v 1.1 1995/09/17 18:50:53 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SUBNAM/4hPUC3,4h3   /,NOP/33/
      DATA EMPTY/4h    /
C
C
C  CALL DEBUG CHECK ROUTINE.
C
      CALL FPRBUG (SUBNAM,1,NOP,IFDEB)
C
C  PUNCH FIRST CARD WITH RUNOFF ZONE NAME, ZONE NUMBER, ALTERNATE
C  ZONE NUMBER, LATITUDE, LONGITUDE, AI ADJUSTMENT FACTOR,
C  SYNTHETIC TEMP ADJUSTMENT FACTOR, PRECIP REC FACTOR, API
C  RECESSION FACTOR.
C  AND SECOND CARD WITH API RECESSION FACTOR WITH SNOW, WE
C  CRITERIA, OUTPUT TIME SERIES FLAG AND CARRYOVER READ/NOREAD
C  FLAG.
C
C  SET CARRYOVER READ/NO READ FLAG DEPENDING ON VALUE OF IPDFLT.
C  IF IPDFLT = 1, DEFAULT C/O WILL BE USED.  IF IPDFLT = 0, ACTUAL
C  C/O WILL BE PUNCHED.
C
      NOZON=PO(8)
      NALZN=PO(9)
      LAT=PO(10)
      LON=PO(11)
      AIADJ=PO(12)
      TCADJ=PO(26)
      PRECF=PO(27)
      ARECF=PO(28)
      ASRF=PO(29)
      WERCR=PO(30)
      IF(IPDFLT) 102,102,101
  101 INCOFL=0
      GO TO 103
  102 INCOFL=1
C
  103 IOUTFL=0
      IF(PO(34)-EMPTY) 105,104,105
  104 IF(PO(37)-EMPTY) 105,106,105
  105 IOUTFL=1
  106 WRITE(IPU,11) (PO(I),I=2,7),NOZON,NALZN,LAT,LON,AIADJ,
     1              TCADJ,PRECF,ARECF,ASRF,WERCR,IOUTFL,INCOFL
C
C  PUNCH THIRD CARD WITH MANDATORY TIME SERIES INFORMATION.
C
      IDELT=PO(13)
      WRITE(IPU,12) IDELT,(PO(I),I=14,25)
C
C  PUNCH OPTIONAL FOURTH CARD WITH API/AI TIME
C  SERIES INFORMATION.
C
      IF(IOUTFL) 108,108,107
  107 WRITE(IPU,13) (PO(I),I=32,37)
C
C  IF CARRYOVER VALUES ARE TO BE DEFAULTED, RETURN.
C
  108 IF(INCOFL) 9999,9999,109
C
C  IF CARRYOVER VALUES ARE TO BE CURRENT VALUES FROM C ARRAY, PUNCH
C  FIFTH CARD WITH CURRENT CARRYOVER VALUES.
C
  109 NEWSTM=CO(1)
      RAINCO=CO(2)
      AICO=CO(3)
      ROCO=CO(4)
      API=CO(5)
      AI=CO(6)
      WE=CO(7)
      AVGT=CO(9)
      AT=CO(10)
      TC=CO(11)
      WRITE(IPU,14) NEWSTM,RAINCO,AICO,ROCO,API,AI,WE,AVGT,AT,TC
C
C
 9999 RETURN
C
C
   11 FORMAT(6A4,I6,3I5,2F5.1,2F5.2,/,2F5.2,2I5)
   12 FORMAT(I5,4(2X,2A4,1X,A4))
   13 FORMAT(5X,2(2X,2A4,1X,A4))
   14 FORMAT(I5,F5.2,F5.1,2F5.2,F5.1,F5.2,3F5.1)
      END
