C MEMBER PUC35
C  (from old member FCPUC35)
C  VERSION 1
C
      SUBROUTINE PUC35(PO,CO)
C
C#######################################################################
C
C  THIS SUBROUTINE PUNCHES CARDS WHICH CAN BE READ INTO THE PIN ROUTINE
C  TO DEFINE OR RE-DEFINE A SEGMENT OR SEGMENTS FOR THE API - HAR
C  OPERATION.
C
C#######################################################################
C
C  PARAMETRIC DATA IS OBTAINED FROM THE PO ARRAY PASSED INTO THIS
C  ROUTINE.  CARRYOVER DATA IS OBTAINED FROM THE CO ARRAY.  IF DEFAULT
C  CARRYOVER VALUES ARE REQUESTED, CARRYOVER IS NOT PUNCHED.
C
C#######################################################################
C
C
C  THE ORDER OF THE PUNCHED PARAMETRIC DATA IS AS FOLLOWS:
C
C     CARD 1:  RID, RNAME, IRNUM, RLAT, RLNG
C     CARD 2:  RFCTR, R24, PMAX, ULIMW, BLIMW, ULIMS, BLIMS
C     CARD 3:  KURV, IDELTA, NSW, IOFAAA, ICOF
C     CARD 4:  TSIDRM, DTCRM, TSIDPE, DTCPE, TSIDRO, DTCRO
C     CARD 5:  TSIDFI, DTCFI, TSIDAP, DTCAP, TSIDAE, DTCAE
C
C     NOTE:  CARD 5 AS LISTED ABOVE IS OPTIONAL, DEPENDING ON THE
C            VALUE OF IOFAAA.  IF IOFAAA = 0, THIS CARD
C            IS NOT PUNCHED.
C
C
C  THE ORDER OF THE PUNCHED CARRYOVER DATA IS AS FOLLOWS:
C
C     CARD 6 (OR 5):  TAPI, TAEI, TFI, SAPI, SAEI, SFI, SRAIM, SRO,
C                     DRAIM, DRO
C     CARD 7 - 8 (OR 6 - 7):  RNSP(I),I=1,24
C
C  FOR DEFINITION OF THESE VARIABLES, SEE THE PIN35 ROUTINE.
C
C#######################################################################
C
      DIMENSION PO(1),CO(1),SUBNAM(2),RID(2),RNAME(5),RNSP(24)
      COMMON /IONUM/ IN,IPR,IPU
      COMMON /FDBUG/ IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON /PUDFLT/ IPDFLT
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_puc/RCS/puc35.f,v $
     . $',                                                             '
     .$Id: puc35.f,v 1.1 1995/09/17 18:50:55 dws Exp $
     . $' /
C    ===================================================================
C
      DATA SUBNAM /4hPUC3,4h5   /,NOP/35/,EMPTY/4h    /
C
C  CALL DEBUG CHECK ROUTINE.
C
      CALL FPRBUG(SUBNAM,1,NOP,IFDEB)
C
C  EXTRACT NEEDED INFO FROM THE PO ARRAY.
C
      RID(1)=PO(2)
      RID(2)=PO(3)
      DO 100 I=1,5
100   RNAME(I)=PO(I+3)
      IRNUM=PO(9)
      RLAT=PO(10)
      RLNG=PO(11)
      RFCTR=PO(12)
      R24=PO(13)
      PMAX=PO(14)
      ULIMW=PO(15)
      BLIMW=PO(16)
      ULIMS=PO(17)
      BLIMS=PO(18)
      KURV=PO(19)
      IDELTA=PO(20)
      NSW=PO(21)
      NSPER=PO(22)
C
C  SET CARRYOVER READ/NO READ FLAG DEPENDING ON VALUE OF IPDFLT.
C  IF IPDFLT = 1, DEFAULT CARRYOVER WILL BE USED.  IF IPDFLT = 0,
C  ACTUAL CARRYOVER WILL BE PUNCHED.
C
      IF(IPDFLT)120,120,110
110   ICOF=0
      GO TO 130
120   ICOF=1
130   IOFAAA=0
      IF(PO(35)-EMPTY)170,140,170
140   IF(PO(38)-EMPTY)170,150,170
150   IF(PO(41)-EMPTY)170,180,170
170   IOFAAA=1
C
C  PUNCH OUT FIRST 3 CARDS OF PARAMETRIC DATA.
C
180   WRITE(IPU,1000)RID,RNAME,IRNUM,RLAT,RLNG
      WRITE(IPU,1010)RFCTR,R24,PMAX,ULIMW,BLIMW,ULIMS,BLIMS
      WRITE(IPU,1020)KURV,IDELTA,NSW,IOFAAA,ICOF
C
C  PUNCH OUT 4TH CARD WITH INFO ON THE MANDATORY TIME SERIES NEEDED
C  BY THE API - HAR OPERATION.
C
      WRITE(IPU,1030)(PO(25+I),I=1,9)
C
C  PUNCH OUT 5TH CARD WITH INFO ON THE OPTIONAL TIME SERIES, IF NEEDED.
C
      IF(IOFAAA)200,200,190
190   WRITE(IPU,1030)(PO(34+I),I=1,9)
C
C  IF CARRYOVER VALUES ARE TO BE DEFAULTED, RETURN.
C
200   IF(ICOF)9999,9999,210
C
C  IF CARRYOVER VALUES ARE TO BE CURRENT VALUES FROM CO ARRAY,
C  PUNCH NEXT 2 (OR 3) CARDS WITH CURRENT CARRYOVER VALUES.
C
210   TAPI=CO(1)
      TAEI=CO(2)
      TFI=CO(3)
      SAPI=CO(4)
      SAEI=CO(5)
      SFI=CO(6)
      SRAIM=CO(7)
      SRO=CO(8)
      DRAIM=CO(9)
      DRO=CO(10)
      DO 220 I=1,NSPER
220   RNSP(I)=CO(10+I)
      WRITE(IPU,1240)TAPI,TAEI,TFI,SAPI,SAEI,SFI,SRAIM,SRO,DRAIM,DRO
      WRITE(IPU,1250)(RNSP(I),I=1,NSPER)
9999  RETURN
1000  FORMAT(2A4,6X,5A4,5X,I4,6X,F5.2,5X,F5.2)
1010  FORMAT(3F5.3,4F5.2)
1020  FORMAT(5I5)
1030  FORMAT(2A4,3X,A4,4X,2A4,3X,A4,5X,2A4,3X,A4)
1240  FORMAT(10F5.2)
1250  FORMAT(12F5.2,/,12F5.2)
      END
