C MEMBER PUC29
C  (from old member FCPUC29)
C
      SUBROUTINE PUC29 (PO,CO)
C                             LAST UPDATE: 02/08/94.09:17:04 BY $WC20SV
C
C
C***********************************************************************
C
C  THIS SUBROUTINE PUNCHES CARDS WHICH CAN BE READ IN THE PIN ROUTINE
C  TO DEFINE OR RE-DEFINE A SEGMENT OR SEGMENTS FOR THE API-MKC
C  OPERATION.
C
C  PARAMETRIC DATA IS THAT WHICH IS CURRENTLY IN THE P ARRAY.  CARRYOVER
C  DATA MAY BE DEFAULTED OR THAT WHICH IS CURRENTLY IN THE C ARRAY.
C
C***********************************************************************
C  SUBROUTINE INITIALLY WRITTEN BY
C        LARRY BLACK - MBRFC     JULY 1982
C***********************************************************************
C  PRINCIPAL VARIABLES...
C
C  FOR DEFINITION OF VARIABLES IN COMMON BLOCKS, SEE SECTION IX.3.3C
C  OF THE NWSRFS USER'S MANUAL.
C
C     AI             CURRENT AI VALUE
C     AIADJ          AI ADJUSTMENT FACTOR
C     AICO           STORM AI VALUE
C     API            CURRENT API VALUE
C     CO(1)          INPUT CARRYOVER DATA FROM C ARRAY
C     IAIREL         API/AI RELATIONSHIP NUMBER
C     IDELT          DELTA-T OF TIME SERIES FOR RAINFALL/MELT AND RUNOFF
C     IFDEB          DEBUG OUTPUT SWITCH, 0 = OFF, 1 = ON
C     IFUTWK         FUTURE WEEK NUMBER
C     INCOFL         READ/NO READ CARRYOVER FLAG, 0 = NO READ, 1 = READ
C     IOTSFL         OUTPUT TIME SERIES FLAG FOR CURRENT API AND/OR AI
C                      VALUES, 0 = NO OUTPUT, 1 = OUTPUT
C     NEWSTM         STORM PERIOD COUNTER
C     NOP            NUMBER OF OPERATION ASSIGNED TO THIS OPERATION
C     NOZON          RUNOFF ZONE NUMBER
C     PO(1)          INPUT PARAMETRIC DATA FROM P ARRAY
C     RAINCO         STORM RAINFALL VALUE
C     ROCO           STORM RUNOFF VALUE
C     SUBNAM(2)      SUBROUTINE NAME
C     WE             CURRENT WATER EQUIVALENT VALUE
C     ZUSE           USER ID CODE FOR USER SUPPLIED API/AI RELATIONSHIP
C
C***********************************************************************
C
C
      DIMENSION PO(1),CO(1),SUBNAM(2)
C
      COMMON /IONUM/ IN,IPR,IPU
      COMMON /FDBUG/ IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON /PUDFLT/ IPDFLT
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_puc/RCS/puc29.f,v $
     . $',                                                             '
     .$Id: puc29.f,v 1.1 1995/09/17 18:50:51 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA SUBNAM/4hPUC2,4h9   /,NOP/29/,ZUSE/4hUSER/,EMPTY/4h    /
C
C
C  CALL DEBUG CHECK ROUTINE.
C
      CALL FPRBUG (SUBNAM,1,NOP,IFDEB)
C
C  SET VALUES OF VARIABLES FOR PUNCHING FIRST CARD.
C
      NOZON=PO(8)
      IAIREL=PO(10)
      IFUTWK=PO(11)
      AIADJ=PO(12)/10.0
C
C  SET CARRYOVER READ/NO READ FLAG DEPENDING ON VALUE OF IPDFLT.
C  IF IPDFLT = 1, DEFAULT C/O WILL BE USED.  IF IPDFLT = 0, ACTUAL
C  C/O WILL BE PUNCHED.
C
      INCOFL=0
      IF(IPDFLT.LE.0) INCOFL=1
C
C  SET CURRENT API AND/OR AI VALUE OUTPUT TIME SERIES FLAG.
C
      IOTSFL=0
      IF(PO(34).NE.EMPTY.OR.PO(37).NE.EMPTY) IOTSFL=1
C
C  PUNCH FIRST CARD WITH RUNOFF ZONE NAME, ZONE NUMBER, RFC ID CODE,
C  API/AI RELATIONSHIP NUMBER, FUTURE WEEK NUMBER, INITIAL AI
C  ADJUSTMENT FACTOR, CARRYOVER READ/NO READ FLAG, AND OUTPUT TIME
C  SERIES FLAG.
C
      WRITE(IPU,11) (PO(I),I=2,7),NOZON,PO(9),IAIREL,IFUTWK,AIADJ,
     1              INCOFL,IOTSFL
C
C  PUNCH CARD WITH MANDATORY TIME SERIES INFORMATION.
C
      IDELT=PO(13)
      WRITE(IPU,12) IDELT,(PO(I),I=14,22)
C
C  IF OUTPUT TIME SERIES ARE TO BE GENERATED FOR CURRENT API AND/OR AI
C  VALUES, PUNCH CARD WITH TIME SERIES INFORMATION.
C
      IF(IOTSFL.LE.0) GO TO 101
      WRITE(IPU,13) (PO(I),I=32,37)
C
C  IF CARRYOVER VALUES ARE TO BE DEFAULTED, RETURN.
C
  101 IF(INCOFL.LE.0) GO TO 102
C
C  IF CARRYOVER VALUES ARE TO BE CURRENT VALUES FROM C ARRAY, PUNCH
C  CARD WITH CURRENT CARRYOVER VALUES.
C
      NEWSTM=CO(1)
      RAINCO=CO(2)/100.0
      AICO=CO(3)/10.0
      ROCO=CO(4)/100.0
      API=CO(5)/100.0
      AI=CO(6)/10.0
      WE=CO(7)/100.0
      WRITE(IPU,14) NEWSTM,RAINCO,AICO,ROCO,API,AI,WE
C
C  IF RFC ID CODE IS SPECIFIED AS USER, PUNCH CARD WITH API/AI
C  RELATIONSHIP CONSTANTS AND RECESSION VALUES.
C
  102 IF(PO(9).NE.ZUSE) RETURN
      WRITE(IPU,15) (PO(I),I=23,30)
      RETURN
C
C
   11 FORMAT(6A4,I6,1X,A4,2I5,F5.1,2I5)
   12 FORMAT(I5,3(2X,2A4,1X,A4))
   13 FORMAT(5X,2(2X,2A4,1X,A4))
   14 FORMAT(I5,F5.2,F5.1,2F5.2,F5.1,F5.2)
   15 FORMAT(6F7.3,2F7.2)
      END
