C MEMBER PUC57
C-----------------------------------------------------------------------
C
C                             LAST UPDATE:
C
C @PROCESS LVL(77)
C
      SUBROUTINE PUC57 (P,C)

C     THIS IS THE CARD PUNCH ROUTINE FOR CONSUMPTIVE USE

C     THIS ROUTINE ORIGINALLY WRITTEN BY
C        JOSEPH PICA  - NWRFC   MAY 1997

C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012

C     POSITION     CONTENTS OF P ARRAY
C      1           VERSION NUMBER OF OPERATION
C      2-19        GENERAL NAME OR TITLE

C     TEMPERATURE INPUT FOR ESTIMATING ET
C     20-21       MEAN AREAL TEMPERATURE TIME SERIES IDENTIFIER
C     22          MEAN AREAL TEMPERATURE DATA TYPE CODE

C     POTENTIAL EVAPORATION INPUT FOR ESTIMATING ET
C     23-24       EVAPORATION TIME SERIES IDENTIFIER
C     25          EVAPORATION DATA TYPE CODE

C     NATURAL FLOW WHICH IS AVAILABLE FOR DIVERSIONS
C     26-27       NATURAL FLOW TIME SERIES IDENTIFIER
C     28          NATURAL FLOW DATA TYPE CODE

C     FLOW AFTER ACCOUNTING FOR DIVERSIONS AND RETURN FLOW OUT
C     29-30       ADJUSTED FLOW TIME SERIES IDENTIFIER
C     31          ADJUSTED FLOW DATA TYPE CODE

C     WATER DIVERTED BASED ON CROP DEMAND AND IRRIGATION EFFICIENCY
C     32-33       DIVERSION FLOW TIME SERIES IDENTIFIER
C     34          DIVERSION FLOW DATA TYPE CODE

C     RETURN FLOW STORAGE ACCUMULATION
C     RETURN FLOW IN IS A FRACTION OF THE DIVERSION FLOW
C     35-36       RETURN FLOW IN TIME SERIES IDENTIFIER
C     37          RETURN FLOW IN DATA TYPE CODE

C     RETURN FLOW STORAGE DECAY
C     RETURN FLOW OUT IS PART OF THE ADJUSTED FLOW
C     38-39       RETURN FLOW OUT TIME SERIES IDENTIFIER
C     40          RETURN FLOW OUT DATA TYPE CODE

C     OTHER LOSSES ARE TRANSPORTATION AND SUBSURFACE LOSSES
C     OTHER LOSSES ARE A FRACTION OF THE DIVERSION FLOW
C     41-42       OTHER LOSSES FLOW TIME SERIES IDENTIFIER
C     43          OTHER LOSSES FLOW DATA TYPE CODE

C     CROP DEMAND IS THE AMOUNT OF FLOW REQUIRED FOR THE CROPS
C     CROP DEMAND IS A FRACTION OF THE DIVERSION FLOW
C     44-45       CROP DEMAND FLOW TIME SERIES IDENTIFIER
C     46          CROP DEMAND FLOW DATA TYPE CODE

C     CROP EVAPOTRANSPIRATION
C     47-48       CROP EVAPOTRANSPIRATION TIME SERIES IDENTIFIER
C     49          CROP EVAPOTRANSPIRATION DATA TYPE CODE

C     50              OPTION FOR ET ESTIMATION METHOD
C     51              LATTITUDE OF IRRIGATED AREA
C     52              IRRIGATED AREA
C     53              IRRIGATION EFFICIENCY
C     54              MINIMUM FLOW
C     55              RETURN FLOW ACCUMULATION RATE
C     56              RETURN FLOW DECAY RATE
C     57              ANNUAL DAYLIGHT HOURS

C     58-422          DAILY EMPIRICAL CROP/METEOROLOGICAL
C                     COEFFICIENTS

C     THE NUMBER OF ELEMENTS REQUIRED IN THE P ARRAY IS  422

C     POSITION     CONTENTS OF C ARRAY
C      1           RETURN FLOW STORAGE

C     THE NUMBER OF ELEMENTS REQUIRED IN THE C ARRAY IS   1

C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012

C                   NWRFS CARDS
C                   FREE FORMAT
C
C                   TEMPERATURE TIME SERIES INPUT
C
C POCATELLO DIVERSIONS
C 0
C BASIN MAT NAT SQME
C ADJ SQME DIV SQME
C RFIN SQME RFOUT SQME LOSS SQME CD SQME CE MAPE
C 43.00 120. 0.70 1.50
C 0.00 0.00 0.10 0.60 0.70 0.80
C 0.90 0.80 0.60 0.30 0.00 0.00
C 0.10 0.005 100.
C
C                   ET TIME SERIES INPUT
C
C POCATELLO DIVERSIONS
C 1
C BASIN MAPE NAT SQME
C ADJ SQME DIV SQME
C RFIN SQME RFOUT SQME LOSS SQME CD SQME CE MAPE
C 43.00 120. 0.70 1.50
C 0.00 0.00 0.10 0.60 0.70 0.80
C 0.90 0.80 0.60 0.30 0.00 0.00
C 0.10 0.005 100.
C

C     CARD 1 - GENERAL USER SUPPLIED INFORMATION
C     CARD 2 - ET ESTIMATION OPTION, TEMPERATURE OR ET TIME SERIES
C     CARD 3 - INPUT TIME SERIES DEFINITION
C        FIELD 1 - TEMPERATURE OR ET TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 2 - TEMPERATURE OR ET DATA TYPE CODE  - 4 CHAR
C        FIELD 3 - NATURAL FLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 4 - NATURAL FLOW DATA TYPE CODE  - 4 CHAR
C     CARD 4 - PRIMARY OUTPUT TIME SERIES DEFINITION
C        FIELD 1 - ADJUSTED FLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 2 - ADJUSTED FLOW DATA TYPE CODE  - 4 CHAR
C        FIELD 3 - DIVERSION FLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 4 - DIVERSION FLOW DATA TYPE CODE  - 4 CHAR
C     CARD 5 - SECONDARY OUTPUT TIME SERIES DEFINITION
C        FIELD 1 - RETURN FLOW IN TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 2 - RETURN FLOW IN DATA TYPE CODE  - 4 CHAR
C        FIELD 3 - RETURN FLOW OUT TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 4 - RETURN FLOW OUT DATA TYPE CODE  - 4 CHAR
C        FIELD 5 - OTHER LOSSES FLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 6 - OTHER LOSSES FLOW DATA TYPE CODE  - 4 CHAR
C        FIELD 7 - CROP DEMAND FLOW TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 8 - CROP DEMAND FLOW DATA TYPE CODE  - 4 CHAR
C        FIELD 9 - CROP ET TIME SERIES IDENTIFIER - 8 CHAR
C        FIELD 10 - CROP ET DATA TYPE CODE  - 4 CHAR
C     CARD 6 - GENERAL IRRIGATION BASIN PARAMETERS
C        FIELD 1 - LATTITUDE OF IRRIGATED AREA
C        FIELD 2 - IRRIGATED AREA
C        FIELD 3 - IRRIGATION EFFICIENCY
C        FIELD 4 - MIMIMUM STREAMFLOW
C     CARD 7 - MID-MONTH EMPIRICAL COEFFICIENTS, JANUARY-JUNE
C     CARD 8 - MID-MONTH EMPIRICAL COEFFICIENTS, JULY-DECEMBER
C     CARD 9 - RETURN FLOW PARAMETERS
C        FIELD 1 - RETURN FLOW ACCUMULATION RATE
C        FIELD 2 - RETURN FLOW DECAY RATE
C        FIELD 3 - RETURN FLOW INITIAL STORAGE

C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012

      DIMENSION P(*),C(*)

      INTEGER      OPTION
      REAL         PREAL(19)
      CHARACTER*4  PCHAR(19)
      EQUIVALENCE (PREAL(1),PCHAR(1))

C     COMMON BLOCKS

      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/IONUM/IN,IPR,IPU
      COMMON/PUDFLT/IPDFLT
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcinit_puc/RCS/puc57.f,v $
     . $',                                                             '
     .$Id: puc57.f,v 1.3 2003/06/17 18:20:06 hsu Exp $
     . $' /
C    ===================================================================
C

      DATA ZERO / 0./
      CALL FPRBUG ('PUC57   ',1,57,IBUG)


C     CARD 1 USER SUPPLIED INFORMATION

      DO 21 II=2,19
  21  PREAL(II) = P(II)
      WRITE(IPU,500) (PCHAR(II),II=2,19)
 500  FORMAT(18A4)


C     CARD 2 OPTION FOR ET ESTIMATION

      OPTION = INT(P(50))
      WRITE(IPU,501) OPTION
 501  FORMAT(I1)


C     CARD 3 INPUT TIME SERIES

      II = 0
      DO 22 JJ=20,28
      II = II+1
  22  PREAL(II) = P(JJ)
      IF (OPTION.EQ.0) THEN
         WRITE(IPU,502) (PCHAR(II),II=1,3),(PCHAR(JJ),JJ=7,9)
 502     FORMAT(2A4,1X,A4,1X,2A4,1X,A4)
      ELSE
         WRITE(IPU,503) (PCHAR(II),II=4,9)
 503     FORMAT(2A4,1X,A4,1X,2A4,1X,A4)
      ENDIF

C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012


C     CARD 4 PRIMARY OUTPUT TIME SERIES

      II = 0
      DO 23 JJ=29,34
      II = II+1
  23  PREAL(II) = P(JJ)
      WRITE(IPU,504) (PCHAR(II),II=1,6)
 504  FORMAT(2A4,1X,A4,1X,2A4,1X,A4)


C     CARD 5 SECONDARY OUTPUT TIME SERIES

      II = 0
      DO 24 JJ=35,49
      II = II+1
  24  PREAL(II) = P(JJ)
      WRITE(IPU,505) (PCHAR(II),II=1,15)
 505  FORMAT(5(2A4,1X,A4,1X))


C     CARD 6 GENERAL IRRIGATION BASIN PARAMETERS

      WRITE(IPU,506) (P(II),II=51,54)
 506  FORMAT(F5.2,1X,F6.0,1X,F4.2,1X,F6.2)


C     CARD 7 & 8 MID MONTH EMPIRICAL COEFFICIENTS

      WRITE(IPU,507) P(73),P(103),P(132),P(163),P(193),P(224),P(254),
     +               P(285),P(316),P(346),P(377),P(407)
 507  FORMAT(6(F4.2,1X),/,6(F4.2,1X))


C     CARD 9 RETURN FLOW PARAMETERS AND CARRYOVER STORAGE

      WRITE(IPU,508) (P(II),II=55,56),C(1)
 508  FORMAT(F4.2,1X,F6.4,1X,F6.0)


      IF (ITRACE.GE.1) WRITE(IODBUG,90)
 90   FORMAT('PUC57:  EXITED:')

      RETURN
      END
