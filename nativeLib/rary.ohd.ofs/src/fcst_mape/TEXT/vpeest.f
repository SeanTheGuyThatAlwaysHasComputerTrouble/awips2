C MODULE VPEEST
C-----------------------------------------------------------------------
C
C  THIS ROUTINE COMPUTES THE STATION POTENTIAL EVAPORATION.
C
C  INPUTS:
C    - INTERNAL STATION POINTERS
C    - DAILY STATION MET DATA FROM PREPROCESSOR DATA BASE:
C       TA  (AIR TEMP)
C       TD  (DEW POINT TEMP)
C       U1  (STATION MEAN WIND SPEED IN MPH)
C       CC  (CLOUD COVER IN TENTHS)
C       PSS (PERCENT SUNSHINE IN PERCENT)
C       QS  (MEASURED SOLAR RADIATION IN LAGLEYS)
C    - PARAMETERS FROM THE PARMATER FILE INDICATING THE ANOMOMETER
C      HEIGHT, THE EXPONENT FOR CONVERTING STATION WIND SPEED TO
C      EQUIVALENT PAN WIND SPEED, INDEX INDICATING THE TYPE OF
C      RADIATION GENERALLY AVAILABLE AT THIS STATION AND THE DAY OF
C      THE RUN.
C
C  OUTPUTS:
C    - ESTIMATED POTENTIAL EVAPORATION FOR THE DAY AT EACH STATION
C
      SUBROUTINE VPEEST (JSTA,JDA,DDATA,PECOMP,PEPARM,MXDDIM,MXPDIM,
     *   MSNG,JPNTRS,JPIDX,MO)
C
      CHARACTER*8 OLDOPN
      INTEGER*2 DDATA(LRY),MSNG,JPNTRS(MRY)
C
      DIMENSION PECOMP(MXDDIM),PEPARM(MXPDIM),UNITS(3,2),SID(2)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/pudbug'
      COMMON /FCTIM2/ INPTZC,NHOPDB,NHOCAL,NHROFF(8)
      COMMON /VSLR/ COSX,SINX,SINSQ,SIN2CS
      COMMON /VFIXD/ LRY,MRY,NDAYS,NDAYOB,LARFIL,LPDFIL
      COMMON /VMODAS/ LSTDAM(13)
      COMMON /VOPT/ JAPTN,JPTN,JMAPE,LSTDY
      COMMON /VSTCTL/ LOOKUP(200),JCMAX,LOOKDN(200)
      COMMON /VSTUP/ PTYPE,DTYPE,METRIC,CPETIM(2),UTYPE,BTYPE,EST(3)
      COMMON /VTIME/ JDAYO,IYRS,MOS,JDAMOS,IHRO,IHRS,JDAMO,JDAYYR
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_mape/RCS/vpeest.f,v $
     . $',                                                             '
     .$Id: vpeest.f,v 1.4 2002/02/11 20:31:35 dws Exp $
     . $' /
C    ===================================================================
C
      DATA UNITS/4hF   ,4hMI  ,4hIN  ,
     *           4hC   ,4hKM  ,4hMM  /
C
C
      IF (IPTRCE.GT.0) WRITE (IOPDBG,*) 'ENTER VPEEST'
C
      IBUG=IPBUG('VEST')
C
      IOPNUM=-1
      CALL FSTWHR ('VPEEST  ',IOPNUM,OLDOPN,IOLDOP)
C
C  CHECK FOR ALPHABETIC LISTING, I.E. LOOKUP(1) IS GREATER THAN ZERO
      KSTA=JSTA
      IF (LOOKUP(1).GT.0) KSTA=LOOKUP(JSTA)
      IF (IBUG.GT.0) WRITE (IOPDBG,10)JSTA,JPIDX,METRIC,KSTA,LOOKUP(1)
10    FORMAT (' IN VPEEST : JSTA=',I5,' JPIDX=',I5,
     *   ' METRIC=',I3,' KSTA=',I4,' LOOKUP(1)=',I2)
C
C  IF VPRLLP IS CONTROLLING THE LOOPING THEN THE PARAMETERS WILL BE IN A
C  DIFFERENT ORDER - JPIDX RATHER THAN JSTA WILL INDEX THE PARMS
      IPTR=(JPIDX-1)*58
      IPEPTR=NDAYOB*(JSTA-1)+JDA
      MDPE=(KSTA-1)*6+1
      IF (IBUG.GT.1) WRITE (IOPDBG,20) (DDATA(I),I=1,24),
     *   IPTR,IPEPTR,MDPE
20    FORMAT (' IN VPEEST BEFORE TEST FOR MISSING DATA: ' /
     *   ' DDATA(1-24)=',12I6 / 13X,12I6 /
     *   ' IPTR=',I5,' IPEPTR=',I5,' MDPE=',I5)
C
      RMSNG1=-99.
      RMSNG2=-999.
C
C  CHECK FOR MISSING DATA AND INSURE THAT EACH INPUT HAS A GOOD VALUE IN
C  IT OR HAS THE CONVENTIONAL MISSING VALUE NUMBER
      MISFLG=0
      IF (DDATA(MDPE).NE.MSNG) GO TO 30
         TA=RMSNG1
         MISFLG=1
         GO TO 35
30    TA=DDATA(MDPE)
      TA=TA/10.
35    IF (DDATA(MDPE+1).NE.MSNG) GO TO 40
         TD=RMSNG1
         MISFLG=1
         GO TO 45
40    TD=DDATA(MDPE+1)
      TD=TD/10.
45    IF (DDATA(MDPE+2).NE.MSNG) GO TO 50
         U1=RMSNG1
         UP=RMSNG1
         MISFLG=1
         GO TO 55
50    U1=DDATA(MDPE+2)
      U1=U1*24./10.
55    IF (DDATA(MDPE+3).NE.MSNG) GO TO 60
         CC=RMSNG2
         GO TO 65
60    CC=DDATA(MDPE+3)
      CCVR=CC/10.
cew strange bug where 1 was greater than one.
cew  solved the symptom but not the cause.
      IF (CCVR .LE. 1.0001) GO TO 65
         WRITE (IPR,62) CCVR
62    FORMAT ('0**WARNING** CLOUD COVER (',F5.2,
     *   ') IS GREATER THAN 10 TENTHS AND WILL BE DIVIDED BY 10.')
         CALL WARN
         CCVR=CCVR/10
65    IF (DDATA(MDPE+4).NE.MSNG) GO TO 70
         PSS=RMSNG2
         GO TO 75
70    PSS=DDATA(MDPE+4)
75    IF (DDATA(MDPE+5).NE.MSNG) GO TO 80
         QS=RMSNG2
         IF (CC.EQ.RMSNG2.AND.PSS.EQ.RMSNG2.AND.QS.EQ.RMSNG2) MISFLG=1
80    IF (MISFLG.GT.0) GO TO 220
C
C  COMPUTE PE
C
C  SET ESTIMATE OF SOLAR RADIATION TO -10. SO THAT WHEN THE DAILY
C  VALUES ARE PRINTED IT WILL BE APPARENT WHEN IT HASH AS BEEN
C  COMPUTED, WHEN IT IS MISSING AND WHEN IT IS NOT USED
      EQS=-10.0
      EQSO=EQS
C
C  THE ORIGINAL REPORT OF SOLAR RADIATION IS SET TO -10 WHICH HELPS
C  INDICATE WHEN GOOD REPORTS OF SOLAR ARE RECEIVED.
      QSO=RMSNG2
C
C  GET THE TYPE OF RADIATION EXPECTED
      MQS=PEPARM(IPTR+13)
      IF (PEPARM(IPTR+13).LT.1) MQS=2
      IF (IBUG.GT.0) WRITE (IOPDBG,90) IPTR,PEPARM(IPTR+13)
90    FORMAT (' IN VPEEST : PEPARM(',I3,'+13)=',F6.1)
C
C  CHECK FOR THE TYPE OF RADIATION EXPECTED
      IF (MQS.EQ.3) GO TO 130
C
C  SKY COVER OR PERCENT SUNSHINE EXPECTED
      IF (MQS.EQ.1) KQS=1
      IF (MQS.EQ.2) KQS=2
C
C  ESTIMATE SOLAR RADIATION FROM PERCENT SUNSHINE OR FROM SKY COVER
120   CALL VSOLAR (KQS,EQS,CCVR,PSS,PEPARM,LRY,IPTR,MXPDIM)
      QS=EQS
      GO TO 160
C
C  RADIATION DATA EXPECTED
130   QS=DDATA(MDPE+5)
      IF (DDATA(MDPE+5).EQ.MSNG) QS=RMSNG2
      IF (QS.EQ.RMSNG2) QSO=RMSNG2
      QSO=QS
      IF (IBUG.GT.0) WRITE (IOPDBG,140) QS,PSS,CC
140   FORMAT (' IN VPEEST : QS= ',F7.0,' PSS= ',F8.1,' CC= ',F7.2)
      IF (QS.GT.1.0) GO TO 160
         IF (PSS.GT.-0.01) THEN
	    KQS=2
	    GO TO 120
	    ENDIF
         IF (CC.LT.-0.01) GO TO 220
	 KQS=1
         GO TO 120
C
160   TA1=TA+398.363
      TA2=TA1*TA1
      ESV=1.0/EXP(7482.6/TA1)
      ZA=PEPARM(IPTR+11)*3.28
      UP=U1*((2.0/ZA)**PEPARM(IPTR+12))
      IF (UP.LT.0.001) UP=0.001
      IF (IBUG.GT.2) WRITE (IOPDBG,170) TA1,TA2,ESV,UP,ZA,
     *   PEPARM(IPTR+12),TD
170   FORMAT (' IN VPEEST : ',
     *   ' TA1=',F10.2,' TA2=',F10.2,' ESV=',E10.5,
     *   ' UP=',F10.2,' ANO HEIGHT=',F5.1,' PEXP=',F6.2,' TD=',F5.1)
C
      IF (TA-TD) 180,180,190
180   EAG=0.0
      GO TO 200
190   EAV=1.0/EXP(7482.6/(TD+398.363))
      DELVP=(6.41334E6*(ESV-EAV))**0.88
      EAG=0.0105*DELVP*(0.37+0.0041*UP)
200   QND=EXP((TA-212.0)*(0.1024-0.01066*ALOG(QS)))-0.0001
C
C  COMPUTE PE
      DENOM=0.015+6.8554E10*ESV/TA2
      IF (IBUG.GT.2) WRITE (IOPDBG,210) ESV,UP,EAV,DELVP,EAG,QND,DENOM
210   FORMAT (' IN VPEEST : ESV=',E10.4,' UP=',F6.0,' EAV=',E10.4,
     *   ' DELVP=',E10.4,' EAG=',E10.4,' QND=',E10.4,' DENOM=',F10.4)
      PE=(QND+EAG)/DENOM+0.0005
      IF (PE.LT.0.0) PE=0.0
      IF (PE.GT.0.0) THEN
         ICONV=1
	 NVAL=1
         CALL UDUCNV ('IN  ','MM  ',ICONV,NVAL,PE,PE,IERR)
	 ENDIF
      IF (PE.LT.0.01) PE=0.01
      GO TO 230
C
220   PE=RMSNG1
      EQS=RMSNG2
      IF (QS.EQ.RMSNG2) QSO=RMSNG2
C
230   PECOMP(IPEPTR)=PE
C
C  CHECK IF TO PRINT COMPUTED STATION DATA AND ESTIMATES
      IF (JAPTN.LT.1) GO TO 340
C
      IF (IBUG.GT.0) WRITE (IOPDBG,240) JDA,JSTA,NDAYOB
240   FORMAT (' IN VPEEST : JDA=',I3,' JSTA=',I4,' NDAYOB=',I4)
      MON=MO
      IF (MON.EQ.13.AND.MOS.EQ.12) MON=1
      IF (IMO.EQ.2.AND.IYRS/4*4.EQ.IYRS) MON=13
      IF (LSTDAM(MON).LT.JDAMO) MON=MON+1
      IF (MON.EQ.14) MON=3
C
      IF (JSTA.GT.1) GO TO 290
      IF (LSTDY.GT.0.AND.JDA.LT.NDAYOB) GO TO 290
C
      IF (JDA.EQ.1.AND.LSTDY.LT.1) GO TO 249
      IF (JDA.EQ.NDAYOB.AND.LSTDY.EQ.1) GO TO 249
      GO TO 252
C
249   WRITE (IPR,250)
250   FORMAT ('0',3X,'INPUT METEOROLOGICAL DATA AND INDIVIDUAL ',
     *   'STATION ESTIMATES OF POTENTIAL EVAPORATION FOR THE ENDING ',
     *   'OF THE HYDROLOGIC DAY')
      WRITE (IPR,260)
260   FORMAT ('0',4X,
     *   11X,9X,2X,8X,2X,
     *   'AIR  ',2X,'DEW  ',2X,'PAN WIND',2X,'SOLAR',2X,
     *   'SKY   ',2X,'SUN  ',2X,'EST  ',2X,'PE')
      WRITE (IPR,270)
270   FORMAT (' ',4X,
     *   11X,9X,2X,8X,2X,
     *   'TEMP ',2X,'POINT',2X,'TRAVEL  ',2X,'RAD  ',2X,
     *   'COVER ',2X,'SHINE',2X,'RAD  ')
      METDIX=METRIC+1
      WRITE (IPR,280) UNITS(1,METDIX),(UNITS(I,METDIX),I=1,3)
280   FORMAT (' ',4X,
     *   'DESCRIPTION',9X,2X,'STATION ',2X,
     *   'DEG',A2,2X,'DEG',A2,2X,A2,'/DAY  ',2X,
     3 'LY   ',2X, 'TENTHS',2X,'PCT  ',2X,'LY   ',2X,A4)
      WRITE (IPR,285)
285   FORMAT (' ',4X,
     *   20('-'),2X,'--------',2X,
     *   '-----',2X,'-----',2X,'--------',2X,'-----',2X,
     *   '------',2X,'-----',2X,'-----',2X,'------')
C
252   JMON=MON
      IF (JMON.EQ.13) JMON=2
      WRITE (IPR,251) JMON,JDAMO,IYRS,IHRS,INPTZC
251   FORMAT (' ',I2.2,'/',I2.2,'/',I4,'-',I2.2,A4)
C
290   JNPTR=IPTR+1
      SID(1)=PEPARM(JNPTR+1)
      SID(2)=PEPARM(JNPTR+2)
      JSTRT=JNPTR+4
      JZEND=JNPTR+8
C
C  CHECK IF NEED TO CONVERT DATA
      ICONV=1
      NVAL=1
      IF (METRIC.EQ.0) THEN
         IF (PE.NE.RMSNG1) THEN
            CALL UDUCNV ('MM  ','IN  ',ICONV,NVAL,PE,PE,IERR)
	    ENDIF
	 ENDIF
      IF (METRIC.EQ.1) THEN
         IF (TA.NE.RMSNG1) THEN
            CALL UDUCNV ('DEGF','DEGC',ICONV,NVAL,TA,TA,IERR)
	    ENDIF
         IF (TD.NE.RMSNG1) THEN
            CALL UDUCNV ('DEGF','DEGC',ICONV,NVAL,TD,TD,IERR)
	    ENDIF
         IF (UP.NE.RMSNG1) THEN
            CALL UDUCNV ('MI/D','KM/D',ICONV,NVAL,UP,UP,IERR)
	    ENDIF
	 ENDIF
C
C  CHECK IF ONLY LAST DAY OF OBSERVED DATA TO BE PRINTED
      IF (LSTDY.EQ.1.AND.JDA.NE.NDAYOB) GO TO 340
C
      WRITE (IPR,320)
     *   (PEPARM(I),I=JSTRT,JZEND),PEPARM(JNPTR+1),PEPARM(JNPTR+2),
     *   TA,TD,UP,QSO,
     *   CC,PSS,EQS,PE
320   FORMAT (' ',4X,
     *   5A4,2X,2A4,2X,
     *   F5.1,2X,F5.1,2X,F8.0,2X,F5.0,2X,
     *   F6.0,2X,F5.0,2X,F5.0,2X,F6.2)
C
C  CHECK IF LAST DAY AND STATION
      IF (JDA.EQ.NDAYOB.AND.JSTA.EQ.LPDFIL) THEN
         IF (EQS.EQ.EQSO) THEN
            WRITE (IPR,330) EQS
330   FORMAT ('0NOTE: A ',F3.0,' IN THE ESTIMATED RADIATION COLUMN ',
     *   'INDICATES THAT SOLAR RADIATION WAS MEASURED AND NO ',
     *   'ESTIMATE WAS NECESSARY.')
            ENDIF
         ENDIF
C
340   CALL FSTWHR (OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C
      IF (IPTRCE.GT.0) WRITE (IOPDBG,*) 'EXIT VPEEST'
C
      RETURN
C
      END
