C MEMBER ROUT44
C  (from old member FCEX44)
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 06/23/95.16:29:02 BY $WC30RE
C
C @PROCESS LVL(77)
C
      SUBROUTINE ROUT44(P,C,QINST,QINEN,QON,QOUT)
C       GIVEN PARAMETERS (P ARRAY),
C       INITIAL CONDITIONS IN THE CARRYOVER ARRAY (C ARRAY),
C       COMPUTE THE PHASE VALUES AND OUTFLOW AT PERIOD END, AND RETURN
C       THEM IN THE CARRYOVER ARRAY.  RETURN RECOMPUTED
C       BEGINNING-OF-PERIOD OUTFLOW (QON).

C       CHANNEL REACH ROUTING ROUTINE; WITH INFLOW INTERPOLATED
C       FOR SUBDIVIDED PERIODS AND TIME STORAGE COMPUTED FROM
C       AVERAGE OF BEGIN PERIOD AND END PERIOD PHASE VALUES

      REAL*4 PH(99)
      REAL*4 P(*)
      REAL*4 C(*)

      INTEGER*4 NPS
      REAL*4    QINST
      REAL*4    QINEN
      REAL*4    QON

      REAL*4    QOUT
      REAL*4    XHR
      INTEGER*4 I,J,K
      REAL*4    TSMIN
      INTEGER*4 N
      REAL*4    XHRN
      REAL*4    XINTRP
      REAL*4    PQI
      REAL*4    SUMOUT
      REAL*4    Q0, QI, TS, O2, PO2, SO2, POO2, OO2

C     FUNCTIONS:
      REAL*4    TSTR44, TSAV44
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_sarroute/RCS/rout44.f,v $
     . $',                                                             '
     .$Id: rout44.f,v 1.3 1996/12/06 20:00:09 dws Exp $
     . $' /
C    ===================================================================
C

C     POSITION     CONTENTS OF P ARRAY
C      1           VERSION NUMBER OF OPERATION
C      2-19        GENERAL NAME OR TITLE

C     USED TO CONSERVE INFLOW HYDROGRAPH VOLUME
C     20-21        START INFLOW TIME SERIES IDENTIFIER
C     22           START INFLOW DATA TYPE CODE

C     INFLOW HYDROGRAPH
C     23-24        END INFLOW TIME SERIES IDENTIFIER
C     25           END INFLOW DATA TYPE CODE

C     USED TO CONSERVE ROUTED HYDROGRAPH VOLUME
C     26-27        START OUTFLOW TIME SERIES IDENTIFIER
C     28           START OUTFLOW DATA TYPE CODE

C     ROUTED OUTFLOW HYDROGRAPH
C     29-30        END OUTFLOW TIME SERIES IDENTIFIER
C     31           END OUTFLOW DATA TYPE CODE

C     32           1 OR 2 INFLOW TIME SERIES SPECIFIED FLAG
C                  = 1, ONLY THE END INFLOW TIME SERIES IS SPECIFIED
C                  = 2, BOTH THE START AND END INFLOW TIME SERIES ARE
C                       SPECIFIED
C     33           1 OR 2 OUTFLOW TIME SERIES SPECIFIED FLAG
C                  = 1, ONLY THE END OUTFLOW TIME SERIES IS SPECIFIED
C                  = 2, BOTH THE START AND END OUTFLOW TIME SERIES ARE
C                       SPECIFIED
C     34           NUMBER OF ROUTING PHASES (MUST BE > 0)
C     35           N VALUE OF KTS/Q**N COMPUTATION
C                  IF N=0, THE TIME OF STORAGE IS EXTRACTED FROM THE
C                           DISCHARGE-TIME OF STORAGE TABLE
C     36           KTS VALUE IN HOURS IF N IS NONZERO
C     37           THE NUMBER OF POINTS ON THE DISCHARGE-TIME OF
C                  STORAGE TABLE IF N=0 (=0 IF N IS NONZERO)
C     38           COMPUTATIONAL TIME INTERVAL (HOURS)
C     39+          THE POINTS OF THE DISCHARGE-TIME OF STORAGE TABLE
C
C     THEREFORE THE NUMBER OF ELEMENTS REQUIRED IN THE P ARRAY IS
C        38 +
C         2 * NUMBER OF POINTS OF THE DISCHARGE-TIME OF STORAGE TABLE

C     POSITION     CONTENTS OF C ARRAY
C      1           INITIAL START INFLOW, IF ONLY END INFLOW TIME SERIES
C                  IS SPECIFIED
C      2+          PHASE FLOW VALUES FROM REACH

      NTS=NINT(P(32))
      NPS=NINT(P(34))
      XHR=P(38)

C     DEFINE NCO - NUMBER OF CARRYOVER ELEMENTS
C     COPY THE PHASE VALUES FROM THE C ARRAY INTO INTERNAL PH ARRAY
      IF (NTS.EQ.1) THEN
         NCO = NPS + 1
         DO 8 I=2,NCO
 8       PH(I-1) = C(I)
      ELSE
         NCO = NPS
         DO 9 I=1,NCO
 9       PH(I) = C(I)
      ENDIF

C     DETERMINE HOW MANY TIME SUB-PERIODS ARE NEEDED.
      TSMIN = TSTR44(QINST,P)
      TSMIN = MIN(TSTR44(QINEN,P),TSMIN)
      DO 10 I=1,NPS
        TSMIN = MIN(TSTR44(PH(I),P),TSMIN)
 10   CONTINUE
      IF(TSMIN.LT.XHR/2) THEN
        IF(TSMIN.LT.0.05) THEN
C         INDETERMINENT TS, SET # OF TIME SUB-PERIODS TO 4
          N = 4
          XHRN = XHR/4
        ELSE
C         N TIME SUB-PERIODS ARE NEEDED, FOR TS > XHR/2.
          N = (XHR/2/TSMIN) + 1.
          XHRN = XHR/N
        ENDIF
      ELSE
C       NO SUB-PERIODS NEEDED.
        N = 1
        XHRN = XHR
      ENDIF

C     REACH INFLOW INTERPOLATION FACTORS, XINTRP AND PQI
      XINTRP=(QINEN-QINST)/N
      PQI=QINST-(XINTRP/2.)
C     INITIALIZE SUM OF SUB-PERIOD OUTFLOWS.
      SUMOUT=0.
C     SET START-OF-PERIOD FLOW AT REACH INITIAL OUTFLOW
      Q0=PH(NPS)
      QON=Q0
C     TIME SUB-PERIOD LOOP
      DO 60 I = 1, N
C       COMPUTE INTERPOLATED REACH AVERAGE INFLOW, QI
        PQI=PQI+XINTRP
        QI = PQI

C       REACH PHASES LOOP
        DO 50 J=1,NPS
C         TEST FOR AVG INFLOW EQUAL TO START OF PERIOD PHASE FLOW
          IF(ABS(QI-PH(J)).LT..0001) GO TO 50
C         GUESS PHASE END OF PERIOD FLOW FOR FIRST TRIAL
          TS = TSTR44(PH(J),P)
          IF(TS.LE.XHRN/2) THEN
            O2 = PH(J)
          ELSE
            O2 = ((QI-PH(J))*XHRN/(TS+(XHRN/2))) + PH(J)
          ENDIF

C         TRIAL LOOP - FOR PHASE END OF PERIOD FLOW. (MAX 20 TRIALS)
          DO 30 K=1,20
C           TIME OF STORAGE IS AVERAGE OVER PERIOD WITH END OF PERIOD
C           BEING A TRIAL VALUE.
            TS = TSAV44(PH(J),O2,P)
            IF(TS.LE.XHRN/2) THEN
C             DO NOT ALLOW TS TO BE LESS THAN HALF OF THE SUB-PERIOD.
              PO2 = QI
            ELSE
              PO2 = ( (QI-PH(J)) * XHRN/(TS+(XHRN/2)) ) + PH(J)
            ENDIF
            SO2 = O2
C           STOP TRIALS IF TRIAL FLOW AND COMPUTED FLOW WITHIN .1%
            IF(ABS(O2-PO2).LT.ABS(O2*.001)) GO TO 40

C           NEXT TRIAL
            IF(MOD(K+1,3).EQ.0) THEN
C             MAKE CONVERGENCE ESTIMATE EACH THIRD TRIAL
C             TO ASSURE ALL PREVIOUS TRIALS OUTFLOWS USED IN
C             THIS ESTIMATE ARE PRODUCED BY ROUTING EQUATION.
              O2 = (O2*POO2-PO2*OO2)/(O2-OO2-PO2+POO2)
            ELSE
              O2 = PO2
            ENDIF

            OO2 = SO2
            POO2 = PO2
 30       CONTINUE
C         END OF TRIAL LOOP - SOLUTION HAS CONVERGED
 40       CONTINUE
C         AVERAGE INFLOW FOR NEXT PHASE
          QI = (PO2 + PH(J))/2.

C         TRANSFER END OF SUB-PERIOD PHASE OUTFLOW TO OUTPUT ARRAY PH
          PH(J) = PO2

 50     CONTINUE

C       ACCUMULATE FOR AVERAGE SUB-PERIOD OUTFLOW
        SUMOUT=SUMOUT+PH(NPS)
 60   CONTINUE
C      WRITE(*,'(I4,4F10.2)') N,Q0,QON,PH(NPS),SUMOUT
      IF(N.GT.1) THEN
C         COMPUTE BEGINNING-OF-PERIOD FLOW TO REPRESENT CORRECT VOLUME.
C         QAVE=( Q0+PH(NPS)+2.*(SUMOUT-PH(NPS)) )/(2.*N)
C         QON= (2.*QAVE)-PH(NPS)
        QON=( ( Q0-PH(NPS)+(2.*SUMOUT) )/N )-PH(NPS)
      ENDIF
C
C  COPY NEWLY ROUTED PHASES INTO INPUT-OUTPUT CARRYOVER ARRAY (C ARRAY)
      IF (NTS.EQ.1) THEN
         DO 70 I=2,NCO
 70      C(I)=PH(I-1)
C        C(1) = PH(1)
         C(1) = QON  
      ELSE
         DO 71 I=1,NCO
 71      C(I)=PH(I)
      ENDIF

C     C(1)=QINEN
C     QOUT=C(NPS+1)
      QOUT=C(NCO)

      RETURN

      END
