C MEMBER XWKS51
C DESC ASSGN WORK SPACE LOCATIONS AND AMONTS FOR EX51.
C----------------------------------------------------------------
C
C@PROCESS LVL(77)
C
      SUBROUTINE XWKS51(PO,CO,D,WK,NPA,IDPT,LOCWS)
C-----------------------------------------------
C  ROUTINE TO ASSIGN WORK SPACE AND STORE PARAMETERS IN
C  THE ORIGINAL SSARR DATABASE FORMAT
C
C--------------------------------------------------------------------
C  WRITTEN BY - KUANG HSU - HRL - APRIL 1994
C--------------------------------------------------------------------
C
C***********************************************************************
C
C WORD IN RECORD, FORMAT(I=INTEGER,R=REAL,A-ALPHA), DESCRIPTION -
C
C  1-24 NOT USED
C 25 I, POINTER TO THE RESERVOIR REGULATION INFORMATION.
C 26-27 NOT USED.
C 28 I, LOCATION OF ELEVATION/STORAGE CURVE TABLE
C 29    NOT USED.
C 30 I, BACKWATER PARAMETER CODE, 
C       =0 -- NON-BACKWATER RESERVOIR.
C       =1 -- OUTFLOW AT COTROLLING STATION 3RD VARIABLE IN BACKWATER
C              TABLE.
C       =2 -- ELEVATION IS 3RD VARIABLE.
C 31 R, MINIMUM DOWNSTREAM FLOW REQUIREMENT   
C 32 I, LOCATION OF BACKWATER TABLE IN RECORD
C 33 R, LAKE ELEVATION UPPER BOUND
C 34 R, LAKE ELEVATION LOWER BOUND
C 35 I, SCAN INDEX TO BACKWATER TABLE (USED BY ROUTINE TLU3)
C 36 R, MAXIMUM TRIBUTARY FLOW TO SHUT DOWN RESERVOIR RELEASE 
C 37    NOT USED
C 38 I, COMPUTED STORAGE (NOTE- THIS IS INTEGER VALUE)
C 39-49 NOT USED
C 50 I, =0, FOR NON-BACKWATER RESERVOIR; =2, FOR BACKWATER RESERVOIR
C 51 I, =1, UPPER RES; =2, SINGLE OR NON-BACK-RES; =4 TRIBU BACK-RES
C 52 I, INDEX TO RECORD OF DISCHARGE AND ELEVATION AT START OF NEXT
C       PERIOD.
C 53-68 NOT USED
C 69 R. COMPUTED ELEVATION 
C 70    NOT USED
C 71 R, COMPUTED OUTFLOW
C WORD 72 THROUGH THE END OF THE RECORD ARE THE FOLLOWING VARIABLE
C        LENGTH ARRAYS-
C
C ELEVATION/STORAGE CURVE IN THE ORDER OF DISCHARGE, ELEVATION,
C    TIME-OF-STORAGE (NOT USED), STORAGE TABLE(LOCATED BY WORD 28)
C    FLOW, ELEVATION, AND TS ARE REAL, STORAGE IS INTEGER.
C BACKWATER TABLE, LOCATED BY WORD 32. THREE WORDS PER POINT- OUTFLOW,
C    ELEVATION, AND CONTROLLING STATION PARAMETER (EITHER FLOW OR ELEV,
C    AS DESCRIBED BY WORD 30)
C
C ISTYP =0, NON-BACK-RES; =1, UPPER BACK; =2, TRIBU BACK; =3, 3-VAR
C***********************************************************************
C
      INCLUDE 'common/fprog'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fengmt'
      INCLUDE 'common/fctime'
      INCLUDE 'common/sarr51'
      INCLUDE 'common/unit51'
      INCLUDE 'common/mod151'
      COMMON/NQEL51/NQEL(2)
C
      DIMENSION PO(*),CO(*),NPA(*),WK(*),LOCWS(*),D(*),IDPT(*)
      DIMENSION CONV(6),IQE(3)
      REAL*8 RESTYP(4),ENGMET(2)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_ssarresv/RCS/xwks51.f,v $
     . $',                                                             '
     .$Id: xwks51.f,v 1.6 2006/03/16 16:21:54 xfan Exp $
     . $' /
C    ===================================================================
C
C
      DATA RESTYP/8H'NOT A' ,8H'UPPER' ,8H'TRIBU' ,8H'3-VAR' /
      DATA ENGMET/8HENGLISH ,8HMETRIC  /
C
      IF (IBUG.GE.1) WRITE(IODBUG,900)
  900 FORMAT(10X,'*** ENTER XWKS51 ***')
C
C----------------------------
C  SET INITIAL VALUES AND COUNTERS
C
C      NDD=LDA-IDADAT+LHR/24
C
C-------------------------------
C  THE FIRST PORTION OF WORK SPACE IS USED FOR HOLDING
C  THE ELEVATION/STORAGE CURVE IN THE ARMY CORPS FORMAT
C  DISCHARGE, ELEVATION, TIME OF STORAGE (NOT USED), STORAGE
C
      L = 0
      LCO = 2
      NRES = PO(9)
C
      DO 100 II=1,NRES
      IRES = II
      LUSE = 0
C
C  L IS THE POINTER TO THE PARAMETER ARRAY OF A NEW RESERVOIR IN WORK
C  SPACE
      LOCWS(IRES) = L+1
C
C  RESERVE 71 WORD TO BE FILLED LATER
C
      DO 90 I=1,71
 90   NPA(L+I) = 0
      LUSE = LUSE +71
C
C  L1 IS THE POINTER TO Q VS E TABLE IN WORK SPACE
C  ONLY ONE RESERVOIR ARRAY RESIDES IN THE MEMORY,
C  THEREFORE, THE LOCATION POINTER ALWAYS BEGIN WITH ONE!
      L1 = LUSE+1
      NPA(L+2) = 3
      NPA(L+28) = LUSE+1
      NPA(L+32) = 0
C
      NPA(L+35) = 0
      NPA(L+50) = 0
C
C  FILL IN INITIAL DISCHARGE, ELEVATION, AND STORAGE
C
      LCOI = LCO + (NRES-IRES)*4
C
C  STORE INITIAL INSTANTANEOUS DISCHARGE
      WK(L+71) = CO(LCOI)
C
C  STORE INITIAL RESERVOIR ELEVATION
      WK(L+69) = CO(LCOI+1)
C
C  STORE INITIAL STORAGE
      IST = CO(LCOI+2)
      NPA(L+38) = IST

C  RESERVE 7 WORD TO BE FILLED LATER
C    WORD IN FCN ARRAY (DEFINED IN ROUTINE STLU2
C      1        LENGTH OF ARRAY IN WORDS.
C      2  SCNI -SCAN INDEX MAINTAINED BY STLU2, POINTS TO POSITION IN
C               ARRAY USED BY PREVIOUS REFERENCE.
C      3        BASETIME RELATION WAS LOADED TO FILE (SYSTEM CLOCK TIME)
C      4        RESERVED
C      5-6      RELATION NAME
C      7  FC - FORMAT CODE (REAL);
C         IF FC = 0, POWER SERIES OF THE FORM:
C                 RSL(2) = A*ARG**N + B*ARG**(N-1)...
C                 N IS IN FCN(8) (REAL)
C                 A IS IN FCN(9)
C                 CONSTANT TERM IS IN FCN(N+9), N MAY EQUAL ZERO.
C         FC  = 2, 3, OR 4, FUNCTION IS TABLE OF POINTS. EACH
C               POINT IS IN FC CONSECUTIVE WORDS.
C         IF FC=4, 4TH VARIABLE IS STORAGE AND IS STORED
C               IN INTEGER FORMAT.
C               POINTS ARE STORED BEGINNING AT FCN(8). IF FC=2,
C               AND VARIABLES ARE Q AND E,
C               FCN(8)=Q1, FCN(9)=E1, FCN(10)=Q2, FCN(11)=E2,...
C         FC = 1, USGS SHIFT-OFFSET FORMAT; PROCESSED BY ROUTINE TLUSO
C 8 THRU (END-20)   VALUES OF RELATION, ORGANIZED AS FC INDICATES.
C (END-19) TO END   DESCRIPTION OF RELATION FROM '...' FIELD ON ZCT CARD
      LUSE = LUSE +7
      L11 = L + L1 +7
      DO 140 I=1,7
 140  NPA(L+L1+I) = 0
      IPTX = NRES-IRES+10
      LPP = PO(IPTX)
      ISTYP = PO(LPP)
      NPA(L+14) = ISTYP
      LPP = LPP+1
C
C  ELEVATION/STORAGE/DISCHARGE CURVE
      NVAL = PO(LPP)
      NQEL(IRES)=NVAL
C
C  FC = 4 AT POSITION 7 FOR RESERVOIR OPERATION
      NPA(L+L1+6) = 4
      IEL = LPP
      IS = IEL + NVAL
      IQ = IS + NVAL
      DO 150 I=1,NVAL
      LOC = L11 + (I-1)*4
C
C  NEXT WORD IS FOR DISCHARGE
      WK(LOC) = PO(IQ+I)
C
C  NEXT WORD IS FOR ELEVATION
      WK(LOC+1) = PO(IEL+I)
C
C  NEXT WORD IS FOR TIME STORAGE (NOT USED)
      WK(LOC+2) = 0.
C
C  NEXT WORD IS FOR STORAGE
      IST = PO(IS+I)
      NPA(LOC+3) = IST
 150  CONTINUE
      LUSE = LUSE + NVAL*4
C
C  RESERVE 20 WORDS FOR DESCRIPTION OF RELATION
C  5 WORD FROM TITLE, THE REST FOR FUTURE USE
      DO 152 I=1,5
      LWK = L+LUSE+I
 152  WK(LWK) = PO(I+1)  
      LUSE = LUSE+20
C
C  STORE TOTAL LENGTH OF RECORD FOR ELEVATION DISCHARGE TABLE
C  INCLUDE 7 WORDS FOR THE HEADER AND 20 WORDS FOR THE TRAILER INFO
C
      NPA(L+L1) = NVAL*4+27
C
      IF (IBUG.LT.2 .OR. LUSE.LE.71) GO TO 156
      IRTYP=ISTYP+1      
      WRITE(IODBUG,1590) RESTYP(IRTYP)
 1590 FORMAT(//,' *** FCN ARRAY FOR ',A8,'BACKWATER RESERVOIR ',
     & 'IN XWKS51 ***')
      DO 155 I=72,LUSE,8
      IFCN1 = I-71
      IE = I+7
      IFCN2 = IE-71
      WRITE(IODBUG,1605) IFCN1,IFCN2
      WRITE(IODBUG,1610) (WK(L+J),J=I,IE)
  155 CONTINUE
 156  CONTINUE
      LPP = LPP + NVAL*3 + 1
C
      IF(ISTYP.EQ.0) THEN
        NPA(L+50) = 0
        NPA(L+51) = 2
        GO TO 180
      END IF
      IF(ISTYP.EQ.3) GO TO 300
C
C  STORE BACKWATER TABLE
C  L2 IS THE POINTER TO BACKWATER TABLE IN WORK SPACE
C  ONLY ONE RESERVOIR ARRAY RESIDES IN THE MEMORY,
C  THEREFORE, THE LOCATION POINTER ALWAYS BEGIN WITH ONE!
      L2 = LUSE + 1
      NPA(L+32) = L2
      NPA(L+50) = 2
      NVAL = PO(LPP)
C
C  THE THREE VARIABLES FOR EACH POINT OF THE BACKWATER CURVE ARE
C  ENTERED IN THE SEQUENCE OF THE BACKWATER OUTFLOW, THE BACKWATER
C  ELEVATION, AND THE CONTROL PARAMETER (DISCHARGE OR ELEVATION OF
C  DOWNSTREAM RESERVOIR)
      NVALBK = NVAL*3
      DO 160 I=1,NVALBK
 160  WK(L+L2+I-1) = PO(LPP+I)
      LUSE = LUSE + NVALBK
      LPP = LPP + NVAL*3 + 1
C
C  STORE INITIAL SCAN INDEX IN BACKWATER TABLE
C
      DO 165 I=1,NVAL
      LWK = L+L2+(I-1)*3
      IF(WK(LWK).LT.WK(L+71)) GO TO 165
      NPA(L+35) = (I-1)*3
      GO TO 166
 165  CONTINUE
 166  CONTINUE
C
C  STORE BACKWATER CONTROL INDICATOR,
C  ICON1=1, FOR FLOW CONTROL; ICON1=2, FOR ELEVATION CONTROL
C
      ICON1 = PO(LPP)
      NPA(L+30) = ICON1
      LPP = LPP + 1
C
C  DOWNSTREAM RESERVOIR CONTROL
      IF(ISTYP.EQ.1) THEN
        NPA(L+50) = 2
        NPA(L+51) = 1
      END IF
C
C  DOWNSTREAM TRIBUTARY CONTROL
      IF(ISTYP.EQ.2) THEN
        NPA(L+50) = 3
        NPA(L+51) = 4
      END IF
C
C  STORE A VALUE LESS THAN -99999. TO SIGNAL END OF TABLE
C
      LUSE = LUSE + 1
      WK(L+LUSE) = -9999999.0
 180  CONTINUE
      NPA(L+1) = LUSE
C
C  STORE MAXIMUM ELEVATION
      EUB = PO(LPP)
      WK(L+33) = EUB
      LPP = LPP + 1
C
C  STORE MINIMUM ELEVATION
      ELB = PO(LPP)
      WK(L+34) = ELB
      LPP = LPP + 1
C
C  STORE MINIMUM RESERVOIR RELEASE
      QRELMN = PO(LPP)
      WK(L+31) = QRELMN
      LPP = LPP + 1
C
C  STORE MAXIMUM TRIBUTARY FLOW TO SHUT DOWN RESERVOIR RELEASE
      QSHUT = PO(LPP)
      WK(L+36) = QSHUT
      LPP = LPP + 1
C
      IF (IBUG.LT.2 .OR. LUSE.LE.0) GO TO 190
      IRTYP = ISTYP+1
      WRITE(IODBUG,1600) RESTYP(IRTYP)
 1600 FORMAT(//,' *** NPA ARRAY FOR ',A8,
     & 'BACKWATER RESERVOIR IN XWKS51 ***')
      DO 185 I=1,LUSE,8
      IE = I+7
      WRITE(IODBUG,1605) I,IE
 1605 FORMAT(' *** POS. NOS. :',I4,' THRU ',I4)
      WRITE(IODBUG,1610) (WK(L+J),J=I,IE)
  185 CONTINUE
 1610 FORMAT(8F10.0)
C
 190  L = L + LUSE
      GO TO 100
C
C
C  THE THREE-VARIABLE RELATIONSHIPS
C        GIVEN 1ST INDEPENDENT ARGUMENT (A1), 2ND INDEPENDENT ARGUMENT
C         (A2), AND TABLE OF FAMILY OF CURVES (T ARRAY) AS LOADED FROM
C         CARD CODE CF CARDS, INTERPOLATE FOR DEPENDENT RESULT (R).
C         TABLE (T) IS ARRANGED AS FOLLOWS-
C         T(1) IS LENGTH OF TABLE
C         T(2) 1ST VALUE OF 2ND INDEP VARIABLE (START OF Z SEGMENT)
C         T(3)-T(4),
C         T(5)-T(6),
C         T(7)-T(8), PAIRS OF WORDS CONTAINING 1ST INDEP, DEPENDENT
C          VALUE OF POINT ON SURFACE. FOR INSTANCE, T(2) T(5) T(6) ARE
C          2ND INDEP, 1ST INDEP, DEPENDENT VALUES OF ONE POINT ON
C          SURFACE.
C         T(9)-T(15) AND EACH 7 WORD GROUP THEREAFTER ARE SAME AS T(2)-
C          T(8). (7 WORD GROUP IS CALLED Z SEGMENT IN PROGRAM)
C        EXAMPLE-IF 1ST INDEPENDENT VARIABLE IS X, 2ND IS Z, AND
C         DEPENDENT VARIABLE IS Y, TABLE IS IN FOLLOWING ORDER-
C          L,Z1,X11,Y11,X12,Y12,X13,Y13,Z2,X21,Y21,X22,Y22,X23,Y23,ETC.
C
 300  CONTINUE
      L2 = LUSE + 1
      NPA(L+2) = 4
      NPA(L+49) = L2-6
      NPA(L+50) = 2
      NPA(L+51) = 1
      NVAL = PO(LPP)
      LPX=LPP+NVAL*3+1
      LPZ=LPX+1
      LPY=LPZ+1
      IQE(1)=PO(LPZ)
      IQE(2)=PO(LPX)
      IQE(3)=PO(LPY)
      IZ = 1
      IW = 1
      IST = 1
      DO 360 K=1,NVAL
      IPO = (K-1)*3
      IPO1 = 3*K
      DO 350 I=IST,3
C
C  STORE ARRAY IN INPUT UNITS
C  TO GET CORRECT LINEAR INTERPOLATION IN DIMENSIONAL FORM IN LU3F51
      SCALE=1.
      IF(IQE(I).EQ.2) SCALE=100.
      IW=IW+1
 350  WK(L+L2+IW-1) = PO(LPP+IPO+I)*SCALE
      IZ = IZ+2
      IST = 2
      POX=PO(LPP+IPO+1)
      POX1=PO(LPP+IPO1+1)
      IF (POX1.LE.POX) GO TO 330
      IIS = IZ/2 + 1
      IF(IIS.GT.3) GO TO 320
C
C  FILLUP 3 PAIRS WITH 0.0
      DO 310 IZI=IIS,3
      DO 310 I=IST,3
      IW=IW+1
 310  WK(L+L2+IW-1) = 0.0
 320  IZ = 1
      IST=1
      GO TO 360
 330  CONTINUE
      IF (IZ.GE.7) THEN
        IZ = 1
        IST=1
      END IF
 360  CONTINUE
      IIS = IZ/2 + 1
      IF(IIS.GT.3) GO TO 375
C
C  FILLUP 3 PAIRS WITH 0.0
      DO 370 IZI=IIS,3
      DO 370 I=IST,3
      IW=IW+1
 370  WK(L+L2+IW-1) = 0.0
 375  CONTINUE
C
C  STORE NUMBER OF VALUES IN THE THREE-VARIABLE RELATIONSHIPS
      WK(L+L2) = IW
      LUSE = LUSE+IW+1
      LPP = LPP+NVAL*3+1
C
C  STORE CONTROL1 INDICATOR (X),
C  ICON1=1, FOR FLOW CONTROL; ICON1=2, FOR ELEVATION CONTROL
C
      ICON1 = PO(LPP)
      NPA(L+30) = ICON1
      NPA(L+56) = ICON1
      LPP = LPP + 1
C
C  STORE CONTROL2 INDICATOR (Z),
C  ICON2=1, FOR FLOW CONTROL; ICON2=2, FOR ELEVATION CONTROL
C
      ICON2 = PO(LPP)
      NPA(L+57) = ICON2
      LPP = LPP + 1
C
C  STORE INDICATOR AT SITE (Y),
C  ISITE=1, FOR FLOW CONTROL; ISITE=2, FOR ELEVATION CONTROL
C
      ISITE = PO(LPP)
      NPA(L+55) = ISITE
      LPP = LPP + 1
C
      NPA(L+1) = LUSE
C
      IF (IBUG.LT.2 .OR. LUSE.LE.0) GO TO 390
      WRITE(IODBUG,1601) 
 1601 FORMAT(//,' *** NPA ARRAY FOR THREE-VARIABLE RELATION ',
     & 'WITHOUT BACKWATER ROUTING IN XWKS51 ***')
      DO 385 I=1,LUSE,8
      IE = I+7
      WRITE(IODBUG,1605) I,IE
      WRITE(IODBUG,1610) (WK(L+J),J=I,IE)
  385 CONTINUE
C
 390  L = L + LUSE
 100  CONTINUE
      LUSE = L-1
C
C---------------------------
C  THE NEXT PORTION OF WORK SPACE IS USED FOR HOLDING SIMULATED TRACES
C  FOR INSTANTANEOUS DISCHARGE AT PERIOD START AND END,
C  MEAN DISCHARGE, POOL ELEVATION AND STORAGE CONTENTS.
C  ENOUGH SPACE IS NEEDED TO HOLD A VALUE FOR EACH QUANTITY FOR EACH
C  PERIOD IN THE RUN.
C  EACH QUANTITY WILL BE FILLED CONTIGUOUSLY FOR TIME-SERIES PURPOSES.
C
      LTW = LUSE + 1
      LOCWS(NRES+1) = LTW

C dws    PO(12) was placed into an integer to replace it in the next
C dws     statement to avoid compiler warnings ... 2006-01-23
 
      NUPO12 = PO(12)

      NTTS = PO(NUPO12)
      NTS = (NTTS-2)/NRES-5
      NDO = NTS*NDD*NTIM24
      DO 500 II=1,NRES
C
C  INITIALIZE SPACE TO 'MISSING' CONDITION.
C
      IRES = II
      LTWI = LTW + (NRES-IRES)*NDO
      DO 515 I=1,NDO
      WK(LTWI+I-1) = -999.0
  515 CONTINUE
      LUSE = LUSE + NDO
 500  CONTINUE
C
C---------------------------------------------------------------
C  NEXT PORTION OF THE WORK SPACE IS RESERVED FOR CARRYOVER STORAGE.
C  SSARRESV NEEDS TO KEEP A RECORD OF CARRYOVER FOR INTERNAL USE FOR
C  EACH TIME INTERVAL.
C  WE NEED AN AMOUNT EQUAL TO THE NUMBER OF CARRYOVER VALUES USED BY
C  THIS DEFINITION OF THE OPERATION. (HELD IN 15TH POSITION OF THE PO
C  ARRAY)
C
      LCW = LUSE + 1
      LOCWS(NRES+2) = LCW
      LCO = 1
C
C  TRANSFER INFLOW CARRYOVER
      WK(LCW) = CO(LCO)
      LUSE = LUSE+1
      LCO = LCO+1
      LCW = LCW+1
      DO 600 II=1,NRES
C
C  TRANSFER CARRYOVER (DISCHARGE, ELEVATION, AND STORAGE) TO WORK ARRAY
C
      IRES = II
      LCOI = LCO + (NRES-IRES)*4
      LCWI = LCW + (NRES-IRES)*4
C
C  TRANSFER INSTANTANEOUS DISCHARGE CARRYOVER
      WK(LCWI) = CO(LCOI)
C
C  TRANSFER ELEVATION  CARRYOVER
      WK(LCWI+1) = CO(LCOI+1)
C
C  TRANSFER STORAGE CARRYOVER
      WK(LCWI+2) = CO(LCOI+2)
C
C  TRANSFER LOCAL FLOW (MAY INCLUDE TRIBUTARY FLOW)
      WK(LCWI+3) = CO(LCOI+3)
      LUSE = LUSE + 4
C
C---------------------------------------------------------------
C  NEXT PORTION OF THE WORK SPACE IS RESERVED FOR REGULATION OPTION
C
 600  CONTINUE
C
      IF(IBUG.LT.2) GO TO 5006
      DO 5005 I=1,NRES
         IZ=RRC(1,I)
         IF (IZ.LE.7) GOTO 5005
         WRITE(IODBUG,5010) (RRC(M,I),M=1,7)
5010     FORMAT(//5X,'RRC =',7F10.1)
         WRITE(IODBUG,5015) (RRC(M,I),M=8,IZ)
5015     FORMAT(5X,'RRC =',6F10.1)
5005  CONTINUE
 5006 CONTINUE
C
      LRCW = LUSE+1
      NRRCT = 0
      DO 650 I=1,6
 650  CONV(I)=1.0
C
C  VARIABLES IN COMMON BLOCK MOD151 ARE STORED IN ENGLISH UNITS
C  IF SEGDEF UNITS IS IN ENGLISH, NO CONVERSION NEEDED
      IF(METSAR.NE.1) GO TO 690
C
C  IF SEGDEF UNITS IS IN METRIC, CONVERT FROM ENGLISH TO METRIC
C  CQ -- CFS TO CMS; CL -- FT TO M;
C  CS -- ACFT TO TCUM (THOUSAND CUBIC METERS)
      CQ = 0.028316874
      CL = 0.3048
      CS = 1.2334818
      CONV(1) = CQ
      CONV(2) = CL
      CONV(3) = CS
      CONV(4) = CQ
      CONV(5) = CL
      CONV(6) = CQ
 690  DO 700 II=1,NRES
      IRES = II
      L = LOCWS(IRES)-1
      NPA(L+25) = LRCW
      NRRC = RRC(1,II)
      IF(NRRC.LE.7) NRRC=7
C      IF(NRRC.LE.7) THEN
C        NPA(L+25)=0
C        GO TO 700
C      ENDIF
      NRRCT = NRRCT+NRRC
      DO 710 I=2,6
 710  NPA(LRCW+I-1) = RRC(I,II)
      NPA(LRCW) = NRRC
      NPA(LRCW+6) = 100
      LRCW = LRCW+7
      IF(NRRC.LE.7) GO TO 725
      NRR = (NRRC-7)/3
      DO 720 IC=1,NRR
      I = (IC-1)*3+8
C
C  STORE TIME IN TENTHS OF HOURS (INTEGER)
C      NPA(LRCW) = RRC(I,II)*10.
      IDUM = IFIX(RRC(I,II))-IFIX(RRC(3,II))
      NPA(LRCW) = IDUM*10
      LRCW = LRCW+1
C
C  STORE VALUE, OUTFLOW, ELEVATION, STORAGE (REAL)
      ICODE = RRC(I+2,II)
      WK(LRCW) = RRC(I+1,II)*CONV(ICODE)
      LRCW = LRCW+1
C
C  STORE DATA CODE OF VALUE (INTEGER)
      NPA(LRCW) = ICODE
      LRCW = LRCW+1
 720  CONTINUE
 725  LUSE = LUSE+NRRC
 700  CONTINUE
C STORE POINTER FOR TIME SERIES WORK SPACE
      LOCWS(NRES+3) = LRCW+1
C
      IF (IBUG.LT.2 .OR. NRRCT.LE.0) GO TO 9000
      NST = NPA(25)
      NND = NST+NRRCT
      WRITE(IODBUG,1620)
 1620 FORMAT(//,' *** WORK ARRAY FOR REGULATION OPTION ***')
      DO 751 I=NST,NND,8
      IE = I+7
      WRITE(IODBUG,1605) I,IE
      WRITE(IODBUG,1611) (NPA(J),J=I,IE)
      WRITE(IODBUG,1610) (WK(J),J=I,IE)
  751 CONTINUE
 1611 FORMAT(8I10)
C
C  PRINT OUT ALLOCATION LOCATIONS FOR REQUIRED AND OPTIONAL WORK SPACE
C  SINGLE RESERVOIR -- 4 LOCATION REQUIRED, THEY ARE
C                   PARAMETER, TIME SERIES, CARRYOVER, REGULATION RECORD
C  TWO RESERVOIR -- 5 LOCATION REQUIRED, THEY ARE
C                   PARAMETER FOR BACKWATER RESERVOIR,
C                   PARAMETER FOR NON-BACKWATER RESERVOIR,
C                   TIME SERIES, CARRYOVER, REGULATION RECORD
C                   
C
 9000 CONTINUE
      LWK = PO(16)
      KWS = NRES+2
      IF (IBUG.LT.2) GO TO 850
      WRITE(IODBUG,1690) LUSE,LWK
 1690 FORMAT(//1X,'NO. OF WORDS OF WORK SPACE USED= ',I5,
     & 5X,'NO. OF WORDS AVAILABLE, PO(16) = ',I5,
     & /1X,'START LOCATIONS FOR REQUIRED WORK SPACE:')
      IF(NRES.EQ.1) WRITE(IODBUG,1691) (LOCWS(K),K=1,KWS),NST
 1691 FORMAT(5X,'TRIBU-LAKE-PARM, TIME-SERIES, CARRYOVER,',
     & ' REGULATION-INFORMATION = ',5I5)
      IF(NRES.EQ.2) WRITE(IODBUG,1692) (LOCWS(K),K=1,KWS),NST
 1692 FORMAT(
     & 5X,'UPPER-LAKE-PARM, LOWER-LAKE-PARM, TIME-SERIES,',
     & ' CARRYOVER, REGULATION-INFORMATION =',5I5)
  850 CONTINUE
C
      IF (ITRACE.GE.1) WRITE(IODBUG,901)
  901 FORMAT(11X,'*** EXIT XWKS51 ***')
C
      RETURN
      END
