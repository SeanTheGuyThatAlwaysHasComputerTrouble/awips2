C MEMBER SWMAP
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 03/01/94.14:03:05 BY $WC20SV
C
C @PROCESS LVL(77)
C
C DESC WRITE OR UPDATE AREA MAP PARAMETER RECORD
C
      SUBROUTINE SWMAP (IVMAP,XMAPID,ITIME,ITMWT,POWER,NSTWT,
     *   ISTWT,NPCPN,NSETS,DESCRP,BASNID,FMAPID,CENTRD,TSTAID,
     *   TSTAWT,CTSTA,CSTAID,CSTAWT,CCSTA,UNUSED,LARRAY,ARRAY,
     *   IPTR,DISP,NSPACE,ISTAT)
C
      REAL XNEW/4HNEW /
      REAL XOLD/4HOLD /
C
      DIMENSION ARRAY(LARRAY)
      DIMENSION XMAPID(2),DESCRP(*),BASNID(*),FMAPID(*),CENTRD(*)
      DIMENSION TSTAID(2,1),TSTAWT(*),CTSTA(2,1),CSTAID(2,1)
      DIMENSION CSTAWT(NPCPN,1),CCSTA(2,1),UNUSED(*)
C
      INCLUDE 'uio'
      INCLUDE 'scommon/sudbgx'
      INCLUDE 'scommon/suoptx'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/ppinit_write/RCS/swmap.f,v $
     . $',                                                             '
     .$Id: swmap.f,v 1.1 1995/09/17 19:16:10 dws Exp $
     . $' /
C    ===================================================================
C
C
C
      IF (ISTRCE.GT.1) WRITE (IOSDBG,190)
      IF (ISTRCE.GT.1) CALL SULINE (IOSDBG,1)
C
C  SET DEBUG LEVEL
      LDEBUG=ISBUG('MAP ')
C
      IF (LDEBUG.GT.0) WRITE (IOSDBG,210) IVMAP,LARRAY
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
C
      ISTAT=0
      NUMERR=0
C
C  CHECK FOR SUFFICIENT SPACE IN PARAMETER ARRAY
      ITM=1
      IF (ITMWT.EQ.1) ITM=0
      MINLEN=24 + 3*NSTWT + (4+NSETS)*NPCPN + ITM*2*NSTWT
      IF (LDEBUG.GT.0) WRITE (IOSDBG,230) MINLEN
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
      IF (MINLEN.LE.LARRAY) GO TO 10
         WRITE (LP,240) LARRAY,MINLEN
         CALL SUERRS (LP,2,NUMERR)
         ISTAT=1
         GO TO 180
C
C  STORE PARAMETER ARRAY VERSION NUMBER
10    ARRAY(1)=IVMAP+.01
C
C  STORE MAP AREA IDENTIFIER
      ARRAY(2)=XMAPID(1)
      ARRAY(3)=XMAPID(2)
C
      NPOS=3
C
C  STORE DESCRIPTIVE INFORMATION
      DO 20 I=1,5
      NPOS=NPOS+1
      ARRAY(NPOS)=DESCRP(I)
20    CONTINUE
C
C  STORE TIME INTERVAL (HOURS)
      NPOS=NPOS+1
      ARRAY(NPOS)=ITIME +.01
C
C  STORE BASIN BOUNDARY IDENTIFIER
      DO 30 I=1,2
      NPOS=NPOS+1
      ARRAY(NPOS)=BASNID(I)
30    CONTINUE
C
C  STORE TYPE OF TIMING WEIGHTS
      NPOS=NPOS+1
      ARRAY(NPOS)=ITMWT +.01
C
C  STORE EXPONENT IN 1/D**POWER
      NPOS=NPOS+1
      ARRAY(NPOS)=POWER
C
C  STORE NUMBER OF STATIONS USED FOR TIME DISTRIBUTION
      NPOS=NPOS+1
      ARRAY(NPOS)=NSTWT +.01
C
C  STORE TYPE OF STATION WEIGHTS
      NPOS=NPOS+1
      ARRAY(NPOS)=ISTWT +.01
C
C  STORE IDENTIFIER OF FUTURE MAP AREA ASSIGNED TO AREA
      DO 40 I=1,2
      NPOS=NPOS+1
      ARRAY(NPOS)=FMAPID(I)
40    CONTINUE
C
C  STORE CENTROID (NWSRFS/HRAP COORDINATES)
      DO 50 I=1,2
      NPOS=NPOS+1
      ARRAY(NPOS)=CENTRD(I)
50    CONTINUE
C
C  THE NEXT TWO POSITIONS ARE UNUSED
      DO 60 I=1,2
      NPOS=NPOS+1
      ARRAY(NPOS)=UNUSED(I)
60    CONTINUE
C
C  STORE NUMBER OF STATIONS USED TO COMPUTE MAP
      NPOS=NPOS+1
      ARRAY(NPOS)=NPCPN +.01
C
C  STORE NUMBER OF SETS OF STATION WEIGHTS
      NPOS=NPOS+1
      ARRAY(NPOS)=NSETS +.01
C
C  STORE STATION IDENTIFIERS OF TIMING STATIONS
      DO 80 J=1,NSTWT
      DO 70 I=1,2
      NPOS=NPOS+1
      ARRAY(NPOS)=TSTAID(I,J)
70    CONTINUE
80    CONTINUE
C
C  STORE TIMING WEIGHTS
      DO 90 I=1,NSTWT
      NPOS=NPOS+1
      ARRAY(NPOS)=TSTAWT(I)
90    CONTINUE
      IF (ITMWT.EQ.1) GO TO 110
C
C  STORE NWSRFS/HRAP COORDINATES OF TIMING STATIONS
      DO 100 J=1,NSTWT
      DO 100 I=1,2
      NPOS=NPOS+1
      ARRAY(NPOS)=CTSTA(I,J)
100   CONTINUE
110   CONTINUE
C
C  STORE STATION IDENTIFIERS OF STATIONS USED TO COMPUTE MAP
      DO 120 J=1,NPCPN
      DO 120 I=1,2
      NPOS=NPOS+1
      ARRAY(NPOS)=CSTAID(I,J)
120   CONTINUE
C
C  STORE 24-HR MAP STATION WEIGHTS
      DO 130 I=1,NSETS
      DO 130 J=1,NPCPN
      NPOS=NPOS+1
      ARRAY(NPOS)=CSTAWT(J,I)
130   CONTINUE
C
C  STORE NWSRFS/HRAP COORDINATES OF STATIONS USED TO COMPUTE MAP
      DO 140 J=1,NPCPN
      DO 140 I=1,2
      NPOS=NPOS+1
      ARRAY(NPOS)=CCSTA(I,J)
140   CONTINUE
C
C
C  WRITE PARAMTER RECORD TO FILE
      IF (LDEBUG.GT.0) WRITE (IOSDBG,220) NPOS
      IF (LDEBUG.GT.0) CALL SULINE (IOSDBG,1)
      CALL SUDOPN (1,'PPP ',IERR)
      IPTR=0
      CALL WPPREC (XMAPID,'MAP ',NPOS,ARRAY,IPTR,IERR)
      IF (IERR.EQ.0) GO TO 150
         CALL SWPPST (XMAPID,'MAP ',NPOS,IPTR,IERR)
         WRITE (LP,250) IERR
         CALL SUERRS (LP,2,NUMERR)
         ISTAT=2
C
150   IF (LDEBUG.GT.0) CALL SUPDMP ('MAP ','BOTH',0,NPOS,ARRAY,ARRAY)
C
      IF (ISTAT.GT.0) GO TO 170
C
C  CHECK MESSAGE LEVEL VALUE
      IF (IOPMLV.LE.1) GO TO 160
C
      IF (NSPACE.EQ.1) WRITE (LP,200)
      IF (NSPACE.EQ.1) CALL SULINE (LP,1)
      IF (DISP.EQ.XNEW) WRITE (LP,260) XMAPID
      IF (DISP.EQ.XOLD) WRITE (LP,270) XMAPID
      CALL SULINE (LP,1)
160   CALL SUDWRT (1,'PPP ',IERR)
      GO TO 180
C
170   IF (DISP.EQ.XNEW) WRITE (LP,280) XMAPID
      IF (DISP.EQ.XOLD) WRITE (LP,290) XMAPID
      CALL SULINE (LP,2)
C
180   IF (ISTRCE.GT.1) WRITE (IOSDBG,300)
      IF (ISTRCE.GT.1) CALL SULINE (IOSDBG,1)
C
      RETURN
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
190   FORMAT (' *** ENTER SWMAP')
200   FORMAT (1H )
210   FORMAT (' IVMAP=',I2,3X,'LARRAY=',I5)
220   FORMAT (' NPOS=',I2)
230   FORMAT (' MINLEN=',I3)
240   FORMAT ('0*** ERROR - IN SWMAP - NOT ENOUGH SPACE IN PARAMETER ',
     *   'ARRAY: NUMBER OF WORDS IN PARAMETER ARRAY=',I5,3X,
     *   'NUMBER OF WORDS NEEDED=',I5)
250   FORMAT ('0*** ERROR - IN SWMAP - UNSUCCESSFUL CALL TO WPPREC : ',
     *   'STATUS CODE=',I3)
260   FORMAT (' *** NOTE - MAP  PARAMETERS SUCCESSFULLY ',
     *   'WRITTEN FOR AREA ',2A4,'.')
270   FORMAT (' *** NOTE - MAP  PARAMETERS SUCCESSFULLY ',
     *   'UPDATED FOR AREA ',2A4,'.')
280   FORMAT ('0*** NOTE - MAP  PARAMETERS NOT SUCCESSFULLY ',
     *   'WRITTEN FOR AREA ',2A4,'.')
290   FORMAT ('0*** NOTE - MAP  PARAMETERS NOT SUCCESSFULLY ',
     *   'UPDATED FOR AREA ',2A4,'.')
300   FORMAT (' *** EXIT SWMAP')
C
      END
