C MODULE XFMAPD
C-----------------------------------------------------------------------
C
      SUBROUTINE XFMAPD (FMAPIN,LFMAPIN,MAXMOD,NUMMOD,IMGMOD,IERR)
C
C  *********************************************************************
C  XFMAPD DECODES THE FMAP24 MOD
C  *********************************************************************
C
C  IMPORTANT VARIABLES INCLUDE:
C
C       ICMDCD = FLAG TO INDICATE WHETHER A COMMAND CARD IS BEING
C                DECODED:
C                  1=COMMAND CARD
C                  0=IDENTIFIER CARD
C       NFIELD = FIELD NUMBER OF IBUF ARRAY BEING PROCESSED
C       IMGMOD = MOD CARDS RETURNED FROM HMODCK
C       FMAPIN = DECODED INPUT ARRAY
C       MAXMOD = MAXIMUM MOD CARDS ALLOWED
C         NMOD = NUMBER OF MODS THAT HAVE BEEN PROCESSED
C       NIDENT = NUMBER OF IDENTIFIER CARDS THAT HAVE BEEN PROCESSED
C                FOR THE MOD BEING PROCESSED
C         NTID = THE TOTAL NUMBER OF IDENTIFIER CARDS THAT HAVE BEEN
C                PROCESSED
C       LIDENT = THE LOCATION OF THE IDENTIFIER CARD COUNTS FOR EACH
C                MOD IN ARRAY FMAPIN
C
C  SIZE NEEDED FOR FMAPIN ARRAY IS 10 TIMES THE MAXIMUM
C    NUMBER OF MOD CARDS ALLOWED (MAXMOD).
C
C  CONTENTS OF FMAPIN ARE AS FOLLOWS.
C
C   POSITION   CONTENTS
C   --------   --------
C       1      NUMBER OF .FMAP24 MODS IN RUN
C     2-9      CONTENTS OF FIRST COMMAND CARD.
C       2      NUMBER OF IDENTIFIER CARDS FOR THE FIRST MOD.
C       3      JULIAN HOUR FOR FIRST MOD (INTERNAL TIME -- END OF
C                HYDROLOGIC DAY)
C       4      TYPE OF IDENTIFIERS
C                0.01 = CHARACTERS
C                1.01 = NUMBERS
C       5      UNITS
C                0.01 = ENGLISH
C                1.01 = METRIC
C     6-9      DISTRIBUTION VALUES ON MOD CARD - NOT NORMALIZED
C                (ALL 0.0 -- UNIFORM)
C   10-19      CONTENTS OF FIRST IDENTIFIER CARD.
C      10      0.01 = SINGLE ID  --  1.01 = RANGE OF IDS
C   11-12      IDENTIFIER (FIRST IF RANGE)
C                BOTH POSITIONS IF CHARACTER, JUST 12 IF NUMBER
C   13-14      SECOND IDENTIFIER (FILLED FOR RANGE ONLY)
C      15      DAILY PRECIPITATION
C   16-19      DISTRIBUTION FOR THESE IDS (NON-NORMALIZED)
C                IF = 0.0 USE DISTRIBUTION ON MOD CARD
C  REPEAT 10-19 FOR EACH IDENTIFIER CARD.
C  REPEAT 2-19 FOR EACH MOD CARD
C
      CHARACTER*8 OLDOPN
      CHARACTER*80 IMGMOD(MAXMOD)
C
      DIMENSION FMAPIN(LFMAPIN)
C
      INCLUDE 'ufreei'
      INCLUDE 'common/ionum'
      INCLUDE 'common/pudbug'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_fmap/RCS/xfmapd.f,v $
     . $',                                                             '
     .$Id: xfmapd.f,v 1.4 2002/02/11 20:37:23 dws Exp $
     . $' /
C    ===================================================================
C
      DATA DOT/4H.   /,M/4HM   /,SLASH/4H/   /
C
C
      IOPNUM=-1
      CALL FSTWHR ('XFMAPD  ',IOPNUM,OLDOPN,IOLDOP)
C
      IF (IPTRCE.GT.0) WRITE (IOPDBG,*) 'ENTER XFMAPD'
C
      IBUG=IPBUG('XFD ')
C
C  GET FMAP24 MODS
      CALL HMODCK ('FMAP24  ',MAXMOD,IMGMOD,NUMMOD,ISTAT)
      IF (ISTAT.NE.0) THEN
         IF (ISTAT.EQ.1) THEN
            WRITE (IPR,500) MAXMOD,MAXMOD
            CALL WARN
            GO TO 10
            ENDIF
         IF (ISTAT.EQ.2) THEN
            WRITE (IPR,510)
            CALL ERROR
            ENDIF
         IERR=1
         GO TO 660
         ENDIF
C
C  CHECK IF ANY FMAP24 MOD CARDS FOUND
10    IF (NUMMOD.EQ.0) GO TO 660
C
      NIDENT=0
      NTID=0
      LIDENT=2
      ICMDCD=0
      NMOD=0
      DO 40 K=1,LFMAPIN
         FMAPIN(K)=.01
40       CONTINUE
C
C  PROCESS EACH MOD CARD
      DO 450 I=1,NUMMOD
         IF (IBUG.GT.0) WRITE (IOPDBG,*) 'I=',I,
     *      ' IMGMOD(I)=',IMGMOD(I)
         NOCDIS=0
         CALL UNPAKS (IMGMOD(I),IBUF,20,80,ISTAT)
         IBEG=1
         IEND=72
         CALL UFREE (IBEG,IEND)
         IF (IBUG.GT.0) WRITE (IOPDBG,70) 'NFIELD=',NFIELD
         IF (NFIELD.EQ.0) THEN
C        NO FIELDS ENCOUNTERED ON THE MOD CARD
           WRITE (IPR,520)
           CALL WARN
           GO TO 450
           ENDIF
C     SEARCH FOR A COMMAND CARD
         IFIELD=1
         ISTART=IFSTRT(IFIELD)
         IEND=IFSTOP(IFIELD)
         CALL USRCHR (DOT,ISTART,IEND,INDEX)
         IF (INDEX.EQ.0) GO TO 260
C     THIS IS A COMMAND CARD
         IFIELD=2
         ISTART=IFSTRT(IFIELD)
         IEND=IFSTOP(IFIELD)
         ICMDCD=0
C     CHECK DATE FIELD FOR AN ASTERISK - NOT VALID FOR FUTURE MAP
         CALL USRAST (IBUF,ISTART,IEND,INDEX)
         IF (INDEX.EQ.IEND+1) GO TO 60
            WRITE (IPR,530)
            WRITE (IPR,640) IBUF
            CALL ERROR
            GO TO 450
C     DECODE THE DATE FIELD
60       IF (IBUG.GT.0) WRITE (IOPDBG,70) (IBUF(J),J=ISTART,IEND)
70    FORMAT (' DATE FIELD = ',8A1)
         CALL HDATEC (ISTART,IEND,0,1,1,JDAY,IHR,JHR,ISTAT)
         IF (IBUG.GT.0) THEN
            WRITE (IOPDBG,*) 'HDATEC ISTAT=',ISTAT
            WRITE (IOPDBG,70) (IBUF(J),J=ISTART,IEND)
            WRITE (IOPDBG,100) JDAY,IHR,JHR
100   FORMAT (' JDAY = ',I10,' IHR = ',I10,' JHR = ',I10)
            ENDIF
        IF (ISTAT.NE.0) THEN
            WRITE (IPR,635)
            WRITE (IPR,640) IBUF
            CALL ERROR
            GO TO 450
            ENDIF
C     CHECK IF INPUT DATE IS THE LAST HOUR FOR FOR THAT DATE
         IF (IHR.EQ.24.OR.IHR.EQ.0) GO TO 130
            WRITE (IPR,540) NFIELD
            WRITE (IPR,640) IBUF
            CALL ERROR
            GO TO 450
C     BEGIN FILLING THE DECODED INPUT ARRAY (FMAPIN)
130      N=8*(NMOD)+10*NTID+3
         FMAPIN(N)=JHR+.01
         ICON=N+1
         IMET=N+2
C     CHECK FOR COMPUTATIONAL ORDER FLAG
         IF (NFIELD.LT.3) GO TO 230
         IFIELD=3
         ISTART=IFSTRT(IFIELD)
         IEND=IFSTOP(IFIELD)
         CALL USRCHR (4HN   ,ISTART,ISTART,INDEX)
         IF (INDEX.NE.0) FMAPIN(ICON)=1.01
C     CHECK FOR METRIC FLAG
         IF (INDEX.EQ.0) GO TO 140
            IF (NFIELD.LT.4) GO TO 230
            IFIELD=4
            ISTART=IFSTRT(IFIELD)
            IEND=IFSTOP(IFIELD)
140      CALL USRCHR (M,ISTART,ISTART,INDEX)
         IF (INDEX.NE.0) FMAPIN(IMET)=1.01
         IF (FMAPIN(IMET).GT.1.00.AND.NFIELD.EQ.3) GO TO 230
C     CHECK FOR A TIME DISTRIBUTION ON THE COMMAND CARD
         IF (INDEX.EQ.0.AND.FMAPIN(ICON).LT.1.0) GO TO 160
         IF (NFIELD.EQ.4.AND.
     *       FMAPIN(ICON).GT.1.0.AND.FMAPIN(IMET).GT.1.0) GO TO 230
         IF ((FMAPIN(ICON).GT.1.0.AND.FMAPIN(IMET).LT.1.0).OR.
     *       (FMAPIN(ICON).LT.1.0.AND.FMAPIN(IMET).GT.1.0)) GO TO 150
         IFIELD=5
         ISTART=IFSTRT(5)
         IEND=IFSTOP(5)
         GO TO 160
150      IFIELD=4
         ISTART=IFSTRT(4)
         IEND=IFSTOP(4)
160      CALL USRCHR (SLASH,ISTART,ISTART,INDEX)
         DO 170 J=1,4
            FMAPIN(N+2+J)=0.0
170         CONTINUE
            NOCDIS=1
         IF (INDEX.NE.0) GO TO 180
            WRITE (IPR,560)
            WRITE (IPR,640) IBUF
            CALL WARN
            GO TO 230
C     CHECK FOR A SPACE AFTER THE SLASH
180      IF (IEND-ISTART.EQ.0) GO TO 190
            WRITE (IPR,620)
            WRITE (IPR,640) IBUF
            CALL WARN
            GO TO 230
C     CHECK IF NUMBER OF FIELDS DOES NOT EXCEED THE NUMBER EXPECTED
190      L=NFIELD-IFIELD
         IF (L.LE.4) GO TO 200
            L=4
            WRITE (IPR,570)
            WRITE (IPR,640) IBUF
            CALL WARN
200      DO 220 K=1,L
            CALL UFIXED (IBUF,FMAPIN(N+2+K),IFSTRT(IFIELD+K),
     *         IFSTOP(IFIELD+K),2,0,ISTAT)
            IF (ISTAT.EQ.0) GO TO 220
               JSTRT=IFSTRT(IFIELD+K)
               JEND=IFSTOP(IFIELD+K)
               JFIELD=IFIELD+K
               WRITE (IPR,580) JFIELD
               WRITE (IPR,640) IBUF
               CALL WARN
               DO 210 J=1,4
                  FMAPIN(N+2+J)=0.0
210               CONTINUE
               ICMDCD=1
               NOCDIS=1
               NMOD=NMOD+1
               IF (NMOD.GT.1) LIDENT=LIDENT+8+10*NIDENT
               NIDENT=0
               GO TO 450
220         CONTINUE
C     CHECK IF A COMMAND CARD TIME DISTRIBUTION HAS BEEN SET
230      IF (NOCDIS.GT. 0) GO TO 250
            DO 240 J=1,4
               FMAPIN(N+2+J)=0.0
240            CONTINUE
250      ICMDCD=1
         NMOD=NMOD+1
         IF (NMOD.GT.1) LIDENT=LIDENT+8+10*NIDENT
         NIDENT=0
         GO TO 450
C     THIS IS NOT A COMMAND CARD
C     CHECK TO SEE THAT A COMMAND CARD HAS BEEN SUCCESSFULLY DECODED
260      IF (ICMDCD.EQ.1) GO TO 270
            WRITE (IPR,590)
            WRITE (IPR,640) IBUF
            CALL ERROR
            GO TO 450
C  DECODE THE IDENTIFIER FIELD
270      IFIELD=1
         ISTART=IFSTRT(1)
         IEND=IFSTOP(1)
C     CHECK THE IDENTIFIER FIELD FOR A RANGE OF IDENTIFIERS
         CALL USRDSH (IBUF,ISTART,IEND,IDASH)
         N=8*(NMOD-1)+10*NTID+10
         IF (IDASH.NE.IEND+1) FMAPIN(N)=1.01
C     CHECK THE COMPUTATIONAL ORDER FLAG TO SEE IF IDENTIFIERS ARE
C     COMPUTATIONAL ORDER NUMBERS
         IF (FMAPIN(ICON).LT.1.0) GO TO 300
C     COMPUTATIONAL ORDER NUMBERS ARE BEING USED
         IF (FMAPIN(N).LT.1.0) GO TO 290
C     A RANGE HAS BEEN SPECIFIED
         CALL UNUMIC (IBUF,ISTART,IDASH-1,NUM)
         FMAPIN(N+2)=NUM+.01
         CALL UNUMIC (IBUF,IDASH+1,IEND,NUM)
         FMAPIN(N+4)=NUM+.01
         IF (IBUG.GT.0) WRITE (IOPDBG,280) FMAPIN(N+4)
280   FORMAT (' FMAPIN(N+4)= ',F10.2)
         GO TO 350
C     A RANGE WAS NOT SPECIFIED
290      CALL UNUMIC (IBUF,ISTART,IEND,NUM)
         FMAPIN(N+2)=NUM+.01
         GO TO 350
C     FMAP AREA IDENTIFIERS ARE BEING USED
300      IF (FMAPIN(N).LT.1.0) GO TO 330
C     A RANGE HAS BEEN SPECIFIED
         MAXLEN=8
         LENGTH=IDASH-ISTART
         IF (LENGTH.GT.MAXLEN) THEN
            WRITE (IPR,600) NFIELD,MAXLEN
            WRITE (IPR,640) IBUF
            CALL ERROR
            GO TO 450
            ENDIF
         CALL FMDPK8 (IBUF,ISTART,LENGTH,FMAPIN(N+1))
         LENGTH=IEND-IDASH
         IF (LENGTH.GT.MAXLEN) THEN
            WRITE (IPR,600) NFIELD,MAXLEN
            WRITE (IPR,640) IBUF
            CALL ERROR
            GO TO 450
            ENDIF
         CALL FMDPK8 (IBUF,IDASH+1,LENGTH,FMAPIN(N+3))
         GO TO 350
C     A RANGE WAS NOT SPECIFIED
330      MAXLEN=8
         LENGTH=IEND-ISTART+1
         IF (LENGTH.GT.MAXLEN) THEN
            WRITE (IPR,600) NFIELD,MAXLEN
            WRITE (IPR,640) IBUF
            CALL ERROR
            GO TO 450
            ENDIF
         CALL FMDPK8 (IBUF,ISTART,LENGTH,FMAPIN(N+1))
C     DECODE THE 24 HOUR PRECIP VALUE
350      IFIELD=2
         ISTART=IFSTRT(2)
         IEND=IFSTOP(2)
         CALL UFIXED (IBUF,FMAPIN(N+5),ISTART,IEND,2,0,ISTAT)
         IF (ISTAT.NE.0) THEN
            WRITE (IPR,610) NFIELD
            WRITE (IPR,640) IBUF
            CALL WARN
            ENDIF
C     DECODE THE TIME DISTRIBUTION ON THE IDENTIFIER CARD
         IF (NFIELD.LT.3) GO TO 370
            IFIELD=3
            ISTART=IFSTRT(3)
            IEND=IFSTOP(3)
            CALL USRCHR (SLASH,ISTART,ISTART,INDEX)
370      DO 380 J=1,4
            FMAPIN(N+5+J)=0.0
380         CONTINUE
         IF (NFIELD.LT.3) GO TO 440
         IF (INDEX.NE.0) GO TO 390
            WRITE (IPR,630)
            WRITE (IPR,640) IBUF
            CALL WARN
            GO TO 440
C     CHECK FOR A SPACE AFTER THE SLASH
390      IF (IEND-ISTART.EQ.0) GO TO 400
            WRITE (IPR,620)
            WRITE (IPR,640) IBUF
            CALL WARN
            GO TO 440
C     CHECK IF NUMBER OF FIELDS DOES NOT EXCEED THE NUMBER EXPECTED
400      L=NFIELD-IFIELD
         IF (L.LE.4) GO TO 410
            L=4
            WRITE (IPR,570)
            WRITE (IPR,640) IBUF
            CALL WARN
410      DO 430 K=1,L
            CALL UFIXED (IBUF,FMAPIN(N+5+K),IFSTRT(IFIELD+K),
     *         IFSTOP(IFIELD+K),2,0,ISTAT)
            IF (ISTAT.EQ.0) GO TO 430
               JSTRT=IFSTRT(IFIELD+K)
               JEND=IFSTOP(IFIELD+K)
               JFIELD=IFIELD+K
               WRITE (IPR,580) JFIELD
               WRITE (IPR,640) IBUF
               CALL WARN
               DO 420 J=1,4
                  FMAPIN(N+5+J)=0.0
420               CONTINUE
               NTID=NTID+1
               NIDENT=NIDENT+1
               FMAPIN(LIDENT)=NIDENT+.01
               GO TO 450
430         CONTINUE
440      NTID=NTID+1
         NIDENT=NIDENT+1
         FMAPIN(LIDENT)=NIDENT+.01
450      CONTINUE
C
      FMAPIN(1)=NMOD+.01
C
      IF (IBUG.GT.0) THEN
         N=NMOD*8+NTID*10+1
         WRITE (IOPDBG,460)
460   FORMAT (' CONTENTS OF FMAPIN AFTER DECODING .FMAP24 MODS:')
         WRITE (IOPDBG,470) (FMAPIN(J),J=1,N)
470   FORMAT (' ',10F10.2)
         WRITE (IOPDBG,480) (FMAPIN(J),J=1,N)
480   FORMAT (' ',10(6X,A4))
         ENDIF
C
660   CALL FSTWHR (OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C
      IF (IPTRCE.GT.0) WRITE (IOPDBG,*) 'EXIT XFMAPD'
C
      RETURN
C
500   FORMAT ('0**WARNING** MAXIMUM NUMBER OF .FMAP24 MOD CARDS (',I4,
     * ') EXCEEDED. ONLY THE FIRST ',I4,' WILL BE PROCESSED.')
510   FORMAT ('0**ERROR** FMAP24 NOT ALLOWED FOR THIS ',
     * 'ROUTINE OR INVALID MOD COMMAND.')
520   FORMAT ('0**WARNING** NO FIELDS ENCOUNTERED ON THE MOD ',
     * 'CARD. CARD SKIPPED.')
530   FORMAT ('0**ERROR** AN ASTERISK CANNOT BE USED AS A ',
     * 'DATE FOR FUTURE MAP MODS.')
540   FORMAT ('0**ERROR** THE DATE IN FIELD ',I2,' DOES NOT ',
     * 'END AT THE LAST HOUR OF THE HYDROLOGIC DAY. CARD SKIPPED.')
560   FORMAT ('0**WARNING** AN UNEXPECTED DATA FIELD WAS ',
     * 'ENCOUNTERED ON A FMAP24 MOD CARD WHERE A TIME ',
     * 'DISTRIBUTION WAS EXPECTED.' /
     * T13,'AN EQUAL DISTRIBUTION IS ASSUMED.')
570   FORMAT ('0**WARNING** MORE THAN 4 TIME DISTRIBUTION ',
     * 'VALUES WERE ENCOUNTERED. ONLY THE FIRST 4 WILL BE USED.')
580   FORMAT ('0**WARNING** FIELD ',I2,' CONTAINS ',
     * 'AN INVALID CHARACTER FOR A TIME DISTRIBUTION FIELD. ',
     * 'AN EQUAL DISTRIBUTION IS ASSUMED.')
590   FORMAT ('0**ERROR** AN IDENTIFIER CARD WAS ',
     * 'ENCOUNTERED BEFORE A VALID FMAP24 CARD ',
     * 'WAS FOUND. THE IDENTIFIER CARD WILL NOT BE PROCESSED.')
600   FORMAT ('0**ERROR** THE IDENTIFIER IN FIELD ',I2,
     * ' HAS MORE THAN ',I2,' CHARACTERS. THE CARD SKIPPED.')
610   FORMAT ('0**WARNING** THE VALUE IN FIELD ',I2,' CONTAINS AN ',
     * 'INVALID CHARACTER. THE PRECIP AMOUNT HAS BEEN SET TO ZERO.')
620   FORMAT ('0**WARNING** A SPACE MUST SEPARATE THE ',
     * 'SLASH AND THE FIRST VALUE IN A TIME DISTRIBUTION FIELD.' /
     * T13,'THE TIME DISTRIBUTION HAS BEEN SET TO EQUAL.')
630   FORMAT ('0**WARNING** A DATA FIELD WAS ENCOUNTERED ',
     * 'AFTER A FUTURE PRECIP AMOUNT THAT DID NOT CONTAIN A SLASH.' /
     * T13,'THE TIME DISTRIBUTION HAS BEEN SET TO EQUAL.')
635   FORMAT ('0**ERROR** INVALID DATE FIELD FOUND. CARD SKIPPED.')
640   FORMAT (' THE CARD PRINTED BELOW CAUSED THIS MESSAGE:' /
     * 1X,80A1)
C
      END
