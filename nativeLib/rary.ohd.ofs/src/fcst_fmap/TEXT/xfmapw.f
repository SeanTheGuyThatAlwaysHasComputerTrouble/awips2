C MODULE XFMAPW
C-----------------------------------------------------------------------
C
      SUBROUTINE XFMAPW (NUMDAY,FPX,MAXPX,MAXCON,NUMFPX,COLST,IFFHR,
     $   ILFHR,IFFPRD,DUNITS,IERR)
C
C  ROUTINE TO WRITE FUTURE PRECIP ARRAY TO PROCESSED DATA BASE TIME 
C  SERIES
C
C  IMPORTANT VARIABLES INCLUDE:
C
C     DUNITS = DEFAULT UNITS
C     FMAPID = FMAP TIME SERIES IDENTIFIER
C      IFFHR = THE LAST HOUR OF THE COMPUTATIONAL PERIOD (1 TO 24)
C              WHEN THE COMPUTATIONAL PERIOD DOES NOT END AT THE
C              END OF A DAY
C      ILFHR = THE LAST HOUR OF THE FUTURE PERIOD WHEN THE FUTURE
C              PERIOD DOES NOT END AT THE END OF A DAY
C     INTVAL = TIME STEP OF THE TIME SERIES
C     LIWBUF = DIMENSIONED LENGTH OF THE WORK BUFFER
C      LWBUF = COMPUTED LENGTH OF THE WORK BUFFER
C     MAXCON = HIGHEST COMPUTATIONAL ORDER NUMBER
C      NPDTX = NUMBER OF VALUES PER TIME STEP
C     NUMFPX = NUMBER OF DECODED MOD CARDS
C      NUMPX = NUMBER OF 6 HOUR INTERVALS IN THE FUTURE PERIOD
C         NX = NUMBER OF EXTRA SPACES IN THE TIME SERIES HEADER
C         PX = 6 HOUR FMAP VALUES
C
      CHARACTER*4 DTYPE,DTYPE2
      CHARACTER*8 OLDOPN,TECHNAME
      DIMENSION FPX(MAXPX,1),FMAPID(2),COLST(1)
      PARAMETER (LPX=124)
      DIMENSION PX(LPX)
      PARAMETER (LIWBUF=300)
      DIMENSION IWBUF(LIWBUF)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/pudbug'
      INCLUDE 'prdcommon/pdatas'
      INCLUDE 'common/fctim2'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_fmap/RCS/xfmapw.f,v $
     . $',                                                             '
     .$Id: xfmapw.f,v 1.4 2000/03/14 12:16:09 page Exp $
     . $' /
C    ===================================================================
C
      DATA XIN/4HIN  /
      DATA XMM/4HMM  /
C
C
      IOPNUM=-1
      CALL FSTWHR ('XFMAPW  ',IOPNUM,OLDOPN,IOLDOP)
C
      IF (IPTRCE.GT.0) WRITE (IOPDBG,*) 'ENTER XFMAPW'
C
      IBUG=IPBUG('XFW ')
C
      DTYPE='MAP'
      DTYPE2='FMAP'
C
C  CHECK SIZE OF THE WORK BUFFER
      IREC=-1
      NX=0
      NPDTX=1
      INTVAL=6
      IF (IBUG.GT.0) WRITE (IOPDBG,*) 'IFFHR=',IFFHR,' ILFHR',ILFHR
      NUMPX=NUMDAY*4-IFFHR/6
      IF (ILFHR.GT.0) NUMPX=NUMPX-(24-ILFHR)/6
      MAXDAY=IPRDMF(DTYPE)
      LWBUF=((((24/INTVAL)*NPDTX*MAXDAY+22+NX-1)/LRECLT)+1)*LRECLT
      IF (LWBUF.GT.LIWBUF) THEN
         WRITE (IPR,100) LIWBUF,LWBUF
         CALL ERROR
         IERR=1
         GO TO 80
         ENDIF
C
C  GET THE CURRENT VALUE FOR THE PRNTFMAP TECHNIQUE
      TECHNAME='PRNTFMAP'
      CALL HPAST (TECHNAME,IDISPL,ISTAT)
      IF (ISTAT.GT.0) CALL FPHPWN (ISTAT,TECHNAME)
      CALL FTEKCK (IDISPL,TECHNAME,1,IDISPL,0,1)
C
C  WRITE THE TIME SERIES FOR EACH COMPUTATIONAL ORDER NUMBER
      IWRTFL=0
      DO 70 I=1,MAXCON
C  SET VALUES FOR AN FMAP AREA WITH NO PRECIP INPUT
         DO 10 II=1,2
            FMAPID(II)=COLST(I*2-2+II)
10          CONTINUE
         DO 20 JJ=1,NUMPX
            PX(JJ)=0.0
20          CONTINUE
C     CHECK ALL INPUT RANGES FOR COMPUTATIONAL ORDER NUMBER I
         IFIND=0
C     IF NO MOD CARDS ENTERED, BYPASS CARD PROCESSING
         IF (NUMFPX.LE.0) GO TO 50
         DO 40 J=1,NUMFPX
            ID1=FPX(J,1)
            ID2=FPX(J,2)
            IF (ID1.GT.I.OR.ID2.LT.I) GO TO 40
C           A COMPUTATIONAL ORDER NUMBER FOR WHICH DATA WAS INPUT FOUND
               IFIND=1
C           SET CONVERSION FACTOR
               CF=1.00
               IF (FPX(J,3).EQ.XIN) CF=25.40
               DO 30 KK=1,NUMPX
                  IF (FPX(J,4+KK).LT. -998.99) GO TO 30
                  PX(KK)=FPX(J,4+KK)*CF
30                CONTINUE
40          CONTINUE
C     ADJUST IFFPRD FOR PDB TIME
50       JHOUR=IFFPRD+NHOPDB+6
C     WRITE THE TIME SERIES TO THE PROCESSED DATA BASE
         CALL WPRDDF (FMAPID,DTYPE,JHOUR,6,NUMPX,XMM,NUMPX,LPX,PX,
     $      LIWBUF,IWBUF,IREC,ISTAT)
         IF (IBUG.GT.0) WRITE (IOPDBG,*) 'ISTAT=',ISTAT,' NUMPX=',NUMPX,
     $      ' LWBUF=',LWBUF
         IF (ISTAT.NE.0) THEN
            LERDTP=0
            NERDTP=0
            CALL QSWPRD (ISTAT,FMAPID,DTYPE2,IFFPRD,INTVAL,NUMPX,NUMPX,
     $         DUNITS,LWBUF,LPX,LERDTP,ERDTP,NERDTP)
            IERR=1
            GO TO 70
            ENDIF
C     CHECK THE VALUE OF THE PRINT TECHNIQUE PRNTFMAP - IF PRNTFMAP IS
C     ON AND THERE WAS DATA INPUT FOR THIS AREA, DISPLAY THE AREA
         IF (IDISPL.LT.1.OR.IFIND.LT.1) GO TO 70
            CF=1.0
            IF (DUNITS.EQ.XIN) CF=1.0/25.4
            SUM=0.0
            DO 60 KK=1,NUMPX
               IF (PX(KK).LT. .001) GO TO 60
               PX(KK)=PX(KK)*CF
               SUM=SUM+PX(KK)
60             CONTINUE
            IF (SUM.EQ.0.0) GO TO 70
            CALL XFMAPP (FMAPID,IFFHR,ILFHR,NUMDAY,PX,NUMPX,DUNITS,
     *         IWRTFL)
            IWRTFL=1
70       CONTINUE
C
80    CALL FSTWHR (OLDOPN,IOLDOP,OLDOPN,IOLDOP)
C
      IF (IPTRCE.GT.0) WRITE (IOPDBG,*) 'EXIT XFMAPW'
C
      RETURN
C
100   FORMAT ('0**ERROR** SIZE OF THE WORK ARRAY (',I5,
     $ ') IS TOO SMALL TO WRITE FMAP TIME SERIES. ',I5,
     $ ' WORDS NEEDED.')
C
      END
