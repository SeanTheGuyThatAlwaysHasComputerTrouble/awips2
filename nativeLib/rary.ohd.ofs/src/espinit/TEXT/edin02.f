C MEMBER EDIN02
C  (from old member EEDIN02)
C
      SUBROUTINE EDIN02(DSP,LEFTD,NVAR,IER)
C .....................................................................
C
C          DISPLAY INPUT SUBROUTINE
C
C THIS ROUTINE IS PART OF ESP INITIALIZATION.
C IT WILL READ INFORMATION NECESSARY FOR DISPLAY ROUTINES
C AND STORE THIS INFO IN THE DSP PORTION OF THE PESP ARRAY.
C
C ORIGINALLY BY JIM SMITH AND ED VANBLARGAN - HRL - NOV,1981
C ......................................................................
C
C VARIABLES:
C IDFLT - DEFAULT SWITCH FOR FREQUENCY VALUES
C           0=DEFAULT     1=SPECIFY FREQ VALUES
C INUM  - NUMBER OF FREQ VALUES (IF IDFLT=1)
C IDIST - FITTED DISTRIBUTION NUMBER
C           1=SAMPLE      2=LOG NORMAL
C           3=NORMAL, NO SAMPLE
C MXNDST- MAX NUMBER OF DISTRIBUTION TYPES ALLOWED.
C IPLT  - PLOT SWITCH (O=NO PLOT    1=YES PLOT)
C ISAMP - SWITCH TO INCLUDE SAMPLE PTS IN PLOT (1=YES, 0=NO)
C ISUM  - "END OF RUN" SUMMARY TABLE SWITCH
C           0=NO TABLE    1=YES
C DSP   - ARRAY WHICH IS DISPLAY PORTION OF PESP ARRAY
C LEFTD - NUMBER OF LOCATIONS LEFT IS DSP ARRAY
C IUSED -   "    "      "     USED  "  "   "
C
C
C STRUCTURE OF DSP ARRAY FOR DISPLAY NUMBER 2 :
C LOCATION      VARIABLE   DESCRIPTION
C ********      ********   ***********
C    1             -       DISPLAY NUMBER=2
C    2             -       NUMBER OF VALUES IN DSP ARRAY
C    3           IDIST     DISTRIBUTION TYPE
C    4           INUM      NUMBER OF FREQ VALUES
C 5 TO INUM+4      -       FREQ VALUES
C  INUM+5        IPLT      PLOT SWITCH
C  INUM+6        ISAMP     SAMPLE SWITCH     "    "   "   "   "
C INUM+7 TO INUM+11        SWITCHES FOR TS TYPES  "   "   "   "
C INUM+6      (FOR IPLT=0) SUMMARY TABLE SWITCH (ISUM)
C INUM+12     (FOR IPLT=1)
C
C
C
C
      DIMENSION DSP(1),SBNAME(2),OLDOPN(2),ITYP(5)
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/fdbug'
      INCLUDE 'common/where'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/espinit/RCS/edin02.f,v $
     . $',                                                             '
     .$Id: edin02.f,v 1.1 1995/09/17 18:46:20 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA NAME,SBNAME/4HEINP,4HEDIN,4H02  /
C
C SET ERROR TRACES
C
      IOLDOP=IOPNUM
      IOPNUM=0
      DO 10 I=1,2
      OLDOPN(I)=OPNAME(I)
10    OPNAME(I)=SBNAME(I)
C
C GET TRACE LEVEL AND DEBUG
C
      IER=0
      IF (ITRACE.GE.1) WRITE(IODBUG,50)
50    FORMAT(1H0,14HEDIN02 ENTERED)
      IBUG=IFBUG(NAME)
C
C READ IN VARIABLE VALUES.
C
      DSP(1)=2.01
      READ(IN,110) IDFLT,INUM,IDIST,IPLT,ISUM
 110  FORMAT(5I5)
C
C CHECK THAT IDIST IS LESS THAN MAX ALLOWED
C AND HARDWIRE NVAR 7 AND 8 TO EMPIRICAL DISTRIBUTION.
C
      MXNDST=3
C
      IF (NVAR.EQ.7 .OR. NVAR.EQ.8) IDIST=1
      IF (IDIST.LE.MXNDST .AND. IDIST.GE.1) GO TO 120
C
      WRITE(IPR,115) IDIST
115   FORMAT(1H0,10X,36H**WARNING INPUT DISTRIBUTION TYPE OF,I3,
     * 56H NOT ALLOWED. THEREFORE DEFAULT TO LOG-NORMAL TRANSFORM.)
      IDIST=2
      CALL WARN
C
C ALSO CHECK IF DEFAULT IS ON. IF SO, JUMP AHEAD, OTHERWISE
C CONTINUE ON TO SPECIFY FREQ VALUES
C
120   IF (IDFLT.EQ.0) GO TO 300
C
C ...READ IN FREQ VALUES DESIRED....
C FIRST, CHECK IF THERE IS SPACE IN ARRAY AND PUT INFO IN
C LOCATIONS 2-4. ALSO, CHECK PLOT SWITCH AFTER READING INPUT.
C LEFTD CHECK AND DSP(2) MUST ACCOUNT FOR IPLT AND ISUM WHICH
C ARE ADDED LATER.
C
      IF (LEFTD.LT.(INUM+6)) GO TO 980
      DSP(2)=INUM+6.01
      DSP(3)=IDIST+.01
      DSP(4)=INUM+.01
      READ(IN,150) (DSP(I+4),I=1,INUM)
 150  FORMAT(10F5.2)
      IUSED=INUM+4
      GO TO 400
C
C DEFAULT FREQUENCY VALUES ARE TO BE USED.  LEFTD CHECK AND DSP(2) MUST
C ACCOUNT FOR IPLT AND ISUM WHICH ARE ADDED LATER.
C
 300  IF (LEFTD.LT.9) GO TO 980
C
      DSP(2)=9.01
      DSP(3)=IDIST+0.01
      DSP(4)=3.01
      DSP(5)=0.10
      DSP(6)=0.50
      DSP(7)=0.90
      IUSED=7
C
C ENTER PLOT SWITCH AND CHECK IF IT IS ON
C
400   IUSED=IUSED+1
      DSP(IUSED)=IPLT+0.01
      IF (IPLT.EQ.0) GO TO 600
C
C PLOT IS DESIRED. THEREFORE, READ SAMPLE SWITCH AND
C INDICATORS FOR TYPES OF TIME SERIES DESIRED ON PLOT WHERE POSITION
C 1=HISTORICAL, 2=ADJUSTED, 3=CONDITIONAL, 4=OBSERVED, 5=BASE PERIOD.
C   0 = TIME SERIES IS NOT TO BE PLOTTED
C   1 = TIME SERIES IS TO BE PLOTTED.
C
      READ(IN,550) ISAMP,(ITYP(I),I=1,5)
 550  FORMAT(6I5)
C
      IF (LEFTD.LT.(IUSED+7)) GO TO 990
      DSP(IUSED+1)=ISAMP+0.01
C
      DO 590 I=1,5
        DSP(IUSED+1+I)=ITYP(I)+0.01
590   CONTINUE
      IUSED=IUSED+6
      DSP(2)=DSP(2)+6
C
C ENTER SUMMARY TABLE SWITCH
C
600   IUSED=IUSED+1
      DSP(IUSED)=ISUM+0.01
C
C ............DEBUG TIME ...............................................
C
      IF (IBUG.EQ.0) GO TO 999
C
      WRITE(IODBUG,910) IUSED
910   FORMAT(1X,22HEDIN02 DEBUG:   IUSED=,I5 / 1X,10HDSP ARRAY:)
      WRITE(IODBUG,920) (DSP(I),I=1,IUSED)
920   FORMAT(1X,10F5.0)
C
      GO TO 999
C .....................................................................
C
C ERROR - NO SPACE IN DSP ARRAY
C
C COMES HERE IF REMAINING CARDS NEED TO BE READ
980   IF (IDFLT.NE.0) READ(IPR,920)
      IF (IPLT.NE.0)  READ(IPR,920)
C COMES HERE WHEN ALL CARDS WERE READ AND THERE IS ERROR
 990  WRITE(IPR,995) IUSED,LEFTD
 995  FORMAT(1H0,10X,43H**ERROR** DISPLAY IGNORED. NOT ENOUGH SPACE,
     X  51H IN THE ESP PARAMETER ARRAY FOR DISPLAY INFORMATION,
     * I5,6H=IUSED,I5,6H=LEFTD)
      IER=IER+1
      CALL ERROR
C
C SET ERROR TRACES IN CB/WHERE/ BACK THE WAY THEY WERE
C
999   IOPNUM=IOLDOP
      OPNAME(1)=OLDOPN(1)
      OPNAME(2)=OLDOPN(2)
C
      RETURN
      END
