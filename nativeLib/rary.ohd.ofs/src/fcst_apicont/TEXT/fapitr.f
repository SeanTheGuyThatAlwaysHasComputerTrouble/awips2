C MEMBER FAPITR
C  (from old member FCFAPITR)
C
      SUBROUTINE FAPITR(PO,IDT)
C.......................................
C     THIS SUBROUTINE ONLY USED BY THE OPERATIONAL PROGRAM. IT CHECKS
C     THE FAPICO COMMON BLOCK TO SEE IF ANY MODS EXIST FOR THE
C     API-CONT OPERATION THAT IS TO BE EXECUTED.  IF MODS EXIST,
C     VALUES ARE MOVED TO THE FCOAPI COMMON BLOCK.  TIMES
C     ARE ADJUSTED TO CORRESPOND WITH THE TIME INTERVAL FOR
C     THE OPERATION.
C.......................................
C     SUBROUTINE WRITTEN BY ...
C         ERIC ANDERSON - HRL - OCT. 1992
C.......................................
      DIMENSION SNAME(2),PO(1)
      REAL SMIX,SMI
C
C     COMMON BLOCKS
      COMMON/FDBUG/IODBUG,ITRACE,IDBALL,NDEBUG,IDEBUG(20)
      COMMON/WHERE/ISEG(2),IOPNUM,OPNAME(2)
      COMMON/FAPICO/NAPICS,APICOS(12,10)
      COMMON/FCOAPI/NACV,JHAPI(10),APICO(4,10),BFX(10),FGCO(2,10)
      COMMON/FCTIME/IDARUN,IHRRUN,LDARUN,LHRRUN,LDACPD,LHRCPD,NOW(5),
     1 LOCAL,NOUTZ,NOUTDS,NLSTZ,IDA,IHR,LDA,LHR,IDADAT
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/fcst_apicont/RCS/fapitr.f,v $
     . $',                                                             '
     .$Id: fapitr.f,v 1.1 1995/09/17 18:57:30 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA SNAME/4HFAPI,4HTR  /
      DATA JBUG,KBUG/4HMODS,4HAPIC/
      DATA BLANK/4H    /
C.......................................
C     TRACE LEVEL=1, DEBUG SWITCH=IBUG.
      CALL FPRBUG(SNAME,1,1,IBUG)
      IF(IFBUG(JBUG).EQ.1) IBUG=1
      IF(IFBUG(KBUG).EQ.1) IBUG=1
C.......................................
C     SET INITIAL VALUES
      NACV=0
      M=2
C.......................................
C     CHECK IF ANY VALUES ARE IN FAPICO.
      IF(NAPICS.GT.0) GO TO 90
      RETURN
C.......................................
C     IF DEBUG ON, PRINT COMMON BLOCK FAPICO.
   90 IF(IBUG.EQ.0) GO TO 100
      WRITE(IODBUG,900) NAPICS
  900 FORMAT(1H0,46HFAPITR DEBUG--CONTENTS OF FAPICO COMMON BLOCK.,2X,
     1  7HNAPICS=,I5)
      DO 91 N=1,NAPICS
      WRITE(IODBUG,901)(APICOS(I,N),I=1,12)
  901 FORMAT(1H ,5X,2A4,F8.0,9F9.2)
   91 CONTINUE
C.......................................
C     COMPUTE STARTING AND ENDING JULIAN HOUR WINDOWS.
  100 JH1=(IDA-1)*24+IHR-IDT-IDT/2
      JH2=(LDA-1)*24+LHR+(IDT-1)/2
C
C     RETRIEVE SMIX.
      LPM=PO(26)
      SMIX=PO(LPM+6)
      IF(IBUG.EQ.1) WRITE(IODBUG,902)IDA,IHR,LDA,LHR,IDT,JH1,JH2,SMIX
  902 FORMAT(1H0,13HFAPITR VALUES,7I8,F9.2)
C.......................................
C     FIND ENTRIES FOR THIS OPERATION.
      DO 101 N=1,NAPICS
      IF((APICOS(1,N).EQ.BLANK).AND.(APICOS(2,N).EQ.BLANK)) GO TO 105
      IF(IUSAME(APICOS(1,N),OPNAME,M).EQ.1) GO TO 105
      GO TO 101
C
C     ENTRY IS FOR THIS OPERATION--CHECK IF WITHIN RUN PERIOD.
  105 JH=APICOS(3,N)
      IF((JH.LT.JH1).OR.(JH.GT.JH2)) GO TO 101
C
C     WITHIN RUN PERIOD--ADJUST TO NEAREST DELTA T.
      JH=((JH+IDT/2)/IDT)*IDT
C
C     CHECK IF ANOTHER VALUE FOR THE SAME TIME.
      IF(NACV.EQ.0) GO TO 102
      DO 103 I=1,NACV
      IF(JH.NE.JHAPI(I)) GO TO 103
C     ANOTHER ENTRY FOR THE SAME TIME EXISTS
      IF((APICOS(1,N).EQ.BLANK).AND.(APICOS(2,N).EQ.BLANK)) GO TO 120
C     SECOND ENTRY IS SPECIFICALLY FOR THIS OPERATION--IT SHOULD
C       OVERRIDE ANY PREVIOUS ENTRIES.
      IF (APICOS(4,N).GE.0.0) APICO(1,I)=APICOS(4,N)
      SMI=APICOS(5,N)
      IF(APICOS(6,N).LT.0.0) GO TO 112
      SMI=SMIX-APICOS(6,N)
      IF(SMI.LT.0.0) SMI=0.0
  112 IF(SMI.GE.0.0)APICO(2,I)=SMI
      IF(APICOS(7,N).GE.0.0) APICO(3,I)=APICOS(7,N)
      IF(APICOS(8,N).GE.0.0) APICO(4,I)=APICOS(8,N)
      IF(APICOS(9,N).GT.-900.0) FGCO(1,I)=APICOS(9,N)
      IF(APICOS(10,N).GE.0.0) FGCO(2,I)=APICOS(10,N)
      IF(APICOS(11,N).GE.0.0) BFX(I)=APICOS(11,N)
      GO TO 101
C     SECOND ENTRY IS FOR ALL OPERATIONS OF THIS TYPE IN
C       THE SEGMENT--ONLY REPLACE MISSING VALUES.
  120 IF(APICO(1,I).LT.0.0) APICO(1,I)=APICOS(4,N)
      IF(APICO(2,I).GE.0.0) GO TO 123
      SMI=APICOS(5,N)
      IF(APICOS(6,N).LT.0.0) GO TO 124
      SMI=SMIX-APICOS(6,N)
      IF(SMI.LT.0.0) SMI=0.0
  124 APICO(2,I)=SMI
  123 IF(APICO(3,I).LT.0.0) APICO(3,I)=APICOS(7,N)
      IF(APICO(4,I).LT.0.0) APICO(4,I)=APICOS(8,N)
      IF(FGCO(1,I).LE.-900.0) FGCO(1,I)=APICOS(9,N)
      IF(FGCO(2,I).LT.0.0) FGCO(2,I)=APICOS(10,N)
      IF(BFX(I).LT.0.0) BFX(I)=APICOS(11,N)
      GO TO 101
  103 CONTINUE
C
C
C     STORE IN FCOAPI COMMON BLOCK.
  102 NACV=NACV+1
      JHAPI(NACV)=JH
      APICO(1,NACV)=APICOS(4,N)
      SMI=APICOS(5,N)
      IF(APICOS(6,N).LT.0.0) GO TO 109
      SMI=SMIX-APICOS(6,N)
      IF(SMI.LT.0.0) SMI=0.0
  109 APICO(2,NACV)=SMI
      APICO(3,NACV)=APICOS(7,N)
      APICO(4,NACV)=APICOS(8,N)
      FGCO(1,NACV)=APICOS(9,N)
      FGCO(2,NACV)=APICOS(10,N)
      BFX(NACV)=APICOS(11,N)
  101 CONTINUE
      IF(NACV.EQ.0) RETURN
C.......................................
C     IF DEBUG ON, PRINT FCOAPI COMMON BLOCK.
      IF(IBUG.EQ.0) RETURN
      WRITE(IODBUG,903) NACV
  903 FORMAT(1H0,46HFAPITR DEBUG--CONTENTS OF FCOAPI COMMON BLOCK.,2X,
     1  5HNACV=,I5)
      DO 110 N=1,NACV
      WRITE(IODBUG,904)JHAPI(N),(APICO(I,N),I=1,4),BFX(N),
     1  (FGCO(I,N),I=1,2)
  904 FORMAT(1H ,5X,I10,7F10.2)
  110 CONTINUE
C.......................................
      RETURN
      END
