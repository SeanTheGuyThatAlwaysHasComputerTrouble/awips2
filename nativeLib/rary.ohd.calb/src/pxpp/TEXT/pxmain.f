C MODULE MAIN_PXPP
C-----------------------------------------------------------------------
C
C  MAIN ROUTINE FOR PROGRAM PXPP.
C
      SUBROUTINE PXPP_MAIN
C
C  PROGRAM PXPP COMPUTES THE FOLLOWING PRECIPITATION QUANTITIES
C  FOR A GIVEN PERIOD OF RECORD:
C     1. RATIO OF EACH STATION TO A DESIGNATED BASE STATION. COMPUTED
C        BASED ON PERIODS WHEN DATA ARE AVAILABLE AT BOTH STATIONS.
C        FOR USE IN DETERMINING STATION MONTHLY MEANS FOR THE
C        MAP PROGRAM.
C     2. MEAN MONTHLY PRECIPITATION (AND ANNUAL) FOR EACH STATION FOR
C        THE PERIOD OF RECORD.
C
      INCLUDE 'common/ionum'
C
C SPECIFY THE MAXIMUM NUMBER OF STATIONS ALLOWED
C
C     Change NSX from 200 to 500 for handling more than 200 time corrections
C                 xiaobiao fan 03/14/02
C
C     PARAMETER (NSX=200,MaxMonth=1200)
      PARAMETER (NSX=500,MaxMonth=1200)
      DIMENSION X(NSX),Y(NSX),ELEV(NSX)
      COMMON/PASS/TSFILENAME,ITYPE
      CHARACTER TSFILENAME(NSX)*132
      INTEGER*2 ITYPE(NSX)
      DIMENSION STNAME(NSX,5),PX(NSX,MaxMonth),NNY(NSX,12),
     1 IORDCN(3,NSX),CHAR(NSX,12),AVGPX(NSX,12),PXYR(NSX),XLAT(NSX),
     2 XLONG(NSX),NPG(3)
      DIMENSION IGN(5),W(5),C(12),NY(12),SPB(12),SPI(12),NFLAG(MaxMonth)
      DIMENSION MOC(10),IYRC(10),CORR(10),NMOC(11),SPX(NSX,2)
      DIMENSION RTBSM(NSX,12)
      DIMENSION PXEST(MaxMonth),RMS(NSX),BIAS(NSX),SEASON(10),SM(12)
      DIMENSION IGS(5,NSX),IGROUP(NSX),BASE(5,MaxMonth),IORDR(NSX)
      DIMENSION CM(NSX,NSX),CMW(NSX,NSX),CMS(NSX,NSX),IGM(NSX,NSX)
      DIMENSION IGMW(NSX,NSX),IGMS(NSX,NSX),E(20,NSX),ITIME(NSX)
      DIMENSION NCOR(NSX),SEAS(10,NSX),IMOC(10,NSX)
      DIMENSION NYRC(10,NSX),TCORR(10,NSX)
      INTEGER FIELD(7),IORDER(NSX),IORD(NSX)
      INTEGER*2 JFLAG(NSX,MaxMonth)
      CHARACTER*4 STAID(2),STID(NSX,2),EFT(NSX),BS(2),EF,ULL,ANSW,CONS
      CHARACTER*4 PUNCH,PRINTC,PRINTD,LLMS,CORRL,CONSIS,MOPT,MANS,MAPO
      CHARACTER*4 CVAR,WEIGHT
      CHARACTER*8 BSTAID,ENDS,ENDC,ST,STA(NSX),STAO(NSX)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/calb/src/pxpp/RCS/pxmain.f,v $
     . $',                                                             '
     .$Id: pxmain.f,v 1.7 2004/08/10 15:10:51 dsa Exp $
     . $' /
C    ===================================================================
C
      DATA SM/4HJAN.,4HFEB.,4HMAR.,4HAPR.,4HMAY ,4HJUNE,
     1        4HJULY,4HAUG.,4HSEPT,4HOCT.,4HNOV.,4HDEC./
      DATA WSYM,SSYM/1HW,1HS/
      DATA CODEM/4HM   /
      DATA FIELD/1,4,5,6,7,10,15/
C
C
C     Subroutine ARGVER outputs the version/date info and exits the
C      program if the first command line argument is "-version"
C
      CALL ARGVER()
C
      CALL UPRIMO_PXPP()
C
      NC=0
      NTIME=1
      MULT=0
      IERROR=0
      MAPG=0
      CVAR=' ADJ'
      MAPO=' ,, '
      CONS=' ,, '
      ANSW='YES '
      MANS='NO  '
      ENDS='ENDSTAN '
      ENDC='ENDCORR '
      WEIGHT='THIE'
C
C  PRINT PAGE HEADER
      CALL USETO1 ('NOPAGNUM',IERR)
      CALL UPAGE (IPR)
      CALL USETO1 ('NOPAGHDR',IERR)
C
C  READ FIRST LINE FROM PARAMETER INPUT FILE
      READ (IN,900,END=999) IMO,IYR,LMO,LYR,BS,ULL,IOBT,UELEV,
     1  IMOW,IMOS,MULT
  900 FORMAT (4I5,2X,2A4,2X,A4,2X,I2,1X,A4,2I5,4X,I1)
C
C  CHECK THE YEAR TO SEE IF IT WAS ENTERED AS 2 DIGITS OR 4
C  ALLOWABLE 2-DIGITS DATES GO FROM THE YEAR 1926 THRU 2025
      IF (IYR.LT. 25) IYR = IYR + 2000
      IF (IYR.LT.100) IYR = IYR + 1900
      IF (LYR.LT. 25) LYR = LYR + 2000
      IF (LYR.LT.100) LYR = LYR + 1900
C
C     READ THE INPUT/OUTPUT OPTION SWITCHES
C
      READ(IN,899) PRINTC,CONSIS,PRINTD,MOPT,UNITS,IPUNCH,MAPG
  899 FORMAT(A4,1X,4(A4,1X),4X,I1,4X,I1)
C
C     DEFINE OUTPUT NAMES FOR ALL I/O SWITCHES
C
      PUNCH='YES '
      IF (IPUNCH.EQ.0) PUNCH='NO  '
      IDSQ=1
      IF (PRINTC.EQ.'SEAS'.OR.PRINTC.EQ.'YEAR') IDSQ=0
      CORRL=PRINTC
      LLMS='NO '
      IF (ULL.EQ.'DGMS')LLMS='YES '
      NAPPLY=0
      IF (PRINTC.EQ.'SEAS') NAPPLY=1
      IOPT=0
      IF (CONSIS.EQ.'YEAR') IOPT=1
      IF (CONSIS.EQ.'SEAS') IOPT=2
      IF (CONSIS.EQ.'YEAR') CONS='PLOT'
      IF (CONSIS.EQ.'SEAS') CONS='SESN'
      IF (MAPG.NE.0) THEN
         MAPG=1
         MAPO='NORM'
         IF (CONSIS.EQ.'YEAR') WEIGHT='PRE '
         IF (CONSIS.EQ.'SEAS') WEIGHT='SEAS'
      ELSE
         ANSW='NO  '
      ENDIF
      IF (MULT.EQ.1) THEN
         MANS='YES '
         ISFN=1
      ENDIF
C
C     WRITE ALL INPUT PARAMETERS TO THE OUTPUT DEVICE
C
      WRITE (IPR,8001)
      WRITE (IPR,8002)
      WRITE (IPR,8003)
      WRITE (IPR,8004)
      WRITE (IPR,8005)
      WRITE (IPR,8006)BS,IOBT,MOPT,UNITS,UELEV
      WRITE (IPR,8007)
      WRITE (IPR,8008)
      WRITE (IPR,8009)
      WRITE (IPR,8010)
      IF (MULT.EQ.1) ISFN=1
      BSTAID=BS(1)//BS(2)
      LMOW=IMOS-1
      LMOS=IMOW-1
      NMO=(LYR*12+LMO)-(IYR*12+IMO)+1
      IF (NMO.GT.MaxMonth) THEN
        WRITE (IPR,8024) IMO, IYR+1, LMO, LYR
        CALL USTOP (IPR,0)
      ENDIF

C
C     NFLAG(I)=1 FOR WINTER MONTHS AND 0 FOR SUMMER MONTHS
C
      DO 88 N=1,NSX
        DO 88 I= 1, NMO
          JYR=(I-1)/12
          MO=I-JYR*12
          MO=IMO+MO-1
          IF (MO.GT.12) MO=MO-12
          NFLAG(I)=1
          IF ((MO.GT.LMOW).AND.(MO.LT.IMOW)) NFLAG(I)=0
   88 CONTINUE
C
C     INITIALIZE MONTHLY PRECIPITATION ARRAY TO MISSING DATA
C
      DO 101 N=1,NSX
        EFT(N)='CALB'
        X(N)=-9999.
        DO 101 I= 1, NMO
          PX(N,I)= -999.0
  101 CONTINUE
C
C     LOOP TO READ IN STATION INFORMATION AND
C     DATABASE LOCATION INFORMATION FOR EACH
C     INDIVIDUAL STATION. AT THE PRESENT THIS IS
C     THE TIME SERIES HEADER CARD.
C        (8 CHARACTERS), LATITUDE, LONGITUDE, STATION ELEVATION,
C        TIME INTERVAL, THE EXTERNAL FILE TYPE (DEFAULT IS CALB), AND
C        THE STATION DESCRIPTION (UP TO 20 CHARACTERS).
C
      NHLY=0
      NDLY=0
      ibs_ok=0
      DO 102 N=1,NSX
      READ(IN,901) STAID,XLAT(N),XLONG(N),HEIGHT,IT,EF,(E(I,N),I=15,19)
  901 FORMAT(2A4,2X,2(1X,F8.4),2X,F6.0,2X,I2,4X,A4,4X,5A4)
      IF (staid(1).eq.bs(1)) then
      IF (staid(2).eq.bs(2)) ibs_ok=ibs_ok+1
      endif
      itype(n)=it
      IF (IT.EQ.1)NHLY=NHLY+1
      IF (IT.EQ.24)NDLY=NDLY+1
      ITIME(N)=IT
      ST=STAID(1)//STAID(2)
      IF (ST.EQ.BSTAID) NBASE=N
      IF (ST.EQ.ENDS) THEN
         GO TO 103
      ENDIF
      IF (EF.NE.'    ')EFT(N)=EF
C
C     CONVERT DEGREES, MINUTES AND SECONDS TO DEGREES DECIMAL FORMAT.
C
      IF (ULL.EQ.'DGMS')CALL CONLL(N,XLAT,XLONG)
      IF (UELEV.NE.CODEM) HEIGHT=HEIGHT*0.3048
      ELEV(N)=HEIGHT
      read(in,902) tsfilename(n)
  902 format(a132)
      E(5,N)=IT+.1
      E(6,N)=LHEAD+.1
      E(10,N)=KMO+.1
      E(11,N)=KDA+.1
      E(12,N)=KYR+.1
      E(13,N)=KHRMIN+.1
      E(14,N)=KSEC+.1
      DO 100 J=1,5
      JN=J+FIELD(7)-1
      STNAME(N,J)=E(JN,N)
      IF (J.GT.2) GO TO 100
      STID(N,J)=STAID(J)
  100 CONTINUE
      NSTA=N
  102 CONTINUE
  103 CONTINUE
c
c check if the base station specified in the input deck is indeed one of
c the stations
      IF (ibs_ok.eq.0) then
      WRITE (IPR,8023)
 8023 format(/'BASE STATION DOES NOT EXIST...RESPECIFY')
      call ustop(IPR,0)
      endif
      WRITE (IPR,8011)PRINTC,SM(IMOW),SM(IMOS),NSTA,LLMS
      WRITE (IPR,8012)
      WRITE (IPR,8013)
      WRITE (IPR,8014)
      WRITE (IPR,8015)
      WRITE (IPR,8016)
      WRITE (IPR,8017)PRINTD,CONSIS,PUNCH,ANSW,MANS
      WRITE (IPR,8018)IMO,IYR,LMO,LYR
      WRITE (IPR,8012)
      WRITE (IPR,8019)
      WRITE (IPR,8020)UELEV
      WRITE (IPR,8021)
      DO 104 I=1,NSTA
      HEIGHT=ELEV(I)/0.3048
      WRITE (IPR,8022) (STID(I,IW),IW=1,2),(E(J,I),J=15,19),
     * XLAT(I),XLONG(I),HEIGHT,ITIME(I),EFT(I)
  104 CONTINUE
C
C***
C     ROUTINE PXFILE STORES MONTHLY SUMS OF PRECIPITATION
C        DATA IN THE PX ARRAY FOR ALL STATIONS.  THIS ROUTINE
C        ALSO SETS THE ENTIRE MONTH TO MISSING IF ANY MISSING DATA
C        ARE ENCOUNTERED IN THAT MONTH.
C***
      CALL PXFILE(NSTA,IMO,IYR,LMO,LYR,UNITS,IOBT,PX,NBASE,E)
C
C     PRINT HEADING.
      WRITE (IPR,905)
  905 FORMAT (25X,29HMAP PRELIMINARY COMPUTATIONS.)
      WRITE (IPR,906)
  906 FORMAT (1H0,78HMEAN MONTHLY AND MEAN ANNUAL PRECIPITATION PLUS MON
     1THLY RATIO TO BASE STATION.)
      WRITE (IPR,907)IMO,IYR,LMO,LYR,BS,UNITS
  907 FORMAT (1H0,18HPERIOD ANALYZED IS,I3,1H/,I4,1X,2HTO,I3,1H/,I4,1H.,
     15X,2A4,21H IS THE BASE STATION.,5X, 9HUNITS ARE, 1X,A4)
      WRITE (IPR,923)SM(IMOW),SM(IMOS)
  923 FORMAT (1H0,A4,1X,29HIS THE FIRST MONTH OF WINTER.,5X,A4,1X,
     129HIS THE FIRST MONTH OF SUMMER.)
C
C     READ IN STATION INFORMATION INCLUDING THE STATION IDENTIFIER
C
C     APPLY CORRECTIONS TO STATIONS (ONE STATION PER CARD--FIVE
C     CORRECTIONS PER STATION); IF SEASONAL ANALYIS IS DESIRED
      DO 177 JJ=1,NSTA
  177 NCOR(JJ)=0
C
C     READ IN THE CORRECTIONS FOR EACH STATION
C
      NSTAC=1
      DO 124 II=1,NSX
         READ(IN,904) STA(II),NC,(MOC(I),IYRC(I),SEASON(I),
     *   CORR(I),I=1,NC)
  904    FORMAT(A8,4X,I3,5X,5(I2,1X,I4,A1,F4.2))
         IF (STA(II).EQ.ENDC) GO TO 125
         IF (II.EQ.1) THEN
            WRITE (IPR,915)
  915       FORMAT (1H0,39HCORRECTION FACTORS USED ARE AS FOLLOWS.,5X,
     1      49HW INDICATES WINTER ONLY, S INDICATES SUMMER ONLY.)
            WRITE (IPR,916)
  916       FORMAT (/,1H ,17HSTATION I.D. CODE,8X,
     1      41HCORRECTIONS AND MONTH WHEN FIRST APPLIED.,/)
         ENDIF
         DO 122 N=1,NSTAC
            IF (STA(II).EQ.STA(N).AND.NSTAC.NE.N) THEN
               J=NCOR(N)+1
               NN=NCOR(N)+NC
               NCOR(N)=NN
C           STORE THE CORRECTION INFORMATION FOR USE LATER.
               DO 121 I=J,NN
                  I1=I-J+1
                  IMOC(I,N)=MOC(I1)
                  NYRC(I,N)=IYRC(I1)
                  SEAS(I,N)=SEASON(I1)
                  TCORR(I,N)=CORR(I1)
  121          CONTINUE
C           ONLY 10 CORRECTIONS ALLOWED PER STATION
               IF (NN.GT.10) THEN
                  WRITE (IPR,917)STA(NSTAC),(IMOC(I,NSTAC),
     *               NYRC(I,NSTAC),SEAS(I,NSTAC),TCORR(I,NSTAC),I=1,NN)
  917             FORMAT(6X,A8,3X,3(5(I2,1H/,I4,1X,A1,F6.2,4X)/17X))
                  WRITE (IPR,857)
  857             FORMAT('0**ERROR** NO MORE THAN 10 CORRECTIONS ARE ',
     *            'ALLOWED PER STATION.')
                  CALL USTOP (IPR,0)
               ENDIF
               IF (N.EQ.NSTAC-1)GO TO 124
            ENDIF
  122    CONTINUE
         NCOR(NSTAC)=NC
         NN=NCOR(NSTAC)
         DO 123 I=1,NN
            IMOC(I,NSTAC)=MOC(I)
            NYRC(I,NSTAC)=IYRC(I)
            SEAS(I,NSTAC)=SEASON(I)
            TCORR(I,NSTAC)=CORR(I)
            STA(NSTAC)=STA(II)
  123    CONTINUE
         NSTAC=NSTAC+1
  124 CONTINUE
  125 CONTINUE
      NSTAC=NSTAC-1
C
C  LOOP TO APPLY CORRECTIONS TO THE DATA THAT NEEDS TO BE CORRECTED.
      IF (NSTAC.EQ.0) CVAR=' ,, '
      DO 178 IJ=1,NSTAC
         IL=NCOR(IJ)
         WRITE (IPR,917)STA(IJ),(IMOC(I,IJ),NYRC(I,IJ),SEAS(I,IJ),
     *      TCORR(I,IJ),I=1,IL)
         IYRC(IL+1)=LYR
         MOC(IL+1)=LMO
         NC=NCOR(IJ)
         IP=1
         DO 170 I=1,IL
            IYRC(I)=NYRC(I,IJ)
            MOC(I)=IMOC(I,IJ)
            CORR(I)=TCORR(I,IJ)
            SEASON(I)=SEAS(I,IJ)
            NMOC(I)=IYRC(I)*12+MOC(I)+1-(IYR*12+IMO)
            IF (NMOC(I).LE.0) IP=I
            IF (I.EQ.1) GO TO 170
            IF (IYRC(I).LE.IYRC(I-1)) THEN
               IF (IYRC(I).LT.IYRC(I-1)) THEN
                  WRITE (IPR,801)
  801             FORMAT(/'DATES FOR CORRECTIONS TO A STATION MUST BE ',
     *           'IN CHRONOLOGICAL ORDER')
                  CALL USTOP(IPR,0)
               ELSE
                  IF (MOC(I).LT.MOC(I-1)) THEN
                     WRITE (IPR,801)
                     CALL USTOP(IPR,0)
                  ENDIF
               ENDIF
            ENDIF
            IF (NMOC(I).GT.0.AND.NMOC(I-1).LE.0) NMOC(I-1)=1
  170    CONTINUE
         NMOC(NC+1)=9999
         DO 171 N=1,NSTA
            ST=STID(N,1)//STID(N,2)
            IF (ST.NE.STA(IJ)) GO TO 171
            IG=N
            GO TO 173
  171    CONTINUE
         WRITE (IPR,914)STA(IJ)
  914    FORMAT(1H0,25HI.D. ON CORRECTION CARD (,A8,39H) DOES NOT MATCH
     1ANY STATION I.D. CODE.)
         IERROR=1
         GO TO 178
  173    CFW=1.0
         CFS=1.0
         DO 174 I=1,NMO
  164       IF (I.NE.NMOC(IP))GO TO 175
            IF (SEASON(IP).NE.WSYM) GO TO 161
            CFW=CORR(IP)
            GO TO 163
  161       IF (SEASON(IP).NE.SSYM) GO TO 162
            CFS=CORR(IP)
            GO TO 163
  162       CFW=CORR(IP)
            CFS=CORR(IP)
  163       IP=IP+1
            GO TO 164
  175       IF (PX(IG,I).LT.0.0)GO TO 174
            JYR=(I-1)/12
            MO=I-JYR*12
            MO=IMO+MO-1
            IF (MO.GT.12) MO=MO-12
            IW=1
            IF ((MO.GT.LMOW).AND.(MO.LT.IMOW)) IW=0
            IF (IW.EQ.1) PX(IG,I)=PX(IG,I)*CFW
            IF (IW.EQ.0) PX(IG,I)=PX(IG,I)*CFS
  174    CONTINUE
  178 CONTINUE
C
C     COMPUTE MONTHLY RATIO OF EACH STATION TO THE BASE STATION.
C
      DO 130 N=1,NSTA
      DO 131 I=1,12
      C(I)=0.0
      NY(I)=0
      SPB(I)=0.0
  131 SPI(I)=0.0
      DO 132 I=1,NMO
      JYR=(I-1)/12
      MO=I-JYR*12
      MO=IMO+MO-1
      IF (MO.GT.12) MO=MO-12
      IF (PX(NBASE,I).LT.0.0) GO TO 132
      IF (PX(N,I).LT.0.0) GO TO 132
C     BOTH STATIONS HAVE VALID MONTHLY TOTAL.
      NY(MO)=NY(MO)+1
      SPB(MO)=SPB(MO)+PX(NBASE,I)
      SPI(MO)=SPI(MO)+ PX(N,I)
  132 CONTINUE
      DO 133 I=1,12
      IF (NY(I).EQ.0) GO TO 134
      IF ((SPB(I).LE.0.0).OR.(SPI(I).LE.0.0)) GO TO 134
      C(I)= SPI(I)/SPB(I)
  134 NNY(N,I)=NY(I)
      CHAR(N,I)=C(I)
      IF (CHAR(N,I).LT.0.01) CHAR(N,I)=0.01
  133 CONTINUE
  130 CONTINUE
C
C     COMPUTE NUMBER OF MONTHS OF DATA AT THE BASE STATION.
C
      DO 140 I=1,12
  140 NY(I)=0
      DO 141 M=1,NMO
      IF (PX(NBASE,M).LT.0.0)GO TO 141
      JYR=(M-1)/12
      MO=M-JYR*12
      MO=IMO+MO-1
      IF (MO.GT.12)MO=MO-12
      NY(MO)=NY(MO)+1
  141 CONTINUE
      DO 142 I=1,12
      NNY(NBASE,I)=NY(I)
      CHAR(NBASE,I)=1.0
  142 CONTINUE
C
C     CALCULATE POLAR STEREOGRAPHIC COORDINATES EQUIVALENT TO THE
C        LATITUDE AND LONGITUDE OF EACH STATION. THESE COORDINATES
C        WILL BE USED TO CALCULATE 1/D**2.
C
      ILLGD=1
      CALL SBLLGD (XLONG,XLAT,NSTA,X,Y,ILLGD,IERR)
      IF (IERROR.GT.0) GO TO 999
      WRITE (IPR,8012)
      WRITE (IPR,922)
  922 FORMAT(1H0,46HSTATION WEIGHTS BASED ON 1.0/DISTANCE SQUARED.)
C
C     COMPUTE CORRELATION MATRIX AND PRINT TABLE.
C
      IF (IDSQ.EQ.0) THEN
         IF (NAPPLY.EQ.1) THEN
            NSKIP=0
            CALL RMATRX(NSTA,NMO,IMO,PX,CHAR,NFLAG,NSKIP,CMW,IGMW)
            NSKIP=1
            CALL RMATRX(NSTA,NMO,IMO,PX,CHAR,NFLAG,NSKIP,CMS,IGMS)
         ELSE
            NSKIP=2
            CALL RMATRX(NSTA,NMO,IMO,PX,CHAR,NFLAG,NSKIP,CM,IGM)
         ENDIF
      ENDIF
C
C     ESTIMATE PRECIPITATION FOR EVERY MONTH AT EACH STATION.  REPLACE
C          MISSING MONTHS WITH ESTIMATE.  COMPUTE RMS ERROR BETWEEN
C          ESTIMATE AND ACTUAL VALUE FOR OTHER MONTHS.  (CAN NOT
C          ESTIMATE PRECIP. DURING MONTHS WHEN STATION CHARACTERISTIC
C          IS NOT DEFINED.
C
      DO 200 N=1,NSTA
         NA=0
      DO 201 M=1,NMO
      PXEST(M)=-999.0
      JYR=(M-1)/12
      MO=M-JYR*12
      MO=IMO+MO-1
      IF (MO.GT.12)MO=MO-12
      MLAST=NMO-11
      IF (M.EQ.MLAST.AND.NNY(N,MO).EQ.0) THEN
         NA=1
         IF (NTIME.EQ.1) THEN
            WRITE (IPR,798)
  798       FORMAT(1X,///'THE FOLLOWING STATIONS HAVE INSUFFICIENT DATA
     *TO ESTIMATE MISSING MONTHS:'/1X,' NO CONSISTENCY PLOTS WILL BE PRI
     *NTED FOR THESE STATIONS')
            NTIME=999
         ENDIF
         GO TO 201
      ENDIF
C
C     FIND ESTIMATORS FOR STATION N AND WEIGHTS.
C
      CALL DSQUAR(N,NSTA,M,PX,X,Y,NEST,IGN,W,NNY,MO)
      IF (NEST.EQ.0)GO TO 201
      ESTPX=0.0
      SW=0.0
      DO 202 I=1,NEST
      IG=IGN(I)
C     ESTPX=ESTPX+W(I)*PX(IG,M)
      ESTPX=ESTPX+(CHAR(N,MO)/CHAR(IG,MO))*W(I)*PX(IG,M)
      SW=SW+W(I)
  202 CONTINUE
      PXEST(M)=ESTPX/SW
  201 CONTINUE
C     COMPUTE RMS ERROR OF ESTIMATE.
      RMS(N)=-999.9
      BIAS(N)=-999.9
      NC=0
      SOMS2=0.0
      SE=0.0
      SO=0.0
      DO 203 M=1,NMO
      IF ((PX(N,M).LT.0.0).OR.(PXEST(M).LT.0.0))GO TO 203
      NC=NC+1
      SOMS2=SOMS2+(PX(N,M)-PXEST(M))**2
      SE=SE+PXEST(M)
      SO=SO+PX(N,M)
  203 CONTINUE
      IF (NC.EQ.0)GO TO 205
      IF (so.eq.0.) go to 205
      RMS(N)=SQRT(SOMS2/NC)
      BIAS(N)=((SE-SO)/SO)*100.0
C
C     REPLACE MISSING MONTHS WITH ESTIMATE.  (INDICATE ESTIMATE BY
C          A NEGATIVE SIGN.)
C
  205 DO 204 M=1,NMO
      IF (PX(N,M).GE.0.0)GO TO 204
      IF (PXEST(M).LT.0.0)GO TO 204
      PX(N,M)=-PXEST(M)
  204 CONTINUE
      IF (NA.EQ.1) THEN
         WRITE (IPR,799)(STID(N,I),I=1,2),(E(INEX,N),INEX=15,19)
  799    FORMAT(1X,'STATION I. D. = ',3X,2A4,'   STATION NAME =   ',5A4)
         WRITE (IPR,800)
  800    FORMAT('  REMOVE THESE STATIONS AND RUN AGAIN'///)
      ENDIF
  200 CONTINUE
C
C     ALL MISSING MONTHS THAT CAN BE ESTIMATED HAVE NOW BEEN ESTIMATED.
C     COMPUTE MEAN MONTHLY PRECIPITATION FOR ALL STATIONS.
C
      DO 150 N=1,NSTA
      DO 151 I=1,12
      NY(I)=0
  151 SPI(I)=0.0
      DO 152 M=1,NMO
      JYR=(M-1)/12
      MO=M-JYR*12
      MO=IMO+MO-1
      IF (MO.GT.12)MO=MO-12
      IF (PX(N,M).LT.-998.0) GO TO 153
      IF (SPI(MO).LT.0.0)GO TO 152
      NY(MO)=NY(MO)+1
      SPI(MO)=SPI(MO)+ABS(PX(N,M))
      GO TO 152
  153 SPI(MO)=-999.0
  152 CONTINUE
      PXYR(N)=0.0
      DO 154 I=1,12
      IF (SPI(I).LT.0.0)GO TO 155
      IF (NY(I).EQ.0)GO TO 155
      AVGPX(N,I)=SPI(I)/NY(I)
      IF (PXYR(N).GE.0.0)PXYR(N)=PXYR(N)+AVGPX(N,I)
      GO TO 154
  155 AVGPX(N,I)=-999.99
      PXYR(N)=-9999.9
  154 CONTINUE
  150 CONTINUE
C
C     COMPUTE SEASONAL PRECIPITATION
C
      DO 180 N=1,NSTA
      DO 181 IS=1,2
  181 SPX(N,IS)=0.0
      DO 182 I=1,12
      IS=1
      IF ((I.GT.LMOW).AND.(I.LT.IMOW)) IS=2
      IF (SPX(N,IS).LT.0.0)GO TO 182
      IF (AVGPX(N,I).LT.0.0)GO TO 183
      SPX(N,IS)=SPX(N,IS)+AVGPX(N,I)
      GO TO 182
  183 SPX(N,IS)=-999.99
  182 CONTINUE
  180 CONTINUE
C
C   PUNCH THE MAP INPUT DECK

C  PUNCH THE @A, @B, @C, AND @D CARDS
      IF (IPUNCH.EQ.1) THEN
         WRITE (IPU,874)IMO,IYR,LMO,LYR
  874    FORMAT(2H@A,2(1X,I3,1X,I5),2X,6HIN  IN,2X,3H,,,)
         IF (CONSIS.NE.'NONE') THEN
            WRITE (IPU,875)WEIGHT,MULT,CVAR,CONS,IMOW,IMOS
  875       FORMAT(2H@B,'   0  ',A4,I4,'  CONT   6  ',A4,2X,A4,2I4)
         ELSE
            WRITE (IPU,885)WEIGHT,MULT,CVAR,CONS,IMOW,IMOS
  885       FORMAT(2H@B,'   0  ',A4,I4,'  CONT   6  ',A4,2X,A4,2I4)
         ENDIF
         WRITE (IPU,877)MAPO
  877    FORMAT(2H@C,3X,A4)
         WRITE (IPU,878)NHLY,NDLY
  878    FORMAT(2H@D,2X,2I4)
         ENDIF
C
C  PRINT THE RESULTS.
      DO 160 N=1,NSTA
      IF (UELEV.NE.CODEM) HEIGHT=ELEV(N)*(1.0/0.3048)
      WRITE (IPR,908)N,(STNAME(N,I),I=1,5), (STID(N,I),I=1,2),XLAT(N),
     1 XLONG(N),HEIGHT,UELEV
  908 FORMAT (1H0,7HSTATION,I4,9X,5A4,10X,6HI.D.= ,2A4,5X,4HLAT=,F6.2,
     15X,5HLONG=,F7.2,5X,10HELEVATION=,F7.0,1X,A4)
      WRITE (IPR,909)(I,I=1,12)
  909 FORMAT (1H0,5HMONTH,15X,12I8,4X,6HANNUAL)
      WRITE (IPR,910)(NNY(N,I),I=1,12)
 910  FORMAT (1H ,10HYEARS USED,10X,12I8)
      WRITE (IPR,911)(CHAR(N,I),I=1,12)
  911 FORMAT (1H , 13HRATIO TO BASE,7X,12F8.2)
      WRITE (IPR,912)(AVGPX(N,I),I=1,12),PXYR(N)
  912 FORMAT (1H ,18HMEAN PRECIPITATION,2X, 12F8.2,F10.2)
      WRITE (IPR,918)SM(IMOW),SM(LMOW),SPX(N,1),SM(IMOS),SM(LMOS),
     *   SPX(N,2)
  918 FORMAT (1H ,16HSEASONAL PRECIP.,19X,A4,1H-,A4,1H=,F7.2,10X,A4,1H-,
     1A4,1H=,F7.2)
      WRITE (IPR,920)RMS(N),UNITS,BIAS(N)
  920 FORMAT (1H ,68HRMS ERROR OF ESTIMATION PROCEDURE BASED ON MONTHS W
     1ITH VALID DATA IS,F8.2,1X,A4,5X,5HBIAS=,F6.1,1X,1H%)
  160 CONTINUE
      IF (IDSQ.EQ.0) THEN
         IF (NAPPLY.EQ.1) THEN
            CALL UPAGE(IPR)
            WRITE (IPR,991)
            CALL CMOUT(NSTA,STNAME,CMW,IGMW)
            CALL UPAGE(IPR)
            WRITE (IPR,992)
            CALL CMOUT(NSTA,STNAME,CMS,IGMS)
         ELSE
            CALL UPAGE(IPR)
            CALL CMOUT(NSTA,STNAME,CM,IGM)
         ENDIF
      ENDIF
  991 FORMAT(1X,' STATION CORRELATIONS FOR THE WINTER MONTHS ONLY',/)
  992 FORMAT(1X,' STATION CORRELATIONS FOR THE SUMMER MONTHS ONLY',/)
      IF (MOPT.EQ.'YES ') THEN
      CALL UPAGE(IPR)
      DO 300 N=1,NSTA
C     SPECIFY FLAG FOR IDENTIFICATION OF MISSING, ESTIMATED OR OBSERVED
C     PRECIPITATION
         DO 310 M=1,NMO
            IF (PX(N,M).LT.-998.) THEN
               JFLAG(N,M)=2
               GO TO 310
               ENDIF
            IF (PX(N,M).LT.0.) THEN
               JFLAG(N,M)=1
               GO TO 310
               ENDIF
            JFLAG(N,M)=0
  310       CONTINUE
         CALL WYSUM (N,PX,STNAME,NMO,IMO,IYR,LYR,UNITS)
  300    CONTINUE
      ENDIF
      NF=0
      K=1
  198 CONTINUE
C
C  PUNCH @F AND @G CARDS FOR MAP INPUT DECK
      IF (IPUNCH.EQ.1) THEN
      DO 840 N=1,NSTA
         IT=E(5,N)
         DO 846 IM = 1,12
            RTBSM(N,IM) = AVGPX(NBASE,IM) * CHAR(N,IM)
  846    CONTINUE
         IF (IT.EQ.1.AND.NF.EQ.0) THEN
            WRITE (IPU,925) (E(I,N),I=15,19),XLAT(N),XLONG(N)
  925       FORMAT(4H@F ',5A4,1H',2(4X,F8.4),'    0. ')
            IF (MAPG.EQ.1)WRITE (IPU,913)(RTBSM(N,I),I=1,12),
     *         (STID(N,I),I=1,2)
  913       FORMAT (2H@G,1X,12F5.2,2X,1H$,2X,2A4)
            IORDER(K)=N
            IORDR(N)=K
            STAO(K)=STID(N,1)//STID(N,2)
            K=K+1
         ELSE
            IF (NF.EQ.1.AND.IT.EQ.24) THEN
               WRITE (IPU,927) (E(I,N),I=15,19),XLAT(N),XLONG(N)
  927       FORMAT(4H@F ',5A4,1H',2(4X,F8.4))
            IF (MAPG.EQ.1)WRITE (IPU,913)(RTBSM(N,I),I=1,12),
     *         (STID(N,I),I=1,2)
            IORDER(K)=N
            IORDR(N)=K
            STAO(K)=STID(N,1)//STID(N,2)
            K=K+1
            ENDIF
         ENDIF
  840 CONTINUE
         IF (NF.EQ.0) THEN
            NF=1
            GO TO 198
         ENDIF
      ENDIF
C
C     PUNCH CORRECTIONS FOR MAP3 INPUT DECK (@O CARDS)
C
      IF (IPUNCH.EQ.1) THEN
      NS=NSTAC
      DO 850 N=1,NS
         DO 841 J=1,NSTA
            IF (STA(N).NE.STAO(J)) GO TO 841
            IORD(N)=J
  841    CONTINUE
         ILIM=NCOR(N)
         DO 845 I=1,ILIM
CCC            NYRC(I,N)=NYRC(I,N)+1900
            IF (SEAS(I,N).EQ.WSYM.OR.SEAS(I,N).EQ.SSYM) THEN
               WRITE (IPU,1001)SEAS(I,N),IORD(N),IMOC(I,N),NYRC(I,N),
     *         TCORR(I,N)
 1001          FORMAT(2H@O,3X,A1,3X,I3,2X,I3,1X,I5,1X,F6.2)
            ELSE
               WRITE (IPU,1002)IORD(N),IMOC(I,N),NYRC(I,N),
     *         TCORR(I,N)
 1002          FORMAT(6H@O  ,,,3X,I3,2X,I3,1X,I5,1X,F6.2)
            ENDIF
  845       CONTINUE
  850    CONTINUE
      IF (NSTAC.GT.0)WRITE (IPU,855)
  855 FORMAT(5H@O 99)
      ENDIF
C
C  CONVERT ESTIMATES TO POSITIVE VALUES TO DO CONSISTENCY PLOTS
C  AND PUNCH @Q CARDS FOR MAP3 INPUT DECK
      IF (IPUNCH.EQ.1) WRITE (IPU,926)
  926 FORMAT(2H@Q)
      NF=0
  199 CONTINUE
      DO 206 N=1,NSTA
      DO 207 M=1,NMO
      IF (PX(N,M).GE.0.0)GO TO 207
      IF (PX(N,M).LT.-998.0)GO TO 207
      PX(N,M)=-PX(N,M)
  207 CONTINUE
      IF (IPUNCH.EQ.1) THEN
         IT=E(5,N)
         LHEAD=E(6,N)
         KMO=E(10,N)
         KDA=E(11,N)
         KYR=E(12,N)
         KHRMIN=E(13,N)
         KSEC=E(14,N)
         IF (IT.EQ.1.AND.NF.EQ.0) THEN
            WRITE (ipu,902) tsfilename(n)
            ELSE
               IF (NF.EQ.1.AND.IT.EQ.24) WRITE (ipu,902) tsfilename(n)
            ENDIF
         ENDIF
  206 CONTINUE
      IF (NF.EQ.0) THEN
         NF=1
         GO TO 199
      ENDIF
      CALL PXCON (IOPT,NSTA,NMO,PX,STNAME,IMO,IYR,UNITS,IMOW,IMOS,
     1   NSX,MaxMonth,IGS,IGROUP,BASE,PRINTD,STID,IORDCN,NG,NPG,JFLAG)
C
C     PUNCH @R AND @S CARDS FOR CONSISTENCY PLOTS IN MAP3
C
      IF (IPUNCH.EQ.1.AND.CONSIS.NE.'NONE') THEN
         WRITE (IPU,891)NG,(NPG(N),N=1,NG)
  891    FORMAT(2H@R,4I5)
         DO 894 N=1,NG
            WRITE (IPU,892)
  892       FORMAT(2H@S)
            NGS=NPG(N)
            WRITE (IPU,893)(IORDR(IORDCN(N,I)),I=1,NGS)
  893       FORMAT(20(15I4/))
  894    CONTINUE
      ENDIF
 8001 FORMAT(10X,'SUMMARY OF PXPP INPUT PARAMETERS AND STATION INFORMATI
     *ON')
 8002 FORMAT(//,49H     BASE        MOST PREVALENT     PRINT MONTHLY,7X,
     *9HUNITS FOR,7X,9HUNITS FOR)
 8003 FORMAT(80H   STATION         OBSERVATION      SUMMARIES FOR     PR
     *ECIPITATION    ELEVATION)
 8004 FORMAT(2X,10HIDENTIFIER,9X,4HTIME,12X,12HEACH STATION,13X,
     *11X,5HINPUT)
 8005 FORMAT(1X,12('-'),3X,16('-'),3X,15('-'),3X,15('-'),3X,9('-'))
 8006 FORMAT(3X,2A4,5X,I4,9H:00 HOURS,12X,A4,13X,A4,13X,A4)
 8007 FORMAT(////,5X,5HPRINT,11X,5HFIRST,14X,5HFIRST,12X,6HNUMBER,
     *6X,11HLAT/LONG IN)
 8008 FORMAT(1X,13H  CORRELATION,5X,8HMONTH OF,11X,8HMONTH OF,14X,
     *2HOF,9X,7HMINUTES)
 8009 FORMAT(5X,6HTABLES,9X,6HWINTER,13X,6HSUMMER,11X,8HSTATIONS,
     *5X,11HAND SECONDS)
 8010 FORMAT(1X,14('-'),3X,10('-'),9X,10('-'),8X,10('-'),4X,11('-'))
 8011 FORMAT(6X,A4,12X,A4,15X,A4,11X,I5,12X,A4)
 8012 FORMAT(////)
 8013 FORMAT(3X,10HPRINT DATE,6X,11HCONSISTENCY,
     *9X,7HPRODUCE,11X,7HINCLUDE,4X,12HUSE MULTIPLE)
 8014 FORMAT(1X,14HON CONSISTENCY,8X,4HPLOT,
     *11X,10HMAP3 INPUT,8X,10H@G CARD IN,2X,11HCALIBRATION)
 8015 FORMAT(6X,4HPLOT,12X,6HOPTION,
     *11X,8HDATA SET,9X,10HMAP3 INPUT,5X,5HFILES)
 8016 FORMAT(1X,14('-'),3X,13('-'),7X,10('-'),8X,10('-'),2X,12('-'))
 8017 FORMAT(7X,A4,12X,A4,14X,A4,15X,A4,9X,A4)
 8018 FORMAT(////,4X,17HPERIOD OF RECORD:,2X,I2,1H/,I4,1X,
     *7HTHROUGH,1X,I2,1H/,I4)
 8019 FORMAT(1X,7HSTATION,4X,11HDESCRIPTION,15X,8HLATITUDE,4X,
     *9HLONGITUDE,3X,9HELEVATION,2X,8H  TIME  ,3X,8HEXTERNAL)
 8020 FORMAT(1X,4HNAME,58X,1H(,A4,1H),4X,8HINTERVAL,3X,9HFILE TYPE)
 8022 FORMAT(1X,2A4,3X,5A4,6X,F7.2,6X,F7.2,5X,F7.1,6X,I2,9X,A4)
 8021 FORMAT(1X,8('-'),3X,11('-'),15X,8('-'),4X,9('-'),3X,9('-'),2X,8('-
     *'),3X,9('-'))
 8024 FORMAT( '0**Fatal error**: maximum 1200 months of data are allowed.
     1 Current time period is ',I2,'/',I4,' to ',I2,'/',I4)
C
  999 CALL USTOP(IPR,0)
C
      END
