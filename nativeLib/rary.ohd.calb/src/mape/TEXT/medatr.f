C$PRAGMA C (GET_APPS_DEFAULTS)
C MODULE MEDATR
C-----------------------------------------------------------------------
C
      SUBROUTINE MEDATR (IYR,LYR,IMO,LMO,LASTDA,IPRINT,
     *   NSTA,IMOW,IMOS,RMAXPE,IFAR,RUNITS,ITUNIT)
C
C   ROUTINE TO READ EVAPORATION DATA, DISTRIBUTE ACCUMULATED DATA,
C   MAKE OBSERVATION TIME SHIFTS, APPLY EVAPORATION ADJUSTMENTS,
C   AND WRITE TO SCRATCH FILE FOR ANALYSIS
C
      INCLUDE 'uiox'
      COMMON /MESTAX/ NWSST(25),TIMEOB(25,11),TPEC(25,11),
     *   IOBCGE(25,10),ITCGE(25,10),SCORF(25,11)
      COMMON /BHTIME/ LHEAD,KMO,KDA,KYR,KHRMIN,KSEC
      COMMON /MEDATX/ X(25),Y(25),PTPE(25,31),NWSSTA(25),PXNAME(25),
     *   FE(25),PENORM(25,12),ELEV(25)
C     
      CHARACTER*4 RUNITS,DUNITS,UNITMM
      CHARACTER*20 PXNAME
      character FILEPE*12
      real    STAID(3),DESCRP(5)
      integer m1, y1, m2, y2, unitno, am1, am2
      integer length
      real    mfac, afac
      character  oformat*12, dirnam*80, filenam*112
      real    flg_m_999, flg_m_998, flg_p_999, flg_p_998
C      
      DIMENSION PEDATA(31),PEDAT2(31),LASTDA(2,12)
      DIMENSION IFAR(25)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/calb/src/mape/RCS/medatr.f,v $
     . $',                                                             '
     .$Id: medatr.f,v 1.6 2002/02/11 18:41:00 dws Exp $
     . $' /
C    ===================================================================
C
C
      DATA WSYM/4HW   /,SSYM/4HS   /
      DATA SEA1/4HSUMM/,SEA2/4HER  /,SEA3/4HWINT/,SEA4/4H    /
      DATA PEMO/4HMOPE/
      DATA UNITMM/'MM  '/
      data  oformat/'(10f6.2)    '/
      data  m1,y1,m2,y2,am1,am2 /6*0/
      data  itime,unitno,ierror /0,0,0/        
      data  mfac,afac /2*0.0/
      data  flg_m_999/-999.0/, flg_m_998/-998.0/
      data  flg_p_999/+999.0/, flg_p_998/+998.0/
C
      IRG=1
C
C  CALCULATE BEGINNING AND ENDING MONTHS INDICES
      NBMO=IYR*12+IMO
      NEMO=LYR*12+LMO
C
C  SET LAST MONTH OF WINTER EQUAL TO INITIAL MONTH OF SUMMER MINUS 1
      LMOW=IMOS-1
C
      RMAXPE=0.0
C
C  SET MAXIMUM ACCUMULATED VALUE THAT WILL BE SPREAD OUT
      ACCMAX=1.0
      IF (RUNITS.EQ.UNITMM) ACCMAX=25.4
C
C  SET MONTH COUNTER EQUAL TO BEGINNING MONTH OF THE PERIOD OF ANALYSIS
10    NCMO=NBMO
      IFLAG=0
C
C  INITIALIZE EVAPORATION ADJUSTMENT FACTORS
      CFW=1.0
      CFS=1.0
      EVC=1.0
C
      IFAR(IRG)=0
      IPHEAD=1
      WRITE (LP,630)
      IF (NWSSTA(IRG).GT.9999) GO TO 20
      WRITE (LP,640) PXNAME(IRG)
20    MISIDC=1
      IDSTA=NWSSTA(IRG)
      ISTATE=NWSST(IRG)
      NOBC=1
      NTC=1
      NUM=IRG
C
      IF (IDSTA.LT.10000) GO TO 50
C
C  DUMMY STATION
30    DO 40 I=1,31
         PEDATA(I)=flg_p_999
40       CONTINUE
      GO TO 600
C
50    IMOX=0
      IYRX=0
      LMOX=0
      LYRX=0
C  READ EVAPORATION TIME SERIES HEADER CARD
CCC      READ (ICD,650) FILEPE,TYPE,IT,LHEAD,STAID,
CCC     *    KMO,KDA,KYR,KHRMIN,KSEC,DESCRP
      read (ICD,655) FILEPE, TYPE
C
C  CHECK FOR MONTHLY DATA
      IF (TYPE.EQ.PEMO) IFAR(IRG)=1
C
C  FIND TIME SERIES
CCC      CALL LOCATX (IMOX,IYRX,LMOX,LYRX,DUNITS,FILEPE,TYPE,IT,STAID,
CCC     *   DESCRP,NXRX,ISEQX)
C
C  Get name of data directory.
      call get_apps_defaults('calb_sta_ts_dir',15,dirnam,length)
C
C  Make whole pathname
C  Truncate the blank spaces off the end of FILEPE, then append it to
C  the end of the file name where the output time series is written.
C
      call ulenth(FILEPE,12,ilen)
      IF (length.gt.0) filenam=dirnam(1:length)
     *                               //'/evap/'//FILEPE(1:ilen)
      if (length.eq.0) filenam=FILEPE(1:ilen)
C
C  CALL CARDLO TO LOCATE THE TIME SERIES AND SEE IF ANY UNITS CONVERSION
C  IS NEEDED. CARDLO WILL ALSO RETURN THE BEGINNING AND ENDING DATES AND
C  THE DATA FORMAT OF THE INPUT DATA FILE.   
      call cardlo (IMOX,IYRX,LMOX,LYRX,m1,y1,m2,y2,runits,DUNITS,
     *             filenam,TYPE,itime,STAID,DESCRP,unitno,oformat,
     *             mfac,afac,am1,am2,ierror)    

      IF (IERROR.NE.0) GO TO 620
C     
      NBMOX=IYRX*12+IMOX
      NEMOX=LYRX*12+LMOX
C
C  DETERMINE THE LAST MONTH OF DATA TO BE READ FROM FILE
      NLMO=NEMOX
      IF (NLMO.GT.NEMO) NLMO=NEMO
      IF (NEMOX.LT.NBMO.OR.NBMOX.GT.NEMO) GO TO 60
      GO TO 70
C
C  DATA ON FILE IS TOTALLY OUTSIDE PERIOD NEEDED BY MAPE
C  TREAT THE STATION AS A DUMMY STATION
60    IDSTA=IDSTA+100000
      GO TO 30
C
C  IF DATA STARTS BEFORE PERIOD NEEDED BY MAPE, SKIP DATA
70    IF (NBMOX.GT.NBMO) GO TO 180
      IF (NBMOX.EQ.NBMO) GO TO 90
C
C  READ PAST DATA ON FILE WHICH OCCURS BEFORE PERIOD OF ANALYSIS
      NN=NBMO-1
      DO 80 I=NBMOX,NN
         ICYR=(I-1)/12
         ICMO=I-ICYR*12
CCC         CALL RDFILE (1,FILEPE,1,NXRX,ISEQX,1,ICMO,ICYR,1,31,PEDATA)
         call cardrd (1,unitno,oformat,am1,am2,mfac,afac,itime,
     +               ICMO,ICYR,PEDATA,1,ierror)    
         IF (IERROR.NE.0) GO TO 620
80       CONTINUE
C
90    ICYR=(NCMO-1)/12
      ICMO=NCMO-ICYR*12
C
C  FIRST READ FROM FILE FOR PERIOD OF ANALYSIS
CCC      CALL RDFILE (1,FILEPE,1,NXRX,ISEQX,1,ICMO,ICYR,1,31,PEDATA)
      call cardrd (1,unitno,oformat,am1,am2,mfac,afac,itime,
     +            ICMO,ICYR,PEDATA,1,ierror)    
      IF (IERROR.NE.0) GO TO 620
      GO TO 120
C
100   ICYR=(NCMO-1)/12
      ICMO=NCMO-ICYR*12
C
C  SHIFT SECOND MONTH OF DATA TO THE PEDATA ARRAY
      DO 110 I=1,31
         PEDATA(I)=PEDAT2(I)
110      CONTINUE
C
120   ICMO2=ICMO+1
      ICYR2=ICYR
      IF (ICMO2.LT.13) GO TO 130
         ICMO2=ICMO2-12
         ICYR2=ICYR2+1
130   IF (NCMO.LT.NLMO) GO TO 170
      IPHEA2=1
      IF (NLMO.LT.NEMOX) GO TO 170
      IF (MISIDC.EQ.2) GO TO 150
C
C  THE LAST MONTH OF DATA ON FILE IS IN THE PEDATA ARRAY
      MISIDC=2
      DO 140 I=1,31
         PEDAT2(I)=flg_p_999
140      CONTINUE
      GO TO 210
C
C  THE CURRENT MONTH IS AFTER THE LAST MONTH ON FILE
150   DO 160 I=1,31
         PEDATA(I)=flg_p_999
160      CONTINUE
C
C  TREAT REMAINING MONTHS AS MISSING
      MISIDC=3
      GO TO 600
C
C  READ SECOND MONTH OF DATA
CCC170   CALL RDFILE (1,FILEPE,1,NXRX,ISEQX,1,ICMO2,ICYR2,1,31,PEDAT2)
170   call cardrd (1,unitno,oformat,am1,am2,mfac,afac,itime,
     +            ICMO2,ICYR2,PEDAT2,1,ierror)    
      IF (IERROR.NE.0) GO TO 620
      GO TO 210
C
C  PERIOD OF ANALYSIS BEGINS BEFORE PERIOD ON FILE
180   MISIDC=1
C
190   IF (NCMO.EQ.NBMOX) GO TO 90
      DO 200 I=1,31
         PEDATA(I)=flg_p_999
200      CONTINUE
      GO TO 600
C
C  DETERMINE THE NUMBER OF DAYS IN THE FIRST MONTH
210   LY=0
      IF ((ICYR-4*(ICYR/4)).EQ.0) LY=1
      LAST=LASTDA((LY+1),ICMO)
C
C  REPLACE -999 WITH 999 AND SPREAD OUT THE ACCUMULATED DATA
C   (-998) IF THE ACCUMULATED TOTAL IS LESS THAN OR EQUAL TO
C   1 INCH AND THE ACCUMULATED DATA DOES NOT LAST MORE THAN 1 WEEK
      IF (IFLAG.EQ.0) GO TO 260
      DO 220 I=1,LAST
         IF (PEDATA(I).NE.flg_m_998) GO TO 240
220      CONTINUE
      DO 230 I=1,LAST
         PEDATA(I)=flg_p_999
230      CONTINUE
      GO TO 400
C
240   IEACC=I
      DO 250 I=1,IEACC
         PEDATA(I)=flg_p_999
250      CONTINUE
      IFLAG=0
C
260   IEACC=1
C
270   DO 280 I=IEACC,LAST
         IF (PEDATA(I).EQ.flg_m_999) PEDATA(I)=flg_p_999
         IF (PEDATA(I).EQ.flg_m_998) GO TO 290
         IF (PEDATA(I).LT.0.0) PEDATA(I)=0.0
280      CONTINUE
      GO TO 400
C
290   IBACC=I
      DO 300 I=IBACC,LAST
         IF (PEDATA(I).EQ.flg_m_999) PEDATA(I)=flg_p_999
         IF (PEDATA(I).NE.flg_m_998) GO TO 310
300      CONTINUE
      GO TO 330
C
310   IEACC=I
      NDACC=IEACC-IBACC+1
      PEINC=PEDATA(IEACC)/NDACC
      IF (PEDATA(IEACC).GT.ACCMAX) PEINC=flg_p_999
      IF (NDACC.GT.7) PEINC=flg_p_999
      DO 320 I=IBACC,IEACC
         PEDATA(I)=PEINC
320      CONTINUE
      GO TO 270
C
C  DETERMINE THE NUMBER OF DAYS IN THE SECOND MONTH
330   LY=0
      IF ((ICYR2-4*(ICYR2/4)).EQ.0) LY=1
      LAST2=LASTDA((LY+1),ICMO2)
      DO 340 I=1,LAST2
         IF (PEDAT2(I).NE.flg_m_998) GO TO 350
340      CONTINUE
      GO TO 380
C
350   IEACC=I
      NDACC=IEACC+LAST-IBACC+1
      PEINC=PEDAT2(IEACC)/NDACC
      IF (PEDAT2(IEACC).GT.ACCMAX) PEINC=flg_p_999
      IF (NDACC.GT.7) PEINC=flg_p_999
      DO 360 I=IBACC,LAST
         PEDATA(I)=PEINC
360      CONTINUE
      DO 370 I=1,IEACC
         PEDAT2(I)=PEINC
370      CONTINUE
      GO TO 400
C
C  ACCUMULATED DATA EXTENDED INTO THE THIRD MONTH, REPLACE
C   WITH 999 AND SET IFLAG=1
380   DO 390 I=IBACC,LAST
         PEDATA(I)=flg_p_999
390      CONTINUE
      IFLAG=1
C
400   IF (MISIDC.NE.1) GO TO 420
C
C  FIRST MONTH OF DATA ON FILE WITHIN THE PERIOD OF ANALYSIS
       MONUM=NCMO-NBMO+1
C
C  DETERMINE IF A CHANGE HAS OCCURRED IN THE OBSERVATION TIME
      IF (MONUM.LT.IOBCGE(IRG,NOBC)) GO TO 410
         NOBC=NOBC+1
         IPHEAD=1
410   HR1=TIMEOB(IRG,NOBC)
      MISIDC=0
      GO TO 430
C
C  SET FIRST MONTHS PARAMETERS EQUAL TO SECOND MONTHS PARAMETERS
420   HR1=HR2
      IPHEAD=IPHEA2
430   MONUM2=NCMO-NBMO+2
      IF (MONUM2.LT.IOBCGE(IRG,NOBC)) GO TO 440
         NOBC=NOBC+1
         IPHEA2=1
         GO TO 450
440   IPHEA2=0
450   HR2=TIMEOB(IRG,NOBC)
      IF (HR1.GT.12.) GO TO 490
C
C  FIRST MONTH HAS A MORNING OBSERVATION TIME - SHIFT THE DATA
C  ONE DAY FORWARD
      NLAST=LAST-1
      DO 460 I=1,NLAST
         PEDATA(I)=PEDATA(I+1)
460      CONTINUE
      IF (HR2.GT.12.) GO TO 470
C
C  SECOND MONTH HAS A MORNING OBSERVATION TIME - PUT FIRST DAY
C  DATA OF THE SECOND MONTH IN THE LAST DAY OF THE FIRST MONTH
      PEDATA(LAST)=PEDAT2(1)
      IF (PEDATA(LAST).LE.flg_m_998) PEDATA(LAST)=flg_p_999
      GO TO 490
C
C  SECOND MONTH HAS AN AFTERNOON OBSERVATION TIME - IF THE FIRST
C  DAY DATA OF THE SECOND MONTH HAS A VALUE LT 1.5*NORMAL TREAT
C  THE LAST DAY DATA OF THE FIRST MONTH AS MISSING. IF THE FIRST
C  DAY DATA OF THE SECOND MONTH HAS A VALUE GE 1.5*NORMAL DIVIDE
C  ITS VALUE BY TWO, AND PUT THIS VALUE IN THE LAST DAY OF THE FIRST
C  MONTH AND IN THE FIRST DAY OF THE SECOND MONTH
470   PEAVG=(PENORM(IRG,ICMO)+PENORM(IRG,ICMO2))/2.0
      PELIM=1.5*PEAVG
      IF (PEDAT2(1).LT.PELIM) GO TO 480
         PEDATA(LAST)=PEDAT2(1)/2.0
         PEDAT2(1)=PEDATA(LAST)
         GO TO 490
480   PEDATA(LAST)=flg_p_999
490   OBT=HR1*100.+0.1
C
C  APPLY EVAPORATION ADJUSTMENT FACTORS
      MONUM=NCMO-NBMO+1
      IF ((MONUM).LT.ITCGE(IRG,NTC)) GO TO 550
      NTC=NTC+1
      IF (SCORF(IRG,NTC).EQ.WSYM) GO TO 500
      IF (SCORF(IRG,NTC).EQ.SSYM) GO TO 510
C
C  APPLY ADJUSTMENT ALL YEAR
      CF1=SEA4
      CF2=SEA4
      GO TO 520
C
C  APPLY ADJUSTMENT TO WINTER
500   CF1=SEA3
      CF2=SEA2
      GO TO 520
C
C  APPLY ADJUSTMENT TO SUMMER
510   CF1=SEA1
      CF2=SEA2
C
520   WRITE (LP,660) ISTATE,IDSTA,ICMO,ICYR,OBT,TPEC(IRG,NTC),CF1,CF2
      IPHEAD=0
      IF (SCORF(IRG,NTC).NE.WSYM) GO TO 530
C
C  WINTER ADJUSTMENT FACTOR
      CFW=TPEC(IRG,NTC)
      GO TO 550
530   IF (SCORF(IRG,NTC).NE.SSYM) GO TO 540
C
C  SUMMER ADJUSTMENT FACTOR
      CFS=TPEC(IRG,NTC)
      GO TO 550
C
C  WINTER AND SUMMER ADJUSTMENT FACTOR
540   CFW=TPEC(IRG,NTC)
      CFS=TPEC(IRG,NTC)
550   CONTINUE
C
C  APPLY EVAPORATION ADJUSTMENT FACTORS AFTER DETERMINING
C  WHETHER MONTH IS WINTER OR SUMMER
      EVC=CFW
      IF ((ICMO.GT.LMOW).AND.(ICMO.LT.IMOW))EVC=CFS
      IF (EVC.EQ.1.0) GO TO 570
      DO 560 I=1,31
         IF (PEDATA(I).GT.flg_p_998) GO TO 560
         PEDATA(I)=PEDATA(I)*EVC
560      CONTINUE
570   DO 580 I=1,31
         IF (PEDATA(I).LT.900.0.AND.PEDATA(I).GT.RMAXPE)
     *      RMAXPE=PEDATA(I)
580      CONTINUE
C
      IF (IPHEAD.EQ.0) GO TO 590
C
      WRITE (LP,670) ISTATE,IDSTA,ICMO,ICYR,OBT
590   IF (IPRINT.GT.0) THEN
         WRITE (LP,680) ICMO,ICYR,(PEDATA(I),I=1,16)
         WRITE (LP,690) (PEDATA(I),I=17,LAST)
         ENDIF
C
C  WRITE DATA TO SCRATCH FILE
600   WRITE (UNIT=ITUNIT,REC=NUM) PEDATA
C
      IF (NCMO.EQ.NEMO) GO TO 610
      NUM=NUM+NSTA
      NCMO=NCMO+1
      IF (IDSTA.GT.9999) GO TO 600
      IF (MISIDC.EQ.1) GO TO 190
      IF (MISIDC.EQ.2) GO TO 150
      IF (MISIDC.EQ.3) GO TO 600
      GO TO 100
C
610   CALL CCLOSL
C
      IF (IRG.EQ.NSTA) GO TO 620
         IRG=IRG+1
         GO TO 10
C
620   RETURN
C
C
630   FORMAT ('0')
640   FORMAT ('0',10X,'EVAPORATION DATA FOR ',A)
C650   FORMAT (A12,A4,I4,I6,1X,A12,1X,I2,1X,I2,1X,I2,1X,I4,1X,I4,2X,A20)
655   format (a12,1x,a4)
660   FORMAT (' NWS STA.',I3,'-',I4,5X,I2,'/',I4,5X,'OB. TIME=',F5.0,
     *   5X,'EVAP CORRECTION ',F5.2,2X,2A4)
670   FORMAT (' NWS STA.',I3,'-',I4,5X,I2,'/',I4,5X,'OB. TIME=',F5.0)
680   FORMAT (' ',1X,I2,'/',I4,4X,16F7.2)
690   FORMAT (' ',12X,16F7.2)
C
      END
