C
C-----------------------------------------------------------------------
C
C @PROCESS LVL(77)
C
      SUBROUTINE MECHEK (MONUM,NTMO,IYR,IMO,PXNAME,GNORM,GMM,IEND,FEPE,
     *   RMAXPE,LASTDA)
C
C  ROUTINE TO COMPARE ESTIMATED AND OBSERVED PE DATA
C
      CHARACTER*20 PXNAME
C
      DIMENSION LASTDA(2,12),SUM(2),GMM(2,31)
      DIMENSION SNUM(7)
      DIMENSION GNORM(12)
      DIMENSION ND(12),SE2(2,12)
C
      INCLUDE 'uiox'
      COMMON /MEPLTX/ XY(70,70),XDUM(5301)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/calb/src/mape/RCS/mechek.f,v $
     . $',                                                             '
     .$Id: mechek.f,v 1.2 1998/10/14 13:26:32 page Exp $
     . $' /
C    ===================================================================
C
C
      DATA BLANK,PLUS,ASTER,DOT/1H ,1H+,1H*,1H./
C
      IF (MONUM.GT.1) GO TO 60
C
      IF (IEND.GT.0) GO TO 10
C
C  PLOT SCALE
      RMAXPE=(RMAXPE*10)+0.5
      ITOP=RMAXPE
      TOP=ITOP
      FST=(TOP/70.)*100.0
      IFST=FST
      FST=IFST
      FST=FST/100.0
      READ (ICD,200) EMAX
10    DO 40 IX=1,70
         DO 40 IY=1,70
            IF (IY.EQ.IX) GO TO 30
            IF ((IY-10*(IY/10)).EQ.0) GO TO 20
               XY(IX,IY)=BLANK
               GO TO 40
20          XY(IX,IY)=DOT
            GO TO 40
30          XY(IX,IY)=PLUS
40      CONTINUE
      DO 50 MO=1,12
         ND(MO)=0
         SE2(1,MO)=0.0
         SE2(2,MO)=0.0
50       CONTINUE
C
C  COMPUTATIONS FOR EACH MONTH
60    MO=MONUM
      NYR=(MO-1)/12
      IYRC=IYR+NYR
      NMO=MO-NYR*12
      IMOC=IMO+NMO-1
      IF (IMOC.LT.13) GO TO 70
         IMOC=IMOC-12
         IYRC=IYRC+1
C
C  CHECK FOR LEAP YEAR TO DETERMINE THE NUMBER OF DAYS IN THE
C  CURRENT MONTH
70    LY=0
      IF ((IYRC-4*(IYRC/4)).EQ.0) LY=1
      LAST=LASTDA((LY+1),IMOC)
      RLAST=LAST
C
C   CALCULATE THE YEAR, MONTH, AND NUMBER OF DAYS IN THE PREVIOUS
C   MONTH
      IYRC1=IYRC
      IMOC1=IMOC-1
      IF (IMOC1.GT.0) GO TO 80
         IMOC1=12
         LAST1=31
         GO TO 90
80    LY=0
      IF ((IYRC1-4*(IYRC1/4)).EQ.0) LY=1
      LAST1=LASTDA((LY+1),IMOC1)
90    RLAST1=LAST1
C
C  DETERMINE THE NEXT MONTH
      IMOC2=IMOC+1
      IF (IMOC2.EQ.13) IMOC2=1
      SUM(1)=0.0
      SUM(2)=0.0
      IL=LAST
      KOUNT=0
      DO 130 I=1,IL
         RI=I
         TO=GMM(1,I)
         TE=GMM(2,I)
         IF (TE.EQ.TO) GO TO 130
         IF (ABS(TO-TE).LE.EMAX) GO TO 100
         WRITE (LP,210) IMOC,I,IYRC,TO,TE
C      CALCULATE THE NORMAL VALUE FOR THE DAY
100      IF (I.LT.16) GO TO 110
C      DAY IS IN THE SECOND HALF OF THE MONTH
         TN=(GNORM(IMOC2)-GNORM(IMOC))*((RI-16.)/
     *      RLAST)+GNORM(IMOC)
         GO TO 120
C      DAY IS IN THE FIRST HALF OF THE MONTH
110      TN=(GNORM(IMOC)-GNORM(IMOC1))*((RLAST1-16.+RI)/
     *      RLAST1)+GNORM(IMOC1)
C      CALCULATE THE SUM OF THE SQUARES OF THE OBSERVED - ESTIMATED
C      FOR THE MONTH
120      SUM(1)=SUM(1)+(TO-TE)*(TO-TE)
C      CALCULATE THE SUM OF THE SQUARES OF THE OBSERVED - NORMAL
C      FOR THE MONTH
         SUM(2)=SUM(2)+(TO-TN)*(TO-TN)
C     CALCULATE THE PLOTTING POSITION FOR THE OBSERVED VS. ESTIMATED PLO
         IX=TE/FST*10.0
         IF (IX.LT.1) IX=1
         IF (IX.GT.70) IX=70
         IY=TO/FST*10.0
         IF (IY.LT.1) IY=1
         IF (IY.GT.70) IY=70
         XY(IX,IY)=ASTER
         KOUNT=KOUNT+1
130      CONTINUE
C
C  CALCULATE THE SUM OF THE SQUARES OF THE OBSERVED - ESTIMATED
C  FOR MONTHS (JAN,FEB,ETC.)
      SE2(1,IMOC)=SE2(1,IMOC)+SUM(1)
C
C  CALCULATE THE SUM OF THE SQUARES OF THE OBSERVED - NORMAL
C  FOR MONTHS (JAN,FEB,ETC.)
      SE2(2,IMOC)=SE2(2,IMOC)+SUM(2)
      IF (KOUNT.EQ.0) GO TO 140
C
C   CALCULATE THE RMS ERROR FOR THE MONTH
      SUM(1)=SQRT(SUM(1)/KOUNT)
C
C  CALCULATE THE NUMBER OF DAYS OF MISSING DATA IN THE MONTHS
      ND(IMOC)=ND(IMOC)+KOUNT
C
140   WRITE (LP,220) IMOC,IYRC,SUM(1)
C
      IF (MONUM.LT.NTMO) GO TO 190
C
C  SUMMARY OF ALL MONTHS
      SUM(1)=0.0
      SUM(2)=0.0
      NDAYS=0
      DO 150 MO=1,12
C     CALCULATE THE SUM OF THE SQUARES OF OBSERVED-ESTIMATED
C     FOR PERIOD
         SUM(1)=SUM(1)+SE2(1,MO)
C     CALCULATE THE SUM OF THE SQUARES OF OBSERVED - NORMAL
C     FOR PERIOD
         SUM(2)=SUM(2)+SE2(2,MO)
         IF (ND(MO).EQ.0) GO TO 150
C     CALCULATE THE RMS ERROR (OBSERVED-ESTIMATED) FOR THE MONTHS
         SE2(1,MO)=SQRT(SE2(1,MO)/ND(MO))
C     CALCULATE THE RMS ERROR (OBSERVED-NORMAL) FOR THE MONTHS
         SE2(2,MO)=SQRT(SE2(2,MO)/ND(MO))
         NDAYS=NDAYS+ND(MO)
150      CONTINUE
C
      IF (NDAYS.EQ.0) GO TO 160
C
C  CALCULATE THE RMS ERROR (OBSERVED-ESTIMATED) FOR THE PERIOD
      SUM(1)=SQRT(SUM(1)/NDAYS)
C
C  CALCULATE THE RMS ERROR (OBSERVED-NORMAL) FOR THE PERIOD
      SUM(2)=SQRT(SUM(2)/NDAYS)
C
160   WRITE (LP,230)
      WRITE (LP,240)
      WRITE (LP,250) (MO,MO=1,12)
      WRITE (LP,260) (SE2(1,MO),MO=1,12),SUM(1)
      WRITE (LP,270) (SE2(2,MO),MO=1,12),SUM(2)
      WRITE (LP,280)
C
C  PLOTS
      CALL UPAGE (LP)
      WRITE (LP,290) PXNAME
      DO 170 I=1,7
         SNUM(I)=FST*I
170      CONTINUE
      WRITE (LP,300) (SNUM(I),I=1,7)
      DO 180 IX=1,70
         SC=(FST*IX)/10.0
         WRITE (LP,310) SC,(XY(IX,IY),IY=1,70)
180      CONTINUE
C
190   RETURN
C
C
200   FORMAT (F5.0)
210   FORMAT (' ',10X,'MAX. ERROR EXCEEDED--',5X,I2,'/',
     *   I2,'/',I2,5X,4HOBS=,F6.3,5X,4HEST=,F6.3)
220   FORMAT (' ROOT MEAN SQUARE FOR ',I3,'/',I2,2X,'=',F6.3 ///)
230   FORMAT ('0')
240   FORMAT ('0',10X,'SUMMARY OF ROOT MEAN SQUARE OF OBS. AND EST.--',
     *   'PLUS STANDARD DEVIATION OF OBS. AND MEAN')
250   FORMAT ('0','MONTH',5X,12I8,5X,'TOTAL')
260   FORMAT (' RMS',7X,12F8.3,F10.3)
270   FORMAT (' S.D.',6X,12F8.3,F10.3)
280   FORMAT (' ')
290   FORMAT ('0OBSERVED VS. ESTIMATED PE VALUES FOR ',
     *   A,10X,'+ IS 45 DEGREE LINE')
300   FORMAT ('0EST/OBS',F11.2,6F10.2)
310   FORMAT (' ',F6.3,1X,70A1)
C
      END
