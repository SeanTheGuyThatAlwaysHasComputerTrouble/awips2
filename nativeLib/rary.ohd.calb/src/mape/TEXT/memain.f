C MODULE MEMAIN
C-----------------------------------------------------------------------
C
C  MAIN ROUTINE FOR MEAN AREAL POTENTIAL EVAPOTRANSPIRATION (MAPE)
C  PROGRAM
C
C  MAPE COMPUTES MEAN DAILY EVAPORATION AND IS DIMENSIONED FOR
C  THE FOLLOWING:
C     10  AREAS
C     25  EVAPORATION STATIONS
C     10  CHANGES IN OBSERVATION TIME PER STATION
C     10  ADJUSTMENTS TO EVAPORATION PER STATION
C
C  CONSISTENCY CHECK IS DIMENSIONED FOR THE FOLLOWING:
C      5  GROUPS OF STATIONS
C    600  MONTHS OF DATA
C
C  MAPE PROGRAM MUST BE RUN FOR AT LEAST TWO MONTHS
C
C
      SUBROUTINE MAPE_MAIN
C
      CHARACTER DSNAME*44
      CHARACTER*4 DTYPE/'MAPE'/
      CHARACTER*4 DDIMN/'L'/
      CHARACTER*4 DUNITS
      CHARACTER*12 FILEN
      CHARACTER*20 GNAME
      CHARACTER*12 AREAID(10)
      CHARACTER*20 ARNAME(10)
C
C  DEFINE ARRAY FOR HOLDING BASIN NAMES
      CHARACTER*12 BASNAM(10), BASZONE(10)
      CHARACTER FILENM*1, FMT*1
C
C  DEFINE COUNTER FOR HOW MANY TIMES SUBROUTINE MEBASN CALLED
      INTEGER ICNT
      INTEGER UNITNO(10)
C
C  DEFINE FLAG FOR MISSING TIMESERIES PE DATA
      REAL  FLG_P_999
C
C  DEFINE FLAG FOR WHETHER MONTHLY STATION TIMESERIES PE DATA
C  AVAILABLE (0), ESTIMATED (1), OR MISSING (2)
      INTEGER*2 PT_FLAG(25,600)
C
      DIMENSION LASTDA(2,12),SM(12)
      DIMENSION W(10,25)
      DIMENSION GNORM(12),GMM(2,31),IFAR(25)
C
      INCLUDE 'uiox'
      INCLUDE 'uoptnx'
      INCLUDE 'upagex'
      COMMON /MEDATX/ X(25),Y(25),PTPE(25,31),NWSSTA(25),PXNAME(25),
     *   FE(25),PENORM(25,12),ELEV(25)
      CHARACTER*20 PXNAME
      COMMON /MESTAX/ NWSST(25),TIMEOB(25,11),TPEC(25,11),
     *   IOBCGE(25,10),ITCGE(25,10),SCORF(25,11)
      COMMON /MEAPPX/ APPE(25,600)
      COMMON /MEAREX/ PEAREA(10,600),MFLAG(600)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/calb/src/mape/RCS/memain.f,v $
     . $',                                                             '
     .$Id: memain.f,v 1.7 2004/08/10 14:57:43 dsa Exp $
     . $' /
C    ===================================================================
C
      DATA LASTDA/31,31,28,29,31,31,30,30,31,31,30,30,
     *            31,31,31,31,30,30,31,31,30,30,31,31/
      DATA SM/4HJAN.,4HFEB.,4HMAR.,4HAPR.,4HMAY ,4HJUNE,
     *        4HJULY,4HAUG.,4HSEPT,4HOCT.,4HNOV.,4HDEC./
      DATA  FLG_P_999/+999.0/
C
C
C     Subroutine ARGVER outputs the version/date info and exits the
C      program if the first command line argument is "-version"
C
      CALL ARGVER()
C
      CALL UPRIMO_MAPE()
C
C  PRINT PAGE HEADER
      ISAV=ICDPUN
      ICDPUN=7
      CALL USETO1 ('NOOVERPRINT',IERR)
      CALL USETO1 ('NOPAGNUM',IERR)
      CALL UPAGE (LP)
      CALL USETO1 ('NOPAGHDR',IERR)
      ICDPUN=ISAV
C
C  SET MAXIMUMS
      MXMON=600
      MNMON=2
      MXSTA=25
      MXAREA=10
      MXTIMC=10
      MXPCOR=10
C
      ICNT=0
      IEND=0
      IERROR=0
C
C  OPEN TEMPORARY FILE
      ITUNIT=19
      FILENM=' '
      FMT='U'
      CALL UPOPEN (ITUNIT,FILENM,31,FMT,ICOND)
C
C  READ RUN DATES AND OPTIONS
      READ (ICD,330,END=320)
     *    IMO,IYR,LMO,LYR,NSTA,NAREA,IPRINT,DUNITS,FILEN,
     *    ICORR,IOPT1,NOOUT,IPUNCH,IELEV,IMOW,IMOS
C
      IF (IOPT1.LT.1) IOPT1=0
C
      IF (NOOUT.LT.1) NOOUT=0
      IF (NOOUT.GT.0.AND.FILEN.EQ.' ') THEN
         CALL UEROR (LP,0,-1)
         WRITE (LP,340) NOOUT
         IERROR=1
         ENDIF
C
      IF (IYR.LT.1900.OR.LYR.LT.1900) THEN
         CALL UEROR (LP,0,-1)
         WRITE (LP,345) IYR, LYR
         IERROR=1
         ENDIF
      NBMO=IYR*12+IMO
      NEMO=LYR*12+LMO
      NTMO=NEMO-NBMO+1
C
      IF (NTMO.GT.MXMON) THEN
         CALL UEROR (LP,0,-1)
         WRITE (LP,350) 'MONTHS',NTMO,MXMON
         IERROR=1
         ENDIF
      IF (NTMO.LT.MNMON) THEN
         CALL UEROR (LP,0,-1)
         WRITE (LP,360) 'MONTHS',NTMO,MNMON
         IERROR=1
         ENDIF
C
      IF (NSTA.GT.MXSTA) THEN
         CALL UEROR (LP,0,-1)
         WRITE (LP,350) 'STATIONS',NSTA,MXSTA
         IERROR=1
         ENDIF
C
      IF (NAREA.GT.MXAREA) THEN
         CALL UEROR (LP,0,-1)
         WRITE (LP,350) 'AREAS',NAREA,MXAREA
         IERROR=1
         ENDIF
C
      IF (NAREA.GT.0.AND.IOPT1.GT.0) THEN
         CALL UEROR (LP,0,-1)
         WRITE (LP,370) NAREA,IOPT1
         IERROR=1
         ENDIF
C
      IF (IERROR.GT.0) GO TO 320
C
      READ (ICD,380) GL
C
C  READ STATION INFORMATION
      DO 20 IRG=1,NSTA
         READ (ICD,390) PXNAME(IRG),X(IRG),Y(IRG),
     *       NWSST(IRG),NWSSTA(IRG),
     *       TIMEOB(IRG,1),ELEV(IRG),FE(IRG)
         READ (ICD,400) (PENORM(IRG,MO),MO=1,12)
         DO 10 MO=1,12
            IF (PENORM(IRG,MO).GT.0.001) GO TO 10
            CALL UWARN (LP,0,-1)
            WRITE (LP,410) PXNAME(IRG),MO,PENORM(IRG,MO)
            PENORM(IRG,MO)=0.01
10          CONTINUE
20       CONTINUE
C
      IF (IOPT1.EQ.0) GO TO 40
C
      NSTA=NSTA+1
      ICHECK=NSTA
C
C  SET ESTIMATED STATION INFORMATION EQUAL TO STATION
C  INFORMATION OF STATION TO BE ESTIMATED
      FE(ICHECK)=FE(IOPT1)
      X(ICHECK)=X(IOPT1)
      Y(ICHECK)=Y(IOPT1)
      NWSST(ICHECK)=NWSST(IOPT1)
      TIMEOB(ICHECK,1)=24.0
      ELEV(ICHECK)=ELEV(IOPT1)
      NWSSTA(ICHECK)=77777
      DO 30 MO=1,12
         PENORM(ICHECK,MO)=PENORM(IOPT1,MO)
30       CONTINUE
      PXNAME(ICHECK)='ESTIMATED STATION. -'
C
40    WRITE (LP,420) IMO,IYR,LMO,LYR
      WRITE (LP,430) SM(IMOW),SM(IMOS)
      WRITE (LP,440)
      WRITE (LP,450)
      WRITE (LP,460) GL
      WRITE (LP,470)
      DO 50 IRG=1,NSTA
         WRITE (LP,480) IRG,PXNAME(IRG),X(IRG),Y(IRG),NWSST(IRG),
     *      NWSSTA(IRG),ELEV(IRG),FE(IRG)
         WRITE (LP,490) (PENORM(IRG,MO),MO=1,12)
50       CONTINUE
C
      IF (NAREA.EQ.0) GO TO 120
C
C  READ AREA INFORMATION FOR MAPE COMPUTATION
      DO 90 N=1,NAREA
         read (ICD,505) ARNAME(N),AREAID(N),basnam(N), baszone(N)
         IF (IELEV.GT.0) GO TO 80
C     READ PREDETERMINED STATION WEIGHTS
         READ (ICD,510) (W(N,IRG),IRG=1,NSTA)
         DO 60 IRG=1,NSTA
            IF (W(N,IRG).GT.1.00) GO TO 70
60          CONTINUE
         GO TO 90
70       CALL UWARN (LP,0,-1)
         WRITE (LP,520) N,IRG,W(N,IRG)
         GO TO 320
C     READ AREA GRID MAPS AND COMPUTE GRID POINT WEIGHTS
80       CALL MEGWP (NSTA,X,Y,W,N)
90       CONTINUE
C
C  PRINT STATION WEIGHTS FOR EACH AREA
      DO 110 N=1,NAREA
         WRITE (LP,535) ARNAME(N),AREAID(N),basnam(N),baszone(N)
         WRITE (LP,540)
         DO 100 IRG=1,NSTA
            WRITE (LP,550) PXNAME(IRG),W(N,IRG)
100         CONTINUE
110      CONTINUE
C
      IF (NOOUT.LT.1) GO TO 120
C
C  CALL ROUTINE TO WRITE TIME SERIES HEADERS
CCC   CALL HEADER (FILEN,DTYPE,DDIMN,DUNITS,24,NAREA,IMO,IYR,LMO,LYR,
CCC  *   AREAID,MXAREA,ARNAME,MXAREA)
C
120   IFMON=IYR*12+IMO
C
C  READ OBSERVATION TIME CHANGES IN ORDER BY TIME
      CALL METIMC (NSTA,IFMON,IMO,IYR,MXTIMC,IERROR)
      IF (IERROR.GT.0) GO TO 320
C
C  READ EVAPORATION CORRECTIONS IN ORDER BY TIME
      CALL MEPCOR (NSTA,IFMON,ICORR,MXPCOR,IERROR)
      IF (IERROR.GT.0) GO TO 320
C
      CALL UPAGE (LP)
      WRITE (LP,440)
      WRITE (LP,450)
      WRITE (LP,560) DUNITS
C
C  READ EVAPORATION DATA FROM CALIBRATION FILES, MAKE CORRECTIONS
C  FOR OBSERVATION TIME AND EVAPORATON ADJUSTMENTS AND WRITE
C  DATA TO SCRATCH FILE
      CALL MEDATR (IYR,LYR,IMO,LMO,LASTDA,IPRINT,NSTA,
     *   IMOW,IMOS,RMAXPE,IFAR,DUNITS,ITUNIT)
      IF (IOPT1.EQ.0) GO TO 130
C
C  SET TYPE OF ESTIMATED STATION DATA EQUAL TO THE INTERVAL
C  OF THE STATION TO BE ESTIMATED DATA
      IFAR(ICHECK)=IFAR(IOPT1)
C
C     -    -    -    -    -    -    -    -    -    -    -    -    -    -
C
130   MONTH=IMO
      LOOPAR=-1
      IYEAR=IYR
      MONUM=1
      DO 140 I=1,NTMO
         MFLAG(I)=0
140      CONTINUE
C
      CALL UPAGE (LP)
C
150   IF (NAREA.EQ.0) GO TO 210
      LOOPAR=LOOPAR+1
      IF (NAREA-3) 160,180,200
160   IF (NAREA.GT.1) GO TO 170
      IF (LOOPAR.EQ.12) LOOPAR=0
      GO TO 190
170   IF (LOOPAR.EQ.6) LOOPAR=0
      GO TO 190
180   IF (LOOPAR.EQ.4) LOOPAR=0
190   IF (LOOPAR.EQ.0) GO TO 200
      GO TO 210
200   CONTINUE
C
C   CHECK FOR YEAR TO DETERMINE THE NUMBER OF DAYS IN THE MONTH
210   LY=0
      IF ((IYEAR-4*(IYEAR/4)).EQ.0) LY=1
      LAST=LASTDA((LY+1),MONTH)
C
C   READ MONTH OF DATA FROM SCRATCH FILE FOR EACH STATION
      CALL MEDATI (NSTA,MONUM,LAST,NTMO,PTPE,ITUNIT)
C
      IL=LAST
      IF (MONUM.GT.1) GO TO 220
      IF (IOPT1.EQ.0) GO TO 220
      CALL UPAGE (LP)
      WRITE (LP,570) PXNAME(IOPT1),FE(IOPT1)
C
C  ESTIMATE MISSING DATA
220   CALL MEEST (NSTA,IL,GL,IOPT1,MONTH,IYEAR,IFAR,IPRINT,
     *   MCOUNT,IEND,LASTDA)
C
C  SET MFLAG=1 IF THE MAPE FOR THE MONTH WILL HAVE 10 OR MORE
C  DAYS WHICH WILL BE BASED ON THE MONTHLY NORMALS INPUT
      IF (MCOUNT.GE.10) MFLAG(MONUM)=1
c
C  SET FLAG FOR INPUT PTPE DATA FOR MONTH TO 'MISSING' (2) IF
C  ANY DAYS OF MONTH STILL FLAGGED 'MISSING' (999.00)
      DO 225 IRG=1,NSTA
         PT_FLAG(IRG,MONUM) = 0
         DO 225 I=1,IL
            IF (PTPE(IRG,I).NE.FLG_P_999) GO TO 225
               PTPE(IRG,I) = 0.0
               PT_FLAG(IRG,MONUM) = 2
225      CONTINUE
C
C  SUBTRACT 2000.0 FROM ALL ESTIMATED VALUES
C  ALSO, FLAG PTPE DATA FOR MONTH TO 'ESTIMATED' (1)
      DO 230 IRG=1,NSTA
         DO 230 I=1,IL
            IF (PTPE(IRG,I).LT.1000.0) GO TO 230
               PTPE(IRG,I)=PTPE(IRG,I)-2000.0
               pt_flag(irg,monum) = 1
230      CONTINUE
C
C  COMPUTE ACCUMULATED POINT PE FOR EACH STATION
      DO 240 IRG=1,NSTA
         APPE(IRG,MONUM)=0.0
         DO 240 I=1,IL
C        STORE STATION MONTHLY TOTALS
            APPE(IRG,MONUM)=APPE(IRG,MONUM)+PTPE(IRG,I)
240      CONTINUE
C
      IF (IOPT1.EQ.0) GO TO 280
C
      IF (MONUM.GT.1) GO TO 260
C
      GNAME=PXNAME(IOPT1)
      FEPE=FE(IOPT1)
      DO 250 I=1,12
         GNORM(I)=PENORM(IOPT1,I)
250      CONTINUE
C
260   DO 270 I=1,LAST
         GMM(1,I)=PTPE(IOPT1,I)
         GMM(2,I)=PTPE(ICHECK,I)
270      CONTINUE
C
C  COMPARE OBSERVED VALUES TO ESTIMATED VALUES
      CALL MECHEK (MONUM,NTMO,IYR,IMO,GNAME,GNORM,GMM,IEND,FEPE,
     *   RMAXPE,LASTDA)
C
280   IF (NAREA.EQ.0) GO TO 290
      IL=LAST
C
C  CALCULATE MEAN AREAL POTENTIAL EVAPORATION
      CALL MEBASN (FILEN,BASNAM,BASZONE,DTYPE,DUNITS,DDIMN,IMO,IYR,
     *             LMO,LYR,ARNAME,NSTA,IL,NAREA,MONTH,IYEAR,W,AREAID,
     *             NOOUT,MONUM,UNITNO,ICNT)
C
290   IF ((IYEAR.EQ.LYR).AND.(MONTH.EQ.LMO)) GO TO 310
      MONTH=MONTH+1
      IF (MONTH.LT.13) GO TO 300
      MONTH=1
      IYEAR=IYEAR+1
300   MONUM=MONUM+1
C
      GO TO 150
C
C     -    -    -    -    -    -    -    -    -    -    -    -    -    -
C
310   CALL UPAGE (LP)
C
      CALL MERIZR (IMO,IYR,LMO,LYR,NSTA,PXNAME,NWSST,NWSSTA,AREAID,
     *   DUNITS,1,ARNAME,IMOW,IMOS)
C
      IF (IOPT1.EQ.0) THEN
         CALL MEPCON (NSTA,NTMO,PXNAME,IMO,IYR,DUNITS,IMOW,IMOS,pt_flag)
         ENDIF
C
      IF (NAREA.GT.0) CALL MERIZR (IMO,IYR,LMO,LYR,NAREA,PXNAME,
     *   NWSST,NWSSTA,AREAID,DUNITS,0,ARNAME,IMOW,IMOS)
C
      IF (IOPT1.EQ.0) GO TO 320
C
      READ (ICD,580,END=320) FE(ICHECK)
      FE(IOPT1)=FE(ICHECK)
      IEND=1
      NAREA=0
      GO TO 130
C
C  CLOSE ALL DATACARD FILES
320   CALL CCLOSL
C
      CALL UPCLOS(ITUNIT,' ',ICOND)
C
      ISTOP=0
      IF (IERROR.GT.0) ISTOP=8
      CALL USTOP (LP,ISTOP)
C
      STOP
C
C- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
330   FORMAT (7I5,1X,A4,A12,7I4)
340   FORMAT ('0*** ERROR - OPTION SPECIFIED TO WRITE TIME SERIES ',
     *   'TO DATA FILE BUT FILE NAME IS BLANK.')
345   FORMAT ('0*** ERROR - INPUT YEARS (',I4,';',I4,') MUST BE ',
     *   'FOUR DIGIT AND MUST BE AT LEAST 1900.')
350   FORMAT ('0*** ERROR - NUMBER OF ',A,' SPECIFIED (',
     *    I4,') IS GREATER THAN MAXIMUM (',I4,').')
360   FORMAT ('0*** ERROR - NUMBER OF ',A,' SPECIFIED (',
     *    I4,') IS LESS THAN MINIMUM (',I4,').')
370   FORMAT ('0*** ERROR - NAREA=',I4,3X,'IOPT1=',I4,
     *   ' THE PROGRAM DOES NOT ALLOW FOR AN EVAPCHK RUN AND ',
     *   'MAPE RUN AT THE SAME TIME.')
380   FORMAT (F6.2)
390   FORMAT (A20,2F5.0,2I5,F5.0,F10.0,F5.0)
400   FORMAT (20X,12F5.0)
410   FORMAT ('0*** WARNING - PROGRAM CANNOT ACCEPT ZERO ',
     *   'OR NEGATIVE MEAN VALUES. 0.01 HAS BEEN SUBSTITUTED FOR ' /
     *   ' STATION ',A,' FOR MONTH ',I2,' TO REPLACE THE VALUE ',
     *   F6.1,'.')
420   FORMAT ('0',15X,'MEAN AREAL POTENTIAL EVAPORATION',10X,'FROM ',
     *   I2,'/',I4,' THROUGH ',I2,'/',I4)
430   FORMAT ('0',13X,A4,' IS THE FIRST MONTH OF WINTER.',5X,A4,
     *   ' IS THE FIRST MONTH OF SUMMER.')
440   FORMAT ('0',14X,'- - CORRECTION FACTORS ARE APPLIED DURING THE ',
     *   'SEASON INDICATED ONLY - -')
450   FORMAT (' ',14X,'IF WINTER OR SUMMER IS NOT INDICATED, THE ',
     *   'FACTOR IS APPLIED TO ALL DATA')
460   FORMAT ('0',20X,'STATION MEANS ARE USED TO ESTIMATE MISSING DATA',
     *   9X,'GRID LENGTH=',F8.4,' KILOMETERS')
470   FORMAT ('0STA. RUN NO.',8X,'STATION NAME',13X,'X',4X,'Y',6X,
     *   'NWS STA. NO.',8X,'ELEVATION-M.',7X,'FE')
480   FORMAT ('0',5X,I2,13X,A,2X,2F5.0,7X,I2,'-',I6,7X,F10.0,10X,F5.1)
490   FORMAT (' ',25X,'MEANS    JAN-DEC',9X,12F6.3)
c
505   format (a20,3x,a12,3x,a12,3x,a12)
510   FORMAT (16F5.3)
520   FORMAT ('0*** WARNING - STATION WEIGHT FOR AREA ',I4,' STATION ',
     *   F10.5,' NO WEIGHT SHOULD BE GREATER THAN ONE (1.00)')
c
535   FORMAT ('0STATION WEIGHTS FOR ',A,5X,'ID=',A,5x,'basin name=',a,
     *        5x,'basin zone=',a)
540   FORMAT ('0')
550   FORMAT (' ',5X,A,5X,F5.3)
560   FORMAT ('0',14X,'UNITS USED IN THIS ANALYSIS ARE ',A4)
570   FORMAT ('0',5X,'COMPARISON OF ESTIMATED AND OBSERVED PE VALUES ',
     *   'FOR ',A,5X,'FE#',F5.1 ///)
580   FORMAT (F10.5)
C
      END


      INCLUDE 'uduntb'
