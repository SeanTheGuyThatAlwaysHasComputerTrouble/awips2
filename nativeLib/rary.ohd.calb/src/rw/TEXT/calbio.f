C MEMBER CALBIO
C  (from old member MCCALBIO)
C
      SUBROUTINE CALBIO (NCTS,NUMEXT,EXTLOC,DTYPE,UNITS,DIM,ITIME,NPDT,
     1IERR)
C.......................................
C     THIS SUBROUTINE OBTAINS EXTERNAL LOCATION INFORMATION FOR READING
C        OR WRITING INDIVIDUAL TIME SERIES FROM THE NWSRFS
C        CALIBRATION DISK FILES. THIS INFORMATION IS OBTAINED FROM
C        CARD INPUT AND BY CALLING LOCATE OR HEADER.
C.......................................
C     SUBROUTINE INITIALLY WRITTEN BY. . .
C            ERIC ANDERSON - HRL     JUNE 1979
C.......................................
C     SPECIAL READWRIT SUBROUTINE PACKAGE COMMON BLOCK/PROCES/
C        IS USED.  CONTENTS OF THIS COMMON BLOCK ARE..
C        IPROC     =0  TIME SERIES BEING WRITTEN MUST BE
C                      GROUPED BY DATA TYPE AND TIME INTERVAL.
C                  =1  TIME SERIES ARE WRITTEN ON AN
C                      INDIVIDUAL BASIS.
C        MYSEQ     AN UNIQUE NUMBER ASSIGNED BY THE USER
C                      FOR EACH TIME SERIES BEING WRITTEN.
C        LSTSEQ(30)    LIST OF NUMBERS CURRENTLY IN USE.
C.......................................
C     SPECIAL READWRIT SUBROUTINE PACKAGE COMMON BLOCK/READWT/
C        ISTOP     =1  STOP IF ERROR OCCURS.
C                  =0  DO NOT STOP IF ERROR OCCURS.
C        IERROR    ERROR FLAG.
C                  =0  NO ERRORS
C                  =1  ERRORS OCCURRED IN READWRIT ROUTINES.
C        IPRINT    CONTROL FOR PRINTING MESSAGE THAT A NEW TIME SERIES
C                  WAS CREATED.
C                  =0  DO NOT PRINT MESSAGE
C                  =1  PRINT MESSAGE
C        ITSH      FORM OF T.S.H.C. INPUT.
C                  =2  NORMAL CASE - WHOLE CARD
C        NTSH      NOT USED IF ITSH=1.
C        ISFN      METHOD OF SEARCHING FOR FILE NAME.
C                  =0  SEARCH INDEX
C                  =1  SEARCH FIRST RECORD ON FILE.
C        IRTSH     METHOD OF READING DATA.
C                  =0  READ MONTHS SEQUENTIALLY
C                  =1  READ MONTHS IN ANY ORDER
C.......................................
C     SPECIAL READWRIT SUBROUTINE PACKAGE COMMON BLOCK/IOX/
C        ITSO      =1  TSO STYLE 80 COL. PRINTOUT
C                  =0  BATCH STYLE PRINTOUT
C        OTHER VARIABLES SET IN BLOCK DATA.
C.......................................
      DIMENSION EXTLOC(1)
      DIMENSION FILEN(3),DID(3),DESCRP(5),SNAME(2)
C
C     COMMON BLOCKS
      INCLUDE 'common/fdbug'
      INCLUDE 'common/ionum'
      INCLUDE 'common/where'
      INCLUDE 'common/fprog'
      INCLUDE 'clbcommon/crwctl'
      INCLUDE 'clbcommon/cprces'
      INCLUDE 'clbcommon/bhtime'
      INCLUDE 'common/calbrt'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/calb/src/rw/RCS/calbio.f,v $
     . $',                                                             '
     .$Id: calbio.f,v 1.2 1996/07/11 19:17:16 dws Exp $
     . $' /
C    ===================================================================
C
C     CALBRT CONTAINS THE INITIAL AND LAST MONTH AND YEAR FOR
C        CALIBRATION RUN.
C
      DATA END/4HEND /
      DATA SNAME/4HCALB,4HIO  /
      DATA IBUG/4HCALI/
C.......................................
C     TRACE LEVEL FOR THIS SUBROUTINE=3.
      IF (ITRACE.GE.3) WRITE (IODBUG,900)
  900 FORMAT (1H0,17H** CALBIO ENTERED)
C.......................................
C     SET INITIAL VALUES
      IERR=0
      IOPNUM=0
      OPNAME(1)=SNAME(1)
      OPNAME(2)=SNAME(2)
      IF (MAINUM.GT.2) GO TO 80
      IMO=0
      IYR=0
      LMO=0
      LYR=0
C
C     INITIALIZE READWT COMMON BLOCK VALUES.
   80 ISTOP=0
      IERROR=0
      NTSH=0
C.......................................
C     CHECK TYPE OF TIME SERIES.
      IF (NCTS.NE.4) GO TO 90
      NUMEXT=0
      IERR=-1
      GO TO 195
   90 IF(NCTS.NE.2) GO TO 100
      WRITE(IPR,914)
  914 FORMAT(1H0,10X,58H**WARNING** 'CALB' FILES CANNOT ACCEPT UPDATE TI
     1ME SERIES.,2X,47HTHE PRECEEDING TIME SERIES IS CHANGED TO INPUT.)
      CALL WARN
      NCTS=1
C.......................................
C     DETERMINE IF THIS IS AN INPUT OR OUTPUT TIME SERIES.
  100 IF (NCTS.GT.2) GO TO 150
C.......................................
C.......................................
C     INPUT TIME SERIES
C
C      READ TIME SERIES HEADER CARD.
      READ (IN,901) FILEN,TYPE,IT,LHEAD,DID,KMO,KDA,KYR,KHRMIN,
     1KSEC,DESCRP
  901 FORMAT (3A4,A4,I4,I6,1X,3A4,1X,I2,1X,I2,1X,I2,1X,I4,1X,I4,2X,5A4)
      IF (FILEN(1).NE.END) GO TO 101
      IERR=1
      NUMEXT=0
      GO TO 195
C
C     CHECK NUMBER OF VALUES PER TIME INTERVAL.
  101 IF (NPDT.EQ.1) GO TO 102
      WRITE (IPR,911)
  911 FORMAT (1H0,10X,79H**ERROR** INPUT TIME SERIES CAN NOT HAVE MORE T
     1HAN ONE VALUE PER TIME INTERVAL.)
      CALL ERROR
      IERR=1
      NUMEXT=0
      GO TO 195
C
C     PRINT TIME SERIES HEADER CARD.
  102 WRITE (IPR,902) FILEN,TYPE,IT,LHEAD,DID,KMO,KDA,KYR,KHRMIN,
     1KSEC,DESCRP
  902 FORMAT (1H ,15X,17HEXTERNAL LOCATION, 11X,2H10,8X,2H20,8X,
     12H30,8X,2H40,8X,2H50,8X,2H60,8X,2H70,8X,2H80,/36X,
     280H----+----+----+----+----+----+----+----+----+----+----+----+---
     3-+----+----+----+, /36X,3A4,A4,I4,I6,1H-,
     43A4,1H-,I2,1H/,I2,1H/,I2,1H-,I4,1H.,I4,2X,5A4)
C.......................................
C     LOCATE THE TIME SERIES ON THE FILE.
      CALL LOCATX(IMO,IYR,LMO,LYR,UNITS,FILEN,TYPE,IT,DID,
     1DESCRP,NXREAD,NSEQ)
      IF(IERROR.EQ.0) GO TO 106
      IF(MAINUM.LT.3) GO TO 104
      WRITE(IPR,915)
  915 FORMAT(1H0,10X,72H**ERROR** PRECEEDING ERRORS OCCURRED IN 'CALB' F
     1ILE READ/WRITE ROUTINES.)
      CALL ERROR
      GO TO 103
  104 WRITE(IPR,916)
  916 FORMAT(1H0,10X,74H**WARNING** PRECEEDING ERRORS OCCURRED IN 'CALB'
     1 FILE READ/WRITE ROUTINES.)
      CALL WARN
      GO TO 103
  106 IF (MAINUM.GT.2) GO TO 103
      WRITE (IPR,917) IMO,IYR,LMO,LYR
  917 FORMAT (1H ,40X,19HPERIOD OF RECORD IS,1X,I2,1H/,I4,1X,2HTO,1X,
     1I2,1H/,I4)
C.......................................
C     MAKE NECESSARY CHECKS.
  103 IF (DTYPE.EQ.TYPE) GO TO 105
      WRITE (IPR,903) TYPE,DTYPE
  903 FORMAT(1H0,10X,47H**WARNING** THE DATA TYPE CODE ON THE T.S.H.C.(,
     1A4,50H) IS NOT THE SAME AS THE CODE FOR THE TIME SERIES(,
     2A4,2H).)
      CALL WARN
  105 IF (IT.EQ.ITIME) GO TO 110
      WRITE (IPR,904) IT,ITIME
  904 FORMAT (1H0,10X,44H**ERROR** THE TIME INTERVAL ON THE T.S.H.C.(,
     1I4,41H) IS NOT THE SAME AS FOR THE TIME SERIES(,
     2I2,1X,7HHOURS).)
      CALL ERROR
C.......................................
C     STORE EXTERNAL LOCATION INFORMATION
  110 IF(MAINUM.GT.2) GO TO 120
C
C     FOR OPERATIONAL PROGRAMS STORE T.S.H.C. VALUES PLUS ALLOW TWO
C        POSITIONS FOR NXREAD AND NSEQ.
      NUMEXT=16
      EXTLOC(1)=FILEN(1)
      EXTLOC(2)=FILEN(2)
      EXTLOC(3)=FILEN(3)
      EXTLOC(4)=TYPE
      EXTLOC(5)=IT+0.01
      EXTLOC(6)=LHEAD+0.01
      EXTLOC(7)=DID(1)
      EXTLOC(8)=DID(2)
      EXTLOC(9)=DID(3)
      EXTLOC(10)=KMO+0.01
      EXTLOC(11)=KDA+0.01
      EXTLOC(12)=KYR+0.01
      EXTLOC(13)=KHRMIN+0.01
      EXTLOC(14)=KSEC+0.01
      EXTLOC(15)=0.01
      EXTLOC(16)=0.01
      GO TO 195
C
C     MCP STORE FILE NAME, NXREAD, AND NSEQ.
  120 NUMEXT = 5
      EXTLOC(1)=FILEN(1)
      EXTLOC(2)=FILEN(2)
      EXTLOC(3)=FILEN(3)
      EXTLOC(4)=NXREAD+0.01
      EXTLOC(5)=NSEQ+0.01
      GO TO 195
C     FINISHED WITH INPUT TIME SERIES.
C.......................................
C.......................................
C     OUTPUT TIME SERIES
C
C        READ AND PRINT FILE NAME, 12-CHARACTER I.D., AND
C        DESCRIPTIVE INFORMATION.
  150 READ (IN,905) FILEN,DID,DESCRP
  905 FORMAT (3A4,8X,3A4,8X,5A4)
      IF (FILEN(1).NE.END) GO TO 151
      IERR=1
      NUMEXT=0
      GO TO 195
C.......................................
C     CHECK THAT TIME SERIES TO BE WRITTEN DOES NOT HAVE
C        MORE THAN ONE VALUE PER TIME INTERVAL.
  151 IF (NPDT.EQ.1) GO TO 155
      WRITE (IPR,907)
  907 FORMAT (1H0,10X,82H**WARNING** OUTPUT TIME SERIES CAN NOT HAVE MOR
     1E THAN ONE VALUE PER TIME INTERVAL.,/16X,50HTHE PRECEEDING TIME SE
     2RIES IS CHANGED TO INTERNAL.)
      CALL WARN
      IERR=-1
      NUMEXT=0
      NCTS=4
      GO TO 195
  155 WRITE (IPR,906) FILEN,DID,DESCRP
  906 FORMAT (1H ,15X,29HEXTERNAL LOCATION--FILE NAME=,3A4,3X,
     15HI.D.=,3A4,5X,5A4)
C.......................................
C     CALL HEADER ROUTINE -- CLIBRATION PROGRAMS ONLY.
      IF (MAINUM.LT.3) GO TO 156
      CALL HEADER (FILEN,DTYPE,DIM,UNITS,ITIME,1,IMO,IYR,
     1LMO,LYR,DID,1,DESCRP,1)
      IF(IERROR.EQ.0) GO TO 156
      WRITE(IPR,915)
      CALL ERROR
C.......................................
C     STORE EXTERNAL LOCATION INFORMATION FOR OUTPUT.
  156 IF(MAINUM.GT.2) GO TO 160
C
C     OPERATIONAL PROGRAMS.
      NUMEXT=7
      EXTLOC(1)=FILEN(1)
      EXTLOC(2)=FILEN(2)
      EXTLOC(3)=FILEN(3)
      EXTLOC(4)=DID(1)
      EXTLOC(5)=DID(2)
      EXTLOC(6)=DID(3)
      EXTLOC(7)=0.01
      GO TO 195
C
C     CALIBRATION PROGRAMS.
  160 NUMEXT=1
      EXTLOC(1)=MYSEQ+0.01
C
C     INCREMENT MYSEQ.
      MYSEQ=MYSEQ+1
C     FINISHED WITH OUTPUT TIME SERIES.
C.......................................
C.......................................
C     DEBUG OUTPUT.
  195 IF (IFBUG(IBUG).EQ.1) GO TO 196
      GO TO 199
  196 WRITE (IODBUG,908) NCTS,NUMEXT,DTYPE,UNITS,DIM,ITIME,NPDT,IERR
  908 FORMAT (1H0,24HSUBROUTINE CALBIO DEBUG-,2I5,3(2X,A4),3I5)
      IF (NUMEXT.EQ.0) GO TO 199
      WRITE (IODBUG,909)
  909 FORMAT (1H ,46HEXTERNAL LOCATION INFORMATION--REAL AND ALPHA.)
      WRITE (IODBUG,912) (EXTLOC(I),I=1,NUMEXT)
  912 FORMAT (1H ,15F8.0)
      WRITE (IODBUG,913) (EXTLOC(I),I=1,NUMEXT)
  913 FORMAT (1H ,15(4X,A4))
      IF (MAINUM.LT.3) GO TO 199
      IF (NCTS.NE.3) GO TO 199
      LOC=EXTLOC(1)
      IF (LOC.EQ.0) GO TO 199
      WRITE (IODBUG,910) LOC,LSTSEQ(LOC)
  910 FORMAT (1H ,10X,16HHEADER SEQ. NO.=,2I5)
C.......................................
  199 CONTINUE
      RETURN
      END
