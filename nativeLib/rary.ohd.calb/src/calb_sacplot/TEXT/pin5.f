C MEMBER PIN5
C  (from old member MCPIN5)
C
      SUBROUTINE PIN5(PO,LEFTP,IUSEP,P,MP)
C.......................................
C     THIS IS THE CARD INPUT SUBROUTINE FOR THE OPERATION THAT PLOTS
C        MEAN DAILY FLOW VALUES FOR A NUMBER OF TIME SERIES.  PLOTS CAN
C        BE IN AN ARITHMETIC, MODIFIED ARITHMETIC, OR SEMI-LOG SCALE.
C        RUNOFF COMPONENTS AND/OR SOIL MOISTURE STORAGES FROM THE
C        SAC-SMA OPERATION CAN BE INCLUDED ON THE PLOT.  SNOW AND
C        SAC-SMA PARAMETERS AND CARRYOVER CAN BE TABULATED FOR SELECTED
C        OPERATIONS PRIOR TO THE PLOT.
C        THIS OPERATION IS ONLY FOR USE IN THE MANUAL CALIBRATION
C        PROGRAM.
C        OPERATION IDENTIFIER = 'SAC-PLOT'
C.......................................
C     SUBROUTINE INITIALLY WRITTEN BY . . .
C            ERIC ANDERSON - HRL     AUGUST 1979
C.......................................
      DIMENSION PO(1),P(MP)
      DIMENSION FPNAME(5),PXID(2),ROID(2),SMID(2),OPNAME(2)
C
C     COMMON BLOCKS
      INCLUDE 'common/fdbug'
      INCLUDE 'common/ionum'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/calb/src/calb_sacplot/RCS/pin5.f,v $
     . $',                                                             '
     .$Id: pin5.f,v 1.2 1996/07/11 19:34:39 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA SLOG,PMOD,YES/4HSLOG,4HMODS,3HYES/
      DATA DL3,DL/4HL3  ,4HL   /
      DATA BLANK/4H    /
      DATA ROCL,SMZC/4HROCL,4HSMZC/
C.......................................
C     TRACE LEVEL FOR THIS SUBROUTINE=1.
      IF (ITRACE.GE.1) WRITE (IODBUG,900)
  900 FORMAT (1H0,15H** PIN5 ENTERED)
C
C     CHECK IF DEBUG OUTPUT IS NEEDED.
      IBUG=0
      IF (IDBALL.GT.0) IBUG=1
      IF (NDEBUG.EQ.0) GO TO 12
      DO 10 I=1,NDEBUG
      IF (IDEBUG(I).EQ.5) GO TO 11
   10 CONTINUE
      GO TO 12
   11 IBUG=1
C.......................................
C     SET DEFAULT VALUES.
   12 LPX=0
      LRO=0
      LSM=0
      LSMA=0
      LSNW=0
C
C     INITIAL VALUES
      IUSEP=0
      NOFILL=0
      IDT=24
C.......................................
C     READ INPUT CARDS AND MAKE CHECKS.
C     CARD NUMBER 1.
      READ (IN,901) FPNAME,NPLOT,PTYPE,AREA,PMAX,SWCH,NSAC,NSNOW
  901 FORMAT (5A4,I5,1X,A4,F10.0,F5.0,2X,A3,2I5)
      IPLOT=2
      IF (PTYPE.EQ.SLOG) IPLOT=1
      IF (PTYPE.EQ.PMOD) IPLOT=3
      IND=0
      IF (SWCH.EQ.YES) IND=1
      IF (NSAC.LT.0) NSAC=0
      IF (NSNOW.LT.0) NSNOW=0
      IF (NPLOT.GT.0) GO TO 100
      WRITE (IPR,902) NPLOT
  902 FORMAT (1H0,10X,51H**ERROR** THE NUMBER OF TIME SERIES TO BE PLOTT
     1ED (,I5,15H ) IS INCORRECT,/16X,35HTHIS OPERATION CAN NOT BE EXECU
     2TED.)
      CALL ERROR
      NOFILL=1
      GO TO 190
C
C     MAKE SURE AREA IS DEFINED.
  100 IF (AREA.GT.0.0) GO TO 101
      WRITE (IPR,903)
  903 FORMAT (1H0,10X,34H**ERROR** THE AREA IS NOT DEFINED.)
      CALL ERROR
C
C     CHECK MAX. PLOT ORDINATE FOR ARITHMETIC OR MODIFIED SCALE PLOT.
  101 IF (IPLOT.EQ.1) GO TO 105
      IF (PMAX.GT.0.0) GO TO 105
C
C     COMPUTE DEFAULT PMAX
      IF (AREA.GT.0.0) GO TO 102
C     DEFAULT CAN NOT BE COMPUTED.
      WRITE (IPR,904)
  904 FORMAT (1H0,10X,75H**ERROR** THE MAXIMUM PLOT ORDINATE IS NOT DEFI
     1NED AND CAN NOT BE COMPUTED.)
      CALL ERROR
      GO TO 105
C     DEFAULT CAN BE COMPUTED.
  102 PMAX=30.0
      IF (IPLOT.GT.2) GO TO 105
      PMAX=PMAX*AREA*0.01
C.......................................
C     CARD NUMBER 2.
C     CHECK SPACE TO SEE IF INFORMATION ON TIME SERIES TO BE PLOTTED CAN
C        BE READ DIRECTLY INTO THE P ARRAY.
  105 IF (LEFTP.GT.(14+NPLOT*7)) GO TO 110
      WRITE (IPR,905)
  905 FORMAT (1H0,10X,75H**ERROR** THIS OPERATION NEEDS MORE SPACE THAN 
     1IS AVAILABLE IN THE P ARRAY.)
      CALL ERROR
      NOFILL=1
      DO 106 N=1,NPLOT
      READ (IN,906) DUMMY
  106 CONTINUE
      GO TO 115
C
C     SPACE IS AVAILABLE - READ INFORMATION INTO PO().
  110 DO 111 N=1,NPLOT
      I1=15+(N-1)*7
      I2=I1+6
      READ (IN,906) (PO(I),I=I1,I2)
  906 FORMAT (2X,2A4,1X,A4,8X,3A4,4X,A1)
      CALL CHEKTS(PO(I1),PO(I1+2),IDT,1,DL3,1,1,IERR)
  111 CONTINUE
      NEXTPO=15+NPLOT*7
C.......................................
C     CARD NUMBER 3.
  115 IF (IND.EQ.0) GO TO 125
      READ (IN,909) PXID,PXTYPE,ITPX,ROID,SMID
  909 FORMAT (2X,2A4,1X,A4,3X,I2,12X,2A4,12X,2A4)
      IF ((PXID(1).EQ.BLANK).AND.(PXID(2).EQ.BLANK)) GO TO 120
C
C     RAIN+MELT TIME SERIES EXISTS.
      IF (NOFILL.EQ.1) GO TO 116
      LPX=NEXTPO
      NEXTPO=NEXTPO+4
  116 CALL CHEKTS(PXID,PXTYPE,ITPX,1,DL,1,1,IERR)
  120 IF ((ROID(1).EQ.BLANK).AND.(ROID(2).EQ.BLANK)) GO TO 122
C
C     RUNOFF COMPONENTS TIME SERIES EXISTS.
      IF (NOFILL.EQ.1) GO TO 121
      LRO=NEXTPO
      NEXTPO=NEXTPO+3
  121 CALL CHEKTS(ROID,ROCL,IDT,1,DL,0,7,IERR)
  122 IF ((SMID(1).EQ.BLANK).AND.(SMID(2).EQ.BLANK)) GO TO 125
C
C     SOIL MOISTURE STORAGES TIME SERIES EXISTS.
      IF (NOFILL.EQ.1) GO TO 123
      LSM=NEXTPO
      NEXTPO=NEXTPO+3
  123 CALL CHEKTS(SMID,SMZC,IDT,1,DL,0,5,IERR)
C
C     TOTAL AMOUNT OF SPACE NEEDED BY PO() NOW KNOWN - CHECK IF THE
C     SPACE IS AVAILABLE.
  125 IF (NOFILL.EQ.1) GO TO 190
      IF (LEFTP.GT.(NEXTPO+3*NSAC+3*NSNOW+1)) GO TO 130
      WRITE (IPR,905)
      CALL ERROR
      NOFILL=1
      GO TO 190
C     SPACE IS AVAILABLE FOR ALL OR PO().
C.......................................
C     CARD NUMBER 4.
  130 IF (NSAC.EQ.0) GO TO 135
      LSMA=NEXTPO
      PO(LSMA)=NSAC+0.01
      NEXTPO=NEXTPO+3*NSAC+1
      DO 131 N=1,NSAC
      I=LSMA+(N-1)*3+1
      READ (IN,906) OPNAME
      PO(I)=OPNAME(1)
      PO(I+1)=OPNAME(2)
      LP=0
      CALL FSERCH (1,OPNAME,LP,P,MP)
      IF (LP.GT.0) GO TO 132
C
C     SAC-SMA OPERATION DOES NOT EXIST.
      WRITE (IPR,911) OPNAME
  911 FORMAT (1H0,10X,36H**ERROR** A SAC-SMA OPERATION (NAME=,2A4,
     138H) NEEDED FOR THIS PLOT DOES NOT EXIST.)
      CALL ERROR
      PO(I+2)=0.01
      GO TO 131
C
C     SAC-SMA OPERATION DOES EXIST.
  132 PO(I+2)=P(LP+17)
  131 CONTINUE
C.......................................
C     CARD NUMBER 5.
  135 IF (NSNOW.EQ.0) GO TO 150
      LSNW=NEXTPO
      PO(LSNW)=NSNOW+0.01
      NEXTPO=NEXTPO+3*NSNOW+1
      DO 136 N=1,NSNOW
      I=LSNW+(N-1)*3+1
      READ (IN,906) OPNAME
      PO(I)=OPNAME(1)
      PO(I+1)=OPNAME(2)
      LP=0
      CALL FSERCH (19,OPNAME,LP,P,MP)
      IF (LP.GT.0) GO TO 137
C
C     SNOW MODEL OPERATION DOES NOT EXIST.
      WRITE (IPR,912) OPNAME
  912 FORMAT (1H0,10X,39H**ERROR** A SNOW MODEL OPERATION (NAME=,2A4,
     138H) NEEDED FOR THIS PLOT DOES NOT EXIST.)
      CALL ERROR
      PO(I+2)=0.01
      GO TO 136
C
C     SNOW MODEL OPERATION DOES EXIST.
  137 PO(I+2)=P(LP+22)
  136 CONTINUE
C.......................................
C     ALL INPUT CARDS HAVE BEEN READ.
C        STORE REMAINING VALUES IN PO().
  150 DO 151 I=1,5
  151 PO(I)=FPNAME(I)
      PO(6)=NPLOT+0.01
      PO(7)=IPLOT+0.01
      PO(8)=AREA
      PO(9)=PMAX
      PO(10)=LPX+0.01
      PO(11)=LRO+0.01
      PO(12)=LSM+0.01
      PO(13)=LSMA+0.01
      PO(14)=LSNW+0.01
      IF (LPX.EQ.0) GO TO 152
      PO(LPX)=PXID(1)
      PO(LPX+1)=PXID(2)
      PO(LPX+2)=PXTYPE
      PO(LPX+3)=ITPX+0.01
  152 IF (LRO.EQ.0) GO TO 153
      PO(LRO)=ROID(1)
      PO(LRO+1)=ROID(2)
      PO(LRO+2)=ROCL
  153 IF (LSM.EQ.0) GO TO 155
      PO(LSM)=SMID(1)
      PO(LSM+1)=SMID(2)
      PO(LSM+2)=SMZC
  155 IUSEP=NEXTPO-1
C     ALL VALUES ARE NOW STORED.
C.......................................
      IF (IBUG.EQ.0) GO TO 190
C     DEBUG OUTPUT -- PRINT PO().
      WRITE (IODBUG,913) IUSEP
  913 FORMAT (1H0,32HPIN5 DEBUG--CONTENTS OF PO ARRAY,5X,17HNUMBER OF VA
     1LUES=,I3)
      WRITE (IODBUG,914) (PO(I),I=1,IUSEP)
  914 FORMAT (1H0,15F8.2)
      WRITE (IODBUG,915) (PO(I),I=1,IUSEP)
  915 FORMAT (1H0,15(4X,A4))
  190 IF (IBUG.EQ.0) GO TO 199
      WRITE (IODBUG,916) NOFILL,NPLOT,LEFTP,NEXTPO,NSAC,NSNOW
  916 FORMAT (1H0,19HPIN5 DEBUG--NOFILL=,I1,2X,6HNPLOT=,I2,2X,6HLEFTP=,
     1I5,2X,7HNEXTPO=,I2,2X,5HNSAC=,I2,2X,6HNSNOW=,I2)
C.......................................
C     CONTENTS OF THE PO ARRAY FOR THE SAC-PLOT OPERATION.
C
C     POSITION                     CONTENTS
C        1-5     NAME OF THE FLOW-POINT BEING PLOTTED
C        6       NUMBER OF DAILY DISCHARGE TIME SERIES PLOTTED
C        7       TYPE OF PLOT SEMI-LOG=1
C                             ARITHMETIC=2
C                             MODIFIED ARITHMETIC=3
C        8       AREA ABOVE FLOW-POINT-KM2
C        9       MAXIMUM PLOT ORDINATE
C       10       LOCATION IN PO OF RAIN+MELT T.S. INFORMATION,=0 IF NO
C                  RAIN+MELT
C       11       LOCATION IN PO OF RUNOFF COMPONENTS T.S. INFORMATION,
C                  =0 IF NO RUNOFF COMPONENTS
C       12       LOCATION IN PO OF SOIL MOISTURE STORAGES T.S.
C                  INFORMATION, =0 IF NO SOIL STORAGES.
C       13       LOCATION IN PO OF INFORMATION ON SAC-SMA OPERATIONS,
C                  =0 IF NO SAC-SMA OPERATIONS TABULATED.
C       14       LOCATION IN PO OF INFORMATION ON SNOW OPERATIONS,
C                  =0 IF NO SNOW OPERATIONS TABULATED.
C     15 THRU    INFORMATION FOR EACH TIME SERIES PLOTTED.
C     14+PO(6)*7   1-2  I.D. OF TIME SERIES
C                  3    DATA TYPE CODE
C                  4-6  12-CHARACTER NAME
C                  7    PLOTTING SYMBOL (1-CHARACTER)
C     STARTING LOCATION OF OPTIONAL VALUES ARE GIVEN ABOVE, THE
C        CONTENTS OF THESE OPTIONAL BLOCKS ARE AS FOLLOWS. . .
C     A.  RAIN+MELT T.S. INFORMATION -- 4 SPACES, 2 FOR I.D., 1 FOR DATA
C            TYPE, AND 1 FOR TIME INTERVAL
C     B.  RUNOFF COMPONENTS AND SOIL MOISTURE STORAGE T.S.
C            INFORMATION -- 3 SPACES FOR EACH, 2 FOR I.D. AND 1 FOR DATA
C            TYPE.
C     C.  SAC-SMA AND SNOW MODEL OPERATION INFORMATION - FOR EACH -- 1
C            SPACE FOR NUMBER OF OPERATIONS PLUS 3 SPACES FOR EACH
C            OPERATION (2 FOR NAME AND 1 FOR LOCATION OF SUMATIONS)
C     MINIMUM NUMBER OF ENTRIES = 14+NPLOT*7
C     MAXIMUM NUMBER OF ENTRIES = 26+NPLOT*7+(NSAC+NSNOW)*3
C     WHERE:     NPLOT=NUMBER OF TIME SERIES PLOTTED.
C                NSAC=NUMBER OF SAC-SMA OPERATIONS TABULATED, AND
C                NSNOW=NUMBER OF SNOW OPERATIONS TABULATED.
C.......................................
  199 CONTINUE
      RETURN
      END
