C MEMBER PIN17
C  (from old member MCPIN17)
C
      SUBROUTINE PIN17(PO,LEFTP,IUSEP,CO,LEFTC,IUSEC)
C.......................................
C        THIS IS THE INPUT SUBROUTINE FOR THE 'WY-PLOT ' OPERATION.
C        PLOTTING IS SIMILAR TO THE 'SAC-PLOT' OPERATION EXCEPT THAT
C        PLOTTING IS ON A WATER YEAR RATHER THAN A MONTHLY BASIS.
C     OPERATION CAN ONLY BE USED IN THE MCP PROGRAM.
C.......................................
C     SUBROUTINE INITIALLY WRITTEN BY...
C        ERIC ANDERSON - HRL     APRIL 1980
C.......................................
      DIMENSION PO(1),CO(1)
      DIMENSION SNAME(2),FPNAME(5),PXID(2),ROID(2),SMID(2)
C
C     COMMON BLOCKS.
      INCLUDE 'common/fdbug'
      INCLUDE 'common/ionum'
      INCLUDE 'common/fwyds'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/calb/src/calb_wyplot/RCS/pin17.f,v $
     . $',                                                             '
     .$Id: pin17.f,v 1.2 1996/07/11 19:33:45 dws Exp $
     . $' /
C    ===================================================================
C
C
C     DATA STATEMENTS
      DATA SNAME/4HPIN1,4H7   /
      DATA SLOG,PMOD,YES/4HSLOG,4HMODS,3HYES/
      DATA DL3,DL/4HL3  ,4HL   /
      DATA BLANK/4H    /
      DATA ROCL,SMZC/4HROCL,4HSMZC/
C.......................................
C     TRACE LEVEL FOR SUBROUTINE=1, DEBUG SWITCH=IBUG
      CALL FPRBUG(SNAME,1,17,IBUG)
C.......................................
C     SET DEFAULT VALUES
      LPX=0
      LRO=0
      LSM=0
      LWY=0
      NPMO=0
C
C     INITIAL VALUES
      IUSEP=0
      IUSEC=0
      NPMO=0
      NOFILL=0
      IDT=24
C.......................................
C     READ INPUT CARDS AND MAKE CHECKS.
C     CARD NUMBER 1.
      READ (IN,901) FPNAME,NPLOT,PTYPE,AREA,PMAX,SWCH
  901 FORMAT (5A4,I5,1X,A4,F10.0,F5.0,2X,A3)
      IPLOT=2
      IF (PTYPE.EQ.SLOG) IPLOT=1
      IF (PTYPE.EQ.PMOD) IPLOT=3
      IND=0
      IF (SWCH.EQ.YES) IND=1
      IF (NPLOT.GT.0) GO TO 100
      WRITE (IPR,902) NPLOT
  902 FORMAT (1H0,10X,51H**ERROR** THE NUMBER OF TIME SERIES TO BE PLOTT
     1ED (,I5,15H ) IS INCORRECT,/16X,35HTHIS OPERATION CAN NOT BE EXECU
     2TED.)
      CALL ERROR
      NOFILL=1
      GO TO 190
C
C     MAKE SURE AREA IS DEFINED.
  100 IF (AREA.GT.0.0) GO TO 101
      WRITE (IPR,903)
  903 FORMAT (1H0,10X,34H**ERROR** THE AREA IS NOT DEFINED.)
      CALL ERROR
C
C     CHECK MAX. PLOT ORDINATE FOR ARITHMETIC OR MODIFIED SCALE PLOT.
  101 IF (IPLOT.EQ.1) GO TO 105
      IF (PMAX.GT.0.0) GO TO 105
C
C     COMPUTE DEFAULT PMAX
      IF (AREA.GT.0.0) GO TO 102
C     DEFAULT CAN NOT BE COMPUTED.
      WRITE (IPR,904)
  904 FORMAT (1H0,10X,75H**ERROR** THE MAXIMUM PLOT ORDINATE IS NOT DEFI
     1NED AND CAN NOT BE COMPUTED.)
      CALL ERROR
      GO TO 105
C     DEFAULT CAN BE COMPUTED.
  102 PMAX=30.0
      IF (IPLOT.GT.2) GO TO 105
      PMAX=PMAX*AREA*0.01
C.......................................
C     CARD NUMBER 2.
C     CHECK SPACE TO SEE IF INFORMATION ON TIME SERIES TO BE PLOTTED CAN
C        BE READ DIRECTLY INTO THE P ARRAY.
  105 NEEDP=14+NPLOT*7
      CALL CHECKP(NEEDP,LEFTP,IERR)
      IF (IERR.EQ.0) GO TO 110
      NOFILL=1
      DO 106 N=1,NPLOT
      READ (IN,906) DUMMY
  106 CONTINUE
      GO TO 115
C
C     SPACE IS AVAILABLE - READ INFORMATION INTO PO().
  110 DO 111 N=1,NPLOT
      I1=15+(N-1)*7
      I2=I1+6
      READ (IN,906) (PO(I),I=I1,I2)
  906 FORMAT (2X,2A4,1X,A4,8X,3A4,4X,A1)
      CALL CHEKTS(PO(I1),PO(I1+2),IDT,1,DL3,1,1,IERR)
  111 CONTINUE
      NEXTPO=15+NPLOT*7
C.......................................
C     CARD NUMBER 3.
  115 IF (IND.EQ.0) GO TO 125
      READ (IN,909) PXID,PXTYPE,ITPX,ROID,SMID,SMTYPE
  909 FORMAT (2X,2A4,1X,A4,3X,I2,12X,2A4,12X,2A4,1X,A4)
      IF ((PXID(1).EQ.BLANK).AND.(PXID(2).EQ.BLANK)) GO TO 120
C
C     RAIN+MELT TIME SERIES EXISTS.
      IF (NOFILL.EQ.1) GO TO 116
      LPX=NEXTPO
      NEXTPO=NEXTPO+4
  116 CALL CHEKTS(PXID,PXTYPE,ITPX,1,DL,1,1,IERR)
  120 IF ((ROID(1).EQ.BLANK).AND.(ROID(2).EQ.BLANK)) GO TO 122
C
C     RUNOFF COMPONENTS TIME SERIES EXISTS.
      IF (NOFILL.EQ.1) GO TO 121
      LRO=NEXTPO
      NEXTPO=NEXTPO+3
  121 CALL CHEKTS(ROID,ROCL,IDT,1,DL,0,7,IERR)
  122 IF ((SMID(1).EQ.BLANK).AND.(SMID(2).EQ.BLANK)) GO TO 125
C
C     SOIL MOISTURE STORAGES TIME SERIES EXISTS.
      IF (NOFILL.EQ.1) GO TO 123
      LSM=NEXTPO
      NEXTPO=NEXTPO+3
  123 IF (SMTYPE.EQ.BLANK) SMTYPE=SMZC
      CALL CHEKTS(SMID,SMTYPE,IDT,1,DL,0,5,IERR)
C
C     TOTAL AMOUNT OF SPACE NEEDED BY PO() NOW KNOWN - CHECK IF THE
C     SPACE IS AVAILABLE.
  125 IF (NOFILL.EQ.1) GO TO 130
      NEEDP=NEXTPO-1
      CALL CHECKP(NEEDP,LEFTP,IERR)
      IF (IERR.EQ.0) GO TO 130
      NOFILL=1
C.......................................
C     OBTAIN SCRATCH FILE INFORMATION.
C     DETERMINE NUMBER OF RECORDS NEEDED PER MONTH.
  130 LENGTH=NPLOT*31
      IF(LPX.GT.0) LENGTH=LENGTH+31
      IF(LRO.GT.0) LENGTH=LENGTH+217
      IF(LSM.GT.0) LENGTH=LENGTH+155
      NPMO=(LENGTH-1)/372+1
C
C     CHECK SPACE AVAILABLE ON SCRATCH FILE.
      NEEDWY=NPMO*12
      LEFTWY=NTWY-NXWY+1
      IF(NEEDWY.LE.LEFTWY) GO TO 135
      WRITE(IPR,905) NEEDWY,LEFTWY
  905 FORMAT(1H0,10X,30H**ERROR** THIS OPERATION NEEDS,I4,1X,39HRECORDS 
     1ON THE WATER YEAR SCRATCH FILE.,2X,4HONLY,I4,1X,14HARE AVAILABLE.)
      CALL ERROR
      NOFILL=1
      GO TO 140
C
C     SPACE IS AVAILABLE ON SCRATCH FILE - STORE LOCATION OF FIRST
C        RECORD TO BE USED.
  135 LWY=NXWY
C.......................................
C     CHECK IF SPACE AVAILABLE FOR CO().
  140 NEEDC=NPLOT*12
      CALL CHECKC(NEEDC,LEFTC,IERR)
      IF(IERR.EQ.0) GO TO 150
      NOFILL=1
      GO TO 190
C.......................................
C     ALL INPUT CARDS HAVE BEEN READ.
C        STORE REMAINING VALUES IN PO().
  150 DO 151 I=1,5
  151 PO(I)=FPNAME(I)
      PO(6)=NPLOT+0.01
      PO(7)=IPLOT+0.01
      PO(8)=AREA
      PO(9)=PMAX
      PO(10)=LPX+0.01
      PO(11)=LRO+0.01
      PO(12)=LSM+0.01
      PO(13)=LWY+0.01
      PO(14)=NPMO+0.01
      IF (LPX.EQ.0) GO TO 152
      PO(LPX)=PXID(1)
      PO(LPX+1)=PXID(2)
      PO(LPX+2)=PXTYPE
      PO(LPX+3)=ITPX+0.01
  152 IF (LRO.EQ.0) GO TO 153
      PO(LRO)=ROID(1)
      PO(LRO+1)=ROID(2)
      PO(LRO+2)=ROCL
  153 IF (LSM.EQ.0) GO TO 155
      PO(LSM)=SMID(1)
      PO(LSM+1)=SMID(2)
      PO(LSM+2)=SMTYPE
  155 IUSEP=NEEDP
C
C     INITIALIZE CO ARRAY TO MISSING INDICATOR.
      DO 160 I=1,NEEDC
  160 CO(I)=-0.001
      IUSEC=NEEDC
C
C     INCREMENT NEXT AVAILABLE RECORD LOCATION ON SCRATCH FILE.
      NXWY=NXWY+NEEDWY
C     ALL ENTRIES HAVE BEEN MADE.
C.......................................
      IF (IBUG.EQ.0) GO TO 190
C     DEBUG OUTPUT -- PRINT PO().
      WRITE (IODBUG,913) IUSEP
  913 FORMAT (1H0,32HPIN5 DEBUG--CONTENTS OF PO ARRAY,5X,17HNUMBER OF VA
     1LUES=,I3)
      WRITE (IODBUG,914) (PO(I),I=1,IUSEP)
  914 FORMAT (1H0,15F8.2)
      WRITE (IODBUG,915) (PO(I),I=1,IUSEP)
  915 FORMAT (1H0,15(4X,A4))
  190 IF (IBUG.EQ.0) GO TO 199
      WRITE (IODBUG,916) NOFILL,NPLOT,LEFTP,NEXTPO,LEFTC,IUSEC,NXWY,NPMO
  916 FORMAT (1H0,19HPIN5 DEBUG--NOFILL=,I1,2X,6HNPLOT=,I2,2X,6HLEFTP=,
     1I5,2X,7HNEXTPO=,I2,2X,6HLEFTC=,I5,2X,6HIUSEC=,I4,2X,5HNXWY=,I4,
     22X,5HNPMO=,I3)
C.......................................
C     CONTENTS OF THE PO ARRAY FOR THE SAC-PLOT OPERATION.
C
C     POSITION                     CONTENTS
C        1-5     NAME OF THE FLOW-POINT BEING PLOTTED
C        6       NUMBER OF DAILY DISCHARGE TIME SERIES PLOTTED
C        7       TYPE OF PLOT SEMI-LOG=1
C                             ARITHMETIC=2
C                             MODIFIED ARITHMETIC=3
C        8       AREA ABOVE FLOW-POINT-KM2
C        9       MAXIMUM PLOT ORDINATE
C       10       LOCATION IN PO OF RAIN+MELT T.S. INFORMATION,=0 IF NO
C                  RAIN+MELT
C       11       LOCATION IN PO OF RUNOFF COMPONENTS T.S. INFORMATION,
C                  =0 IF NO RUNOFF COMPONENTS
C       12       LOCATION IN PO OF SOIL MOISTURE STORAGES T.S.
C                  INFORMATION, =0 IF NO SOIL STORAGES.
C       13       RECORD NUMBER OF THE FIRST RECORD USED BY THIS
C                  OPERATION ON THE WATER YEAR SCRATCH FILE.
C       14       NUMBER OF SCRATCH FILE RECORDS USED PER MONTH.
C     15 THRU    INFORMATION FOR EACH TIME SERIES PLOTTED.
C     14+PO(6)*7   1-2  I.D. OF TIME SERIES
C                  3    DATA TYPE CODE
C                  4-6  12-CHARACTER NAME
C                  7    PLOTTING SYMBOL (1-CHARACTER)
C     STARTING LOCATION OF OPTIONAL VALUES ARE GIVEN ABOVE, THE
C        CONTENTS OF THESE OPTIONAL BLOCKS ARE AS FOLLOWS. . .
C     A.  RAIN+MELT T.S. INFORMATION -- 4 SPACES, 2 FOR I.D., 1 FOR DATA
C            TYPE, AND 1 FOR TIME INTERVAL
C     B.  RUNOFF COMPONENTS AND SOIL MOISTURE STORAGE T.S.
C            INFORMATION -- 3 SPACES FOR EACH, 2 FOR I.D. AND 1 FOR DATA
C            TYPE.
C     MINIMUM NUMBER OF ENTRIES = 14+NPLOT*7
C     MAXIMUM NUMBER OF ENTRIES = 24+NPLOT*7
C     WHERE:     NPLOT=NUMBER OF TIME SERIES PLOTTED.
C
C     THE CO ARRAY CONTAINS 12 ENTRIES PER TIME SERIES
C        TO BE PLOTTED.  THESE ARE THE TOTAL DISCHARGE
C        IN CMSD FOR EACH MONTH OF THE WATER YEAR.
C.......................................
  199 CONTINUE
      RETURN
      END
