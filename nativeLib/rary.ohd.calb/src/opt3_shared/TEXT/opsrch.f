C     MEMBER OPSRCH
C-----------------------------------------------------------------------

C
      SUBROUTINE OPSRCH(A,MA,IEND,IPASS1,OA,MOA,ILOCOA,MILOC,NUMA,IZY,
     *NCOUN,NN,IPMOVE)
C
C..............................................
C     THIS SUBROUTINE PERFORMS THE PATTERN SEARCH OPTIMIZATION SCHEME.
C..............................................
C     SUBROUTINE MODIFIED TO BE COMPATIBLE WITH OPT3 BY
C            LARRY BRAZIL - HRL   JUNE 1981   VERSION 1
C..............................................
C
C
C
C
C     PATTERN  SEARCH  WITH  MODIFICATIONS
C
C     DEFINITION  OF  PROGRAM  VARIABLES
C
C     NUMA= NUMBER OF A(I)  PARAMETERS  TO BE OPTIMIZED
C
C     A(I)= CURRENT VALUE OF PARAMETER I
C
C     B(I)= VALUE OF  PARAMETER  I AFTER PREVIOUS LOCAL EXCURSION
C
C     BA(I)= VALUE OF  PARAMETER  I AFTER PRESENT LOCAL EXCURSION
C
C     BESTA(I)= VALUE OF PARAMETER I FOR RUN WITH LOWEST CRITERIA VALUE
C
C     NNBEST= RUN NUMBER THAT HAS THE LOWEST CRITERIA VALUE
C
C     NPER= IF = 1 DDELTA(I) MUST BE IN PERCENT/100
C           IF = 0 DDELTA(I) MUST BE AN ABSOLUTE VALUE
C
C     DDELTA(I)= WHEN NPER=1  DELTA(I)=ABS(DDELTA(I)*A(I))
C                WHEN NPER=0  DELTA(I)=DDELTA(I)
C
C     DELTA(I)= INCREMENT ADDED OR SUBTRACTED TO A(I) DURING A LOCAL EXC
C
C     OPTIM= VALUE OF THE OPTIMIZATION CRITERION
C
C     NN= NUMBER OF TIMES MAIN PROGRAM HAS CALLED OPT
C
C     NSIGN(I)=  NSIGN(I)=0  THEN + DELTA(I) APPLIED FIRST
C                NSIGN(I)=1  - DELTA(I) APPLIED FIRST
C
C     MAXN= MAXIMUM NUMBER OF TIMES MAIN PROGRAM MAY CALL OPT BEFORE
C                  OPTIMIZATION IS ABORTED
C
C     KC= MAXIMUM NUMBER OF TIMES DELTA(I) MAY BE HALVED BEFORE
C        OPTIMIZATION IS TERMINATED (MAXN OVER-RIDES KC)
C
C     PCENTO= PERCENT/100 CRITERION MUST AT LEAST CHANGE IN KSTOP TRIALS
C               OR ANALYSIS IS STOPPED.
C
C
C
C
C
C
C
C      DIMENSION    DELTA(16),BA(16),B(16),NSIGN(16),LES(16),CRITER(10)
C      DIMENSION    ICLOSL(16),ICLOSE(16),AT(3,16),IOUT(2,16),NIN(16)
C      DIMENSION    BESTA(16),OA(MOA),ILOCOA(MILOC),A(MA)
C
      integer, parameter::MaxNopt=100
      integer, parameter::MaxNpg=2*MaxNopt+1,MaxNpt=MaxNopt*MaxNpg
      DIMENSION    DELTA(MaxNopt),BA(MaxNopt),B(MaxNopt),NSIGN(MaxNopt),
     &             LES(MaxNopt),CRITER(10)
      DIMENSION    ICLOSL(MaxNopt),ICLOSE(MaxNopt),AT(3,MaxNopt),
     &             IOUT(2,MaxNopt),NIN(MaxNopt)
      DIMENSION    BESTA(MaxNopt),OA(MOA),ILOCOA(MILOC),A(MA)
C
       INTEGER      ROUTE,SNOW
C
C  OPTIMIZATION COMMON BLOCKS.
C
      INCLUDE 'common/ionum'
      INCLUDE 'common/sysbug'
      INCLUDE 'common/fdbug'
      INCLUDE 'ocommon/opschm'
C
      COMMON/OPSAVE/LES,BA,B,ICLOSL,ICLOSE,AT,IOUT,NIN,DELTA,ISTOP,
     * LC,IT,ICOUN,LDELT,NSTART,NSAVE,NUM1,BEST,BESTA,NSIGN,
     * CRITER,YX,YY,LL,YOPTIM
      SAVE
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/calb/src/opt3_shared/RCS/opsrch.f,v $
     . $',                                                             '
     .$Id: opsrch.f,v 1.5 2003/08/26 17:44:41 wkwock Exp $
     . $' /
C    ===================================================================
C
C
      DATA IBUG/4HOPT /
C
      IF(ITRACE.GE.1) WRITE(IODBUG,1000)
 1000 FORMAT(1H0,17H** OPSRCH ENTERED)
C
      MAXIN=3
      IF (IPASS1.EQ.0) GO TO 90
C
C  INITIALIZATION  ROUTINE
      NN=1
      NCOUN=1
      NNBEST=1
      BEST=OPTIM
C
      DO 30 I=1,NUMA
      LES(I)=0
      BA(I)=A(I)
      B(I)=A(I)
      BESTA(I)=A(I)
      ICLOSL(I)=0
      ICLOSE(I)=0
      AT(1,I)=A(I)
      AT(2,I)=A(I)
      AT(3,I)=A(I)
      IOUT(1,I)=0
      IOUT(2,I)=0
      NIN(I)=0
      IF (NPER.EQ.0) GO TO 10
      IF (NPER.EQ.1) GO TO 20
      GO TO 9999
10    DELTA(I)=OA(ILOCOA(I)+5)
      GO TO 30
20    DELTA(I)=ABS(OA(ILOCOA(I)+5)*A(I))
30    CONTINUE
C
      ISTOP=0
      LC=0
      IT=1
      IZY=0
      ICOUN=0
      LDELT=0
      NSTART=1
      NSAVE=0
      NUM1=NUMA
      IF(NUMA.GT.8) NUM1=8
C
      WRITE(IPR,710)
C  PRINT THE INITIAL POINT AND ITS STATISTICS
      WRITE(IPR,581)
      WRITE(IPR,582) (OA(ILOCOA(J)+2),OA(ILOCOA(J)+3),J=1,NUM1)
      WRITE(IPR,584) (A(J),J=1,NUM1)
      IF (NOPT.LT.9) GO TO 201
      WRITE(IPR,582) (OA(ILOCOA(J)+2),OA(ILOCOA(J)+3),J=9,NUMA)
      WRITE(IPR,584) (A(J),J=9,NUMA)
  201 CONTINUE
      WRITE(IPR,586) DRMS,VRMS,SABSDF,SABSLG,PBIAS,RMOD1,RCOF,
     *               HMLE,TLAMDA
C
      YOPTIM = 1.0E+20
      WRITE(IPR,720)
      WRITE(IPR,740) (OA(ILOCOA(I)+2),OA(ILOCOA(I)+3),I=1,NUM1)
80    IF(IOPTIM.EQ.1) WRITE(IPR,731)
      IF(IOPTIM.EQ.2) WRITE(IPR,732)
      IF(IOPTIM.EQ.5) WRITE(IPR,733)
      WRITE(IPR,730)
C
      YX=OPTIM
      YY=YX
C
C  BEGIN HERE IF THIS IS NOT FIRST PASS
C
90    YS=OPTIM
C
C     RECORD THE STATISTICS FOR THE BEST PARAMETER ESTIMATE
      IF(OPTIM.LE.YOPTIM) THEN
        YDRMS = DRMS
        YVRMS = VRMS
        YSABDF = SABSDF
        YSABLG = SABSLG
        YRMOD1 = RMOD1
        YHMLE = HMLE
        YPBIAS = PBIAS
        YRCOF = RCOF
        YLAMDA = TLAMDA
        YOPTIM = OPTIM
      END IF
         WRITE(IPR,770) NCOUN,NN,YS,PBIAS,RCOF,(A(I),I=1,NUM1)
         IF(NUMA.LT.9) GO TO 110
         WRITE(IPR,780) (A(I),I=9,NUMA)
 110  IF(IPASS1.EQ.1) WRITE(IPR,720)
C
C  CHECK OBJECTIVE FUNCTION
C
      IF (OPTIM.GE.BEST)  GO TO 130
      BEST=OPTIM
      NNBEST=NN
      DO 120 I=1,NUMA
120   BESTA(I)=A(I)
C
130   NN=NN+1
      IF(NN.GT.MAXN) GO TO 640
      IF(LES(IT).EQ.1) GO TO 280
C
C  LOCAL EXCURSION WITH +DELTA(I) FIRST
C
      IF(IZY.GT.0) GO TO 210
      IF(YS.GE.YY) GO TO 140
      NSAVE=1
      YX=YS
      YY=YS
  140 WRITE(IPR,740) (OA(ILOCOA(I)+2),OA(ILOCOA(I)+3),I=1,NUM1)
      IF(NUMA.LT.9) GO TO 150
      WRITE(IPR,750) (OA(ILOCOA(I)+2),OA(ILOCOA(I)+3),I=9,NUMA)
  150 WRITE(IPR,730)
160   IZY=IZY+1
170   IF(IOUT(1,IZY).EQ.0) GO TO 180
      WRITE(IPR,820) OA(ILOCOA(IZY)+2),OA(ILOCOA(IZY)+3),A(IZY)
      ISTOP=ISTOP+1
      IF(ISTOP.EQ.NUMA) GO TO 670
      IZY=IZY+1
      IF(IZY.GT.NUMA) GO TO 270
      GO TO 170
180   IT=IZY
      IF(LES(IZY).EQ.1) GO TO 340
190   LL=0
C
      A(IZY)=A(IZY)+DELTA(IZY)
      NSIGN(IZY)=0
      IF(ICLOSE(IZY).EQ.0) GO TO 200
      LL=LL+1
      GO TO 220
200   LL=LL+1
      GO TO 630
210   IF(YX.GT.YS) GO TO 250
220   GO TO (230,240,260),LL
230   A(IZY)=A(IZY)-2.0*DELTA(IZY)
      NSIGN(IZY)=1
      IF(ICLOSL(IZY).EQ.1) GO TO 240
      GO TO 200
240   A(IZY)=A(IZY)+DELTA(IZY)
      NSIGN(IZY)=0
      GO TO 260
250   YX=YS
260   IF(IZY.LT.NUMA) GO TO 160
270   IT=1
      IZY=0
      ISTOP=0
      IF(YY.EQ.YX) GO TO 550
      YY=YX
      GO TO 420
C
C  LOCAL EXCURSION WITH -DELTA(I) FIRST
C
280   IF(IZY.GT.0) GO TO 360
      IF(YS.GE.YY) GO TO 290
      NSAVE=1
      YX=YS
      YY=YS
  290 WRITE(IPR,740) (OA(ILOCOA(I)+2),OA(ILOCOA(I)+3),I=1,NUM1)
      IF(NUMA.LT.9) GO TO 300
      WRITE(IPR,750) (OA(ILOCOA(I)+2),OA(ILOCOA(I)+3),I=9,NUMA)
300   WRITE(IPR,730)
310   IZY=IZY+1
320   IF(IOUT(1,IZY).EQ.0) GO TO 330
      WRITE(IPR,820) OA(ILOCOA(IZY)+2),OA(ILOCOA(IZY)+3),A(IZY)
      ISTOP=ISTOP+1
      IF(ISTOP.EQ.NUMA) GO TO 670
      IZY=IZY+1
      IF(IZY.GT.NUMA) GO TO 270
      GO TO 320
330   IT=IZY
      IF(LES(IZY).EQ.0) GO TO 190
340   LL=0
      A(IZY)=A(IZY)-DELTA(IZY)
      NSIGN(IZY)=1
      IF(ICLOSL(IZY).EQ.0) GO TO 350
      LL=LL+1
      GO TO 370
350   LL=LL+1
      GO TO 630
360   IF(YX.GT.YS) GO TO 400
370   GO TO(380,390,410),LL
380   A(IZY)=A(IZY)+2.0*DELTA(IZY)
      NSIGN(IZY)=0
      IF(ICLOSE(IZY).EQ.1) GO TO 390
      GO TO 350
390   A(IZY)=A(IZY)-DELTA(IZY)
      NSIGN(IZY)=1
      GO TO 410
400   YX=YS
410   IF(IZY.LT.NUMA) GO TO 310
      IT=1
      IZY=0
      ISTOP=0
      IF(YY.EQ.YX) GO TO 550
      YY=YX
C
C  COMPUTE NEW DELTA'S
C
420   IF(NPER.EQ.0) GO TO 440
      DO 430 I=1,NUMA
      DELTA(I)=ABS(OA(ILOCOA(I)+5)*A(I))
430   CONTINUE
440   LC=0
      NSAVE=0
450   WRITE(IPR,800)
C
C  CHECK STOPPING CRITERIA (%CHANGE IN KSTOP TRIALS)
C
      CRITER(10)=YY
      IF(NCOUN.LT.(KSTOP+1)) GO TO 460
      TIMEOU=(CRITER(10-KSTOP)-CRITER(10))/CRITER(10-KSTOP)
      IF(TIMEOU.LT.PCENTO) GO TO 650
460   NCOUN=NCOUN+1
      DO 462 I=1,9
  462 CRITER(I)=CRITER(I+1)
C
C  CHECK TO REMOVE INACTIVE PARAMETERS
C
      DO 480 I=1,NUMA
      IOUT(2,I)=IOUT(1,I)
      AT(1,I)=AT(2,I)
      AT(2,I)=AT(3,I)
      AT(3,I)=A(I)
      NIN(I)=NIN(I)+1
      DIFF=ABS(AT(3,I)-AT(1,I))
      IF(DIFF.LT.2.0*DELTA(I)) GO TO 470
      IOUT(1,I)=0
      GO TO 480
470   IF(NIN(I).LT.MAXIN) GO TO 480
      NIN(I)=0
      IOUT(1,I)=1
      IF(IOUT(2,I).EQ.1) IOUT(1,I)=0
480   CONTINUE
C
C  PATTERN  MOVE  ROUTINE
C
      DO 540 I=1,NUMA
      LES(I)=NSIGN(I)
      BA(I)=A(I)
      IF(IOUT(1,I).EQ.1) GO TO 490
      A(I)=2.0*A(I)-B(I)
C
C  CHECK  UPPER  AND  LOWER  CONSTRAINTS
C
490   CC=A(I)-DELTA(I)
      CD=A(I)+DELTA(I)
      DL=OA(ILOCOA(I)+6)
      DU=OA(ILOCOA(I)+7)
      IF(CC.GE.DL) GO TO 500
      ICLOSL(I)=1
      A(I)=DL
      GO TO 510
500   ICLOSL(I)=0
510   IF(CD.LE.DU) GO TO 520
      ICLOSE(I)=1
      A(I)=DU
      GO TO 530
520   ICLOSE(I)=0
530   B(I)=BA(I)
540   CONTINUE
      IPMOVE=1
      GO TO 630
C
550   LC=LC+1
C
C  DESTROY  PRESENT  PATTERN
C
      IF(LC-1)640,560,590
560   IF(NSAVE.EQ.1) GO TO 600
      DO 570 I=1,NUMA
      A(I)=BA(I)
      IOUT(1,I)=0
      IOUT(2,I)=0
      NIN(I)=0
570   CONTINUE
580   ICOUN=ICOUN+1
      GO TO 620
C
C  CHECK NO. OF RESOLUTIONS
C
590   IF(LDELT.GE.KC) GO TO 660
C
C  HALVE  DELTA(I)  (RESOLUTION)
C
600   NSAVE=0
      DO 610 I=1,NUMA
      OA(ILOCOA(I)+5)=OA(ILOCOA(I)+5)*0.5
      DELTA(I)=DELTA(I)*0.5
      IOUT(1,I)=0
      IOUT(2,I)=0
      NIN(I)=0
610   CONTINUE
C
      LDELT=LDELT+1
620   WRITE(IPR,810) ICOUN,LDELT
      WRITE(IPR,790) NCOUN,NN,YY,(A(I),I=1,NUM1)
      IF(NUMA.LT.9) GO TO 110
      WRITE(IPR,780) (A(I),I=9,NUMA)
      GO TO 110
C
630   CONTINUE
      GO TO 9999
C
640   IEND=1
      WRITE (IPR,845)  MAXN
      GO TO 680
C
650   IEND=1
      PCENTA=PCENTO*100.
      WRITE (IPR,850)  PCENTA,KSTOP
      GO TO 680
C
660   IEND=1
      WRITE (IPR,860)  KC
      GO TO 680
C
670   IEND=1
      WRITE (IPR,870)
C
680   DO 690 I=1,NUMA
690   A(I)=BESTA(I)
C  PRINT THE FINAL PARAMETER ESTIMATES AND ITS STATISTICS
      WRITE(IPR,830)
      WRITE(IPR,582) (OA(ILOCOA(J)+2),OA(ILOCOA(J)+3),J=1,NUM1)
      WRITE(IPR,584) (A(J),J=1,NUM1)
      IF (NOPT.LT.9) GO TO 501
      WRITE(IPR,582) (OA(ILOCOA(J)+2),OA(ILOCOA(J)+3),J=9,NUMA)
      WRITE(IPR,584) (A(J),J=9,NUMA)
  501 CONTINUE
      WRITE(IPR,586) YDRMS,YVRMS,YSABDF,YSABLG,YPBIAS,YRMOD1,YRCOF,
     *               YHMLE,YLAMDA
C
C
 9999 IF(ITRACE.GE.1) WRITE(IODBUG,1002)
 1002 FORMAT(1H0,14H** EXIT OPSRCH)
C
      DO 425 I=1,NUMA
      DL=OA(ILOCOA(I)+6)
      DU=OA(ILOCOA(I)+7)
C
      IF(A(I).LT.DL) A(I)=DL
      IF(A(I).GT.DU) A(I)=DU
  425 CONTINUE
      RETURN
C
C
  581 FORMAT(1H0,//,1X,'*** PRINT THE INITIAL POINT AND ITS STATISTICS',
     *           ' ***',/,1X,62(1H=))
  582 FORMAT(1H0,/,3X,13(2X,2A4))
  584 FORMAT(1H0,13F10.3)
  586 FORMAT(1H0,/,2X,'DAILY RMS',6X,'MON VRMS',5X,'/O-S/**EXP',2X,
     *       '/LOG ERR/**EXP',3X,'% BIAS',6X,'1.-MOD RCOF',6X,'R COEF',
     *       6X,'HMLE VALUE',3X,'LAMDA VALUE',/,2X,9(1H-),6X,8(1H-),5X,
     *       10(1H-),2X,14(1H-),3X,6(1H-),6X,11(1H-),6X,6(1H-),6X,
     *       10(1H-),3X,11(1H-),//,9(E12.4,2X))
700   FORMAT (//20H0SUBROUTINE  OPTMZ.     )
710   FORMAT(1H1)
720   FORMAT (///)
730   FORMAT (41X,42(2H--))
731   FORMAT(14X,'(CMSD)',10X,'(DAILY Q''S)')
732   FORMAT(15X,'(MM)',11X,'(DAILY Q''S)')
733   FORMAT(14X,'(1.-RM)',9X,'(DAILY Q''S)')
740   FORMAT (1H0,'TRIAL RUN   CRITERION  % BIAS  R COEF',1X,8(2X,2A4))
750   FORMAT (45X,8(2X,2A4))
760   FORMAT(1H0,31H*** INITIAL PARAMETER VALUES.     )
770   FORMAT (1H0,I4,I5,E12.4,F8.2,F8.4,8F10.3)
780   FORMAT (44X,8F10.3)
790   FORMAT (1H0,I4,I5,E12.4,16X,8F10.3)
800   FORMAT (1H0,'PATTERN  MOVE')
810   FORMAT(1H0,'PATTERN=',I4,'   RESOLUTION=',I5)
820   FORMAT ('0PARAMETER ',2A4,'= ',F9.3,'  REMOVED FROM OPTIMIZATION')
  830 FORMAT(1H0,//,1X,'*** PRINT THE FINAL PARAMETER ESTIMATES AND',
     *       ' THE STATISTICS ***',/,1X,62(1H=))
845   FORMAT ( 66H0*** OPTIMIZATION TERMINATED BECAUSE THE MAXIMUM NUMBE
     1R OF RUNS  (,I3,20H)  HAS BEEN REACHED.   )
850   FORMAT ( 74H0*** OPTIMIZATION TERMINATED BECAUSE THE CRITERION VAL
     1UE WAS NOT CHANGED  ,F5.2,14H  PERCENT IN  ,I2,9H  TRIALS.     )
860   FORMAT ( 73H0*** OPTIMIZATION TERMINATED BECAUSE THE MAXIMUM NUMBE
     1R OF RESOLUTIONS  (,I2,21H)  HAS BEEN EXCEEDED.     )
870   FORMAT ( 57H0*** ALL PARAMETERS HAVE BEEN REMOVED FROM OPTIMIZATIO
     1N.   )
C
C
      END
