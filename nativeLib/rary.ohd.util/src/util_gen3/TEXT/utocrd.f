C MODULE UTOCRD
C-----------------------------------------------------------------------
C
      SUBROUTINE UTOCRD (IPUNIT,NPOS,CHAR,LCHAR,NSPACE,CARD,ITYPE,
     *   NCHECK,LNUM,ISTAT)
C
C  ROUTINE UTOCRD PUTS THE SPECIFIED CHARACTERS INTO THE OUTPUT
C  BUFFER. WHEN THE BUFFER IS FULL, THE BUFFER IS WRITTEN TO
C  THE OUTPUT UNIT.
C
C   INPUT VARIABLES
C     NPOS   - CURRENT POSITION ON CARD
C     CHAR   - STRING TO BE OUTPUT ON CARD
C     LCHAR  - NUMBER OF CHARACTERS IN CHAR
C     NSPACE - NUMBER OF BLANKS TO BE ADDED TO RIGHT OF STRING
C     CARD   - ARRAY FOR HOLDING STRING DATA TO BE OUTPUT
C     ITYPE  - DATA TYPE FLAG
C             -1 = UNSPECIFED - IF CHARACTER, DO NOT ENCLOSE IN QUOTES
C                               EMBEDDED BLANKS OR COMMAS
C              0 = UNSPECIFIED
C              1 = INTEGER
C              2 = REAL
C              3 = CHARACTER - ENCLOSE IN QUOTES IF EMBEDDED
C                              BLANKS OR COMMAS
C              4 = CHARACTER - DO NOT ENCLOSE IN QUOTES IF EMBEDDED
C                              BLANKS OR COMMAS
C     NCHECK - MAXIMUM NUMBER OF CHARACTERS IN A FIELD
C              (USED TO PREVENT SHORT FIELDS FROM SPANNING 2 CARDS)
C     LNUM   - CARD NUMBER (NOT CURRENTLY USED)
C
C   OUTPUT VARIABLES
C     ISTAT  - STATUS CODE
C
C
      CHARACTER*1 CHAR(LCHAR)
      CHARACTER*1 TEMP(72)
      CHARACTER*26 LETRS/'ABCDEFGHIJKLMNOPQRSTUVWXYZ'/
      CHARACTER*80 CARD
C
      INCLUDE 'uiox'
      INCLUDE 'ucmdbx'
      INCLUDE 'ufreex'
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/util/src/util_gen3/RCS/utocrd.f,v $
     . $',                                                             '
     .$Id: utocrd.f,v 1.2 2002/02/11 20:46:27 dws Exp $
     . $' /
C    ===================================================================
C
C
      IF (ICMTRC.GT.0) THEN
         CALL ULINE (ICMPRU,1)
         WRITE (ICMPRU,*) 'ENTER UTOCRD'
         ENDIF
C
      IF (ICMDBG.GT.0) THEN
         CALL ULINE (ICMPRU,1)
         WRITE (ICMPRU,*) 'NPOS=',NPOS,' NSPACE=',NSPACE,
     *      ' ITYPE=',ITYPE,' NCHECK=',NCHECK,' LNUM=',LNUM
         CALL ULINE (ICMPRU,1)
         WRITE (ICMPRU,'(1X,A,I2,A,80A1)') 'LCHAR=',LCHAR,'CHAR=',
     *      (CHAR(I),I=1,LCHAR)
         CALL ULINE (ICMPRU,1)
         WRITE (ICMPRU,*) 'CARD=',CARD
         ENDIF
C
      ISTAT=0
C
      IFPOS=1
      ILPOS=LCHAR
      NCHAR=LCHAR
C
      CALL SUBSTR (CHAR,1,LCHAR,TEMP,1)
C
C  CHECK IF TYPE SPECIFIED
      IXTYPE=ITYPE
      IF (IXTYPE.EQ.-1) IXTYPE=0
      IF (IXTYPE.GE.1.AND.IXTYPE.LE.4) GO TO 10
C
      IF (ICMDBG.GT.0) THEN
         CALL ULINE (ICMPRU,1)
         WRITE (ICMPRU,*) 'LCHAR=',LCHAR
         ENDIF
C
C  GET LENGTH OF CHARACTER STRING TO BE OUTPUT
      CALL ULENTH (CHAR,LCHAR,LENGTH)
C
      IXTYPE=0
      IPRERR=0
C
C  CHECK IF INTEGER NUMBER
      CALL UFA2I (CHAR,1,LENGTH,INTEGR,IPRERR,LP,IERR)
      IF (IERR.EQ.0) THEN
         IXTYPE=1
         GO TO 10
         ENDIF
C
C  CHECK IF REAL NUMBER
      NDEC=0
      CALL UFA2F (CHAR,1,LENGTH,NDEC,REAL,IPRERR,LP,IERR)
      IF (IERR.EQ.0) THEN
         IXTYPE=2
         ENDIF
C
C  SET INDICATOR THAT LEADING AND TRAILING ZEROS CAN BE REMOVED
10    IZERO=1
C
C  CHECK IF INTEGER TYPE SPECIFIED
      IF (IXTYPE.EQ.1) THEN
         IZERO=0
         GO TO 50
         ENDIF
C
C  CHECK IF CHARACTER TYPE SPECIFIED
      IF (IXTYPE.NE.3.AND.IXTYPE.NE.4) GO TO 30
         IZERO=0
         GO TO 50
C
C  CHECK IF ANY CHARACTERS IN STRING
30    DO 40 I=1,LEN(LETRS)
         CALL UINDEX (CHAR,LCHAR,LETRS(I:I),1,ICOL)
         IF (ICOL.EQ.0) GO TO 40
            IZERO=0
            GO TO 50
40       CONTINUE
C
C  FIND FIRST NON-ZERO NON-BLANK CHARACTER
50    DO 60 I=1,ILPOS
         IF (IZERO.EQ.0.AND.CHAR(I).NE.' ') GO TO 70
         IF (IZERO.EQ.1.AND.CHAR(I).NE.' '.AND.CHAR(I).NE.'0') GO TO 70
60       CONTINUE
      GO TO 80
70    IFPOS=I
C
C  FIND LAST NON-ZERO NON-BLANK CHARACTER
80    DO 90 N=1,ILPOS
         I=ILPOS-N+1
         IF (IZERO.EQ.0.AND.CHAR(I).NE.' ') GO TO 100
         IF (IZERO.EQ.1.AND.CHAR(I).NE.' '.AND.CHAR(I).NE.'0') GO TO 100
90       CONTINUE
      GO TO 110
100   ILPOS=I
C
C  CHECK IF ALL BLANKS
110   IF (IFPOS.GT.0.OR.ILPOS.GT.0) GO TO 120
         NCHAR=LCHAR
         GO TO 170
C
C  CALCULATE NUMBER OF CHARACTERS TO BE PROCESSED
120   NCHAR=ILPOS-IFPOS+1
      IF (ICMDBG.GT.0) THEN
         CALL ULINE (ICMPRU,1)
         WRITE (ICMPRU,*) 'ILPOS=',ILPOS,' IFPOS=',IFPOS,
     *      ' NCHAR=',NCHAR
         ENDIF
C
C  CHECK IF CHARACTER TYPE SPECIFIED
      IF (IXTYPE.EQ.3) GO TO 130
      IF (IXTYPE.EQ.4) GO TO 170
C
C  CHECK IF ONLY ONE CHARACTER
      IF (NCHAR.NE.1) GO TO 130
C
C  ONLY 1 NON-BLANK NON-ZERO CHARACTER - CHECK IF DECIMAL POINT
      IF (TEMP(IFPOS).NE.'.') GO TO 170
         CALL SUBSTR ('0.',1,2,TEMP,1)
         IFPOS=1
         NCHAR=2
         GO TO 170
C
C  CHECK FOR EMBEDDED BLANKS OR COMMAS
130   IF (NCHAR.EQ.1) GO TO 170
      IF (ITYPE.EQ.-1) GO TO 170
      DO 140 I=IFPOS,ILPOS
         IF (CHAR(I).EQ.' ') GO TO 160
140      CONTINUE
      DO 150 I=IFPOS,ILPOS
         IF (CHAR(I).EQ.',') GO TO 160
150      CONTINUE
      GO TO 170
C
C  EMBEDDED BLANK OR COMMA FOUND - ADD QUOTES
160   IFPM1=IFPOS-1
      IF (IFPM1.GT.0) CALL SUBSTR (CHAR,1,IFPM1,TEMP,1)
      IF (ICMDBG.GT.0) THEN
         WRITE (ICMPRU,*) 'IFPOS=',IFPOS,' IFPM1=',IFPM1
         CALL ULINE (ICMPRU,1)
         ENDIF
      TEMP(IFPOS)=''''
      IFPP1=IFPOS+1
      CALL SUBSTR (CHAR,IFPOS,NCHAR,TEMP,IFPP1)
      TEMP(ILPOS+2)=''''
      ILPP1=ILPOS+1
      NCHAR1=LCHAR-ILPOS
      NCHAR2=ILPOS+3
      CALL SUBSTR (CHAR,ILPP1,NCHAR1,TEMP,NCHAR2)
      NCHAR=NCHAR+2
C
C  SET NUMBER OF BLANK SPACES TO BE LEFT AT END OF CARD
170   ISPACE=0
C
C  CHECK IF ENOUGH ROOM ON CARD
      IF (NPOS+NCHECK.GT.ICDSTP-ISPACE) GO TO 180
      IF (NPOS+NCHAR+NSPACE.GT.ICDSTP-ISPACE) GO TO 180
      CALL SUBSTR (TEMP,IFPOS,NCHAR,CARD,NPOS)
      NPOS=NPOS+NCHAR+NSPACE
      GO TO 200
C
C  OUTPUT CARD
180   CALL UPNCRD (IPUNIT,CARD)
      NPOS=6
      CALL SUBSTR (TEMP,IFPOS,NCHAR,CARD,NPOS)
      NPOS=NPOS+NCHAR+NSPACE
C
200   IF (ICMDBG.GT.0) THEN
         CALL ULINE (ICMPRU,1)
         WRITE (ICMPRU,*) 'CARD=',CARD
         ENDIF
C
      IF (ICMTRC.GT.0) THEN
         CALL ULINE (ICMPRU,1)
         WRITE (ICMPRU,*) 'EXIT UTOCRD'
         ENDIF
C
      RETURN
C
      END
