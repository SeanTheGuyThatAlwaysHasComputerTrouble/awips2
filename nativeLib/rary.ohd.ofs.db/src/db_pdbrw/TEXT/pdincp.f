C MEMBER PDINCP
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 06/19/95.13:35:17 BY $WC20SV
C
C @PROCESS LVL(77)
C
      SUBROUTINE PDINCP (ISIRAY,IOLDTA,IHR,DATA)
C
C  SUBROUTINE TO DETERMINE IF ANY INCREMENTAL PRECIP VALUES CAN BE
C  COMPUTED FROM A PARTIAL DAY 24 PRECIP TOTAL.  THE RULES USED TO
C  DETERMINE THIS ARE:
C
C     1. NEW 24 PRECIP VALUE MUST NOT BE MISSING,
C     2. STATION MUST HAVE EITHER A PP03 OR PP06 TYPE DEFINED,
C     3. OLD 24 PRECIP TOTAL MUST NOT BE MISSING (UNLESS THE
C        REPORT TIME OF THE NEW PP24 DATA IS AT THE END OF THE
C        FIRST PERIOD OF THE DAY),
C     4. OLD 24 PRECIP TOTAL MUST HAVE BEEN FOR THE IMMEDIATELY
C        PRECEDING TIME INTERVAL (EITHER 3 OR 6 HOURS EARLIER),
C        UNLESS, FOR PP06 TYPES, THE REPORT TIME OF THE PP24 DATA
C        IS AT THE END OF THE DAY (12Z) AND THE LAST REPORT WAS
C        FOR 0Z.  THEN THE 12 HR INCREMENT IS SPLIT BETWEEN THE
C        PERIODS ENDING AT 6Z AND 12Z.
C
C
      INCLUDE 'uio'
      INCLUDE 'udebug'
      INCLUDE 'pdbcommon/pdincs'
C
      DIMENSION ISIRAY(1),INCVR(2)
      INTEGER*2 ISIRAY,IOLDTA
      INTEGER PPVR(2)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/db_pdbrw/RCS/pdincp.f,v $
     . $',                                                             '
     .$Id: pdincp.f,v 1.1 1995/09/17 18:44:01 dws Exp $
     . $' /
C    ===================================================================
C
      DATA PPVR/4HPP03,4HPP06/
      DATA INCVR/3,6/
C
      IF (IPDTR.GT.0) WRITE (IOGDB,*) '*** ENTER PDINCP'
C
      IF (IPDDB.GT.0) WRITE (IOGDB,20) IOLDTA,IHR,DATA
20    FORMAT (' PREVIOUS PP24 VALUE (ENCODED)=',I7,3X,
     *   'HOUR OF NEW PP24 VALUE=',I2,3X,
     *   'NEW PP24 VALUE (NOT ENCODED)=',F10.4)
C
C  SET DEFAULT EXIT CONDITIONS
C
      RUNINC=.FALSE.
      NDATIN=0
C
C  DETERMINE IF NEW PP24 VALUE IS MISSING.  IF SO, THEN NOTHING
C  CAN BE DONE TO CALCULATE INCREMENTAL PRECIP
C
      IF (DATA.LE.-999.) GO TO 70
C
C  FIRST, DETERMINE IF STATION HAS PP03 OR PP06 DATA.
C  NOTE THAT STATION CAN HAVE ONLY ONE 'PPVR' TYPE, SO IF FIRST
C  ONE IS FOUND, STOP SEARCHING FOR OTHER
C
      DO 30 I=1,2
         IF (IPDFDT(ISIRAY,PPVR(I)).NE.0) GO TO 40
30       CONTINUE
      GO TO 70
C
C  NOW KNOW WE HAVE NON-MISSING NEW PP24 VALUE AND STATION HAS EITHER
C  PP03 OR PP06 TYPE DATA.  NEXT IS TO GENERATE PREVIOUSLY STORED
C  24 PRECIP VALUE AND REPORTING TIME FROM THE ENCODED DATA VALUE.
C  NOTE THAT IF ENCODED DATA VALUE IS -9, THEN OLD DATA WAS MISSING.
C
40    INCTYP=PPVR(I)
      INCINT=INCVR(I)
C
      IF (IOLDTA.NE.-9) GO TO 50
C
C  HERE WE HAVE MISSING OLD 24 HR PRECIP VALUE.
C  THIS IS OK IF THIS IS THE FIRST PERIOD OF THE DAY FOR THE PPVR TYPE.
C
      IF (IHR.NE.INCINT) GO TO 70
C
C  THIS IS FIRST PERIOD OF DAY FOR 'PPVR' TYPE
C
      NDATIN=1
      DATINC=DATA
      RUNINC=.TRUE.
      GO TO 70
C
C  HERE BOTH THE OLD PP24 AND NEW PP24 VALUES ARE NON-MISSING.
C  DECODE THE OLD PP24 VALUE TO DETERMINE ITS REPORT TIME.
C  THEN SEE IF THE REPORT TIME WAS ONE INTERVAL AGO.
C
C  NOTE THAT FOR NEW DATA AT THE END OF THE DAY (IHR=0), THE OLD
C  DATA MAY HAVE BEEN REPORTED LAST AT THE HALF DAY (HOUR=12).  IN
C  THIS CASE, THE INCREMENTAL VALUE CAN BE COMPUTED FOR THE HALF
C  DAY PERIOD AND EVENLY DIVIDED BETWEEN THE LAST TWO PERIODS OF
C  THE DAY.  THIS CAN ONLY BE DONE IF TYPE IS 'PP06'.
C
C  DECODE THE OLD DATA VALUE
C
50    CALL PDGTPP (IOLDTA,DATOLD,IHROLD,INTPPO)
C
C  NOW COMPUTE THE INTERVAL BETWEEN THE LAST AND CURRENT REPORTING
C  TIMES
C
      IHRNEW=IHR
      IF (IHRNEW.EQ.0) IHRNEW=24
      INTRVL=IHRNEW-IHROLD
      IF (INTRVL.NE.INCINT) GO TO 60
C
C  REPORTING TIMES ARE OK.  NOW COMPUTE THE INCREMENTAL VALUE.
C
      DATINC=DATA-DATOLD
      NDATIN=1
      RUNINC=.TRUE.
      GO TO 70
C
C  LAST REPORT TIME WAS NOT ONE INTERVAL AGO.
C  CHECK FOR SPECIAL CASE OF END OF DAY PP24 VALUE.
C
60    IF (IHRNEW.NE.24.OR.INTRVL.NE.12.OR.INCTYP.NE.PPVR(2)) GO TO 70
C
C  HERE WE HAVE AN END OF DAY REPORT FOR A STATION WITH 'PP06' TYPE AND
C  THE LAST REPORT WAS AT HR 12 (0Z).  COMPUTE THE 12 HOUR INCREMENT,
C  SPLIT IT IN TWO, AND NOTE THAT THERE WILL BE TWO INCREMENTAL VALUES
C  TO BE POSTED.
C
      DATINC=(DATA-DATOLD)/2
      NDATIN=2
      RUNINC=.TRUE.
C
70    IF (IPDDB.GT.0) THEN
         IF (RUNINC) THEN
            WRITE (IOGDB,80) INCTYP,INCINT,NDATIN,DATINC
            ENDIF
         ENDIF
80    FORMAT (' INCTYP=',A4,3X,
     *   'INCINT=',I2,
     *   'NDATIN=',I2,3X,
     *   'DATINC=',F10.4)
C
      IF (IPDTR.GT.0) WRITE (IOGDB,*)
     *   '*** EXIT PDINCP -',
     *   ' RUNINC=',RUNINC
C
      RETURN
C
C
      END
