C MODULE PDSTSS
C-----------------------------------------------------------------------
C          ROUTINE:  PDSTSS
C
C             VERSION:  1.0.0
C
C                DATE:  2-25-83
C
C              AUTHOR:  JANINE FRANZOI
C                       DATA SCIENCES INC
C
C***********************************************************************
C    THIS ROUTINE UPDATES THE STRANGER STATION STATISTICS RECORD
C    BY REPLACING THE LARGEST AND SECOND LARGEST VALUES WITH NEW
C    DATA.  IT CALLS ROUTINE PDSTS2 WHICH SEARCHES THE NUMBER OF
C    DAYS IN THE REPORT PERIOD (EARLIEST-LATEST JULIAN DAY), TO
C    REPLACE THE SECOND LARGEST VALUE IF NECESSARY.
C***********************************************************************
C          ARGUMENT LIST:
C
C         NAME    TYPE  I/O   DIM   DESCRIPTION
C
C       JDAY       I     I     1    JULIAN DAY OF REPORTED VALUE
C       IX         I     I     1    LONGITUDE (POLAR STEREOGRAPHIC)
C       IY         I     I     1    LATITUDE (POLAR STEREOGRAPHIC)
C       IZ         I     I     1    INDEX OF PSSR IN DIRECTORY
C       IDF        I     I     1    INDEX OF LOGICAL UNIT
C       IV        I*2    I     1    VALUE OF REPORT DATE
C       IWRFLG      I    I      1   UPDATE FLAG
C                                      1=NEW REPORT CREATED
C                                      0=UPDATE TO EXISTING REPORT
C       ISTAT     I     O      1    STATUS INDICATOR
C                                      0=NORMAL
C                                      4=READ/WRITE ERROR TO FILE
C***********************************************************************
      SUBROUTINE PDSTSS (JDAY,IX,IY,IV,IZ,IDF,IWRFLG,ISTAT)
C
      INCLUDE 'uio'
      INCLUDE 'udebug'
      INCLUDE 'pdbcommon/pdsifc'
      INCLUDE 'pdbcommon/pddtdr'
      INCLUDE 'pdbcommon/pdunts'
C
      INTEGER*2 ISSTAT(32),IV,I2LG,LAT,LON,IY,IX,LAT2,LON2
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/db_pdbrw/RCS/pdstss.f,v $
     . $',                                                             '
     .$Id: pdstss.f,v 1.3 1998/07/06 12:05:04 page Exp $
     . $' /
C    ===================================================================
C
C
      ISTAT=0
C
      IF (IPDTR.GT.0) WRITE (IOGDB,10)
10    FORMAT (' *** ENTER PDSTSS')
C
C  CONVERT JULIAN DAY TO MONTH,DAY,YEAR
C
      JDAYHR=JDAY*24
      CALL DDGHC2 (JDAYHR,IYR,IMO,IDAY,IHR)
C
C  READ STATISTICS RECORD FROM FILE
C
      IREC=IDDTDR(14,IZ)
      CALL UREADT (KPDDDF(IDF),IREC,ISSTAT,ISTAT)
      IF (ISTAT.NE.0) GO TO 160
C
C  UPDATE REPORT DATE
C
      IF (ISSTAT(1).LT.0) CALL UMEMST (0,ISSTAT(1),6)
      ISSTAT(13)=IMO
CC    ISSTAT(14)=IYR
      ISSTAT(14)=  IYR - (IYR/100)*100
      IF (ISSTAT(15).LT.0) CALL UMEMOV (JDAY,ISSTAT(15),1)
      CALL UMEMOV (ISSTAT(17),JREPRT,1)
      IF (JDAY.GT.JREPRT) CALL UMEMOV (JDAY,ISSTAT(17),1)
C
C  SEE IF VALUE IS LARGER THAN LARGEST
C
      IF (IV.GT.ISSTAT(19)) GO TO 80
C
C  CHECK LAT-LON OF LARGEST - IS THIS THE SAME REPORT
C
      CALL UMEMOV (ISSTAT(20),LGDAY,1)
      IF (IPDDB.EQ.1) WRITE (LP,20) ISSTAT(22),ISSTAT(23),LGDAY
20    FORMAT (' IN PDSTSS - LAT OF LARGEST=',I6,
     *       ' LONG OF LARGEST=',I6,' LARGEST DAY=',I6)
      IF (IY.EQ.ISSTAT(22).AND.IX.EQ.ISSTAT(23).AND.JDAY.EQ.LGDAY)
     *   GO TO 40
C
C  CHECK IF THIS IS LARGER THAN 2ND LARGEST
C
      IF (IV.GT.ISSTAT(24)) GO TO 110
C
C  CHECK IF IT'S EQUAL TO THE 2ND LARGEST REPORT
C
      CALL UMEMOV (ISSTAT(25),LG2DAY,1)
      IF (IPDDB.EQ.1) WRITE (LP,30) ISSTAT(27),ISSTAT(28),LG2DAY
30    FORMAT (' IN PDSTSS - LAT OF 2ND LARGEST=',I6,
     *       ' LONG OF 2ND LARGEST=',I6,' 2ND LARGEST DAY=',I6)
      IF (IY.EQ.ISSTAT(27).AND.IX.EQ.ISSTAT(28).AND.JDAY.EQ.LG2DAY)
     *   GO TO 70
      GO TO 120
40    CONTINUE
C
C  SAME REPORT
C
      IF (IV.LT.ISSTAT(24)) GO TO 60
C
C  REPLACE LARGEST VALUE - THE SAME REPORT WAS FOUND
C
      ISSTAT(19)=IV
      GO TO 120
60    CONTINUE
C
C  SECOND LARGEST BECOMES LARGEST
C
      ISSTAT(19)=ISSTAT(24)
      CALL UMEMOV (ISSTAT(25),ISSTAT(20),1)
      CALL UMEMOV (ISSTAT(27),ISSTAT(22),1)
70    CONTINUE
C
C  FIND NEW SECOND LARGEST
C
      CALL UMEMOV (ISSTAT(20),IDATE,1)
      LAT=ISSTAT(22)
      LON=ISSTAT(23)
      CALL PDSTS2 (IDATE,LAT,LON,IZ,I2LG,LAT2,LON2,IDAY2,ISTAT)
      IF (ISTAT.NE.0) GO TO 160
      IF (I2LG.LT.0) GO TO 120
C
C  UPDATE NEW SECOND LARGEST VALUE
C
      ISSTAT(24)=I2LG
      CALL UMEMOV (IDAY2,ISSTAT(25),1)
      ISSTAT(27)=LAT2
      ISSTAT(28)=LON2
      GO TO 120
80    CONTINUE
C
C  CHECK IF SAME REPORT AS THE LARGEST
C
      CALL UMEMOV (ISSTAT(20),LGDAY,1)
      IF (IY.NE.ISSTAT(22).AND.IX.NE.ISSTAT(23).AND.JDAY.NE.LGDAY)
     *   GO TO 90
C
C  REPLACE LARGEST VALUE SAME REPORT WAS FOUND
C
      ISSTAT(19)=IV
      GO TO 120
90    CONTINUE
C
C  MOVE OLD LARGEST INTO SECOND LARGEST
C
      ISSTAT(24)=ISSTAT(19)
      CALL UMEMOV (ISSTAT(20),ISSTAT(25),2)
C
C  MOVE STATISTICS FOR NEW LARGEST VALUE
C
      ISSTAT(19)=IV
      CALL UMEMOV (JDAY,ISSTAT(20),1)
      ISSTAT(22)=IY
      ISSTAT(23)=IX
      GO TO 120
110   CONTINUE
C
C  REPLACE SECOND LARGEST WITH NEW VALUE
C
C
      ISSTAT(24)=IV
      CALL UMEMOV (JDAY,ISSTAT(25),1)
      ISSTAT(27)=IY
      ISSTAT(28)=IX
120   CONTINUE
      IF (IDDTDR(18,IZ).LT.28) IDDTDR(18,IZ)=28
C
C  WRITE UPDATED RECORD BACK TO FILE
C
      ISSTAT(IMO)=ISSTAT(IMO)+1
      CALL UWRITT (KPDDDF(IDF),IREC,ISSTAT,ISTAT)
      IF (ISTAT.NE.0) GO TO 160
C
      IF (IPDDB.GT.0) WRITE (IOGDB,140) (ISSTAT(I),I=1,28)
140   FORMAT (' UPDATE=',14I6/1X,14I6)
      IF (IPDTR.GT.0) WRITE (IOGDB,150)
150   FORMAT (' *** EXIT PDSTSS')
      GO TO 170
160   CONTINUE
C
C  SYSTEM ERROR
C
      ISTAT=4
C
170   RETURN
C
      END
