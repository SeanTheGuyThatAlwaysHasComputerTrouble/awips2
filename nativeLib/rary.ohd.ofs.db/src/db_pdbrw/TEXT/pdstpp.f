C MEMBER PDSTPP
C-----------------------------------------------------------------------
C
C                             LAST UPDATE: 09/28/95.16:01:30 BY $WC20SV
C
C @PROCESS LVL(77)
C
      SUBROUTINE PDSTPP (IDAY,IVALI,IOLDT1,IREC,ISTAIX,IRCOFS,ISIRAY)
C
C          ROUTINE:  PDSTPP
C
C             VERSION:  1.0.0
C
C                DATE:  01-31-83
C
C              AUTHOR:  SONJA R SIEGEL
C                       DATA SCIENCES INC
C
C***********************************************************************
C
C          DESCRIPTION:
C
C    THIS ROUTINE WILL ENTER PP24 STATISTICS INTO THE STATION
C    INFORMATION RECORD.
C
C***********************************************************************
C
C          ARGUMENT LIST:
C
C         NAME    TYPE  I/O   DIM   DESCRIPTION
C
C       IDAY       I*4   I    1     DATE OF THIS REPORT
C       IVALI      I*2   I    1     CURRENT REPORTED VALUE
C       IOLDT1     I*2   I    1     OLD VALUE FOR THIS REPORT
C       IREC        I    I    1     RECORD NUMBER OF ISIREC FOR REWRIT
C       ISTAIX      I    I    1     LOCATION IN DATA RECORD OF THIS
C       IRCOFS      I    I    1     RECORD OFFSET OF STATION
C       ISIRAY     I*2   O    ?     STATION INFORMATION RECORD
C
C***********************************************************************
C
C          COMMON:
C
      INCLUDE 'udebug'
      INCLUDE 'uio'
      INCLUDE 'pdbcommon/pdsifc'
      INCLUDE 'pdbcommon/pdunts'
      INCLUDE 'pdbcommon/pdbdta'
C
C***********************************************************************
C
C          DIMENSION AND TYPE DECLARATIONS:
C
      INTEGER*2 ISIRAY(*),IVALI,IOLDT1
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2 /                                 '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/db_pdbrw/RCS/pdstpp.f,v $
     . $',                                                             '
     .$Id: pdstpp.f,v 1.2 1996/01/16 22:54:18 page Exp $
     . $' /
C    ===================================================================
C
C
C***********************************************************************
C
C          DATA:
C
C
C***********************************************************************
C
C
C
      IF (IPDDB.GT.0) WRITE (IOGDB,10)
10    FORMAT (' *** ENTER PDSTPP')
C
C IST IS THE POINTER TO BEGINNING OF STATS IN ARRAY-1
C
      IST=ISIRAY(10)*3+10
C
C SEE IF ANY STATS
C
      IF (ISIRAY(IST+2).NE.0.OR.ISIRAY(IST+1).NE.0) GO TO 20
C
C NO STATS ENTER TODAYS
C
      CALL UMEMOV (IDAY,ISIRAY(IST+1),1)
C
C ENTER DATE OF THIS REPORT
C
20    CALL UMEMOV (ISIRAY(IST+3),IDN,1)
      IF (IDAY.GT.IDN) CALL UMEMOV (IDAY,ISIRAY(IST+3),1)
C
C IS THIS A MISSING OR REAL REPORT
C
      CALL PDGTPP (IOLDT1,OLDTA,IHR,IOLDTA)
C
C IF OLD DATA WAS ESTIMATED, TREAT AS MISSING
C
      IF (IHR.EQ.24) IOLDT1 = MISSPP
      IF (IOLDT1.EQ.MISSPP. AND. IVALI.NE.MISSPP)
     *     ISIRAY(IST+5)=ISIRAY(IST+5)+1
C
C UPDATE TOTAL NUMBER OF REPORTS
C
      ISIRAY(IST+6)=ISIRAY(IST+6)+1
C
C UPDATE ZERO REPORTS IF APPROPRIATE
C EXTRACT PP FROM ENCODED VALUE IN IVALI AND IOLDT1
C
      CALL PDGTPP (IVALI,RNEW,IHR,IVAL)
      CALL PDGTPP (IOLDT1,OLDTA,IHR,IOLDTA)
      IF (IVAL.EQ.0.AND.IOLDTA.NE.0) ISIRAY(IST+7)=ISIRAY(IST+7)+1
      IF (IVAL.NE.0.AND.IOLDTA.EQ.0) ISIRAY(IST+7)=ISIRAY(IST+7)-1
C
C NOW ACCUMULATE SUM (MISSING IS TREATED LIKE ZERO)
C
      IF (IOLDT1.EQ.MISSPP) OLDTA=0
      IF (IVALI.EQ.MISSPP) RNEW=0
       CALL UMEMOV (ISIRAY(IST+8),SUMI,1)
      SUMI=SUMI-OLDTA+RNEW
      CALL UMEMOV (SUMI,ISIRAY(IST+8),1)
C
C NOW CHECK LARGEST VALUE
C
      CALL UMEMOV (ISIRAY(IST+11),IOLDAY,1)
C
C MAKE SURE NOT REPLACING SAME REPORT
C
       IF (IDAY.EQ.IOLDAY) GO TO 50
      IF (IVAL.LE.ISIRAY(IST+10)) GO TO 40
C
C SET SECOND LARGEST TO OLD FIRST, ALSO DATE
C
      ISIRAY(IST+13)=ISIRAY(IST+10)
      CALL UMEMOV (ISIRAY(IST+11),ISIRAY(IST+14),1)
C
C SET FIRST LARGEST
C
      ISIRAY(IST+10)=IVAL
      CALL UMEMOV (IDAY,ISIRAY(IST+11),1)
      GO TO 50
C
C CHECK SECOND LARGEST
C
40    IF (IVAL.LE.ISIRAY(IST+13)) GO TO 50
      ISIRAY(IST+13)=IVAL
      CALL UMEMOV (IDAY,ISIRAY(IST+14),1)
C
C ACCUMULATE SUM SQUARED
C
50    RNEW=RNEW*RNEW
      OLSQR=OLDTA*OLDTA
      CALL UMEMOV (ISIRAY(IST+17),SUM,1)
      SUM=SUM-OLSQR+RNEW
      CALL UMEMOV (SUM,ISIRAY(IST+17),1)
       IF (IPDDB.GT.0) WRITE (IOGDB,60) OLDTA,OLSQR,IVAL,RNEW,SUM
60    FORMAT (' OLDTA,OLSQR,IVAL,RNEW,SUM',2F10.4,I8,2F10.4)
C
C SMALLEST NON ZERO
C
      IF (IVAL.LE.0) GO TO 70
C
C IF ZERO PUT A VALUE IN
C
      IF (ISIRAY(IST+16).EQ.0) ISIRAY(IST+16)=IVAL
      IF (IVAL.LT.ISIRAY(IST+16)) ISIRAY(IST+16)=IVAL
C
C CHECK IF REPLACING A LARGEST OR A 2ND LARGEST
C
70    IF (IOLDTA.EQ.MISSNG) GO TO 120
      CALL UCMPAR (IDAY,ISIRAY(IST+11),1,J)
      IF (J.NE.0) GO TO 80
C
C REPLACING LARGEST, SEE IF STILL LARGE
C
      IF (IVAL.GE.ISIRAY(IST+10)) GO TO 100
C
C NOT LARGE SEE IF LARGER THAN 2ND
C  IF SO USE IT ANYWAY
C
      IF (IVAL.GE.ISIRAY(IST+13)) GO TO 100
C
C MOVE 2ND INTO LARGEST AND FIND A NEW 2ND, COULD BE TODAY BUT NOT LARGE
C
      ISIRAY(IST+10)=ISIRAY(IST+13)
      CALL UMEMOV (ISIRAY(IST+14),ISIRAY(IST+11),1)
C
C GO FIND AN NEW SECOND
C
      GO TO 90
C
C SEE IF REPLACING 2ND LARGEST
C
80    CALL UCMPAR (IDAY,ISIRAY(IST+14),1,J)
      IF (J.NE.0) GO TO 120
C
C REPLACING 2ND LARGEST, SEE IF STILL SECOND LARGEST
C
      IF (IVAL.GE.ISIRAY(IST+13).AND.IVAL.LT.ISIRAY(IST+10)) GO TO 110
C
C FIND A NEW 2ND
C
90    CALL UMEMOV (ISIRAY(IST+11),NODAY,1)
      CALL PDSTP2 (NODAY,ISTAIX,IRCOFS,IVAL2,IDAY2,ISTAT)
      IF (ISTAT.NE.0) GO TO 120
      ISIRAY(IST+13)=IVAL2
      CALL UMEMOV (IDAY2,ISIRAY(IST+14),1)
      GO TO 120
C
C RESET LARGEST TO THIS VALUE
C
100   ISIRAY(IST+10)=IVAL
      GO TO 120
C
C RESET 2ND LARGEST TO THIS VALUE
C
110   ISIRAY(IST+13)=IVAL
C
C ALL DONE REWRITE STATION INFORMATION RECORD
C
120   LRCPD2=LRCPDI*2
      N=ISIRAY(1)
      NREC=IUNRCD(N,LRCPD2)
      CALL WVLRCD (KPDSIF,IREC,NREC,ISIRAY,LRCPDI,ISTAT)
C
      J=IST+7
      LL=IST+10
      L=IST+16
      IST=IST+1
      IF (IPDDB.GT.0) WRITE (IOGDB,130) (ISIRAY(K),K=IST,J),SUMI,
     *  (ISIRAY(K),K=LL,L),SUM
130   FORMAT (' PDSTPP COMPLETE, STATS=',7I6,F10.4,7I6,F10.4)
C
      RETURN
C
      END
