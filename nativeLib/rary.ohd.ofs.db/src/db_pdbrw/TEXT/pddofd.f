C MODULE PDDODF
C-----------------------------------------------------------------------
C
       SUBROUTINE PDDOFD (IFUT,IREV,NUMOB,IOBS,IRRBUF,IFRBUF,ISTAT)
C
C  THIS ROUTINE DELETES ALL FUTURE DATA BETWEEN THE HOUR OF LAST
C  OBSERVED DATA AND THE HOUR WHICH WILL BE THE HOUR OF LAST
C  OBSERVED AFTER THE CALL TO THE WRITE RRS ROUTINE. IF THERE ARE
C  NO OBSERVED DATA FOR THIS RECORD, FUTURE DATA IS DELETED FROM
C  THE START OF THE RECORD. FREE POOL DATA IS INCLUDED.
C
C  ARGUMENT LIST:
C
C       NAME     TYPE   I/O   DIM   DESCRIPTION
C       ------   ----   ---   ---   -----------
C       IFUT       I     I     1    FUTURE WRITE FLAG:
C                                    0=NON-FUTURE
C                                    1=FUTURE
C       IREV       I     I     1    REVISION WRITE INDICATOR:
C                                      0=NOT REVISION
C                                      1=REVISION
C       NUMOB      I     I     1    NUMBER OF OBS FOR THIS DATA TYPE
C       IOBS       I     I     ?    OBSERVATION SETS
C       IRRBUF     I    I/O    ?    ARRAY FOR RRS RECORD
C       IFRBUF     I    I/O    ?    ARRAY FOR FREE POOL
C       ISTAT      I     O     1    STATUS INDICATOR:
C                                     O=NORMAL RETURN
C                                    >0=ERROR
C
      INCLUDE 'uio'
      INCLUDE 'udebug'
      INCLUDE 'pdbcommon/pdrrsc'
C
      DIMENSION IRRBUF(1),IFRBUF(1),IOBS(1)
C
C    ================================= RCS keyword statements ==========
      CHARACTER*68     RCSKW1,RCSKW2
      DATA             RCSKW1,RCSKW2/                                '
     .$Source: /fs/hseb/ob72/rfc/ofs/src/db_pdbrw/RCS/pddofd.f,v $
     . $',                                                             '
     .$Id: pddofd.f,v 1.3 2005/03/16 14:21:13 wkwock Exp $
     . $' /
C    ===================================================================
C
C
      ISTAT=0
C
      IF (IPDTR.GT.0) WRITE(IOGDB,10)
10    FORMAT (' ENTER PDDOFD')
C
C  CHECK IF FUTURE DATA
      IF (IFUT.NE.0) GO TO 380
C
C  CHECK NUMBER OF OBSERVATIONS
      IF (IRRBUF(8).EQ.0) GO TO 380
C
C  GET NUMBER OF VALUES PER OBSERVATION
      NPEROB=IRRBUF(14)
C
C  CHECK IF DATE OF LATEST VALUE IS SAME AS THAT OF LAST OBSERVED DATA
      ICHK=JULMIN(NPEROB,IRRBUF(IRRBUF(11)))
      IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'ICHK=',ICHK,' IRRBUF(16)=',IRRBUF(16)
         ENDIF
      IF (ICHK.EQ.IRRBUF(16)) GO TO 380
C
C  GET NEW LAST OBSERVED TIME
      ILAST=0
      IPOS=1
      DO 20 I=1,NUMOB
         IF (IOBS(IPOS).GT.ILAST) ILAST=IOBS(IPOS)
         IPOS=IPOS+NPEROB
20       CONTINUE
      IF (ILAST.EQ.0) GO TO 380
C
C  CHECK IF NEW LAST OBSERVED TIME IS GREATER THAN HOUR OF LAST OBSERVED
      IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'ILAST=',ILAST,' IRRBUF(16)=',IRRBUF(16)
         ENDIF
      IF (ILAST.LE.IRRBUF(16)) GO TO 380
C
C  CHECK NEW LAST OBSERVED TIME IS LESS THAN LATEST VALUE
      ICHK=JULMIN(NPEROB,IRRBUF(IRRBUF(11)))
      IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'ILAST=',ILAST,' ICHK=',ICHK
         ENDIF
ckwz.r24-32.10/27/04      IF (ILAST.LT.ICHK) GO TO 30
ckwz.IRRBUF(IRRBUF(11)) is the latest obs. time. It was a typo?
      IF (ILAST.LE.ICHK) GO TO 380
C
C  DELETE ALL FUTURE DATA
      CALL PDDFUT (1,0,IRRBUF,IFRBUF,ISTAT)
      GO TO 380
C
C  CHECK HOUR OF LAST OBSERVED DATA VALUE
30    IF (IPDDB.GT.0) WRITE (LP,*) 'IRRBUF(16)=',IRRBUF(16)
      IF (IRRBUF(16).NE.0) GO TO 110
C
C  CHECK IF ANY FREE POOL RECORDS
      IF (IPDDB.GT.0) WRITE (LP,*) 'IRRBUF(13)=',IRRBUF(13)
      IF (IRRBUF(13).EQ.0) GO TO 80
C
C  DELETE FROM FREE POOL UP TO NEW LAST OBSERVED
40    IF (IPDDB.GT.0) WRITE (LP,*) 'IRRBUF(13)=',IRRBUF(13)
      CALL PDRDFR (IRRBUF(13),IFRBUF,ISTAT)
      IF (ISTAT.NE.0) GO TO 380
      NVALS=IFRBUF(2)
      IPOS=3
      DO 50 I=1,NVALS
         ICHK=JULMIN(NPEROB,IFRBUF(IPOS))
         IF (ILAST.GT.ICHK) GO TO 60
         IPOS=IPOS+NPEROB
50       CONTINUE
C
C  DELETE THIS FREE POOL RECORD
      IREC=IRRBUF(13)
      IRRBUF(13)=IFRBUF(1)
      IFRBUF(1)=-1
      IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'DELETING RECORD ',IREC
         ENDIF
      CALL PDWRFR (IREC,IFRBUF,ISTAT)
      IF (ISTAT.NE.0) GO TO 380
      IF (IRRBUF(13).NE.0) GO TO 40
      IRRBUF(15)=0
      GO TO 80
C
C  MOVE DATA DOWN IN THIS FREE POOL
60    IF (I.EQ.1) GO TO 70
         IFRBUF(2)=NVALS-I+1
         N=IFRBUF(2)*NPEROB
         CALL UMEMOV (IFRBUF(IPOS),IFRBUF(3),N)
         IF (IPDDB.GT.0) THEN
            WRITE (LP,*) 'WRITING RECORD ',IRRBUF(13)
            ENDIF
         CALL PDWRFR (IRRBUF(13),IFRBUF,ISTAT)
         IF (ISTAT.NE.0) GO TO 380
70    IRRBUF(15)=JULMIN(NPEROB,IFRBUF(3))
      GO TO 380
C
C  START DELETING AT START OF REGULAR RECORD
80    IS=IRRBUF(9)
      IE=IRRBUF(11)
      IF (IE.LT.IS) IE=IRRBUF(1)-NPEROB+1
90    DO 100 I=IS,IE,NPEROB
         ICHK=JULMIN(NPEROB,IRRBUF(I))
         IF (ICHK.GT.ILAST) GO TO 380
         IRRBUF(8)=IRRBUF(8)-1
         IRRBUF(9)=I
100      CONTINUE
C
C  GO BACK IF WRAP AROUND
      IF (IRRBUF(8).LE.0) GO TO 360
      IF (IE.EQ.IRRBUF(11)) GO TO 360
      IS=IRRBUF(1)-IRRBUF(7)*NPEROB+1
      IE=IRRBUF(11)
      GO TO 90
C
C  HAVE SOME OBSERVED - DELETE PART OF FUTURE
C
C  CHECK IF ANY FREE POOL RECORDS
110   IF (IPDDB.GT.0) WRITE (LP,*) 'IRRBUF(13)=',IRRBUF(13)
      IF (IRRBUF(13).EQ.0) GO TO 240
C
C  CHECK IF LAST OBSERVED TIME IS GREATER THAN TIME OF EARLIEST VALUE
      ICHK=JULMIN(NPEROB,IRRBUF(IRRBUF(9)))
      IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'IRRBUF(16)=',IRRBUF(16),' ICHK=',ICHK
         ENDIF
      IF (IRRBUF(16).GT.ICHK) GO TO 240
C
C  READ FREE POOL AND FIND LAST OBSERVED
      IREC=IRRBUF(13)
      ISREC=0
120   IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'READING RECORD ',IREC
         ENDIF
      CALL PDRDFR (IREC,IFRBUF,ISTAT)
      IF (ISTAT.NE.0) GO TO 380
      IPOS=3
      NVALS=IFRBUF(2)
      DO 130 I=1,NVALS
         ICHK=JULMIN(NPEROB,IFRBUF(IPOS))
         IF (IPDDB.GT.0) THEN
            WRITE (LP,*) 'IRRBUF(16)=',IRRBUF(16),' ICHK=',ICHK
            ENDIF
         IF (ICHK.GT.IRRBUF(16)) GO TO 140
         IPOS=IPOS+NPEROB
130      CONTINUE
      ISREC=IREC
      IREC=IFRBUF(1)
      IF (IREC.NE.0) GO TO 120
      GO TO 240
C
C  CHECK IF FIRST VALUE IN RECORD
140   IF (IPDDB.GT.0) WRITE (LP,*) 'I=',I
      IF (I.NE.1) GO TO 150
C
C  GET PREVIOUS
      IF (ISREC.EQ.0) GO TO 360
      IF (IPDDB.GT.0) WRITE (LP,*) 'ISREC=',ISREC
      CALL PDRDFR (ISREC,IFRBUF,ISTAT)
      IF (ISTAT.NE.0) GO TO 380
      IREC=ISREC
      GO TO 190
C
C  CHECK SAME RECORD FOR NEW OBSERVED HOUR
150   IF (IPDDB.GT.0) WRITE (LP,*) 'NVALS=',NVALS
      IF (I.EQ.NVALS) GO TO 170
      II=I+1
      IPOSS=IPOS+NPEROB
      NDEL=1
      DO 160 K=II,NVALS
         ICHK=JULMIN(NPEROB,IFRBUF(IPOSS))
         IF (IPDDB.GT.0) THEN
            WRITE (LP,*) 'ILAST=',ILAST,' ICHK=',ICHK
            ENDIF
         IF (ILAST.GT.ICHK) GO TO 180
         NDEL=NDEL+1
         IPOSS=IPOSS+NPEROB
160      CONTINUE
C
C  DELETE REST OF THIS RECORD
170   IF (IPDDB.GT.0) WRITE (LP,*) 'I=',I
      IFRBUF(2)=I-1
      GO TO 190
C
C  SHIFT DATA AND WRITE RECORD
180   IFRBUF(2)=NVALS-NDEL
      N=(NVALS-K)*NPEROB
      CALL UMEMOV (IFRBUF(IPOSS),IFRBUF(IPOS),N)
      IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'WRITING RECORD ',IREC
         ENDIF
      CALL PDWRFR (IREC,IFRBUF,ISTAT)
      GO TO 380
C
C  READ FREE POOL LOOKING FOR NEW OBSERVED HOUR
190   ITREC=IFRBUF(1)
      IF (ITREC.EQ.0) GO TO 230
      IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'READING FREE POOL RECORD ',ITREC
         ENDIF
      CALL PDRDFR (ITREC,IFRBUF(IFREEL+1),ISTAT)
      IF (ISTAT.NE.0) GO TO 380
      IPOS=IFREEL+3
      NVALS=IFRBUF(IFREEL+2)
      DO 200 I=1,NVALS
         ICHK=JULMIN(NPEROB,IFRBUF(IPOS))
         IF (IPDDB.GT.0) THEN
            WRITE (LP,*) 'ILAST=',ILAST,' ICHK=',ICHK
            ENDIF
         IF (ILAST.GT.ICHK) GO TO 210
         IPOS=IPOS+NPEROB
200      CONTINUE
C
C  NOT FOUND - DELETE RECORD
      IFRBUF(1)=IFRBUF(IFREEL+1)
      IFRBUF(IFREEL+1)=-1
      IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'DELETING RECORD ',ITREC
         ENDIF
      CALL PDWRFR (ITREC,IFRBUF(IFREEL+1),ISTAT)
      IF (ISTAT.NE.0) GO TO 380
      GO TO 190
C
C  FOUND IT - SHIFT DATA AND WRITE RECORD
210   IF (I.EQ.1) GO TO 220
         IFRBUF(IFREEL+2)=NVALS-I+1
         N=IFRBUF(IFREEL+2)*NPEROB
         CALL UMEMOV (IFRBUF(IPOS),IFRBUF(IFREEL+3),N)
         IF (IPDDB.GT.0) THEN
            WRITE (LP,*) 'WRITING RECORD ',ITREC
            ENDIF
         CALL PDWRFR (ITREC,IFRBUF(IFREEL+1),ISTAT)
         IF (ISTAT.NE.0) GO TO 380
220   IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'WRITING RECORD ',ITREC
         ENDIF
      CALL PDWRFR (IREC,IFRBUF,ISTAT)
      IF (ISTAT.NE.0) GO TO 380
      GO TO 380
C
C  WRITE FREE POOL WITH CURRENT LAST OBSERVATION
230   IF (IPDDB.GT.0) THEN
         WRITE (LP,*) 'WRITING RECORD ',IREC
         ENDIF
      CALL PDWRFR (IREC,IFRBUF,ISTAT)
      IF (ISTAT.NE.0) GO TO 380
C
C  FIND LAST OBSERVED IN RECORD
240   IS=IRRBUF(9)
      IE=IRRBUF(11)
      IF (IE.LT.IS) IE=IRRBUF(1)-NPEROB+1
      IWRAPC=0
250   DO 260 I=IS,IE,NPEROB
         ICHK=JULMIN(NPEROB,IRRBUF(I))
         IF (IRRBUF(16).LT.ICHK) GO TO 270
260      CONTINUE
C
C  GO BACK FOR WRAP AROUND
      IF (IE.EQ.IRRBUF(11)) GO TO 360
      IWRAPC=1
      IS=IRRBUF(1)-IRRBUF(7)*NPEROB+1
      IE=IRRBUF(11)
      GO TO 250
C
C  FIND FIRST HOUR AFTER NEW OBSERVED TIME
270   IWRAPN=IWRAPC
280   DO 290 J=IS,IE,NPEROB
         ICHK=JULMIN(NPEROB,IRRBUF(J))
         IF (ILAST.LT.ICHK) GO TO 300
290      CONTINUE
C
C  GO BACK FOR WRAP
      IF (IE.EQ.IRRBUF(11)) GO TO 360
      IS=IRRBUF(1)-IRRBUF(7)*NPEROB+1
      IE=IRRBUF(11)
      IWRAPN=1
      GO TO 280
C
C  MOVE FIRST DATA UP
300   IF (I.EQ.IRRBUF(9)) GO TO 340
      IF (IWRAPC.EQ.1) GO TO 320
      IS=I-1
      IE=J-1
      IF (IWRAPN.EQ.1) IE=IRRBUF(1)
      NVALS=I-IRRBUF(9)
      IF (IS.EQ.IE) GO TO 380
      DO 310 IPOS=1,NVALS
         IRRBUF(IE)=IRRBUF(IS)
         IS=IS-1
         IE=IE-1
310      CONTINUE
      IRRBUF(9)=IE+1
      IRRBUF(8)=IRRBUF(8)-(IE-IS)/NPEROB
      IF (IWRAPN.EQ.0) GO TO 380
C
C  DO WRAPPED PART OF RECORD
      IS=IRRBUF(1)-IRRBUF(7)*NPEROB+1
      GO TO 330
C
C  MOVE END DOWN
320   IS=I
330   IE=J
      NVALS=IRRBUF(11)+NPEROB-1-IE+1
      IF (NVALS.EQ.0) GO TO 380
      CALL UMEMOV (IRRBUF(IE),IRRBUF(IS),NVALS)
      N=IE-IS
      IRRBUF(11)=IRRBUF(11)-N
      IRRBUF(8)=IRRBUF(8)-N/NPEROB
      GO TO 380
C
C  CONTINUE DELETING FROM END OF FREE POOL
340   IF (IWRAPN.EQ.1) GO TO 350
         IRRBUF(8)=IRRBUF(8)-(J-IRRBUF(9))/NPEROB
         IRRBUF(9)=J
         GO TO 380
350   IRRBUF(8)=(IRRBUF(11)-J)/NPEROB+1
      IRRBUF(9)=J
      GO TO 380
C
C  SYSTEM ERROR
360   WRITE (LP,370)
370   FORMAT ('0**ERROR** SYSTEM ERROR IN PDDOFD.')
      ISTAT=4
C
380   IF (IPDTR.GT.0) WRITE(IOGDB,390)
390   FORMAT (' EXIT PDDOFD')
C
      RETURN
C
      END
