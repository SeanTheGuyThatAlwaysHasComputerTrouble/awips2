c ---------------------------------------------------------------------
C
      SUBROUTINE DFLT(IDAM,TFM,HDM,HDE,BME,BW,VOL,
     *SA,CM,D,NS,NCS,BS,BSS,IDFLT,SYM,CMS,DISTTN,IDIST,TWN)
C
	COMMON/S4/XLAT(50),XLON(50)
      COMMON/IONUM/IOUT,IBOTH,IBTH
      COMMON/IOANUM/IOUTA
      DIMENSION D(50),BS(8,50),CM(8,50),IDFLT(8)
      DIMENSION BSS(8,50)
C
      CHARACTER*1 SYM(9),STAR
      CHARACTER*10 ANUM
      CHARACTER*26 TWN(50),EMPTY,RVLOC
      DATA STAR/'*'/
C
C
C          THIS SUBROUTINE SETS ALL DEFAULT VALUES IN THE MODEL
C
C          COMPUTE THE DAM HEIGHT
C
      HDM=HDE-BME
C
C          COMPUTE THE DEFAULT DAM FAILURE TIME
C
      IF(TFM.GT.0.) GO TO 10
      IF(IDAM.EQ.0) TFM=HDM/10.
      IF(IDAM.EQ.1) TFM=HDM/40.
      IF(IDAM.EQ.2) TFM=HDM/50.
      IDFLT(1)=1
      SYM(1)=STAR
C
C          COMPUTE THE DEFAULT BREACH WIDTH
C
   10 IF(ABS(BW).GT.0.0) GO TO 20
      IF(IDAM.EQ.0) BW=3.*HDM
      IF(IDAM.EQ.1) BW=5.*HDM
      IDFLT(2)=1
      SYM(2)=STAR
C
C          COMPUTE THE VOLUME AND SURFACE AREA DEFAULT VALUES
C
  20  IF(VOL.GT.0.) GO TO 140
      VOL=HDM*SA/2.
      IDFLT(6)=1
      SYM(6)=STAR
C
  140 IF(SA.GT.0.) GO TO 150
      SA=2.*VOL/HDM
      IDFLT(7)=1
      SYM(7)=STAR
C
C          COMPUTE THE DEFAULT MANNING N VALUES
C
  150 DO 165 I=1,NS
         IF(CM(1,I).GT.0.) GO TO 160
         IDFLT(5)=1
         CM(1,I)=0.05
  160    DO 165 K=1,NCS
            IF(CM(K,I).LT.0.0001) IDFLT(5)=1
            IF(K.GT.1.AND.CM(K,I).LT.0.0001) CM(K,I)=CM(1,I)
  165 CONTINUE
      IF(IDFLT(5).EQ.1) SYM(5)=STAR
C
      IF(BW.LT.0.01.AND.IDAM.EQ.2) BW=0.9*(BS(NCS,1)+BSS(NCS,1))
C
      IIB=0
      IF(BW.LT.-0.1) IIB=1
      IF(IIB.EQ.0) GO TO 164
      BW=ABS(BW)
      GO TO 169
  164 BWSV=BW
      IF(BW.LE.1.5*(BS(2,1)+BSS(2,1))) GO TO 169
      BW=2./3.*BWSV
      IF(IBTH.EQ.1) PRINT 168, BWSV,BW
      WRITE(IOUT,168) BWSV,BW
C         THE FOLLOWING CHANGES MADE ON 6/20/89
  168 FORMAT(//25X,'******** W A R N I N G ********'//
     *2X,23HORIGINAL BREACH WIDTH (,F6.1,17H) WAS CHANGED TO(,
     *F6.1,22H) BECAUSE IT WAS WIDER,
     */,2X,'THAN 1.5*(THE 2ND CHANNEL WIDTH) OF THE 1ST CROSS SECTION.
     *THIS CONSTRAINT'/2X,'CAN BE REMOVED BY ENTERING BW AS A NEGATIVE V
     *ALUE OR BY ENLARGING THE LOWER'/2X,
     *'PORTION OF THE 1ST CROSS SECTION OR BY RAISING THE BOTTOM BREACH
     *ELEVATION'/2X,'TO MORE NEARLY CONFORM WITH THE NATURAL TOPOGRAPHY
     *OF THE 1ST SECTION.'/)
C         END OF CHANGE OF 6/20/89
C
C         DETERMINE THE DEFAULT VALUE FOR CMS
C
  169 IF(CMS.GT.0.01) GO TO 200
C     CMS=0.3
      CMS=0.5
      SYM(8)=STAR
      IDFLT(8)=1
C
C	 LOCATE THE CROSS SECTION CLOSEST TO THE PRIMARY POINT OF INTEREST
C
  200 DO 220 I=2,NS
      IF(DISTTN.GT.D(I)) GO TO 220
      RAT1=ABS(DISTTN-D(I-1))
      RAT2=ABS(DISTTN-D(I))
      DISTTN=D(I)
      IDIST=(I)
      IF(RAT1.LT.RAT2) THEN
        DISTTN=D(I-1)
        IDIST=I-1
      END IF
      GO TO 230
  220 CONTINUE
  230 IF(DISTTN.LE.0.0001.OR.DISTTN.GT.D(NS)) THEN
        DISTTN=D(NS)
        IDIST=NS
      END IF
      IF(IBTH.EQ.1) WRITE(IOUTA,235) DISTTN
      WRITE(IOUT,235) DISTTN
  235 FORMAT(//' ***** DISTANCE TO PRIMARY POINT OF INTEREST MOVED TO TH
     *E CROSS SECTION'/     ' ***** CLOSEST TO THIS LOCATION (MI ',F6.2,
     *')')

C        CREATE RIVE LOCATION NAMES AT EACH CROSS SECTION IF THEY DON'T EXIST

      EMPTY='                          '
	DO 250 I=1,NS
	  CALL TOCHAR(D(I),ANUM,ICOL)
	  RVLOC='RM'//ANUM(1:ICOL-1)
	  IF(TWN(I).EQ.EMPTY) THEN
	    TWN(I)=RVLOC
	    SYM(9)=STAR
	  ENDIF
  250 CONTINUE
      RETURN
      END
